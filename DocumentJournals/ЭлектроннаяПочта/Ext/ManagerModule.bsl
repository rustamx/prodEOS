
Процедура ОбработкаПолученияФормы(ВидФормы, Параметры, ВыбраннаяФорма, ДополнительнаяИнформация, СтандартнаяОбработка)
	
	Если ВидФормы = "ФормаСписка" Тогда
		
		ОтображениеОбластиЧтения =
			ВстроеннаяПочтаСервер.ПолучитьПерсональнуюНастройку("ОтображениеОбластиЧтения");
			
		Если Не ЗначениеЗаполнено(ОтображениеОбластиЧтения) Тогда
			ОтображениеОбластиЧтения = Перечисления.ВариантыОтображенияОбластиЧтения.НеОтображать;
		КонецЕсли;
		
		ВидСписка = ВстроеннаяПочтаСервер.ПолучитьПерсональнуюНастройку("ВидСписка");
		ПоказыватьДеревоПапок = (ВидСписка = "Обычный");
		ОбластьСправа = Перечисления.ВариантыОтображенияОбластиЧтения.Справа;
		
		ДвустрочныйРежим = (ОтображениеОбластиЧтения = ОбластьСправа);
		
		Если ДвустрочныйРежим Тогда
			ВыбраннаяФорма = "ФормаСпискаДвустрочная";
			СтандартнаяОбработка = Ложь;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры


// Удаляет порцию устаревших данных.
// 
// Возвращаемое значение - Булево - Истина, если были найдены устаревшие данные, в противном случае Ложь.
// 
Функция УдалитьПорциюУстаревшихДанных() Экспорт
	
	БылиПисьмаДляУдаления = Ложь;
	
	УстановитьПривилегированныйРежим(Истина);
	
	ЗаписьЖурналаРегистрации(
		НСтр("ru = 'Очистка папок от старых писем'; en = 'Folder cleanup of old emails'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка()),
		УровеньЖурналаРегистрации.Информация,,,
		НСтр("ru = 'Начало выполнения'; en = 'Start run'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка()));
	
	Настройки = РегистрыСведений.НастройкиАвтоочисткиПапокПисем.Выбрать();
	Пока Настройки.Следующий() Цикл
		Запрос = Новый Запрос;
		
		Запрос.Текст = 
			"ВЫБРАТЬ ПЕРВЫЕ 1000
			|	ЭлектроннаяПочта.Ссылка КАК Письмо
			|ИЗ
			|	ЖурналДокументов.ЭлектроннаяПочта КАК ЭлектроннаяПочта
			|ГДЕ
			|	ЭлектроннаяПочта.ПометкаУдаления = ЛОЖЬ
			|	И ЭлектроннаяПочта.Папка = &Папка
			|	И ЭлектроннаяПочта.Дата < &ДатаУстаревания
			|
			|УПОРЯДОЧИТЬ ПО
			|	ЭлектроннаяПочта.Дата";
		Запрос.УстановитьПараметр("Папка", Настройки.Папка);
		ДатаУстаревания = ТекущаяДата() - Настройки.ПериодУстаревания * 24 * 60 * 60;
		Запрос.УстановитьПараметр("ДатаУстаревания", ДатаУстаревания);
		Выборка = Запрос.Выполнить().Выбрать();
		
		Пока Выборка.Следующий() Цикл
			
			БылиПисьмаДляУдаления = Истина;
			
			Попытка
				
				ЗаблокироватьДанныеДляРедактирования(Выборка.Письмо);
				ПисьмоОбъект = Выборка.Письмо.ПолучитьОбъект();
				ПисьмоОбъект.УстановитьПометкуУдаления(Истина);
				РазблокироватьДанныеДляРедактирования(Выборка.Письмо);
				
			Исключение
				ЗаписьЖурналаРегистрации(
					НСтр("ru = 'Очистка папок от старых писем'; en = 'Folder cleanup of old emails'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка()),
					УровеньЖурналаРегистрации.Ошибка,,,
					ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			КонецПопытки;
				
		КонецЦикла;
			
	КонецЦикла;
	
	ЗаписьЖурналаРегистрации(
		НСтр("ru = 'Очистка папок от старых писем'; en = 'Folder cleanup of old emails'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка()),
		УровеньЖурналаРегистрации.Информация,,,
		НСтр("ru = 'Завершение'; en = 'Completion'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка()));
	
	Возврат БылиПисьмаДляУдаления;
	
КонецФункции


