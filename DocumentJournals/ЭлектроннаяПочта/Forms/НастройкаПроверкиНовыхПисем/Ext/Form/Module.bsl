&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// оповещения о новых письмах
	ВремяПроверкиНовыхПисем = ВстроеннаяПочтаСервер.ПолучитьПерсональнуюНастройку("ВремяПроверкиНовыхПисем");
	ПроверятьНаличиеНовыхПисем = ВстроеннаяПочтаСервер.ПолучитьПерсональнуюНастройку("ПроверятьНаличиеНовыхПисем");
	
	МассивУчетныхЗаписей = ВстроеннаяПочтаСервер.ПолучитьУчетныеЗаписиТекущегоПользователя();
	Для Каждого Запись Из МассивУчетныхЗаписей Цикл
		УчетныеЗаписи.Добавить(Запись);
	КонецЦикла;

	Если МассивУчетныхЗаписей.Количество() > 1 Тогда

		Элементы.ГруппаНастройкаДляОднойУчетнойЗаписи.Видимость = Ложь;
		Элементы.ГруппаНастройкаДляНесколькихУчетныхЗаписей.Видимость = Истина;

		Для Каждого УчетнаяЗапись Из МассивУчетныхЗаписей Цикл
			
			Строка = ПроверятьНаличиеНовыхПисемТаблица.Добавить();
			Строка.УчетнаяЗапись = УчетнаяЗапись;
			
			КлючНастройки = Строка(УчетнаяЗапись); 
			Строка.Показывать = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
				"ВстроеннаяПочта/ПоказыватьОповещениеОПриходеПисьма",
				КлючНастройки,
				Ложь);

			Строка.ИспользоватьPush = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
				"ВстроеннаяПочта/ОтправлятьОповещениеОПриходеПисьмаИспользуяPush",
				КлючНастройки,
				Ложь);

		КонецЦикла;
		
	ИначеЕсли МассивУчетныхЗаписей.Количество() = 1 Тогда
		
		Элементы.ГруппаНастройкаДляОднойУчетнойЗаписи.Видимость = Истина;
		Элементы.ГруппаНастройкаДляНесколькихУчетныхЗаписей.Видимость = Ложь;

		УчетнаяЗапись = МассивУчетныхЗаписей[0];
		КлючНастройки = Строка(УчетнаяЗапись); 
		ПоказыватьОповещение = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
			"ВстроеннаяПочта/ПоказыватьОповещениеОПриходеПисьма",
			КлючНастройки,
			Ложь);

		ОтправлятьУведомленияНаМобильныеУстройства = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
			"ВстроеннаяПочта/ОтправлятьОповещениеОПриходеПисьмаИспользуяPush",
			КлючНастройки,
			Ложь);

	Иначе	
		Элементы.ГруппаНастройкиОповещений.Видимость = Ложь;
		Элементы.ГруппаНастройкаДляОднойУчетнойЗаписи.Видимость = Ложь;
		Элементы.ГруппаНастройкаДляНесколькихУчетныхЗаписей.Видимость = Ложь;
	КонецЕсли;	
	
	НастройкиОповещений = ВстроеннаяПочтаСервер.ПолучитьНастройкиОповещенийОНовыхПисьмах();
	
	Если НастройкиОповещений.ПроверятьНаличиеНовыхПисем 
		И НастройкиОповещений.ИнтервалПроверкиВМинутах <> 0 Тогда
		
		ОповещенияОНовыхПисьмахВключены = Истина;
		
	КонецЕсли;	
	
	Элементы.ВремяПроверкиНовых.Доступность = ПроверятьНаличиеНовыхПисем;
	Элементы.ГруппаНастройкаДляОднойУчетнойЗаписи.Доступность = ПроверятьНаличиеНовыхПисем;
	Элементы.ГруппаНастройкаДляНесколькихУчетныхЗаписей.Доступность = ПроверятьНаличиеНовыхПисем;
	
	Элементы.ДекорацияМинутОднаУчетнаяЗапись.Заголовок 
		= ПолучитьПодписьМинут(ВремяПроверкиНовыхПисем);
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьСтруктуруНастройки(МассивСтруктур, Объект, Настройка, Значение)
	
	МассивСтруктур.Добавить(Новый Структура ("Объект, Настройка, Значение", Объект, Настройка, Значение));
	
КонецПроцедуры

&НаКлиенте
Процедура Установить(Команда)
	
	Если ПроверятьНаличиеНовыхПисем И ВремяПроверкиНовыхПисем = 0 Тогда
		ОчиститьСообщения();
		ТекстСообщения = НСтр("ru = 'Укажите время проверки прихода новых писем.'; en = 'Specify time interval to check for new emails.'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,, "ВремяПроверкиНовыхПисем");
		Возврат;
	КонецЕсли;
	
	СохранитьНастройки();
	
	ВстроеннаяПочтаКлиент.ОтключитьПроверкуПоступленияНовыхПисем();
	ВстроеннаяПочтаКлиент.ВключитьПроверкуПоступленияНовыхПисем();
	
	Закрыть(КодВозвратаДиалога.ОК);
	
КонецПроцедуры

&НаСервере
Процедура СохранитьНастройки()
	
	МассивСтруктур = Новый Массив;
	
	// Оповещения о новых письмах
	ДобавитьСтруктуруНастройки(МассивСтруктур, "ВстроеннаяПочта", "ВремяПроверкиНовыхПисем", ВремяПроверкиНовыхПисем);
	ДобавитьСтруктуруНастройки(МассивСтруктур, "ВстроеннаяПочта", "ПроверятьНаличиеНовыхПисем", ПроверятьНаличиеНовыхПисем);
	
	ОповещенияОНовыхПисьмахВключеныСейчас = Ложь;
	
	Если ПроверятьНаличиеНовыхПисем 
		И ВремяПроверкиНовыхПисем <> 0 Тогда
		
		ОповещенияОНовыхПисьмахВключеныСейчас = Истина;
		
	КонецЕсли;	
	
	Если УчетныеЗаписи.Количество() > 1 Тогда
		
		Для Каждого Строка Из ПроверятьНаличиеНовыхПисемТаблица Цикл
			
			КлючНастройки = Строка(Строка.УчетнаяЗапись); 
			ДобавитьСтруктуруНастройки(МассивСтруктур, "ВстроеннаяПочта/ПоказыватьОповещениеОПриходеПисьма", 
				КлючНастройки, Строка.Показывать);

			ДобавитьСтруктуруНастройки(МассивСтруктур, "ВстроеннаяПочта/ОтправлятьОповещениеОПриходеПисьмаИспользуяPush", 
				КлючНастройки, Строка.ИспользоватьPush);

		КонецЦикла;
		
	ИначеЕсли УчетныеЗаписи.Количество() = 1 Тогда
		
		УчетнаяЗапись = УчетныеЗаписи[0];
		КлючНастройки = Строка(УчетнаяЗапись); 
		ДобавитьСтруктуруНастройки(МассивСтруктур, "ВстроеннаяПочта/ПоказыватьОповещениеОПриходеПисьма", 
			КлючНастройки, ПоказыватьОповещение);

		ДобавитьСтруктуруНастройки(МассивСтруктур, "ВстроеннаяПочта/ОтправлятьОповещениеОПриходеПисьмаИспользуяPush", 
			КлючНастройки, ОтправлятьУведомленияНаМобильныеУстройства);

	КонецЕсли;
	
	Если ОповещенияОНовыхПисьмахВключеныСейчас И Не ОповещенияОНовыхПисьмахВключены Тогда
		ВстроеннаяПочтаСервер.ЗаписатьДатуВРегистрПроверкаПоступленияНовыхПисем(
			ПользователиКлиентСервер.ТекущийПользователь(), ТекущаяДатаСеанса());
	КонецЕсли;
	
	// Оповещения о новых письмах
	
	ОбщегоНазначения.ХранилищеОбщихНастроекСохранитьМассив(МассивСтруктур);
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	Закрыть(Неопределено);
КонецПроцедуры


&НаКлиенте
Процедура ПроверятьНаличиеНовыхПисемПриИзменении(Элемент)
	
	Элементы.ВремяПроверкиНовых.Доступность = ПроверятьНаличиеНовыхПисем;
	
	Элементы.ГруппаНастройкаДляОднойУчетнойЗаписи.Доступность = ПроверятьНаличиеНовыхПисем;
	Элементы.ГруппаНастройкаДляНесколькихУчетныхЗаписей.Доступность = ПроверятьНаличиеНовыхПисем;
	
КонецПроцедуры


&НаКлиенте
Процедура ВремяПроверкиНовыхПриИзменении(Элемент)
	Элементы.ДекорацияМинутОднаУчетнаяЗапись.Заголовок 
		= ПолучитьПодписьМинут(ВремяПроверкиНовыхПисем);
КонецПроцедуры

// Подпись к количеству минут по склонениям
&НаКлиентеНаСервереБезКонтекста
Функция ПолучитьПодписьМинут(ЧислоМинут)
	
	Если ЧислоМинут > 10 И ЧислоМинут < 20 Тогда
		Подпись = НСтр("ru = 'минут'; en = 'minutes'");
	Иначе
		Срок = ЧислоМинут - Цел(ЧислоМинут / 10) * 10;
		Если Срок = 0 Тогда
			Подпись = НСтр("ru = 'минут'; en = 'minutes'");
		ИначеЕсли Срок = 1 Тогда
			Подпись = НСтр("ru = 'минуту'; en = 'minute'");
		ИначеЕсли Срок < 5 Тогда
			Подпись = НСтр("ru = 'минуты'; en = 'minutes'");
		Иначе
			Подпись = НСтр("ru = 'минут'; en = 'minutes'");
		КонецЕсли;
	КонецЕсли;
	
	Возврат Подпись;
	
КонецФункции


