////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнитьРеквизитыФормыИзПараметров(Параметры);
	УправлениеДоступностью();
	
КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ МОДУЛЯ

&НаСервере
Процедура ЗаполнитьРеквизитФормыИзПараметра(Параметры, ИмяПараметра, ИмяРеквизита = "")
	
	Если Параметры.Свойство(ИмяПараметра) Тогда
		ЭтаФорма[?(ПустаяСтрока(ИмяРеквизита), ИмяПараметра, ИмяРеквизита)] = Параметры[ИмяПараметра];
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьРеквизитыФормыИзПараметров(Параметры)
	
	ЗаполнитьРеквизитФормыИзПараметра(Параметры, "ВнутреннийНомер");
	ЗаполнитьРеквизитФормыИзПараметра(Параметры, "ЗаголовкиИнтернета");
	ЗаполнитьРеквизитФормыИзПараметра(Параметры, "Дата");
	ЗаполнитьРеквизитФормыИзПараметра(Параметры, "Получено", "ПолученоОтправлено");
	ЗаполнитьРеквизитФормыИзПараметра(Параметры, "Отправлено", "ПолученоОтправлено");
	ЗаполнитьРеквизитФормыИзПараметра(Параметры, "Письмо");
	ЗаполнитьРеквизитФормыИзПараметра(Параметры, "Кодировка");
	ЗаполнитьРеквизитФормыИзПараметра(Параметры, "УчетнаяЗапись");
	
	ТекущаяУчетнаяЗапись = УчетнаяЗапись;
	
КонецПроцедуры

&НаСервере
Процедура УправлениеДоступностью()

	Если ВстроеннаяПочтаКлиентСервер.ЭтоИсходящееПисьмо(Письмо) Тогда
		Элементы.Заголовки.Заголовок = НСтр("ru='Идентификаторы'; en = 'Ids'");
		Элементы.ПолученоОтправлено.Заголовок = НСтр("ru='Отправлено'; en = 'Sent'");
	ИначеЕсли ВстроеннаяПочтаКлиентСервер.ЭтоВходящееПисьмо(Письмо) Тогда
		Элементы.Заголовки.Заголовок = НСтр("ru='Заголовки интернета'; en = 'Internet headers'");
		Элементы.ПолученоОтправлено.Заголовок = НСтр("ru='Получено'; en = 'Received'");
	КонецЕсли;
	
КонецПроцедуры


