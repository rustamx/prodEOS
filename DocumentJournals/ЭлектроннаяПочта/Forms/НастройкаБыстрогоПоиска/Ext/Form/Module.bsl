
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	ОтборВПапке.Очистить();
	Для Каждого Строка Из Параметры.ОтборВПапке Цикл
		НоваяСтрока = ОтборВПапке.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
	КонецЦикла;	
	
	СписокСписковВыбора = Параметры.СписокСписковВыбора;
	
	ТекущийПользователь = ПользователиКлиентСервер.ТекущийПользователь();
	ТекущаяСтрокаВОтбореВПапке = -1;		
	
КонецПроцедуры

&НаКлиенте
Процедура Готово(Команда)
	
	ПараметрыВозврата = Новый Структура("ОтборВПапке, СписокСписковВыбора", ОтборВПапке, СписокСписковВыбора);
	Закрыть(ПараметрыВозврата);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборВПапкеЗначениеОчистка(Элемент, СтандартнаяОбработка)
	
	Строка = ОтборВПапке[Элементы.ОтборВПапке.ТекущаяСтрока];
	Если Строка.Тип = "Строка" Тогда
		Строка.Значение = "";
	ИначеЕсли Строка.Тип = "Дата" Тогда
		Строка.Значение = Дата(1,1,1);
	ИначеЕсли Строка.Тип = "ПеречислениеСсылка.ФлагиОбъектов" Тогда
		Строка.Значение = ПредопределенноеЗначение("Перечисление.ФлагиОбъектов.ПустаяСсылка");
	ИначеЕсли Строка.Тип = "Булево" Тогда
		Строка.Значение = Неопределено;
	КонецЕсли;
	Элементы.ОтборВПапке.ЗакончитьРедактированиеСтроки(Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборВПапкеЗначениеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОбновитьСписокВыбораУТекущегоПоляОтбора(ДанныеВыбора);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьСписокВыбораУТекущегоПоляОтбора(ДанныеВыбора = Неопределено)
	
	//Обновление списка выбора у элемента
	Если ДанныеВыбора = Неопределено Тогда
		ДанныеВыбора = Новый СписокЗначений;
	КонецЕсли;
		
	СписокПоследнихЗначений = СписокСписковВыбора[Элементы.ОтборВПапке.ТекущаяСтрока].Значение;
	Для Каждого ПоследнееЗначение Из СписокПоследнихЗначений Цикл
		
		ДанныеВыбора.Добавить(
			ПоследнееЗначение.Значение,
			ПоследнееЗначение.Представление,
			ПоследнееЗначение.Пометка,
			ПоследнееЗначение.Картинка);
			
		КонецЦикла;
		
	Элементы.ОтборВПапкеЗначение.КнопкаВыбора = 
		СписокПоследнихЗначений.Количество() > 0
		Или Найти(ОтборВПапке[Элементы.ОтборВПапке.ТекущаяСтрока].Тип, "Перечисление") > 0 
		Или Найти(ОтборВПапке[Элементы.ОтборВПапке.ТекущаяСтрока].Тип, "Булево") > 0;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборВПапкеЗначениеАвтоПодбор(Элемент, Текст, ДанныеВыбора, Параметры, Ожидание, СтандартнаяОбработка)
	
	ДанныеВыбора = Новый СписокЗначений;
	
	Если Элементы.ОтборВПапке.ТекущиеДанные <> Неопределено Тогда
		Если Элементы.ОтборВПапке.ТекущиеДанные.Параметр = "Адресаты" Тогда
			
			Если ПустаяСтрока(Текст) Или СтрДлина(Текст) < 2 Тогда
				Возврат;
			КонецЕсли;
			
			ЭтоВебКлиент = Ложь;
			#Если ВебКлиент Тогда
				ЭтоВебКлиент = Истина;
			#КонецЕсли
			
			ДанныеВыбора = ВстроеннаяПочтаСервер.ПолучитьДанныеВыбораДляЭлектронногоПисьма(
				Текст, 
				ТекущийПользователь, 
				ЭтоВебКлиент);
			ВстроеннаяПочтаКлиент.ЗаполнитьКартинкиВСпискеВыбора(ДанныеВыбора);		
				
			Если ДанныеВыбора.Количество() <> 0 Тогда
				Возврат;
			КонецЕсли;
			
		КонецЕсли;
	КонецЕсли;	
	
	Для Каждого ЭлементСписка из Элемент.СписокВыбора Цикл
		Если Найти(НРег(Строка(ЭлементСписка.Значение)), НРег(Текст)) = 1 Тогда
			ДанныеВыбора.Добавить(
				ЭлементСписка.Значение,
				ЭлементСписка.Представление,
				ЭлементСписка.Пометка,
				ЭлементСписка.Картинка);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборВПапкеЗначениеОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)

	СтандартнаяОбработка = Ложь;

	ВыбраннаяСтрока = ВыбранноеЗначение;
	
	Если ТипЗнч(ВыбранноеЗначение) = Тип("Структура") Тогда
		ВыбраннаяСтрока = ВыбранноеЗначение.Представление;
	КонецЕсли;	

	Элементы.ОтборВПапке.ТекущиеДанные.Значение = ВыбраннаяСтрока;
	
	Элементы.ОтборВПапке.ЗакончитьРедактированиеСтроки(Ложь);

КонецПроцедуры

&НаКлиенте
Процедура ОтборВПапкеПриАктивизацииСтроки(Элемент)
	
	Если ТекущаяСтрокаВОтбореВПапке <> Элементы.ОтборВПапке.ТекущаяСтрока Тогда
		ОбновитьСписокВыбораУТекущегоПоляОтбора();
		Элементы.ОтборВПапкеЗначение.РедактированиеТекста = Истина;
		Если Найти(НРег(Элементы.ОтборВПапке.ТекущиеДанные.Тип), "булево") > 0 
			Или Найти(НРег(Элементы.ОтборВПапке.ТекущиеДанные.Тип), "перечисление") > 0 Тогда
			
			Элементы.ОтборВПапкеЗначение.РедактированиеТекста = Ложь;
			Элементы.ОтборВПапкеЗначение.КнопкаВыпадающегоСписка = Ложь;
			
		Иначе	
			Элементы.ОтборВПапкеЗначение.РедактированиеТекста = Истина;
		КонецЕсли;
		ТекущаяСтрокаВОтбореВПапке = Элементы.ОтборВПапке.ТекущаяСтрока;
	КонецЕсли;
	
КонецПроцедуры

