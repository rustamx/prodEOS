&НаКлиенте
Перем ПервоеОткрытие;

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Если Не Параметры.Свойство("Письмо") Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Если Параметры.Свойство("ПечататьНемедленно") Тогда
		ПечататьНемедленно = Параметры.ПечататьНемедленно;
	КонецЕсли;
	
	Письмо = Параметры.Письмо;
	
	ТекстHTML = ВстроеннаяПочтаСервер.СформироватьHTMLПредставлениеПисьма(
		Письмо,
		УникальныйИдентификатор,
		Истина, // ВыводитьШапку
		Истина, // СокращенныйСписокАдресатов
		Не ПечататьНемедленно); // ВключитьРежимРедактированияHTML
		
КонецПроцедуры

&НаКлиенте
Процедура Печать(Команда)
	
	Элементы.ТекстHTML.Документ.execCommand("Print"); 
	
КонецПроцедуры

&НаКлиенте
Процедура ТекстHTMLДокументСформирован(Элемент)
	
	Если ПечататьНемедленно Тогда
		
		Если ПервоеОткрытие = Неопределено Тогда
			
			ПервоеОткрытие = Ложь;
			Элементы.ТекстHTML.Документ.execCommand("Print"); 
			ПодключитьОбработчикОжидания("ЗакрытиеФормы", 1, Истина);
			
		КонецЕсли;	
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗакрытиеФормы()
	
	Закрыть();
	
КонецПроцедуры	

&НаКлиенте
Процедура ТекстHTMLПриНажатии(Элемент, ДанныеСобытия, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ВстроеннаяПочтаКлиент.ОткрытьСсылку(ДанныеСобытия.Href, ДанныеСобытия.Element, Письмо, Элемент.Документ);
	
КонецПроцедуры


