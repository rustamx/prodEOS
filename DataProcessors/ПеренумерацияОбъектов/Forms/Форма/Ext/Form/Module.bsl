
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	КартинкаБизнесПроцесса = 23;
	КартинкаСправочника = 3;
	КартинкаДокумента = 7;
	
	ДлинаПрефикса = 3;
	
	// Перенумерация файлов и версий файлов
	
	ДобавитьОбъектДляПеренумерации(НСтр("ru = 'Файлы'; en = 'Files'"),
		КартинкаСправочника, "Справочник.Файлы", "Код", 50);
	ДобавитьОбъектДляПеренумерации(НСтр("ru = 'Версии файлов'; en = 'File versions'"),
		КартинкаСправочника, "Справочник.ВерсииФайлов", "Код", 50);
		
	
	// Перенумерация шаблонов процессов
	
	ДобавитьОбъектДляПеренумерации(НСтр("ru = 'Шаблоны исполнений'; en = 'Templates performances'"),
		КартинкаСправочника, "Справочник.ШаблоныИсполнения", "Код", 50);
		
	ДобавитьОбъектДляПеренумерации(НСтр("ru = 'Шаблоны комплексных бизнес процессов'; en = 'Templates of composite processes'"),
		КартинкаСправочника, "Справочник.ШаблоныКомплексныхБизнесПроцессов", "Код", 50);
		
	ДобавитьОбъектДляПеренумерации(НСтр("ru = 'Шаблоны ознакомлений'; en = 'Templates of examinations'"),
		КартинкаСправочника, "Справочник.ШаблоныОзнакомления","Код", 50);
		
	ДобавитьОбъектДляПеренумерации(НСтр("ru = 'Шаблоны поручений'; en = 'Templates of orders'"),
		КартинкаСправочника, "Справочник.ШаблоныПоручения", "Код", 50);
		
	ДобавитьОбъектДляПеренумерации(НСтр("ru = 'Шаблоны приглашений'; en = 'Templates of invitations'"),
		КартинкаСправочника, "Справочник.ШаблоныПриглашения", "Код", 50);
		
	ДобавитьОбъектДляПеренумерации(НСтр("ru = 'Шаблоны рассмотрений'; en = 'Templates of reviewals'"),
		КартинкаСправочника, "Справочник.ШаблоныРассмотрения", "Код", 50);
		
	ДобавитьОбъектДляПеренумерации(НСтр("ru = 'Шаблоны регистраций'; en = 'Templates of registrations'"),
		КартинкаСправочника, "Справочник.ШаблоныРегистрации", "Код", 50);
		
	ДобавитьОбъектДляПеренумерации(НСтр("ru = 'Шаблоны согласований'; en = 'Templates of approvals'"),
		КартинкаСправочника, "Справочник.ШаблоныСогласования", "Код", 50);
		
	ДобавитьОбъектДляПеренумерации(НСтр("ru = 'Шаблоны составных бизнес процессов'; en = 'Templates of compound processes'"),
		КартинкаСправочника, "Справочник.ШаблоныСоставныхБизнесПроцессов", "Код", 50);
		
	ДобавитьОбъектДляПеренумерации(НСтр("ru = 'Шаблоны утверждений'; en = 'Templates of confirmations'"),
		КартинкаСправочника, "Справочник.ШаблоныУтверждения", "Код", 50);
		
	// Перенумерация процессов
	ДобавитьОбъектДляПеренумерации(НСтр("ru = 'Исполнения'; en = 'Performances'"),
		КартинкаБизнесПроцесса, "БизнесПроцесс.Исполнение", "Номер", 45);
		
	ДобавитьОбъектДляПеренумерации(НСтр("ru = 'Комплексные процессы'; en = 'Composite processes'"),
		КартинкаБизнесПроцесса, "БизнесПроцесс.КомплексныйПроцесс", "Номер", 45);
		
	ДобавитьОбъектДляПеренумерации(НСтр("ru = 'Обработки внутренних документов'; en = 'Processing of internal documents'"),
		КартинкаБизнесПроцесса, "БизнесПроцесс.ОбработкаВнутреннегоДокумента", "Номер", 45);
		
	ДобавитьОбъектДляПеренумерации(НСтр("ru = 'Обработки входящих документов'; en = 'Processing of incoming documents'"),
		КартинкаБизнесПроцесса, "БизнесПроцесс.ОбработкаВходящегоДокумента", "Номер", 45);
		
	ДобавитьОбъектДляПеренумерации(НСтр("ru = 'Обработки исходящих документов'; en = 'Processing of outgoing documents'"),
		КартинкаБизнесПроцесса, "БизнесПроцесс.ОбработкаИсходящегоДокумента", "Номер", 45);
		
	ДобавитьОбъектДляПеренумерации(НСтр("ru = 'Ознакомления'; en = 'Examinations'"),
		КартинкаБизнесПроцесса, "БизнесПроцесс.Ознакомление", "Номер", 45);
	
	ДобавитьОбъектДляПеренумерации(НСтр("ru = 'Поручения'; en = 'Orders'"),
		КартинкаБизнесПроцесса, "БизнесПроцесс.Поручение", "Номер", 45);
		
	ДобавитьОбъектДляПеренумерации(НСтр("ru = 'Приглашения'; en = 'Invitations'"),
		КартинкаБизнесПроцесса, "БизнесПроцесс.Приглашение", "Номер", 45);
		
	ДобавитьОбъектДляПеренумерации(НСтр("ru = 'Рассмотрения'; en = 'Reviewals'"),
		КартинкаБизнесПроцесса, "БизнесПроцесс.Рассмотрение", "Номер", 45);
	
	ДобавитьОбъектДляПеренумерации(НСтр("ru = 'Регистрации'; en = 'Registrations'"),
		КартинкаБизнесПроцесса, "БизнесПроцесс.Регистрация", "Номер", 45);
		
	ДобавитьОбъектДляПеренумерации(НСтр("ru = 'Решения вопросов выполнения задач'; en = 'Task execution issue solvings'"),
		КартинкаБизнесПроцесса, "БизнесПроцесс.РешениеВопросовВыполненияЗадач", "Номер", 45);
		
	ДобавитьОбъектДляПеренумерации(НСтр("ru = 'Согласования'; en = 'Approvals'"),
		КартинкаБизнесПроцесса, "БизнесПроцесс.Согласование", "Номер", 45);
		
	ДобавитьОбъектДляПеренумерации(НСтр("ru = 'Утверждения'; en = 'Confirmations'"),
		КартинкаБизнесПроцесса, "БизнесПроцесс.Утверждение", "Номер", 45);
		
	// Перенумерация писем
	
	ДобавитьОбъектДляПеренумерации(НСтр("ru = 'Входящие письма'; en = 'Incoming emails'"),
		КартинкаДокумента, "Документ.ВходящееПисьмо", "Номер", 50);
		
	ДобавитьОбъектДляПеренумерации(НСтр("ru = 'Исходящие письма'; en = 'Outgoing emails'"),
		КартинкаДокумента, "Документ.ИсходящееПисьмо", "Номер", 50);
		
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура УстановитьПометкуВсемОбъектам(Команда)
	
	Для Каждого Стр ИЗ СписокОбъектов Цикл
		Стр.Пометка = Истина;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура СнятьПометкуСоВсехОбъектов(Команда)
	
	Для Каждого Стр ИЗ СписокОбъектов Цикл
		Стр.Пометка = Ложь;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьПеренумерациюОбъектов(Команда)
	
	Если ПеренумероватьОбъекты() Тогда
		ТекстПредупреждения = НСтр("ru = 'Перенумерация объектов завершена.'; en = 'Objects renumeration completed.'");
		ПоказатьПредупреждение(, ТекстПредупреждения);
	Иначе
		ТекстПредупреждения = НСтр("ru = 'Не выбрано ни одного объекта для перенумерации.'; en = 'You have not selected any objects for renumeration.'");
		ПоказатьПредупреждение(, ТекстПредупреждения);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ПеренумероватьОбъекты()
	
	УстановитьПривилегированныйРежим(Истина);
	
	Попытка
		УстановитьМонопольныйРежим(Истина);
	Исключение
		
		ТекстИсключения = НСтр("ru = 'Не удалось установить монопольный режим.
			|С информационной базой работают другие пользователи.
			|Перенумерация объектов выполняется только в монопольном режиме!';
			|en = 'Could not set single-user mode.
			|Other users are working with infobase .
			|Renumeration objects is performed only in exclusive mode!'");
		
		ВызватьИсключение ТекстИсключения;
	КонецПопытки;
	
	ДокументооборотПраваДоступа.УстановитьИспользованиеПравДоступа(Ложь);
	
	Запрос = Новый Запрос;
	
	Отбор = Новый Структура;
	Отбор.Вставить("Пометка", Истина);
	
	НайденныеСтроки = СписокОбъектов.НайтиСтроки(Отбор);
	
	Если НайденныеСтроки.Количество() = 0 Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Для Каждого Стр ИЗ НайденныеСтроки Цикл
		
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	Таблица.Ссылка,
			|	Таблица.%1
			|ИЗ
			|	%2 КАК Таблица";
			
		Запрос.Текст = СтрШаблон(
			Запрос.Текст, Стр.ПолеНумерации, Стр.ТаблицаВЗапросе);
		
		Выборка = Запрос.Выполнить().Выбрать();
		
		Пока Выборка.Следующий() Цикл
			
			Номер = СокрЛП(Выборка[Стр.ПолеНумерации]);
			
			Если СтрДлина(Номер) = Стр.ДлинаНомераКода Тогда
				Продолжить;
			КонецЕсли;
			
			ОбъектДляИзменения = Выборка.Ссылка.ПолучитьОбъект();
			
			ПрефиксОбъекта = Лев(Номер, ДлинаПрефикса);
			
			ДлинаНомера = СтрДлина(Номер) - ДлинаПрефикса;
			
			НомерОбъекта = Прав(Номер, ДлинаНомера);
			
			НомерОбъекта = СтроковыеФункцииКлиентСервер.ДополнитьСтроку(
				НомерОбъекта, Стр.ДлинаНомераКода - ДлинаПрефикса, "0", "Слева");
			
			ОбъектДляИзменения[Стр.ПолеНумерации] = ПрефиксОбъекта + НомерОбъекта;
			
			ОбновлениеИнформационнойБазы.ЗаписатьДанные(ОбъектДляИзменения, Истина);
			
		КонецЦикла;
		
		СписокОбъектов.Удалить(Стр);
		
	КонецЦикла;
	
	ДокументооборотПраваДоступа.УстановитьИспользованиеПравДоступа(Истина);
	
	УстановитьМонопольныйРежим(Ложь);
	
	Возврат Истина;
	
КонецФункции

&НаСервере
Процедура ДобавитьОбъектДляПеренумерации(Представление, Картинка, ТаблицаВЗапросе, ПолеНумерации, ДлинаНомераКода)
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	Таблица.%1
		|ИЗ
		|	%2 КАК Таблица
		|ГДЕ
		|	НЕ (Таблица.%1 ПОДОБНО &ШаблонНомера)
		|	ИЛИ Таблица.%1 ПОДОБНО &ШаблонНомераСПробелом";
		
	Запрос.Текст = СтрШаблон(
		Запрос.Текст,
		ПолеНумерации,
		ТаблицаВЗапросе);
		
	ШаблонНомера = "";
	Для Индекс = 1 По ДлинаНомераКода Цикл
		ШаблонНомера = ШаблонНомера + "_";
	КонецЦикла;
	
	ШаблонНомераСПробелом = Лев(ШаблонНомера, ДлинаНомераКода-1) + " ";
	
	Запрос.УстановитьПараметр("ШаблонНомера", ШаблонНомера);
	Запрос.УстановитьПараметр("ШаблонНомераСПробелом", ШаблонНомераСПробелом);
	
	Если Запрос.Выполнить().Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	СтрОбъект = СписокОбъектов.Добавить();
	СтрОбъект.Представление = Представление;
	СтрОбъект.Картинка = Картинка;
	СтрОбъект.ТаблицаВЗапросе = ТаблицаВЗапросе;
	СтрОбъект.ПолеНумерации = ПолеНумерации;
	СтрОбъект.ДлинаНомераКода = ДлинаНомераКода;
	
КонецПроцедуры

#КонецОбласти
