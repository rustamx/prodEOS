
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Если Не Параметры.Свойство("ИмяПеречисления") Тогда 
		Возврат
	КонецЕсли;
	
	МетаданныеПеречисления = Метаданные.Перечисления.Найти(Параметры.ИмяПеречисления);
	Если МетаданныеПеречисления = Неопределено Тогда
		Возврат
	КонецЕсли;
	
	Если Не Параметры.Свойство("ЗаголовокФормы") Или Не ЗначениеЗаполнено(Параметры.ЗаголовокФормы)Тогда
		Заголовок = МетаданныеПеречисления.Представление();
	Иначе
		Заголовок = Параметры.ЗаголовокФормы;
	КонецЕсли;
	
	ОтмеченныеЗначения = Неопределено;
	Параметры.Свойство("ОтмеченныеЗначения", ОтмеченныеЗначения);

	Запрос = Новый Запрос;
	ТекстЗапроса = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ТаблицаПеречисления.Ссылка КАК Значение,
		|	ПРЕДСТАВЛЕНИЕ(ТаблицаПеречисления.Ссылка) КАК Представление,
		|	ВЫБОР
		|		КОГДА ТаблицаПеречисления.Ссылка В (&ОтмеченныеЗначения)
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК Пометка
		|ИЗ
		|	Перечисление.%ИмяПеречисления% КАК ТаблицаПеречисления
		|
		|УПОРЯДОЧИТЬ ПО
		|	ТаблицаПеречисления.Порядок";
		
	Запрос.Текст = СтрЗаменить(ТекстЗапроса, "%ИмяПеречисления%", Параметры.ИмяПеречисления);
	Запрос.УстановитьПараметр("ОтмеченныеЗначения", ОтмеченныеЗначения);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Список.Добавить(Выборка.Значение, Выборка.Представление, Выборка.Пометка);
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура Готово(Команда)
	
	ОтмеченныеЗначения = Новый Массив;
	Для каждого Эл из Список Цикл
		Если Эл.Пометка Тогда
			ОтмеченныеЗначения.Добавить(Эл.Значение);
		КонецЕсли;
	КонецЦикла;
	
	Закрыть(ОтмеченныеЗначения);
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	Закрыть(Неопределено);
КонецПроцедуры

&НаКлиенте
Процедура ОтметитьВсе(Команда)
	Список.ЗаполнитьПометки(Истина);
КонецПроцедуры

&НаКлиенте
Процедура СнятьВсе(Команда)
	Список.ЗаполнитьПометки(Ложь);
КонецПроцедуры

&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ТекущийЭлементСписка = Список.НайтиПоЗначению(Элементы.Список.ТекущиеДанные.Значение);
	ТекущийЭлементСписка.Пометка = Не ТекущийЭлементСписка.Пометка;
	
КонецПроцедуры


