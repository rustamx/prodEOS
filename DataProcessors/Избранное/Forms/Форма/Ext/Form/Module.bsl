
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	ОбновитьСписокСервер();
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьСписокСервер()
	СписокИзбранных.Очистить();
	Избранное = ХранилищеСистемныхНастроек.Загрузить("Общее/ИзбранноеРаботыПользователя", "");
	Если Избранное = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	МассивСсылок = Новый Массив;
	Для Каждого Элемент Из Избранное Цикл
		МассивСсылок.Добавить(Элемент.НавигационнаяСсылка);
	КонецЦикла;
	
	ПредставленияСсылок = ПолучитьПредставленияНавигационныхСсылок(МассивСсылок);
	
	Для Сч = 1 По ПредставленияСсылок.Количество() Цикл
		
		Представление = ПредставленияСсылок[Сч - 1];
		Если Представление <> Неопределено Тогда
			СписокИзбранных.Добавить(МассивСсылок[Сч - 1], Представление.Текст);
		КонецЕсли;	
		
	КонецЦикла;
	
КонецПроцедуры	

&НаКлиенте
Процедура ОбновитьВыполнить()
	ОбновитьСписокСервер();
КонецПроцедуры

&НаКлиенте
Процедура ПерейтиВыполнить()
	Если Элементы.Избранное.ТекущиеДанные <> Неопределено Тогда
		ПерейтиПоНавигационнойСсылке(Элементы.Избранное.ТекущиеДанные.Значение);

	КонецЕсли;	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьВыполнить()
	
	ОписаниеОповещения = 
		Новый ОписаниеОповещения("ДобавитьВыполнитьЗавершение", ЭтотОбъект);
	
	ОткрытьФорму(
		"Обработка.Избранное.Форма.ФормаДобавленияСсылки",,,,,,
		ОписаниеОповещения,
		РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс);
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьВыполнитьЗавершение(Результат, Параметры) Экспорт
	
	Если ТипЗнч(Результат) <> Тип("Структура") Тогда
		Возврат;
	КонецЕсли;
	
	Если Результат.КодВозврата = КодВозвратаДиалога.ОК Тогда
		ТекстОшибки = "";
		Если Не ДобавитьСсылкуИОбновитьСписокСервер(Результат.Ссылка, ТекстОшибки) Тогда
			
			ПоказатьПредупреждение(, ТекстОшибки); 
			
        КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ДобавитьСсылкуИОбновитьСписокСервер(НавСсылка, ТекстОшибки, СсылкаОбъекта = Неопределено)
	
	Если СписокИзбранных.НайтиПоЗначению(НавСсылка) <> Неопределено Тогда
		
		Представление = НавСсылка;
		Если СсылкаОбъекта <> Неопределено Тогда
			Представление = Строка(СсылкаОбъекта);
		КонецЕсли;	
		
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Ссылка ""%1""  уже есть в избранном.'; en = 'Link ""%1"" already exists in the Favorites.'"),
			Представление);
		
		Возврат Ложь;
		
	КонецЕсли;	
	
	Ссылки = Новый Массив;
	Ссылки.Добавить(НавСсылка);
	ПредставленияСсылок = ПолучитьПредставленияНавигационныхСсылок(Ссылки);
	Если ПредставленияСсылок.Количество() <> 0 Тогда
		Избранное = ХранилищеСистемныхНастроек.Загрузить("Общее/ИзбранноеРаботыПользователя", "");
		Если Избранное = Неопределено тогда
			Избранное = Новый ИзбранноеРаботыПользователя;
		КонецЕсли;	
		ЭлементИзбранного = Новый ЭлементИзбранногоРаботыПользователя;
		ЭлементИзбранного.НавигационнаяСсылка = НавСсылка;
		Избранное.Добавить(ЭлементИзбранного);
		ХранилищеСистемныхНастроек.Сохранить("Общее/ИзбранноеРаботыПользователя", "", Избранное);
		ОбновитьСписокСервер();
		Возврат Истина;
	КонецЕсли;	
	
	ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'Ссылка ""%1"" не найдена.'; en = 'Link ""%1"" is not found.'"),
		НавСсылка);
	
	Возврат Ложь;
	
КонецФункции

&НаКлиенте
Процедура УдалитьВыполнить()
	Если Элементы.Избранное.ТекущиеДанные <> Неопределено Тогда
		УдалитьСсылкуИОбновитьСписокСервер(Элементы.Избранное.ТекущаяСтрока);
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура УдалитьСсылкуИОбновитьСписокСервер(Идентификатор)
	Избранное = ХранилищеСистемныхНастроек.Загрузить("Общее/ИзбранноеРаботыПользователя", "");
	Избранное.Удалить(СписокИзбранных.Индекс(СписокИзбранных.НайтиПоИдентификатору(Идентификатор)));
	ХранилищеСистемныхНастроек.Сохранить("Общее/ИзбранноеРаботыПользователя", "", Избранное);
	ОбновитьСписокСервер();
КонецПроцедуры

&НаКлиенте
Процедура ИзбранноеПередУдалением(Элемент, Отказ)
	УдалитьВыполнить();
	Отказ = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ИзбранноеПередНачаломИзменения(Элемент, Отказ)
	ПерейтиВыполнить();
	Отказ = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ИзбранноеПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	Отказ = Истина;	
	Если Не Копирование Тогда
		ДобавитьВыполнить();
 	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ИзбранноеПроверкаПеретаскивания(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Поле)
	СтандартнаяОбработка = Ложь;
КонецПроцедуры

&НаКлиенте
Процедура ИзбранноеПеретаскивание(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Поле)
	СтандартнаяОбработка = Ложь;
	
	Если ТипЗнч(ПараметрыПеретаскивания.Значение) = Тип("Массив") Тогда
		
		Для Каждого ОбъектПринятый Из ПараметрыПеретаскивания.Значение Цикл
			Ссылка = ПолучитьНавигационнуюСсылку(ОбъектПринятый);
			
			ТекстОшибки = "";
			Если Не ДобавитьСсылкуИОбновитьСписокСервер(Ссылка, ТекстОшибки, ОбъектПринятый) Тогда
				ПоказатьПредупреждение(, ТекстОшибки);
			КонецЕсли;
			
		КонецЦикла;
			
	КонецЕсли;
	
КонецПроцедуры


