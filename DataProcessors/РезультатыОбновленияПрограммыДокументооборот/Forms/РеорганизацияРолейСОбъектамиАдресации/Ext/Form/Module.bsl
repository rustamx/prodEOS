#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ОбновитьДанныеНаСервере();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыПодобработчики

&НаКлиенте
Процедура ПодобработчикиВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ТекущиеДанные = Элементы.Подобработчики.ТекущиеДанные;
	
	Если ТекущиеДанные.Статус = НСтр("ru = 'Не выполнялось'; en = 'Not executed'") Тогда
		ПоказатьПредупреждение(, 
			НСтр("ru = 'Эти данные будут обрабатываться после завершения предыдущих шагов.'; en = 'These data will be processed after the completion of previous steps.'"));
		Возврат;
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("СобытиеЖурналаРегистрации",
		НСтр("ru = 'Реорганизация ролей.'; en = 'Reorganization of roles.'")
		+ " " + ТекущиеДанные.Наименование);
	ПараметрыФормы.Вставить("ДатаНачала", ВремяНачалаОтложенногоОбновления);
	
	ОткрытьФорму("Обработка.ЖурналРегистрации.Форма.ЖурналРегистрации", ПараметрыФормы);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОбновитьДанные(Команда)
	
	ОбновитьДанныеНаСервере();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ОбновитьДанныеНаСервере()
	
	СведенияОбОбновлении = ОбновлениеИнформационнойБазыСлужебный.СведенияОбОбновленииИнформационнойБазы();
	ВремяНачалаОтложенногоОбновления = СведенияОбОбновлении.ВремяНачалаОтложенногоОбновления;
	
	ПараметрыОбработчиков = Неопределено;
	Для Каждого СтрокаДереваБиблиотека Из СведенияОбОбновлении.ДеревоОбработчиков.Строки Цикл
		Для Каждого СтрокаДереваВерсия Из СтрокаДереваБиблиотека.Строки Цикл
			Для Каждого СтрокаОбработчика Из СтрокаДереваВерсия.Строки Цикл
				Если СтрокаОбработчика.ИмяОбработчика 
					= "ОбновлениеИнформационнойБазыДокументооборот.РеорганизацияРолейСОбъектамиАдресации" Тогда
					ПараметрыОбработчиков = СтрокаОбработчика.СтатистикаВыполнения["ПараметрыОбработчика"];
					Прервать;
				КонецЕсли;
			КонецЦикла;
		КонецЦикла;
	КонецЦикла;
	
	ПараметрыОбработчика = Неопределено;
	Если ТипЗнч(ПараметрыОбработчиков) = Тип("Структура")
		И ПараметрыОбработчиков.Свойство("РеорганизацияРолейСОбъектамиАдресации", 
			ПараметрыОбработчика) Тогда
		Подобработчики.Очистить();
		Для Каждого Подобработчик Из ПараметрыОбработчика.Подобработчики Цикл
			НоваяСтрока = Подобработчики.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Подобработчик);
		КонецЦикла;
		
	Иначе // при завершении обработки БСП очистит параметры.
		Для Каждого Подобработчик Из Подобработчики Цикл
			Подобработчик.Статус = НСтр("ru = 'Завершено'; en = 'Completed'");
			Подобработчик.Объектов = 0;
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
