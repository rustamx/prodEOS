&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Параметры.Свойство("Использовать", Использовать);
	Если Параметры.Свойство("Данные") Тогда
		Параметры.Данные.Свойство("PATH", PATH);
		Параметры.Данные.Свойство("USER", USER);
		Параметры.Данные.Свойство("PASSWORD", PASSWORD);
	КонецЕсли;
	СохранениеВводимыхЗначений.ЗагрузитьСписокВыбора(ЭтаФорма, "PATH");
	СохранениеВводимыхЗначений.ЗагрузитьСписокВыбора(ЭтаФорма, "USER");
КонецПроцедуры

&НаКлиенте
Процедура ОК(Команда)
	Если Не ПустаяСтрока(PATH) Тогда
		СохранениеВводимыхЗначенийКлиент.ОбновитьСписокВыбора(ЭтаФорма, "PATH");
	КонецЕсли;
	Если Не ПустаяСтрока(USER) Тогда
		СохранениеВводимыхЗначенийКлиент.ОбновитьСписокВыбора(ЭтаФорма, "USER");
	КонецЕсли;
	ВидПочтовогоКлиента = ПредопределенноеЗначение("Перечисление.ВидыПочтовыхКлиентов.TheBat");
	Наименование = Строка(ВидПочтовогоКлиента);
	ДобавитьЗначениеКСтрокеЧерезРазделитель(Наименование, ", ", СокрЛП(USER));
	
	Результат = Новый Структура;
	Результат.Вставить("Наименование", Наименование);
	Результат.Вставить("ВидПочтовогоКлиента", ВидПочтовогоКлиента);
	Результат.Вставить("Использовать", Использовать);
	Результат.Вставить("Данные", Новый Структура);
	Результат.Данные.Вставить("PATH", PATH);
	Результат.Данные.Вставить("USER", USER);
	Результат.Данные.Вставить("PASSWORD", PASSWORD);
	Закрыть(Результат);
КонецПроцедуры

&НаКлиенте
Процедура PATHНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Диалог = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.ВыборКаталога);
	Диалог.Заголовок = НСтр("ru = 'Укажите каталог файла thebat.exe'; en = 'Specify the thebat.exe file directory'");
	Диалог.Каталог = PATH;
	Если Диалог.Выбрать() Тогда
		PATH = Диалог.Каталог;
	КонецЕсли;
	
КонецПроцедуры

