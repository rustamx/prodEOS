&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Параметры.Свойство("Наименование", Наименование);
	Параметры.Свойство("Использовать", Использовать);
	Если Параметры.Свойство("Данные") Тогда
		Параметры.Данные.Свойство("Папка", Папка);
		Параметры.Данные.Свойство("Путь", Путь);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СформироватьДеревоПапок()
	
	Application = Новый COMОбъект("Outlook.Application");
	
	Попытка
		
		Folder = Application.GetNamespace("MAPI");
		ДобавитьВложенныеПапки(Папки, Folder);
		
	Исключение
		// Может закончится неудачей
	КонецПопытки;
	
	Application = Неопределено;
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьВложенныеПапки(КореньПриемник, КореньИсточник)
	
	Folders = КореньИсточник.Folders;
	КоличествоПапок = Folders.Count();
	Если КоличествоПапок > 0 Тогда
		Для НомерПапки = 1 По КоличествоПапок Цикл
			Folder = Folders.Item(НомерПапки);
			Если Folders.Item(НомерПапки).DefaultItemType <> 0 Тогда
				Продолжить;
			КонецЕсли;
			ПапкаПриемник = КореньПриемник.ПолучитьЭлементы().Добавить();
			ПапкаПриемник.Папка = Folder.Name;
			ПапкаПриемник.Путь = Folder.FolderPath;
			ПапкаПриемник.Иконка = 0;
			ДобавитьВложенныеПапки(ПапкаПриемник, Folder);
			Если ПапкаПриемник.Путь = Путь Тогда
				Элементы.Папки.ТекущаяСтрока = ПапкаПриемник.ПолучитьИдентификатор();
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Выбрать(Команда = Неопределено)
	
	Если Элементы.Папки.ТекущаяСтрока = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Папка = Элементы.Папки.ТекущиеДанные.Папка;
	Путь = Элементы.Папки.ТекущиеДанные.Путь;
	Наименование = Строка(ПредопределенноеЗначение("Перечисление.ВидыПочтовыхКлиентов.MSOutlook")) + ", " + Путь;
	
	Результат = Новый Структура;
	Результат.Вставить("Наименование", Наименование);
	Результат.Вставить("ВидПочтовогоКлиента", ПредопределенноеЗначение("Перечисление.ВидыПочтовыхКлиентов.MSOutlook"));
	Результат.Вставить("Использовать", Использовать);
	Результат.Вставить("Данные", Новый Структура);
	Результат.Данные.Вставить("Папка", Папка);
	Результат.Данные.Вставить("Путь", Путь);
	Закрыть(Результат);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	СформироватьДеревоПапок();
	
КонецПроцедуры

&НаКлиенте
Процедура ПапкиВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Выбрать();
	
КонецПроцедуры


