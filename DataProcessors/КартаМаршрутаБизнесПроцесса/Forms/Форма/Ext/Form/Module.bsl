////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Параметры.БизнесПроцесс) Тогда
		БизнесПроцесс = Параметры.БизнесПроцесс;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ОбновитьКартуМаршрута();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ЗадачаВыполнена" Тогда
		
		ПроцессЗадачи = 
			ОбщегоНазначенияДокументооборотВызовСервера.ЗначениеРеквизитаОбъекта(Параметр, "БизнесПроцесс");
			
		Если ПроцессЗадачи = БизнесПроцесс Тогда
			ОбновитьКартуМаршрута();
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ШАПКИ ФОРМЫ

&НаКлиенте
Процедура КартаМаршрутаВыбор(Элемент)
	
	ОткрытьСписокЗадачТочкиМаршрута();
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

&НаКлиенте
Процедура ЗадачиВыполнить(Команда)
	
	ОткрытьСписокЗадачТочкиМаршрута();
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаКлиенте
Процедура ОбновитьКартуМаршрута()
	
	КартаМаршрута = Неопределено;
	ПодключитьОбработчикОжидания("ОбновитьКартуМаршрутаПродолжение", 0.2, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьКартуМаршрутаПродолжение()
	
	ЗаполнитьКартуМаршрута();
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьКартуМаршрута()
	
	Если ЗначениеЗаполнено(БизнесПроцесс) Тогда
		КартаМаршрута = БизнесПроцесс.ПолучитьОбъект().ПолучитьКартуМаршрута();
	ИначеЕсли БизнесПроцесс <> Неопределено Тогда
		КартаМаршрута = БизнесПроцессы[БизнесПроцесс.Метаданные().Имя].ПолучитьКартуМаршрута();
		Возврат;
	Иначе
		КартаМаршрута = Новый ГрафическаяСхема;
		Возврат;
	КонецЕсли;
	
	КартаМаршрута.РежимОтрисовкиСетки = РежимОтрисовкиСеткиГрафическойСхемы.НеРисовать;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьСписокЗадачТочкиМаршрута()

	#Если ВебКлиент Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Данная операция недоступна в веб-клиенте.'; en = 'The operation is not available in web client.'"));
		Возврат;
	#КонецЕсли
	
	ОчиститьСообщения();
	ТекЭлемент = Элементы.КартаМаршрута.ТекущийЭлемент;
	
	Если Не ЗначениеЗаполнено(БизнесПроцесс) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			НСтр("ru = 'Необходимо указать бизнес-процесс.'; en = 'It is necessary to specify a business process.'"),,
			"БизнесПроцесс");
		Возврат;
	КонецЕсли;
	
	Если ТекЭлемент = Неопределено ИЛИ
		НЕ (ТипЗнч(ТекЭлемент) = Тип("ЭлементГрафическойСхемыДействие")
		ИЛИ ТипЗнч(ТекЭлемент) = Тип("ЭлементГрафическойСхемыВложенныйБизнесПроцесс")) Тогда
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			НСтр("ru = 'Необходимо выбрать точку действия или вложенный бизнес-процесс карты маршрута.'; en = 'It is necessary to select an action pount or subprocess of the route map.'"),,
			"КартаМаршрута");
		Возврат;
	КонецЕсли;

	ЗаголовокФормы = НСтр("ru = 'Задачи по точке маршрута процесса'; en = 'Tasks by the process route point'");
	
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("Отбор",
		Новый Структура("БизнесПроцесс,ТочкаМаршрута", БизнесПроцесс, ТекЭлемент.Значение));
	ПараметрыОткрытия.Вставить("ЗаголовокФормы", ЗаголовокФормы);
	ПараметрыОткрытия.Вставить("ПоказыватьЗадачи", 0);
	ПараметрыОткрытия.Вставить("ВидимостьОтборов", Ложь);
	ПараметрыОткрытия.Вставить("БлокировкаОкнаВладельца", РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	ПараметрыОткрытия.Вставить("Задача", Строка(ТекЭлемент.Значение));
	ПараметрыОткрытия.Вставить("БизнесПроцесс", Строка(БизнесПроцесс));
	
	ОткрытьФорму("Задача.ЗадачаИсполнителя.ФормаСписка", ПараметрыОткрытия,
		ЭтаФорма, БизнесПроцесс);
	
КонецПроцедуры


