
#Область ОбработчикиСобытий

&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	ИмяМенеджераПечати = "Справочник.ЭДПрисоединенныеФайлы";
	ПараметрыПечати = Новый Структура;
	
	Если ОбменСКонтрагентамиСлужебныйВызовСервера.ЭтоСлужебныйДокумент(ПараметрКоманды) Тогда
		ИмяМакета = "ЭД";
		ПараметрыПечати.Вставить("Идентификатор", "ЭД");
		
	ИначеЕсли ЭтоПроизвольныйДокумент(ПараметрКоманды) Тогда
		ИмяМакета = "КарточкаЭД";
		ПараметрыПечати.Вставить("Идентификатор", "КарточкаЭД");
		
	Иначе
		ИмяМакета = "ЭД,КарточкаЭД";
		ПараметрыПечати.Вставить("Идентификатор", "ЭД,КарточкаЭД");
		
	КонецЕсли;
	
	ПараметрыПечати.Вставить("ЗаголовокФормы", НСтр("ru = 'Печать электронного документа'; en = 'Printing electronic document'"));
	ПараметрыПечати.Вставить("Представление", НСтр("ru = 'Печать электронного документа'; en = 'Printing electronic document'"));
	
	УправлениеПечатьюКлиент.ВыполнитьКомандуПечати(ИмяМенеджераПечати, ИмяМакета, ПараметрКоманды, ПараметрыВыполненияКоманды.Источник, ПараметрыПечати);
	
КонецПроцедуры

&НаСервере
Функция ЭтоПроизвольныйДокумент(ПараметрКоманды)
	
	Возврат ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ПараметрКоманды,"ВладелецФайла.ВидЭД")
		= Перечисления.ВидыЭД.ПроизвольныйЭД;
	
КонецФункции

#КонецОбласти
