&НаСервере
Перем СообщениеОбОшибке;

Функция СоздатьСоединениеСБазойДанных()
	
	Соединение = Новый COMОбъект ("ADODB.Connection");
	Если ФорматБазыДанных = 0 Тогда
		Соединение.Open("Driver={Microsoft Access Driver (*.mdb)};Dbq=" + ФайлMDB);
	Иначе
		СтрокаСоединения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				"Provider=sqloledb;Data Source=%1;Initial Catalog=%2;User Id=%3;Password=%4;",
				ИмяSQLСервера, ИмяБазы, ИмяПользователяБазыДанных, Пароль);
		Соединение.Open (СтрокаСоединения);
	КонецЕсли;
	
	Возврат Соединение;
	
КонецФункции

&НаКлиенте
Процедура DocbaseНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ДиалогВыбораФайла =	Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.ВыборКаталога);
	ДиалогВыбораФайла.Заголовок = "Выбор каталога сервера";
	
	Если ДиалогВыбораФайла.Выбрать() Тогда
		КаталогСервера = ДиалогВыбораФайла.Каталог;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПодготовитьДанныеПользователяИБДляЗаписи(ИмяПользователя)
	
	СообщениеОбОшибке = "";
	Пароль1СПредприятия = "";
	ПользовательИБ = Новый Структура;
	
	ПользовательИБ.Вставить("ПользовательИнфБазыИмя", ИмяПользователя);
	ПользовательИБ.Вставить("ПользовательИнфБазыПолноеИмя", ИмяПользователя);
	ПользовательИБ.Вставить("ПользовательИнфБазыАутентификацияСтандартная", Истина);
	ПользовательИБ.Вставить("ПользовательИнфБазыАутентификацияОС", Ложь);
	ПользовательИБ.Вставить("ПользовательИнфБазыПользовательОС", "");
	ПользовательИБ.Вставить("ПользовательИнфБазыПоказыватьВСпискеВыбора", Истина);
	ПользовательИБ.Вставить("ПользовательИнфБазыЗапрещеноИзменятьПароль", Ложь);
	ПользовательИБ.Вставить("ПользовательИнфБазыПароль", Пароль1СПредприятия);
	ПользовательИБ.Вставить("ПользовательИнфБазыУникальныйИдентификатор", Новый УникальныйИдентификатор);
	
	Возврат ПользовательИБ;
	
КонецФункции

&НаСервере
Процедура ЗагрузкаПользователей()
	
	Справочник = Справочники.Пользователи;

	СоответсвиеИдентификатораПользователяСсылке = Новый ТаблицаЗначений;
	СоответсвиеИдентификатораПользователяСсылке.Колонки.Добавить("Идентификатор");
	СоответсвиеИдентификатораПользователяСсылке.Колонки.Добавить("Ссылка");

	Соединение = СоздатьСоединениеСБазойДанных();
	
	Recordset = Новый COMОбъект ("ADODB.Recordset");
	Recordset.Open ("select * from UserGroupInfo", Соединение, 0, 1);
	Пока НЕ Recordset.EOF Цикл
		hUG = Recordset.Fields("hUG").Value;
		Группа = Recordset.Fields("AsGroup").Value;
		ИмяПользователя = Recordset.Fields("Name").Value;
		
		Если Группа = 0 Тогда 
			
			// Если пользователь с таким именем уже есть то не создаем его
			НовыйПользователь = Справочники.Пользователи.НайтиПоНаименованию(ИмяПользователя, Истина);
			Если НовыйПользователь = Неопределено Или НовыйПользователь.Пустая() Тогда
				
				ДанныеПользователяИБ = ПодготовитьДанныеПользователяИБДляЗаписи(ИмяПользователя);

				Результат = Пользователи.ЗаписатьПользователяИБ(
					ИмяПользователя,
					ДанныеПользователяИБ,
					Неопределено, 
					Истина,
					СообщениеОбОшибке);
											
				Если Не Результат Тогда
																		
					ДобавитьОшибку("Ошибка при создании пользователя: " + ИмяПользователя);
					
				Иначе 
					
					СвойстваПользователяИБ = Неопределено;
					
					Если Пользователи.ПрочитатьПользователяИБ(ИмяПользователя, СвойстваПользователяИБ) Тогда
						НовыйПользователь = Справочник.СоздатьЭлемент();
						НовыйПользователь.ИдентификаторПользователяИБ = 
							СвойстваПользователяИБ.ПользовательИнфБазыУникальныйИдентификатор;
						НовыйПользователь.Наименование = ИмяПользователя;
						НовыйПользователь.Записать();
					КонецЕсли;
					
				КонецЕсли;
				
			КонецЕсли;
			
			НоваяСтрока = СоответсвиеИдентификатораПользователяСсылке.Добавить();
			НоваяСтрока.Идентификатор = Recordset.Fields("hUG").Value;
			НоваяСтрока.Ссылка = НовыйПользователь.Ссылка;
			
		КонецЕсли;
		
		Recordset.MoveNext();
	КонецЦикла;
	
	АдресСоответсвиеИдентификатораПользователяСсылке = ПоместитьВоВременноеХранилище(
		СоответсвиеИдентификатораПользователяСсылке, ЭтаФорма.УникальныйИдентификатор);
	
	Соединение.Close();

КонецПроцедуры

&НаСервере
Функция  ПолучитьВидДокумента( Тип,  ВидДокумента)
	
	Если Тип =  1 Тогда
		Спр = Справочники.ВидыВходящихДокументов;
	ИначеЕсли Тип =  2 Тогда
		Спр =  Справочники.ВидыИсходящихДокументов;
	ИначеЕсли Тип =  3 Тогда
		Спр =  Справочники.ВидыВнутреннихДокументов;
	КонецЕсли;
	
	Ссылка = Спр.НайтиПоНаименованию(ВидДокумента);
	
	Если  Ссылка = Спр.ПустаяСсылка() Тогда
		Элемент = Спр.СоздатьЭлемент();
		Элемент.Наименование = ВидДокумента;
		Элемент.Записать();
		Ссылка = Элемент.Ссылка;
	КонецЕсли;

	Возврат Ссылка;
	
КонецФункции

&НаСервере
Функция  ПолучитьКонтрагента(ИмяКонтрагента)
	
	Спр = Справочники.Контрагенты;
	Ссылка = Спр.НайтиПоНаименованию(ИмяКонтрагента);
	
	Если  Ссылка = Спр.ПустаяСсылка() Тогда
		Элемент = Спр.СоздатьЭлемент();
		Элемент.Наименование = ИмяКонтрагента;
		Элемент.ЮрФизЛицо = Перечисления.ЮрФизЛицо.ЮрЛицо;
		Элемент.Записать();
		Ссылка = Элемент.Ссылка;
	КонецЕсли;

	Возврат Ссылка;
	
КонецФункции
	
&НаСервере
Процедура ЗагрузкаПодверсии()
	
	Справочник = Справочники.ВерсииФайлов;
	
	СоответствиеИдентификатораВерсииФайлаСcылке = Новый ТаблицаЗначений;
	СоответствиеИдентификатораВерсииФайлаСcылке.Колонки.Добавить("Идентификатор");
	СоответствиеИдентификатораВерсииФайлаСcылке.Колонки.Добавить("Ссылка");
	
	СоответствиеИдентификатораФайлаСcылке = Новый ТаблицаЗначений;
	СоответствиеИдентификатораФайлаСcылке = ПолучитьИзВременногоХранилища(АдресСоответствиеИдентификатораФайлаСcылке);
		
	СоответсвиеИдентификатораПользователяСсылке = Новый ТаблицаЗначений;
	СоответсвиеИдентификатораПользователяСсылке = ПолучитьИзВременногоХранилища(АдресСоответсвиеИдентификатораПользователяСсылке);
	
	СоответсвиеИдентификатораПапкиСсылке = Новый ТаблицаЗначений;
	СоответсвиеИдентификатораПапкиСсылке = ПолучитьИзВременногоХранилища(АдресСоответсвиеИдентификатораПапкиСсылке);
		
	Соединение = СоздатьСоединениеСБазойДанных();
	Recordset = Новый COMОбъект ("ADODB.Recordset");
	Запрос ="select
			| 	hDocument,
			| 	hSubVersion,
			| 	hParentVersion,
			| 	SubVersionName,
			| 	SubVersionFile,
			| 	SubVersionFileSize,
			| 	hCreator,
			| 	Created,
			| 	Modified,
			|   UserComment,
			|   Edition,
			|   RealFileName,
			|	FileVolumePath
			| from
			|   SubVersion
			|";
	Recordset.Open ( Запрос	, Соединение, 0, 1);
	
	Пока НЕ Recordset.EOF Цикл
		
		Попытка
			Идентификатора = Recordset.Fields("hDocument").Value;
			
			Владелец = СоответствиеИдентификатораФайлаСcылке.Найти(Идентификатора, "Идентификатор");
			Если  Владелец = Неопределено Тогда
				ДобавитьОшибку("Версия не загружена. Не найден файл ИД:" + Строка(Идентификатора));
				ДобавитьОшибку("	Наименование версии: " + Строка(Recordset.Fields("SubVersionName").Value));
				ДобавитьОшибку("	Наименование файла версии: " + Строка(Recordset.Fields("SubVersionFile").Value));
				ДобавитьОшибку("	Том версии: " + Строка(Recordset.Fields("FileVolumePath").Value));
					
				Recordset.MoveNext();
				Продолжить;
			КонецЕсли;
			
			НоваяВерсияВладелец = Владелец.Ссылка;
			
			НоваяВерсияПолноеНаименование = Recordset.Fields("SubVersionName").Value;
			НоваяВерсияНаименование = Recordset.Fields("SubVersionName").Value;
			
			SubVersionFile = Recordset.Fields("SubVersionFile").Value;
			
			Том = Recordset.Fields("FileVolumePath").Value;
			Имяфайла = КаталогСервера + "\" + SubVersionFile;
			Если Том <> "DOCBASE\" Тогда
				ИмяФайла = Том + SubVersionFile;
			КонецЕсли;	
			
			Файл = Новый Файл(Имяфайла);
			НоваяВерсияИмяБезРасширения = Файл.ИмяБезРасширения;
	     	НоваяВерсияРасширение = РасширениеБезТочки(Файл.Расширение);
			
			НоваяВерсияРазмер = Recordset.Fields("SubVersionFileSize").Value;
			
			Идентификатор = Recordset.Fields("hCreator").Value;
			НайденнаяСсылка = СоответсвиеИдентификатораПользователяСсылке.Найти(Идентификатор, "Идентификатор"); 
			Если НайденнаяСсылка <> Null И НайденнаяСсылка <> Неопределено Тогда
				НоваяВерсияАвтор = НайденнаяСсылка.Ссылка;
			Иначе	
				ДобавитьОшибку("Ошибка определения автора версии: " + Строка(Идентификатор));
				ДобавитьОшибку("	Наименование версии: " + Строка(Recordset.Fields("SubVersionName").Value));
				ДобавитьОшибку("	Наименование файла версии: " + Строка(Recordset.Fields("SubVersionFile").Value));
				ДобавитьОшибку("	Том версии: " + Строка(Recordset.Fields("FileVolumePath").Value));
			КонецЕсли;	
			
			НоваяВерсияДатаСоздания = Recordset.Fields("Created").Value;
			НоваяВерсияДатаМодификацииУниверсальная = Recordset.Fields("Modified").Value;
			НоваяВерсияКомментарий = Recordset.Fields("UserComment").Value;
			НоваяВерсияНомерВерсии = Recordset.Fields("Edition").Value;
			
			Данные = Новый ДвоичныеДанные(Имяфайла);
			
			// Поместим Файл в ВременноеХранилище
			АдресВременногоХранилищаФайла = ПоместитьВоВременноеХранилище(Данные);
			
			СведенияОФайле = РаботаСФайламиКлиентСервер.СведенияОФайле("ФайлСВерсией");
			СведенияОФайле.ИмяБезРасширения = НоваяВерсияИмяБезРасширения;
			СведенияОФайле.Размер = НоваяВерсияРазмер;
			СведенияОФайле.РасширениеБезТочки = НоваяВерсияРасширение;
			СведенияОФайле.АдресВременногоХранилищаФайла = АдресВременногоХранилищаФайла;
			СведенияОФайле.ВремяИзменения = НоваяВерсияДатаМодификацииУниверсальная;
			СведенияОФайле.ВремяИзмененияУниверсальное = НоваяВерсияДатаМодификацииУниверсальная;
			СведенияОФайле.НоваяВерсияДатаСоздания = НоваяВерсияДатаСоздания;
			
			НоваяВерсия = РаботаСФайламиВызовСервера.СоздатьВерсию(НоваяВерсияВладелец, СведенияОФайле);
			
			УдалитьИзВременногоХранилища(АдресВременногоХранилищаФайла);	
						
			НоваяСтрока = СоответствиеИдентификатораВерсииФайлаСcылке.Добавить();
			НоваяСтрока.Идентификатор = Строка(Recordset.Fields("hDocument").Value) + "-" + Строка(Recordset.Fields("hSubVersion").Value);
			НоваяСтрока.Ссылка = НоваяВерсия.Ссылка;		
		Исключение
			// Если в структуре версий документа 1С:Архива 3.0 есть какие-то ошибки, то 
			// ошибочные версии не добавляются
			Инфо = ИнформацияОбОшибке();
			ДобавитьОшибку("Ошибка при добавлении версии:");
			ДобавитьОшибку("	Наименование версии: " + Строка(Recordset.Fields("SubVersionName").Value));
			ДобавитьОшибку("	Наименование файла версии: " + Строка(Recordset.Fields("SubVersionFile").Value));
			ДобавитьОшибку("	Том версии: " + Строка(Recordset.Fields("FileVolumePath").Value));
			ДобавитьОшибку("	Описание:" + Строка(Инфо.Описание));
			ДобавитьОшибку("	Модуль:" + Строка(Инфо.ИмяМодуля));
			ДобавитьОшибку("	Номер строки:" + Строка(Инфо.НомерСтроки));
		КонецПопытки;
		
		Recordset.MoveNext();

	КонецЦикла;
	Recordset.Close();
	
	// Устанавливаем владельца и родительскую версию
	Recordset.Open ( Запрос	, Соединение, 0, 1);
	Пока НЕ Recordset.EOF Цикл
		
		ИдентификаторВерсии = Строка(Recordset.Fields("hDocument").Value) + "-" + Строка(Recordset.Fields("hSubVersion").Value);
		НайденнаяСсылкаВерсии = СоответствиеИдентификатораВерсииФайлаСcылке.Найти(ИдентификаторВерсии, "Идентификатор"); 
		
		ИдентификаторФайла = Recordset.Fields("hDocument").Value;
		НайденнаяСсылкаФайла = СоответствиеИдентификатораФайлаСcылке.Найти(ИдентификаторФайла, "Идентификатор"); 
		
		ИдентификаторРодительскойВерсии = Строка(Recordset.Fields("hDocument").Value) + "-" + Строка(Recordset.Fields("hParentVersion").Value);
		НайденнаяСсылкаРодВерсии = СоответствиеИдентификатораВерсииФайлаСcылке.Найти(ИдентификаторРодительскойВерсии, "Идентификатор"); 
		
		Если НайденнаяСсылкаВерсии <> Неопределено Тогда
			
			Элемент = НайденнаяСсылкаВерсии.Ссылка.ПолучитьОбъект();
		
			Если НайденнаяСсылкаФайла <> Неопределено Тогда
				Элемент.Владелец = НайденнаяСсылкаФайла.Ссылка;
			КонецЕсли;
			
			Если НайденнаяСсылкаРодВерсии <> Неопределено Тогда
				Элемент.РодительскаяВерсия = НайденнаяСсылкаРодВерсии.Ссылка;
			КонецЕсли;	
			
			Элемент.Записать();
			
		КонецЕсли;	
	
		Recordset.MoveNext();
		
	КонецЦикла;
	
	Recordset.Close();
	
	//Дописывание ТекущаяВерсия в Файлы
	Запрос ="select
			| 	hDocument,
			|	hCurrentSubVersion
			| from
			|   DocumentInfo
			|";

	Recordset.Open ( Запрос	, Соединение, 0, 1);
	Пока НЕ Recordset.EOF Цикл
		
		Идентификатор = Recordset.Fields("hDocument").Value;
		НайденнаяСсылка = СоответствиеИдентификатораФайлаСcылке.Найти(Идентификатор, "Идентификатор"); 
		
		Если НайденнаяСсылка <> Неопределено Тогда
			
			Элемент = НайденнаяСсылка.Ссылка.ПолучитьОбъект();
			
			Если Элемент <> Неопределено Тогда
				Идентификатор = Строка(Recordset.Fields("hDocument").Value) + "-" + Строка(Recordset.Fields("hCurrentSubVersion").Value);
				НайденнаяСсылка = СоответствиеИдентификатораВерсииФайлаСcылке.Найти(Идентификатор, "Идентификатор"); 
				
				Если НайденнаяСсылка <> Неопределено Тогда
					Элемент.ТекущаяВерсия =  НайденнаяСсылка.Ссылка;
				
					ВерсияОбъект = Элемент.ТекущаяВерсия.ПолучитьОбъект();
					Элемент.ТекстХранилище = ВерсияОбъект.ТекстХранилище;
					
					Элемент.Записать();
				Иначе	
					ДобавитьОшибку("Ошибка связывания текущей версии с файлом - не найдена версия:" + Строка(Идентификатор));
				КонецЕсли;
			Иначе
				ДобавитьОшибку("Ошибка связывания текущей версии с файлом - объект файла не найден:" + Строка(НайденнаяСсылка));
			КонецЕсли;
		Иначе
			ДобавитьОшибку("Ошибка связывания текущей версии с файлом - файл не найден:" + Строка(Идентификатор));
		КонецЕсли;
		
		Recordset.MoveNext();
		
	КонецЦикла;
	
	Recordset.Close();
	
	Соединение.Close();
	
	СоответствиеИдентификатораВерсииФайлаСcылке = ПоместитьВоВременноеХранилище(СоответсвиеИдентификатораПапкиСсылке, ЭтаФорма.УникальныйИдентификатор);
	АдресСоответствиеИдентификатораФайлаСcылке = ПоместитьВоВременноеХранилище(СоответствиеИдентификатораФайлаСcылке, ЭтаФорма.УникальныйИдентификатор);
	
КонецПроцедуры

&НаСервере
Процедура ЗагрузкаВходящиеДокументы(МассивСвязей)
	
	Справочник = Справочники.ВходящиеДокументы;
	
	НеопределенногоВида = "Неопределен";
	
	СоответствиеИдентификатораДокументаСcылке = Новый ТаблицаЗначений;
	СоответствиеИдентификатораДокументаСcылке.Колонки.Добавить("Идентификатор");
	СоответствиеИдентификатораДокументаСcылке.Колонки.Добавить("Ссылка");
	
	СоответсвиеИдентификатораПользователяСсылке = Новый ТаблицаЗначений;
	СоответсвиеИдентификатораПользователяСсылке = ПолучитьИзВременногоХранилища(АдресСоответсвиеИдентификатораПользователяСсылке);
	
	
	Соединение = СоздатьСоединениеСБазойДанных();
	Recordset = Новый COMОбъект ("ADODB.Recordset");
	Запрос ="select
			| 	hDocument,
			| 	DocumentName,
			| 	Created,
			| 	DocDescr,
			| 	RegNum,
			| 	DocumentType,
			| 	Sender,
			| 	SenderDate,
			| 	SenderRegNum,
			|	ShortComment,
			|   MasterDocRegNum
			| from
			|   DocumentInfo
			| where
			|   Category = 1
			|";
	Recordset.Open ( Запрос	, Соединение, 0, 1);
	Пока НЕ Recordset.EOF Цикл
		НовыйДокумент = Справочники.ВходящиеДокументы.СоздатьЭлемент();
		НовыйДокумент.Заголовок = Recordset.Fields("DocumentName").Value;
		НовыйДокумент.ДатаСоздания = Recordset.Fields("Created").Value;
		НовыйДокумент.ДатаРегистрации = Recordset.Fields("Created").Value;
		
		НовыйДокумент.Содержание = Recordset.Fields("ShortComment").Value;
		РазмерМемо = Recordset.Fields("DocDescr").ActualSize;
		Если РазмерМемо > 0 Тогда
			НовыйДокумент.Содержание = Recordset.Fields("DocDescr").GetChunk(РазмерМемо);
		КонецЕсли;
		
		НовыйДокумент.РегистрационныйНомер = Recordset.Fields("RegNum").Value;
		ВидДокумента = Recordset.Fields("DocumentType").Value;
		Если ПустаяСтрока(ВидДокумента) Тогда
			ВидДокумента = НеопределенногоВида;
		КонецЕсли;
		НовыйДокумент.ВидДокумента = ПолучитьВидДокумента( 1,  ВидДокумента);
		
		ИмяКонтрагента = Recordset.Fields("Sender").Value;
		Если ПустаяСтрока(ИмяКонтрагента) Тогда
			ИмяКонтрагента = НеопределенногоВида;
		КонецЕсли;
		НовыйДокумент.Отправитель = ПолучитьКонтрагента(ИмяКонтрагента);
		
		НовыйДокумент.ИсходящаяДата = Recordset.Fields("SenderDate").Value;
		НовыйДокумент.ИсходящийНомер = Recordset.Fields("SenderRegNum").Value;
		
		НовыйДокумент.ГрифДоступа = ГрифВходящих;
		
		НовыйДокумент.Записать();
		
		Делопроизводство.ЗаписатьСостояниеДокумента(
			НовыйДокумент.Ссылка, 
			ТекущаяДата(), 
			Перечисления.СостоянияДокументов.Зарегистрирован, 
			Пользователи.ТекущийПользователь());
		
		НоваяСтрока = СоответствиеИдентификатораДокументаСcылке.Добавить();
		НоваяСтрока.Идентификатор = Recordset.Fields("hDocument").Value;
		НоваяСтрока.Ссылка = НовыйДокумент.Ссылка;		
		
		ВОтветНа = Recordset.Fields("MasterDocRegNum").Value;
		Если Не ПустаяСтрока(ВОтветНа) Тогда 
			СвязьДокументов = Новый Структура();
			СвязьДокументов.Вставить("Документ", НовыйДокумент.Ссылка);
			СвязьДокументов.Вставить("ТипДокумента", "Входящий");
			СвязьДокументов.Вставить("ВОтветНа", ВОтветНа);
			МассивСвязей.Добавить(СвязьДокументов);
		КонецЕсли;
		
		Recordset.MoveNext();
		
	КонецЦикла;
	Recordset.Close();

	Соединение.Close();
	
	АдресСоответствиеИдентификатораДокументаСcылке = ПоместитьВоВременноеХранилище(СоответствиеИдентификатораДокументаСcылке, ЭтаФорма.УникальныйИдентификатор);
	
КонецПроцедуры

&НаСервере
Процедура ЗагрузкаИсходящиеДокументы(МассивСвязей)
	
	Справочник = Справочники.ВходящиеДокументы;
	
	НеопределенногоВида = "Неопределен";
	
	СоответствиеИдентификатораДокументаСcылке = Новый ТаблицаЗначений;
	СоответствиеИдентификатораДокументаСcылке = ПолучитьИзВременногоХранилища(АдресСоответствиеИдентификатораДокументаСcылке);

	СоответсвиеИдентификатораПользователяСсылке = Новый ТаблицаЗначений;
	СоответсвиеИдентификатораПользователяСсылке = ПолучитьИзВременногоХранилища(АдресСоответсвиеИдентификатораПользователяСсылке);
	

	Соединение = СоздатьСоединениеСБазойДанных();
	Recordset = Новый COMОбъект ("ADODB.Recordset");
	Запрос ="select
			| 	hDocument,
			| 	hAuthor,
			| 	DocumentName,
			| 	Created,
			| 	DocDescr,
			| 	RegNum,
			| 	DocumentType,
			| 	Sender,
			| 	SenderDate,
			| 	SenderRegNum,
			|	ShortComment,
			|   MasterDocRegNum
			| from
			|   DocumentInfo
			| where
			|   Category = 2
			|";
	Recordset.Open ( Запрос	, Соединение, 0, 1);
	
	Пока НЕ Recordset.EOF Цикл
		НовыйДокумент = Справочники.ИсходящиеДокументы.СоздатьЭлемент();
		НовыйДокумент.Заголовок = Recordset.Fields("DocumentName").Value;
		
		Автор = Recordset.Fields("hAuthor").Value;
		НовыйДокумент.Зарегистрировал = СоответсвиеИдентификатораПользователяСсылке.Найти(Автор, "Идентификатор");
		
		НовыйДокумент.ДатаСоздания = Recordset.Fields("Created").Value;
		НовыйДокумент.ДатаРегистрации = Recordset.Fields("Created").Value;
		
		НовыйДокумент.Содержание = Recordset.Fields("ShortComment").Value;
		РазмерМемо = Recordset.Fields("DocDescr").ActualSize;
		Если РазмерМемо > 0 Тогда
			НовыйДокумент.Содержание = Recordset.Fields("DocDescr").GetChunk(РазмерМемо);
		КонецЕсли;
		
		НовыйДокумент.РегистрационныйНомер = Recordset.Fields("RegNum").Value;
		ВидДокумента = Recordset.Fields("DocumentType").Value;
		Если ПустаяСтрока(ВидДокумента) Тогда
			ВидДокумента = НеопределенногоВида;
		КонецЕсли;
		НовыйДокумент.ВидДокумента = ПолучитьВидДокумента( 2,  ВидДокумента);
		
		ИмяКонтрагента = Recordset.Fields("Sender").Value;
		Если ПустаяСтрока(ИмяКонтрагента) Тогда
			ИмяКонтрагента = НеопределенногоВида;
		КонецЕсли;
		
		НоваяСтрока = НовыйДокумент.Получатели.Добавить();

		НоваяСтрока.Получатель = ПолучитьКонтрагента(ИмяКонтрагента);
		НоваяСтрока.Отправлен = Истина;
		НоваяСтрока.ДатаОтправки = Recordset.Fields("Created").Value;
		
		НовыйДокумент.ГрифДоступа = ГрифИсходящих;
		НовыйДокумент.Записать();
		Делопроизводство.ЗаписатьСостояниеДокумента(
			НовыйДокумент.Ссылка, 
			ТекущаяДата(), 
			Перечисления.СостоянияДокументов.Зарегистрирован, 
			Пользователи.ТекущийПользователь());
		
		НоваяСтрока = СоответствиеИдентификатораДокументаСcылке.Добавить();
		НоваяСтрока.Идентификатор = Recordset.Fields("hDocument").Value;
		НоваяСтрока.Ссылка = НовыйДокумент.Ссылка;		
		
		ВОтветНа = Recordset.Fields("MasterDocRegNum").Value;
		Если Не ПустаяСтрока(ВОтветНа) Тогда
			СвязьДокументов = Новый Структура();
			СвязьДокументов.Вставить("Документ", НовыйДокумент.Ссылка);
			СвязьДокументов.Вставить("ТипДокумента", "Исходящий");
			СвязьДокументов.Вставить("ВОтветНа", ВОтветНа);
			МассивСвязей.Добавить(СвязьДокументов);
		КонецЕсли;
		
		Recordset.MoveNext();
		
	КонецЦикла;
	
	Recordset.Close();
	
	Соединение.Close();

КонецПроцедуры

&НаСервере
Процедура ЗагрузкаФайлы()
	
	Справочник = Справочники.Файлы;
	
	НеопределенногоВида = "Неопределен";
	
	СоответствиеИдентификатораДокументаСcылке = Новый ТаблицаЗначений;
	СоответствиеИдентификатораДокументаСcылке = ПолучитьИзВременногоХранилища(АдресСоответствиеИдентификатораДокументаСcылке);
	
	СоответствиеИдентификатораФайлаСcылке = Новый ТаблицаЗначений;
	СоответствиеИдентификатораФайлаСcылке.Колонки.Добавить("Идентификатор");
	СоответствиеИдентификатораФайлаСcылке.Колонки.Добавить("Ссылка");
	
	СоответсвиеИдентификатораПользователяСсылке = Новый ТаблицаЗначений;
	СоответсвиеИдентификатораПользователяСсылке = ПолучитьИзВременногоХранилища(АдресСоответсвиеИдентификатораПользователяСсылке);
	
	СоответсвиеИдентификатораПапкиСсылке = Новый ТаблицаЗначений;
	СоответсвиеИдентификатораПапкиСсылке = ПолучитьИзВременногоХранилища(АдресСоответсвиеИдентификатораПапкиСсылке);
	
	Соединение = СоздатьСоединениеСБазойДанных();
	Recordset = Новый COMОбъект ("ADODB.Recordset");
	Запрос ="select
			| 	hDocument,
			| 	DocumentName,
			| 	Created,
			| 	DocDescr,
			|	hCurrentSubVersion,
			|   hParentFolder,
			|   hAuthor,
			|   Category,
			|	ShortComment
			| from
			|   DocumentInfo
			|";
	Recordset.Open ( Запрос	, Соединение, 0, 1);
	Пока НЕ Recordset.EOF Цикл
		
		ТекущаяВерсия = Recordset.Fields("hCurrentSubVersion").Value;
		Если ТекущаяВерсия <> Null Тогда
			
			НовыйДокумент = Справочники.Файлы.СоздатьЭлемент();
			НовыйДокумент.ПолноеНаименование = Recordset.Fields("DocumentName").Value;
			НовыйДокумент.Наименование = Recordset.Fields("DocumentName").Value;
			НовыйДокумент.ДатаСоздания = Recordset.Fields("Created").Value;
			НовыйДокумент.Описание = Recordset.Fields("ShortComment").Value;
			РазмерМемо = Recordset.Fields("DocDescr").ActualSize;
			Если РазмерМемо > 0 Тогда
				НовыйДокумент.Описание = Recordset.Fields("DocDescr").GetChunk(РазмерМемо);
			КонецЕсли;
			
			КатегорияФайла = Recordset.Fields("Category").Value;
			Если КатегорияФайла = 1 ИЛИ КатегорияФайла = 2 Тогда // Входящий или Исходящий документ
				
				Идентификатор = Recordset.Fields("hDocument").Value;
				Если   Идентификатор <>  Null  Тогда
					НайденнаяСсылка = СоответствиеИдентификатораДокументаСcылке.Найти(Идентификатор, "Идентификатор"); 
					Если НайденнаяСсылка <> Null И НайденнаяСсылка <> Неопределено Тогда
						НовыйДокумент.ВладелецФайла = НайденнаяСсылка.Ссылка;
					КонецЕсли;
				КонецЕсли;	
				
			ИначеЕсли КатегорияФайла = 3 Тогда // Папка файлов	
				
				Идентификатор = Recordset.Fields("hParentFolder").Value;
				Если   Идентификатор <>  Null  Тогда
					НайденнаяСсылка = СоответсвиеИдентификатораПапкиСсылке.Найти(Идентификатор, "Идентификатор"); 
					Если НайденнаяСсылка <> Null И НайденнаяСсылка <> Неопределено Тогда
						НовыйДокумент.ВладелецФайла = НайденнаяСсылка.Ссылка;
					КонецЕсли;	
				КонецЕсли;
				
			КонецЕсли;	

			Идентификатор = Recordset.Fields("hAuthor").Value;
			НайденнаяСсылкаАвтор = СоответсвиеИдентификатораПользователяСсылке.Найти(Идентификатор,"Идентификатор"); 
			Если НайденнаяСсылкаАвтор <> Null И НайденнаяСсылкаАвтор <> Неопределено Тогда
				НовыйДокумент.Автор = НайденнаяСсылкаАвтор.Ссылка;
			Иначе	
				ДобавитьОшибку("Ошибка определения автора файла:" + Строка(Идентификатор));
				ДобавитьОшибку("	Наименование файла: " + Строка(Recordset.Fields("DocumentName").Value));
			КонецЕсли;	
			
			НовыйДокумент.ХранитьВерсии = Истина;
			
			Если Не ЗначениеЗаполнено(НовыйДокумент.ВладелецФайла) Тогда
				Папка = Справочники.ПапкиФайлов.НайтиПоНаименованию("Файлы без папки");
				Если Папка = Справочники.ПапкиФайлов.ПустаяСсылка() Тогда
					Папка = Справочники.ПапкиФайлов.СоздатьЭлемент();
					Папка.Наименование = "Файлы без папки";
					Папка.Записать();
				КонецЕсли;	
				
				НовыйДокумент.ВладелецФайла = Папка.Ссылка;
			КонецЕсли;		
			
			НовыйДокумент.Записать();
			
			НоваяСтрока = СоответствиеИдентификатораФайлаСcылке.Добавить();
			НоваяСтрока.Идентификатор = Recordset.Fields("hDocument").Value;
			НоваяСтрока.Ссылка = НовыйДокумент.Ссылка;
			
		КонецЕсли;
			
		Recordset.MoveNext();
		
	КонецЦикла;
	
	Recordset.Close();

	Соединение.Close();
	
	АдресСоответствиеИдентификатораФайлаСcылке = ПоместитьВоВременноеХранилище(СоответствиеИдентификатораФайлаСcылке, ЭтаФорма.УникальныйИдентификатор);
	
КонецПроцедуры

&НаСервере
Процедура  ЗагрузкаПапок()
	
	Справочник = Справочники.ПапкиФайлов;
	
	СоответствиеИдентификатораДокументаСcылке = Новый ТаблицаЗначений;
	СоответствиеИдентификатораДокументаСcылке = ПолучитьИзВременногоХранилища(АдресСоответствиеИдентификатораДокументаСcылке);
	
	СоответсвиеИдентификатораПользователяСсылке = Новый ТаблицаЗначений;
	СоответсвиеИдентификатораПользователяСсылке = ПолучитьИзВременногоХранилища(АдресСоответсвиеИдентификатораПользователяСсылке);
	
	СоответсвиеИдентификатораПапкиСсылке = Новый ТаблицаЗначений;
	СоответсвиеИдентификатораПапкиСсылке.Колонки.Добавить("Идентификатор");
	СоответсвиеИдентификатораПапкиСсылке.Колонки.Добавить("Ссылка");
	  	
	Соединение = СоздатьСоединениеСБазойДанных();
	Recordset = Новый COMОбъект ("ADODB.Recordset");
	Запрос ="select
			| 	hFolder,
			| 	Name,
			| 	hParentFolder,
			| 	FolderDescr,
			|	hAuthor,
			| 	CreationDate
			|from
			|   FolderInfo
			|";
	Recordset.Open ( Запрос	, Соединение, 0, 1);
	Пока НЕ Recordset.EOF Цикл
		НовыйДокумент = Справочник.СоздатьЭлемент();
		НовыйДокумент.Описание = Recordset.Fields("FolderDescr").Value;
		НовыйДокумент.Наименование = Recordset.Fields("Name").Value;
		НовыйДокумент.ДатаСоздания = Recordset.Fields("CreationDate").Value;
		
		Идентификатор = Recordset.Fields("hAuthor").Value;
		Если Идентификатор <> Null Тогда
			НайденнаяСсылка = СоответсвиеИдентификатораПользователяСсылке.Найти(Идентификатор, "Идентификатор"); 
			Если НайденнаяСсылка <> Неопределено Тогда
				НовыйДокумент.Ответственный = НайденнаяСсылка.Ссылка;
			КонецЕсли;	
		КонецЕсли;
		
		НовыйДокумент.Записать();
		
		НоваяСтрока = СоответсвиеИдентификатораПапкиСсылке.Добавить();
		НоваяСтрока.Идентификатор = Recordset.Fields("hFolder").Value;
		НоваяСтрока.Ссылка = НовыйДокумент.Ссылка;
	
		Recordset.MoveNext();
	КонецЦикла;
	
	Recordset.Close();
	
	Recordset.Open ( Запрос	, Соединение, 0, 1);
	Пока НЕ Recordset.EOF Цикл
		
		Наименование = Recordset.Fields("Name").Value;
		Идентификатор = Recordset.Fields("hFolder").Value;
		НайденнаяСсылка = СоответсвиеИдентификатораПапкиСсылке.Найти(Идентификатор, "Идентификатор"); 
				
		ИдентификаторРодитель = Recordset.Fields("hParentFolder").Value;
		НайденнаяСсылкаРодитель = СоответсвиеИдентификатораПапкиСсылке.Найти(ИдентификаторРодитель, "Идентификатор"); 
		Если НайденнаяСсылкаРодитель <> Неопределено Тогда
			Элемент = НайденнаяСсылка.Ссылка.ПолучитьОбъект();
			Элемент.Родитель = НайденнаяСсылкаРодитель.Ссылка;
			Элемент.Записать();
		КонецЕсли;
	
		Recordset.MoveNext();
	КонецЦикла;
	
	Recordset.Close();
	
	Соединение.Close();
	
	АдресСоответсвиеИдентификатораПапкиСсылке = ПоместитьВоВременноеХранилище(СоответсвиеИдентификатораПапкиСсылке, ЭтаФорма.УникальныйИдентификатор);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьПользователей(Команда)
	ЗагрузкаПользователей();
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьВходящиеДокументы(Команда)
	МассивСвязей = Новый Массив();
	ЗагрузкаВходящиеДокументы(МассивСвязей);
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьИсходящиеДокументы(Команда)
	МассивСвязей = Новый Массив();
	ЗагрузкаИсходящиеДокументы(МассивСвязей);
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьПодверсии(Команда)
	ЗагрузкаПодверсии();
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьФайлы(Команда)
	ЗагрузкаФайлы();
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьПапки(Команда)
	ЗагрузкаПапок();
КонецПроцедуры

&НаСервере
Процедура ЗагрузкаРабочиеКаталогиФайлов()
	
	СоответсвиеИдентификатораПапкиСсылке = Новый ТаблицаЗначений;
	СоответсвиеИдентификатораПапкиСсылке = ПолучитьИзВременногоХранилища(АдресСоответсвиеИдентификатораПапкиСсылке);

	СоответсвиеИдентификатораПользователяСсылке = Новый ТаблицаЗначений;
	СоответсвиеИдентификатораПользователяСсылке = ПолучитьИзВременногоХранилища(АдресСоответсвиеИдентификатораПользователяСсылке);

	Соединение = СоздатьСоединениеСБазойДанных();
	Recordset = Новый COMОбъект ("ADODB.Recordset");
	Запрос ="select
			| 	hFolder,
			| 	hUser,
			| 	path
			| from
			|   FolderWorkingDir";
			
	Recordset.Open ( Запрос	, Соединение, 0, 1);
	Пока НЕ Recordset.EOF Цикл
		
		Идентификатор = Recordset.Fields("hFolder").Value;
		СсылкаПапки = СоответсвиеИдентификатораПапкиСсылке.Найти(Идентификатор, "Идентификатор"); 
		Если СсылкаПапки <> Неопределено Тогда
			Идентификатор = Recordset.Fields("hUser").Value;
			НайденнаяСсылка = СоответсвиеИдентификатораПользователяСсылке.Найти(Идентификатор, "Идентификатор"); 
			Если НайденнаяСсылка <> Неопределено Тогда
				
				Попытка
					РабочийКаталог = Recordset.Fields("path").Value;
					РаботаСФайламиВызовСервера.СохранитьРабочийКаталог(СсылкаПапки, РабочийКаталог);
				Исключение
					// Такая запись уже существует
				КонецПопытки;
				
			КонецЕсли;	
		КонецЕсли;
		
		Recordset.MoveNext();
		
	КонецЦикла;
	
    Recordset.Close();
	Соединение.Close();
	
КонецПроцедуры

&НаКлиенте
Процедура ФорматБазыДанныхПриИзменении(Элемент)
	УстановитьВидимостьПолей();
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьПолей()
	
	Если ФорматБазыДанных = 0 Тогда
		Элементы.ИмяSQLСервера.Видимость = Ложь;
		Элементы.ИмяSQLСервера.АвтоОтметкаНезаполненного = Ложь;
		Элементы.ИмяБазы.Видимость = Ложь;
		Элементы.ИмяБазы.АвтоОтметкаНезаполненного = Ложь;
		Элементы.ИмяПользователя.Видимость = Ложь;
		Элементы.ИмяПользователя.АвтоОтметкаНезаполненного = Ложь;
		Элементы.Пароль.Видимость = Ложь;
		Элементы.ПутьКФайлуMDB.Видимость = Истина;
		Элементы.ПутьКФайлуMDB.АвтоОтметкаНезаполненного = Истина;
	Иначе	
		Элементы.ИмяSQLСервера.Видимость = Истина;
		Элементы.ИмяSQLСервера.АвтоОтметкаНезаполненного = Истина;
		Элементы.ИмяБазы.Видимость = Истина;
		Элементы.ИмяБазы.АвтоОтметкаНезаполненного = Истина;
		Элементы.ИмяПользователя.Видимость = Истина;
		Элементы.ИмяПользователя.АвтоОтметкаНезаполненного = Истина;
		Элементы.Пароль.Видимость = Истина;
		Элементы.ПутьКФайлуMDB.Видимость = Ложь;
		Элементы.ПутьКФайлуMDB.АвтоОтметкаНезаполненного = Ложь;
	КонецЕсли;	
	
КонецПроцедуры

Функция НайтиИсходящийПоРегНомеру(РегНомер)
	
	Возврат Справочники.ИсходящиеДокументы.НайтиПоРеквизиту("РегистрационныйНомер", РегНомер);
	
КонецФункции	

Функция НайтиВходящийПоРегНомеру(РегНомер)
	
	Возврат Справочники.ВходящиеДокументы.НайтиПоРеквизиту("РегистрационныйНомер", РегНомер);
	
КонецФункции	

&НаСервере
Процедура УстановитьСвязи(МассивСвязей)
	
	Для каждого Связь Из МассивСвязей Цикл
		
		Документ = Связь.Документ;
		
		Если Связь.ТипДокумента = "Входящий" Тогда
			ВОтветНа = НайтиИсходящийПоРегНомеру(Связь.ВОтветНа);
			ТипСвязи = Справочники.ТипыСвязей.ПолученВОтветНа;
		Иначе	
			ВОтветНа = НайтиВходящийПоРегНомеру(Связь.ВОтветНа);
			ТипСвязи = Справочники.ТипыСвязей.ОтправленВОтветНа;
		КонецЕсли;	
		
		Если ЗначениеЗаполнено(ВОтветНа) И 
			(ТипЗнч(ВОтветНа) = Тип("СправочникСсылка.ВходящиеДокументы") 
			ИЛИ ТипЗнч(ВОтветНа) = Тип("СправочникСсылка.ИсходящиеДокументы")) Тогда
			СвязиДокументов.СоздатьСвязь(Документ, ВОтветНа, ТипСвязи);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры	

&НаКлиенте
Процедура ЗагрузитьДанные(Команда)
	
	ОчиститьСообщения();
	Если Не ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;	
	
	Состояние(НСтр("ru = 'Выполняется загрузка пользователей'; en = 'Users are being imported'"));
	ЗагрузкаПользователей();
	
	МассивСвязей = Новый Массив();
	Состояние(НСтр("ru = 'Выполняется загрузка входящих документов'; en = 'Incoming documents are being imported'"));
	ЗагрузкаВходящиеДокументы(МассивСвязей);
	Состояние(НСтр("ru = 'Выполняется загрузка исходящих документов'; en = 'Outgoing documents are being imported'"));
	ЗагрузкаИсходящиеДокументы(МассивСвязей);	
	
	Состояние(НСтр("ru = 'Выполняется установка связей между входящими и исходящими документами'; en = 'Setting up relations between incoming and outgoing documents'"));
	УстановитьСвязи(МассивСвязей);
	
	Состояние(НСтр("ru = 'Выполняется загрузка папок'; en = 'Folders are being imported'"));
	ЗагрузкаПапок();
	
	Состояние(НСтр("ru = 'Выполняется загрузка файлов'; en = 'Importing files'"));
	ЗагрузкаФайлы();
	
	Состояние(НСтр("ru = 'Выполняется загрузка версий файлов'; en = 'File versions are being imported'"));
	ЗагрузкаПодверсии();
	
	Состояние(НСтр("ru = 'Выполняется загрузка рабочих каталогов пользователей'; en = 'Importing users working directories'"));
	ЗагрузкаРабочиеКаталогиФайлов();
	
	Если СтрДлина(Ошибки) = 0 Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Загрузка данных из 1С:Архива 3.0 успешно завершена!'; en = 'Importing data from 1C:Archive 3.0 successfully completed!'"));
	Иначе	
		ПараметрыФормыПротокола = Новый Структура("Текст", Ошибки);
		
		ОткрытьФорму("Обработка.ЗагрузкаДанныхИз1САрхива30.Форма.ФормаПротокола",
			ПараметрыФормыПротокола, ЭтаФорма,,,,, РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс);
		
	КонецЕсли;	
	
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура ПутьКФайлуMDBНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ДиалогВыбораФайла =	Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	ДиалогВыбораФайла.Заголовок = "Выбор файла базы данных";
	ДиалогВыбораФайла.Фильтр = "Файлы MS Access (MDB)|*.mdb";
	
	Если ДиалогВыбораФайла.Выбрать() Тогда
		ФайлMDB = ДиалогВыбораФайла.ПолноеИмяФайла;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьВидимостьПолей();
	
КонецПроцедуры

&НаСервере
Функция РасширениеБезТочки(стрРасширение)
	Расширение = НРег(СокрЛП(стрРасширение));
	Если Сред(Расширение, 1, 1) = "." Тогда
		Расширение = Сред(Расширение, 2);
	КонецЕсли;
	Возврат Расширение;
КонецФункции // РасширениеБезТочки()

&НаСервере
Процедура Добавитьошибку(Текст)
	
	Ошибки = Ошибки + Текст + Символы.ВК;
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	Если ФорматБазыДанных = 0 Тогда
		// MS Access
		ПроверяемыеРеквизиты.Добавить("ФайлMDB");
	Иначе
		// SQL Сервер
		ПроверяемыеРеквизиты.Добавить("ИмяSQLСервера");
		ПроверяемыеРеквизиты.Добавить("ИмяБазы");
		ПроверяемыеРеквизиты.Добавить("ИмяПользователяБазыДанных");
	КонецЕсли;	
	
	ПроверяемыеРеквизиты.Добавить("КаталогСервера");
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьГрифыДоступа") Тогда
		ПроверяемыеРеквизиты.Удалить(ПроверяемыеРеквизиты.найти("ГрифВходящих"));
		ПроверяемыеРеквизиты.Удалить(ПроверяемыеРеквизиты.найти("ГрифИсходящих"));
	КонецЕсли;	
	
КонецПроцедуры


