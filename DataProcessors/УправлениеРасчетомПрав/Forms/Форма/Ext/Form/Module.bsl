
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ОтложенноеОбновлениеПрав = 
		Константы.ДокументооборотИспользоватьОтложенноеОбновлениеПравДоступа.Получить();
	
	Если ОтложенноеОбновлениеПрав Тогда
		ОбновитьСведенияОбОчереди();
		ОбновитьОтчетОбОчереди();
	КонецЕсли;
	
	Элементы.ГруппаОчередь.Видимость = ОтложенноеОбновлениеПрав;
	Элементы.ГруппаЗамерСкорости.Видимость = Элементы.ГруппаЗамерСкорости.Видимость И ОтложенноеОбновлениеПрав;
	Элементы.ОтчетыПоОчередиОбновления.Видимость = ОтложенноеОбновлениеПрав;
	
	ТекущийЭлемент = Элементы.НастройкиСсылка;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьСведенияОбОчереди()
	
	РазмерОчереди1 = РегистрыСведений.ОчередьОбновленияПравДоступа.РазмерОчереди(1);
	РазмерОчереди2 = РегистрыСведений.ОчередьОбновленияПравДоступа.РазмерОчереди(2);
	
	СведенияОбОперативнойОчереди = Новый ФорматированнаяСтрока("",
		Новый ФорматированнаяСтрока(НСтр("ru = 'Оперативная'; en = 'Rapid'"),, Новый Цвет(60, 100, 190)),
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = ' очередь обновления: %1 заданий'; en = ' update queue: %1 tasks'"), РазмерОчереди1));
	
	СведенияОДолгойОчереди = Новый ФорматированнаяСтрока("",
		Новый ФорматированнаяСтрока(НСтр("ru = 'Долгая'; en = 'Slow'"),, Новый Цвет(70, 150, 70)),
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = ' очередь обновления: %1 заданий'; en = ' update queue: %1 tasks'"), РазмерОчереди2));
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьОтчетОбОчереди()
	
	КоличествоТочек = 201;
	
	РазмерДолгойОчереди = Справочники.Метрики.ДолгаяОчередь;
	РазмерОперативнойОчереди = Справочники.Метрики.ОперативнаяОчередь;
	
	ЦветаСерий = Новый Соответствие;
	ЦветаСерий.Вставить(РазмерДолгойОчереди, Новый Цвет(70, 150, 70));
	ЦветаСерий.Вставить(РазмерОперативнойОчереди, Новый Цвет(60, 100, 190));
	
	ВыбранныеПоказатели = Новый Массив;
	ВыбранныеПоказатели.Добавить(РазмерДолгойОчереди);
	ВыбранныеПоказатели.Добавить(РазмерОперативнойОчереди);
	
	ТекущаяДатаСеанса = ТекущаяДатаСеанса();
	ЗначенияПоказателей = Метрики.ПолучитьДанныеДляВыводаОтчета(
		ТекущаяДатаСеанса - 24*3600, ТекущаяДатаСеанса, ВыбранныеПоказатели, КоличествоТочек, Ложь);
	
	Диаграмма.Очистить();
	Диаграмма.Обновление = Ложь;
	Диаграмма.ОтображатьЛегенду = Ложь;
	Диаграмма.ОбластьПостроения.ОтображатьЛинииЗначенийШкалы = Ложь;
	Диаграмма.ОбластьПостроения.ОриентацияМеток = ОриентацияМетокДиаграммы.Горизонтально;
	
	Диаграмма.ОбластьПостроения.ОтображатьПодписиШкалыТочек = Ложь;
	
	Для Каждого Стр Из ЗначенияПоказателей Цикл
		
		Точка = Диаграмма.УстановитьТочку(Стр.ОтображаемаяДата);
		ИндексТочки = Диаграмма.Точки.Индекс(Точка);
		Точка.Текст = "";
		
		Серия = Диаграмма.УстановитьСерию(Стр.Показатель);
		Серия.Маркер = ТипМаркераДиаграммы.Нет;
		Серия.Цвет = ЦветаСерий.Получить(Стр.Показатель);
		
		Диаграмма.УстановитьЗначение(Точка, Серия, 
			Окр(Стр.Результат),, "" + Стр.Результат * Стр.Коэффициент);
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьСведенияОбОчередиКлиент(Элемент)
	
	ОбновитьСведенияОбОчереди();
	ОбновитьОтчетОбОчереди();
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьОчередь(Элемент)
	
	ОткрытьФорму("РегистрСведений.ОчередьОбновленияПравДоступа.Форма.ФормаСписка");
	
КонецПроцедуры

&НаКлиенте
Процедура НастройкиНажатие(Элемент)
	
	ОткрытьФорму("Обработка.НастройкаПрограммы.Форма.ПраваДоступа",,
		ЭтаФорма,,,,, РежимОткрытияОкнаФормы.Независимый);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновлениеВручнуюНажатие(Элемент)
	
	ОткрытьФорму("Обработка.ОбновлениеПравДоступа.Форма.Форма");
	
КонецПроцедуры

&НаКлиенте
Процедура ЗамерСкоростиНажатие(Элемент)
	
	ОткрытьФорму("Обработка.АнализВремениОбработкиОчереди.Форма");
	
КонецПроцедуры

&НаКлиенте
Процедура ОтчетыДинамикаОчередиНажатие(Элемент)
	
	ПоказателиРазмераОчереди = Новый СписокЗначений;
	ПоказателиРазмераОчереди.Добавить(
		ПредопределенноеЗначение("Справочник.Метрики.ДолгаяОчередь"));
	ПоказателиРазмераОчереди.Добавить(
		ПредопределенноеЗначение("Справочник.Метрики.ОперативнаяОчередь"));
	
	ПараметрыОткрытия = Новый Структура("ВыбранныеПоказатели, Период",
		ПоказателиРазмераОчереди,
		Новый СтандартныйПериод(ВариантСтандартногоПериода.Последние7Дней));
	
	ОткрытьФорму("Отчет.Метрики.Форма", ПараметрыОткрытия);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтчетыСоставОчередиНажатие(Элемент)
	
	ОткрытьФорму("Отчет.СоставОчередиОбновленияПрав.Форма");
	
КонецПроцедуры

&НаКлиенте
Процедура ДескрипторыДоступаНажатие(Элемент)
	
	ОткрытьФорму("Отчет.ДескрипторыДоступа.Форма");
	
КонецПроцедуры

&НаКлиенте
Процедура СведенияОбИБНажатие(Элемент)
	
	ОткрытьФорму("Обработка.СведенияОбИнформационнойБазе.Форма");
	
КонецПроцедуры


