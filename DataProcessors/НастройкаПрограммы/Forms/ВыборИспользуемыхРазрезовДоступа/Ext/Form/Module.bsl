
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ТекущийСписокРазрезов = Константы.ИспользованиеРазрезовДоступа.Получить().Получить();
	НеАктуальныеРазрезы = ДокументооборотПраваДоступаПовтИсп.ОтключенныеРазрезыДоступа(Истина, Ложь);
	
	Для Каждого Эл Из ТекущийСписокРазрезов Цикл
		
		Если НеАктуальныеРазрезы.Найти(Эл.Значение) <> Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		ИспользуемыеРазрезыДоступа.Добавить(Эл.Значение, Эл.Представление, Эл.Пометка);
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура Готово(Команда)
	
	Лимит = 10000;
	КоличествоОбъектовКОбработке = КоличествоОбъектовКОбработке();
	
	Если КоличествоОбъектовКОбработке > Лимит Тогда
		Оповещение = Новый ОписаниеОповещения("ГотовоПродолжить", ЭтотОбъект);
		ТекстВопроса = СтрШаблон(
			НСтр("ru = 'После принятия изменений будет обработано %1 объектов. Продолжить?'; en = 'After the modification %1 objects will be processed. Proceed?'"),
			КоличествоОбъектовКОбработке);
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
	Иначе
		ГотовоПродолжить(КодВозвратаДиалога.Да, Неопределено);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ГотовоПродолжить(Результат, Параметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		ЗаписатьНастройкиНаСервере();
		Закрыть(Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция КоличествоОбъектовКОбработке()
	
	Результат = 0;
	
	ИзмененныеРазрезы = Новый Массив;
	СтарыйСписок = Константы.ИспользованиеРазрезовДоступа.Получить().Получить();
	НовыйСписок = ИспользуемыеРазрезыДоступа;
	
	Для Каждого ЭлСтарогоСписка Из СтарыйСписок Цикл
		ТекущийРазрез = ЭлСтарогоСписка.Значение;
		ЭлНовогоСписка = НовыйСписок.НайтиПоЗначению(ТекущийРазрез);
		Если ЭлНовогоСписка = Неопределено Или ЭлСтарогоСписка.Пометка <> ЭлНовогоСписка.Пометка Тогда
			ИзмененныеРазрезы.Добавить(ТекущийРазрез);
		КонецЕсли;
	КонецЦикла;
	
	ПредметыДоступаСРазрезами = ДокументооборотПраваДоступаПереопределяемый.ПредметыДоступаСРазрезами();
	Для Каждого СтрПредмета Из ПредметыДоступаСРазрезами Цикл
		Для Каждого Разрез Из ИзмененныеРазрезы Цикл
			Если СтрПредмета.ВидыДоступа.НайтиПоЗначению(Разрез) <> Неопределено Тогда
				
				ИмяТаблицы = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СтрПредмета.ОбъектМетаданных, "ПолноеИмя");
				
				Запрос = Новый Запрос;
				Запрос.Текст = "ВЫБРАТЬ КОЛИЧЕСТВО(*) КАК Количество ИЗ " + ИмяТаблицы;
				Выборка = Запрос.Выполнить().Выбрать();
				Выборка.Следующий();
				
				Результат = Результат + Число(Выборка.Количество);
				
				Прервать;
				
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Процедура ЗаписатьНастройкиНаСервере()
	
	ТекущийСписокРазрезов = Константы.ИспользованиеРазрезовДоступа.Получить().Получить();
	Для Каждого Эл Из ИспользуемыеРазрезыДоступа Цикл
		НайденныйЭлемент = ТекущийСписокРазрезов.НайтиПоЗначению(Эл.Значение);
		Если НайденныйЭлемент <> Неопределено Тогда
			НайденныйЭлемент.Пометка = Эл.Пометка;
		КонецЕсли;
	КонецЦикла;
	
	Константы.ИспользованиеРазрезовДоступа.Установить(Новый ХранилищеЗначения(ТекущийСписокРазрезов));
	
КонецПроцедуры


