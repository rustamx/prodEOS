#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	РежимРаботы = ОбщегоНазначенияПовтИсп.РежимРаботыПрограммы();
	РежимРаботы = Новый ФиксированнаяСтруктура(РежимРаботы);
	
	КаталогВременныхФайлов = СформироватьПутьWindowsLinux(
		НаборКонстант.КаталогСообщенийОбменаДаннымиДляWindows, 
		НаборКонстант.КаталогСообщенийОбменаДаннымиДляLinux);
	
	ЭтоФайловаяБаза = ОбщегоНазначения.ИнформационнаяБазаФайловая();
	
	ОбновитьЗаголовокНастройкиИспользуемыхРазрезов();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	УстановитьДоступность();
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Если ТекущийОбъект.ДокументооборотИспользоватьОграничениеПравДоступа 
		И ТекущийОбъект.ДокументооборотИспользоватьОграничениеПравДоступа <> Константы.ДокументооборотИспользоватьОграничениеПравДоступа.Получить() Тогда
		
		ПоказатьВопросПриИзмененииОграниченияДоступаНаУровнеЗаписей = Истина;
		
	Иначе
		
		ПоказатьВопросПриИзмененииОграниченияДоступаНаУровнеЗаписей = Ложь;
		
	КонецЕсли;
	
	// Ограничение прав доступа
	ТекущийОбъект.ОграничиватьДоступНаУровнеЗаписей = ТекущийОбъект.ДокументооборотИспользоватьОграничениеПравДоступа;
	
	Если НаборКонстант.ГрупповойРасчетПравДоступаРазмерПорции = 0 Тогда
		ТекущийОбъект.ГрупповойРасчетПравДоступаРазмерПорции = 100;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	ОбновитьПовторноИспользуемыеЗначения();
	ОбновитьИнтерфейс();
	
	Если ПоказатьВопросПриИзмененииОграниченияДоступаНаУровнеЗаписей Тогда
		Если Не ПараметрыЗаписи.Свойство("ПоказанВопросОПересчетеПрав") Тогда
			
			ОписаниеОповещения = Новый ОписаниеОповещения(
				"ОбработкаОтветаНаВопросОПересчетеПрав",
				ЭтотОбъект,
				ПараметрыЗаписи);
			
			ПараметрыВопроса = Новый Структура("ОтложенноеОбновление", 
				НаборКонстант.ДокументооборотИспользоватьОтложенноеОбновлениеПравДоступа);
			
			ОткрытьФорму("Обработка.НастройкаПрограммы.Форма.ВопросОПолномПересчетеПрав", ПараметрыВопроса,
				ЭтаФорма,,,, ОписаниеОповещения, РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс);
			
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	Если ПараметрыЗаписи.Свойство("Закрыть") Тогда
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОтветаНаВопросОПересчетеПрав(Результат, ПараметрыЗаписи) Экспорт
	
	Если Результат = Истина Тогда
		ДокументооборотПраваДоступаКлиент.ОбновитьВсеПрава(
			НаборКонстант.ДокументооборотИспользоватьОтложенноеОбновлениеПравДоступа);
	КонецЕсли;
	
	ПараметрыЗаписи.Вставить("ПоказанВопросОПересчетеПрав", Истина);
	ПослеЗаписи(ПараметрыЗаписи);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ДокументооборотИспользоватьОграничениеПравДоступаПриИзменении(Элемент)
	
	Если НаборКонстант.ДокументооборотИспользоватьОграничениеПравДоступа Тогда
		Если Не ЭтоФайловаяБаза Тогда
			НаборКонстант.ДокументооборотИспользоватьОтложенноеОбновлениеПравДоступа = Истина;
		КонецЕсли;	
	КонецЕсли;
	
	УстановитьДоступность();
	
КонецПроцедуры

&НаКлиенте
Процедура ДокументооборотИспользоватьОтложенноеОбновлениеПравДоступаПриИзменении(Элемент)
	
	ВидимостьПредупрежденияОбОтложенномОбновлении = Ложь;
	
	Если НаборКонстант.ДокументооборотИспользоватьОтложенноеОбновлениеПравДоступа Тогда
		Если ЭтоФайловаяБаза Тогда
			ВидимостьПредупрежденияОбОтложенномОбновлении = Истина;
		КонецЕсли;
	КонецЕсли;
	
	Если Элементы.ПредупреждениеОбОтложенномОбновлении.Видимость <> ВидимостьПредупрежденияОбОтложенномОбновлении Тогда
		Элементы.ПредупреждениеОбОтложенномОбновлении.Видимость = ВидимостьПредупрежденияОбОтложенномОбновлении;
	КонецЕсли;

	УстановитьДоступность();
	
КонецПроцедуры

&НаКлиенте
Процедура ИсключенияВПолитикахДоступаПриИзменении(Элемент)
	
	Если Не НаборКонстант.СпецРазрешенияВПолитикахДоступа И ЕстьСпецРазрешенияДоступа() Тогда
		Оповещение = Новый ОписаниеОповещения("ИсключенияВПолитикахДоступаПриИзмененииПродолжение", ЭтотОбъект);
		ТекстВопроса = НСтр("ru = 'Все введенные исключения будут удалены. Продолжить?'; en = 'All exceptions entered will be deleted. Continue?'");
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет,, КодВозвратаДиалога.Нет);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ИсключенияВПолитикахДоступаПриИзмененииПродолжение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> КодВозвратаДиалога.Да Тогда
		НаборКонстант.СпецРазрешенияВПолитикахДоступа = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьЛокальныхАдминистраторовПриИзменении(Элемент)
	
	Если Не НаборКонстант.ИспользоватьЛокальныхАдминистраторов И ЕстьРазрешенияДоступаДляЛА() Тогда
		Оповещение = Новый ОписаниеОповещения(
			"ИспользоватьЛокальныхАдминистраторовПриИзмененииПродолжение", ЭтотОбъект);
		ТекстВопроса = НСтр("ru = 'Права локальных администраторов будут удалены. Продолжить?'; en = 'Local administrators permissions will be deleted. Proceed?'");
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет,, КодВозвратаДиалога.Нет);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьЛокальныхАдминистраторовПриИзмененииПродолжение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> КодВозвратаДиалога.Да Тогда
		НаборКонстант.ИспользоватьЛокальныхАдминистраторов = Истина;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ИспользоватьПротоколированиеРаботыПользователейПриИзменении(Элемент)
	
	Если Не НаборКонстант.ИспользоватьПротоколированиеРаботыПользователей Тогда
		Элементы.СрокХраненияПротоколаРаботыПользователей.Доступность = Ложь;
		Элементы.КаталогСохраненияКопииПротоколаРаботыПользователей.Доступность = Ложь;
	Иначе
		Элементы.СрокХраненияПротоколаРаботыПользователей.Доступность = Истина;
		Элементы.КаталогСохраненияКопииПротоколаРаботыПользователей.Доступность = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КаталогСохраненияКопииПротоколаРаботыПользователейНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыФормы = Новый Структура("Заголовок, ПолныйПутьWindows, ПолныйПутьLinux",
		НСтр("ru = 'Путь сохранения копии протокола работы пользователей'; en = 'Path to save copy of user activity log'"),
		НаборКонстант.КаталогСохраненияКопииПротоколаРаботыПользователейWindows,
		НаборКонстант.КаталогСохраненияКопииПротоколаРаботыПользователейLinux);
		
	ОписаниеОповещения = Новый ОписаниеОповещения(
		"КаталогСохраненияКопииПротоколаРаботыПользователейНачалоВыбораПродолжение",
		ЭтотОбъект);
	ОткрытьФорму("ОбщаяФорма.ВыборПутиWindowsLinux", ПараметрыФормы,,,,,
		ОписаниеОповещения, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура КаталогСохраненияКопииПротоколаРаботыПользователейНачалоВыбораПродолжение(Результат, Параметры)Экспорт 
	
	Если ТипЗнч(Результат) = Тип("Структура") Тогда
		
		НаборКонстант.КаталогСохраненияКопииПротоколаРаботыПользователейWindows = Результат.ПолныйПутьWindows;
		НаборКонстант.КаталогСохраненияКопииПротоколаРаботыПользователейLinux = Результат.ПолныйПутьLinux;
		
		КаталогСохраненияКопииПротоколаРаботыПользователей = СформироватьПутьWindowsLinux(
			НаборКонстант.КаталогСохраненияКопииПротоколаРаботыПользователейWindows, 
			НаборКонстант.КаталогСохраненияКопииПротоколаРаботыПользователейLinux);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КаталогСохраненияКопииПротоколаРаботыПользователейОчистка(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура НастройкиВходаПользователей(Команда)
	
	ОткрытьФорму("ОбщаяФорма.НастройкиВходаПользователей", , ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ИспользованиеРазрезовДоступа(Команда)
	
	ОповещениеОЗакрытии = Новый ОписаниеОповещения("ИспользованиеРазрезовДоступаОкончание", ЭтотОбъект);
	ОткрытьФорму("Обработка.НастройкаПрограммы.Форма.ВыборИспользуемыхРазрезовДоступа",,
		ЭтаФорма,,,, ОповещениеОЗакрытии);
	
КонецПроцедуры

&НаКлиенте
Процедура ИспользованиеРазрезовДоступаОкончание(Результат, Параметры) Экспорт
	
	Если Результат = Истина Тогда
		ОбновитьЗаголовокНастройкиИспользуемыхРазрезов();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьПолитикиДоступа(Команда)
	
	ОткрытьФорму("Обработка.ПолитикиДоступа.Форма");
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура УстановитьДоступность()
	
	ИспользоватьОграничениеПрав = НаборКонстант.ДокументооборотИспользоватьОграничениеПравДоступа;
	ОтложенноеОбновление = НаборКонстант.ДокументооборотИспользоватьОтложенноеОбновлениеПравДоступа;
	
	Элементы.ДокументооборотИспользоватьОтложенноеОбновлениеПравДоступа.Доступность = ИспользоватьОграничениеПрав;
	Элементы.ГрупповойРасчетПравДоступаРазмерПорции.Доступность = ИспользоватьОграничениеПрав И ОтложенноеОбновление;
	Элементы.ДобавлятьРуководителямДоступПодчиненных.Доступность = ИспользоватьОграничениеПрав;
	Элементы.ПроверятьСоответствиеРабочихГруппОбщимНастройкам.Доступность = ИспользоватьОграничениеПрав;
	Элементы.ГруппаНастройкиПолитикДоступа.Доступность = ИспользоватьОграничениеПрав;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ЕстьСпецРазрешенияДоступа()
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ИСТИНА КАК ЕстьЗаписи
		|ИЗ
		|	РегистрСведений.РазрешенияДоступаИсключительные КАК РазрешенияДоступаИсключительные");
		
	ЕстьЗаписи = Не Запрос.Выполнить().Пустой();
	
	Возврат ЕстьЗаписи;
	
КонецФункции

&НаСервереБезКонтекста
Функция ЕстьРазрешенияДоступаДляЛА()
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ИСТИНА КАК ЕстьЗаписи
		|ИЗ
		|	РегистрСведений.РазрешенияДляЛокальныхАдминистраторов КАК РазрешенияДляЛокальныхАдминистраторов");
		
	ЕстьЗаписи = Не Запрос.Выполнить().Пустой();
	
	Возврат ЕстьЗаписи;
	
КонецФункции

Функция СформироватьПутьWindowsLinux(ПолныйПутьWindows, ПолныйПутьLinux)
	
	СтрокаПути = "";
	
	Если ЗначениеЗаполнено(ПолныйПутьWindows) Тогда
		СтрокаПути = СтрокаПути + ПолныйПутьWindows;
	КонецЕсли;	
	
	Если ЗначениеЗаполнено(ПолныйПутьLinux) Тогда
		
		Если ЗначениеЗаполнено(СтрокаПути) Тогда
			СтрокаПути = СтрокаПути + ";";
		КонецЕсли;	
		
		СтрокаПути = СтрокаПути + ПолныйПутьLinux;
	КонецЕсли;	
	
	Возврат СтрокаПути;
	
КонецФункции	

&НаКлиенте
Процедура КомандаЗаписатьИЗакрыть(Команда)
	
	ПараметрыЗаписи = Новый Структура;
	ПараметрыЗаписи.Вставить("Закрыть", Истина);
	Записать(ПараметрыЗаписи);
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьЗаголовокНастройкиИспользуемыхРазрезов()
	
	КолИспользуемых = 0;
	СписокРазрезов = Константы.ИспользованиеРазрезовДоступа.Получить().Получить();
	НеАктуальныеРазрезы = ДокументооборотПраваДоступаПовтИсп.ОтключенныеРазрезыДоступа(Истина, Ложь);
	
	Для Каждого Эл Из СписокРазрезов Цикл
		
		Если НеАктуальныеРазрезы.Найти(Эл.Значение) <> Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		Если Эл.Пометка Тогда
			КолИспользуемых = КолИспользуемых + 1;
		КонецЕсли;
		
	КонецЦикла;
	
	Элементы.ИспользованиеРазрезовДоступа.Заголовок = СтрШаблон(
		НСтр("ru = 'Используемые разрезы доступа (%1)'; en = 'Access regions used (%1)'"), КолИспользуемых);
	
КонецПроцедуры

#КонецОбласти
