
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	// Установим значение по умолчанию если оно до сих пор не задано.
	Если Не ЗначениеЗаполнено(КонстантыНабор.ВариантИспользованияPushУведомлений) Тогда
		КонстантыНабор.ВариантИспользованияPushУведомлений = 
			Перечисления.ВариантыPushУведомлений.ИспользоватьВспомогательныйСервис1С;
	КонецЕсли;

	// Настроим отображение сертификата в форме.
	ИмяФайла = "<Сертификат не загружен>";
	СертификатЗагружен = Ложь;

	ДанныеСертификата = Константы.СертификатМобильногоПриложенияДляiOS.Получить().Получить();
	Если ТипЗнч(ДанныеСертификата) = Тип("Структура")Тогда

		Сертификат = ДанныеСертификата.Сертификат;
		ИмяФайла   = ДанныеСертификата.ИмяФайла;

		АдресСертификата = ПоместитьВоВременноеХранилище(Сертификат);

	КонецЕсли;

	// Настроим элементы формы.
	УстановитьДоступностьЭлементовФормы();
	НастроитьДоступностьЭлементовAPNSНаСервере();
	НастроитьДоступностьЭлементовGCMНаСервере();

	Элементы.ИспользоватьПушУведомления1C.РасширеннаяПодсказка.Заголовок = 
		Новый ФорматированнаяСтрока(
			Новый ФорматированнаяСтрока(
				НСтр("ru='Подробнее о параметрах отправки push-уведомлений через сервис фирмы ""1С"" можно прочитать на сайте ИТС.
					|'; en = 'You can learn more on parameters of push-notifications through ""1C LLC"" service by visiting ITS website.'")),
			Новый ФорматированнаяСтрока(НСтр("ru='Подробнее'; en = 'Learn more'"),,,, "http://its.1c.ru/db/v83doc#bookmark:dev:TI000001548"));

	Элементы.ИспользоватьAPNS.РасширеннаяПодсказка.Заголовок = 
		Новый ФорматированнаяСтрока(
			Новый ФорматированнаяСтрока(
				НСтр("ru='Подробнее о параметрах отправки push-уведомлений через Apple Push Notification Service можно прочитать на сайте ИТС.
				|'; en = 'You can learn more on parameters of push-notifications through Apple Push Notification Service by visiting ITS website.'")),
			Новый ФорматированнаяСтрока(НСтр("ru='Подробнее'; en = 'Learn more'"),,,, "http://its.1c.ru/db/v83doc#bookmark:dev:TI000001546"));
		
	Элементы.ИспользоватьGCM.РасширеннаяПодсказка.Заголовок = 
		Новый ФорматированнаяСтрока(
			Новый ФорматированнаяСтрока(
				НСтр("ru='Подробнее о параметрах отправки push-уведомлений через Google Cloud можно прочитать на сайте ИТС.
				|'; en = 'You can learn more on parameters of push-notifications through Google Cloud by visiting ITS website.'")),
			Новый ФорматированнаяСтрока(НСтр("ru='Подробнее'; en = 'Learn more'"),,,, "http://its.1c.ru/db/v83doc#bookmark:dev:TI000001547"));

КонецПроцедуры

&НаСервере
Процедура ПриЗаписиНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)

	Если СертификатЗагружен И Не АдресСертификата = "" Тогда

		ДвоичныеДанные = ПолучитьИзВременногоХранилища(АдресСертификата);
		ДанныеСертификата = Новый Структура();
		ДанныеСертификата.Вставить("Сертификат", ДвоичныеДанные);
		ДанныеСертификата.Вставить("ИмяФайла",   ИмяФайла);

		Константы.СертификатМобильногоПриложенияДляiOS.Установить(
			Новый ХранилищеЗначения(
				ДанныеСертификата, 
				Новый СжатиеДанных(9)));

	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)

	ОбновитьПовторноИспользуемыеЗначения();
	ОбновитьИнтерфейс();

	Если ПараметрыЗаписи.Свойство("Закрыть") Тогда
		Закрыть();
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаЗаписатьИЗакрыть(Команда)

	ПараметрыЗаписи = Новый Структура;
	ПараметрыЗаписи.Вставить("Закрыть", Истина);
	Записать(ПараметрыЗаписи);

КонецПроцедуры

&НаКлиенте
Процедура ПолучитьКлюч(Команда)

	ПерейтиПоНавигационнойСсылке("https://pushnotifications.1c.com/push/publishers/new");

КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьСертификат(Команда)

	Оповещение = Новый ОписаниеОповещения("ЗагрузитьСертификатЗавершение", ЭтаФорма);

	НачатьПомещениеФайла(Оповещение, , "", Истина, УникальныйИдентификатор);

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ИспользоватьПушУведомления1CПриИзменении(Элемент)

	УстановитьДоступностьЭлементовФормы();

КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьAPNSПриИзменении(Элемент)

	НастроитьДоступностьЭлементовAPNSНаСервере();

КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьGCMПриИзменении(Элемент)

	НастроитьДоступностьЭлементовGCMНаСервере();

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьДоступностьЭлементовФормы()

	Если КонстантыНабор.ВариантИспользованияPushУведомлений = 
		Перечисления.ВариантыPushУведомлений.ИспользоватьВспомогательныйСервис1С Тогда
		Элементы.ГруппаНастройкиСервиса1С.Доступность = Истина;
		Элементы.ГруппаНастройкиНепосредственно.Доступность = Ложь;

	ИначеЕсли КонстантыНабор.ВариантИспользованияPushУведомлений = 
		Перечисления.ВариантыPushУведомлений.ОтправлятьСамостоятельно Тогда
		Элементы.ГруппаНастройкиСервиса1С.Доступность = Ложь;
		Элементы.ГруппаНастройкиНепосредственно.Доступность = Истина;
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура НастроитьДоступностьЭлементовAPNSНаСервере()

	Элементы.ЗагрузитьСертификат.Доступность = КонстантыНабор.ИспользоватьAPNS;
	Элементы.СертификатЗагружен.Доступность  = КонстантыНабор.ИспользоватьAPNS;
	Элементы.КартинкаСертификат.Доступность  = КонстантыНабор.ИспользоватьAPNS;

КонецПроцедуры

&НаСервере
Процедура НастроитьДоступностьЭлементовGCMНаСервере()

	Элементы.НомерПриложенияGCM.Доступность = КонстантыНабор.ИспользоватьGCM;
	Элементы.КлючCервераОтправителяGCM.Доступность = КонстантыНабор.ИспользоватьGCM;

КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьСертификатЗавершение(Результат, АдресВременногоХранилища, ВыбранноеИмя, 
	ДопПараметры) Экспорт

	Если Результат Тогда

		АдресСертификата   = АдресВременногоХранилища;
		СертификатЗагружен = Не ПустаяСтрока(АдресСертификата);

		Если СертификатЗагружен Тогда
			ИмяФайла = 
				Сред(ВыбранноеИмя, СтрНайти(ВыбранноеИмя, "\", НаправлениеПоиска.СКонца, , 1) + 1);
		КонецЕсли;

	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура СертификатЗагруженНажатие(Элемент, СтандартнаяОбработка)

	СтандартнаяОбработка = Ложь;

	Если НЕ АдресСертификата = "" Тогда

		ПараметрыФормы = Новый Структура("АдресСертификата", АдресСертификата);

		ОткрытьФорму("ОбщаяФорма.Сертификат", ПараметрыФормы, ЭтаФорма, ,,,, 
			РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс);

	КонецЕсли;

КонецПроцедуры

#КонецОбласти


