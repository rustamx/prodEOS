
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ИмяПодсистемы = Параметры.ИмяПодсистемы;
	
	Если Не ПустаяСтрока(ИмяПодсистемы) Тогда
		Подсистема = Метаданные.Подсистемы.Найти(ИмяПодсистемы);
		Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Отчеты раздела ""%1""'; en = 'Reports of section ""%1""'"), Подсистема.Синоним);
	Иначе
		Заголовок = НСтр("ru = 'Все отчеты'; en = 'All reports'");
	КонецЕсли;
	
	ОбновитьДеревоОтчетов();
	
КонецПроцедуры

&НаСервере
Функция ПодсистемыСодержатОтчет(Подсистемы, Отчет)
	
	Для каждого Эл Из Подсистемы Цикл
		Если ПодсистемаСодержитОтчет(Эл, Отчет) Тогда
			Возврат Истина;
		КонецЕсли;	
	КонецЦикла;
	
	Возврат Ложь;
	
КонецФункции
	
&НаСервере
Функция ПодсистемаСодержитОтчет(Подсистема, Отчет)
	
	Если Подсистема.Состав.Содержит(Отчет) Тогда
		Возврат Истина;
	КонецЕсли;
	
	Для каждого Эл Из Подсистема.Подсистемы Цикл
		Если ПодсистемаСодержитОтчет(Эл, Отчет) Тогда
			Возврат Истина;
		КонецЕсли;	
	КонецЦикла;
	
	Возврат Ложь;
	
КонецФункции

&НаСервере
Процедура ПолучитьПодсистемы(Подсистемы, Подсистема)
	
	Подсистемы.Добавить(Подсистема);
	Для каждого Эл Из Подсистема.Подсистемы Цикл
		ПолучитьПодсистемы(Подсистемы, Эл);
	КонецЦикла	
	
КонецПроцедуры	
	
&НаСервере
Процедура ОбновитьДеревоОтчетов()
	
	Подсистемы = Новый Массив;
	ПолучитьПодсистемы(Подсистемы, Метаданные.Подсистемы.Найти(ИмяПодсистемы));
	
	Дерево = РеквизитФормыВЗначение("ДеревоОтчетовИВариантов");
	Дерево.Строки.Очистить();
	
	ТекстЗапроса =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
		|	ВариантыОтчетов.Наименование,
		|	ВариантыОтчетов.КлючВарианта,
		|	НЕ ВариантыОтчетов.Пользовательский КАК Предопределенный
		|ИЗ
		|	Справочник.ВариантыОтчетов КАК ВариантыОтчетов
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкиВариантовОтчетов КАК НастройкиВариантовОтчетов
		|		ПО (НастройкиВариантовОтчетов.Вариант = ВариантыОтчетов.Ссылка)
		|			И (НастройкиВариантовОтчетов.Пользователь = &ТекущийПользователь)
		|ГДЕ
		|	ВариантыОтчетов.Отчет = &ИдентификаторОтчета
		|	И НЕ ВариантыОтчетов.ПометкаУдаления
		|	И (НЕ ВариантыОтчетов.ТолькоДляАвтора
		|				И ЕСТЬNULL(НастройкиВариантовОтчетов.Видимость, ВариантыОтчетов.ВидимостьПоУмолчанию)
		|			ИЛИ ВариантыОтчетов.Автор = &ТекущийПользователь)
		|
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ПредопределенныеВариантыОтчетов.Наименование,
		|	ПредопределенныеВариантыОтчетов.КлючВарианта,
		|	ИСТИНА
		|ИЗ
		|	Справочник.ПредопределенныеВариантыОтчетов КАК ПредопределенныеВариантыОтчетов
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкиВариантовОтчетов КАК НастройкиВариантовОтчетов
		|		ПО (НастройкиВариантовОтчетов.Вариант = ПредопределенныеВариантыОтчетов.Ссылка)
		|			И (НастройкиВариантовОтчетов.Пользователь = &ТекущийПользователь)
		|ГДЕ
		|	ПредопределенныеВариантыОтчетов.Отчет = &ИдентификаторОтчета
		|	И НЕ ПредопределенныеВариантыОтчетов.ПометкаУдаления
		|
		|УПОРЯДОЧИТЬ ПО
		|	Предопределенный УБЫВ,
		|	ВариантыОтчетов.Наименование";
	
	ЗапросПоВариантам = Новый Запрос;
	ЗапросПоВариантам.Текст = ТекстЗапроса;
	ЗапросПоВариантам.УстановитьПараметр("ТекущийПользователь", ПользователиКлиентСервер.ТекущийПользователь());
	
	Для Каждого Эл из Метаданные.Отчеты Цикл
		
		Если (ПустаяСтрока(ИмяПодсистемы) ИЛИ ПодсистемыСодержатОтчет(Подсистемы, Эл))
			И МожноВключитьОтчетВСписок(Эл) Тогда
			
			Если ПравоДоступа("Использование", Эл) Тогда
				
				ОтчетМенеджер = Отчеты[Эл.Имя];
				Если Эл.ОсновнаяСхемаКомпоновкиДанных = Неопределено Тогда
					Продолжить;
				КонецЕсли;	
				Макет = ОтчетМенеджер.ПолучитьМакет(Эл.ОсновнаяСхемаКомпоновкиДанных.Имя);
				
				// Добавляем отчет
				ВетвьОтчет = Дерево.Строки.Добавить();
				ВетвьОтчет.Синоним = Эл.Синоним;
				ВетвьОтчет.Ключ = Эл.Имя;
				ВетвьОтчет.ИндексКартинки = 0;
				
				Если Эл.ХранилищеВариантов = Неопределено Тогда
					
					// Добавляем варианты из числа предопределенных в макете 
					Для каждого Вариант Из Макет.ВариантыНастроек Цикл
						Строка = ВетвьОтчет.Строки.Добавить();
						Строка.Синоним = Вариант.Представление;
						Строка.Ключ = Вариант.Имя;
						Строка.ИндексКартинки = 2;
					КонецЦикла;
					
					// Добавляем пользовательские варианты
					СписокВариантов = ХранилищеВариантовОтчетов.ПолучитьСписок("Отчет." + Эл.Имя);
					Для каждого Вариант Из СписокВариантов Цикл
						Строка = ВетвьОтчет.Строки.Добавить();
						Строка.Синоним = Вариант.Представление;
						Строка.Ключ = Вариант.Значение;
						Строка.ИндексКартинки = 1;
					КонецЦикла;	
					
				Иначе
							
					// Добавляем варианты 
					ЗапросПоВариантам.УстановитьПараметр("ИдентификаторОтчета", 
						ОбщегоНазначения.ИдентификаторОбъектаМетаданных(Эл.ПолноеИмя()));
					
					УстановитьПривилегированныйРежим(Истина);
					ТаблицаВариантов = ЗапросПоВариантам.Выполнить().Выгрузить();
					УстановитьПривилегированныйРежим(Ложь);
					
					Для Каждого СтрокаТаблицы Из ТаблицаВариантов Цикл
						Строка = ВетвьОтчет.Строки.Добавить();
						Строка.Синоним = СтрокаТаблицы.Наименование;
						Строка.Ключ    = СтрокаТаблицы.КлючВарианта;
						Строка.ИндексКартинки = ?(СтрокаТаблицы.Предопределенный = Истина, 2, 1);
					КонецЦикла;
				
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЕсли;	
		
	КонецЦикла;
	
	ЗначениеВДанныеФормы(Дерево, ДеревоОтчетовИВариантов);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьОтчет(Команда)
	
	ОткрытьОтчетВыполнить();
	
КонецПроцедуры

&НаКлиенте
Процедура Обновить(Команда)
	
	ОбновитьДеревоОтчетов();
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоОтчетовИВариантовВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОткрытьОтчетВыполнить();
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьОтчетВыполнить()
	
	ТекущиеДанные = Элементы.ДеревоОтчетовИВариантов.ТекущиеДанные;
	Если ТекущиеДанные.ПолучитьРодителя() <> Неопределено Тогда
		
		ПараметрыФормы = Новый Структура("КлючВарианта", ТекущиеДанные.Ключ);
		ИмяФормыОтчета = "Отчет." + ТекущиеДанные.ПолучитьРодителя().Ключ + ".ФормаОбъекта";
		ОткрытьФорму(ИмяФормыОтчета, ПараметрыФормы, , ИмяФормыОтчета + ТекущиеДанные.Ключ);
		
	Иначе 
		
		ПоказатьПредупреждение(, НСтр("ru = 'Выберите какой-либо вариант отчета!'; en = 'Select a report option!'"));
		
	КонецЕсли;	
	
КонецПроцедуры	

&НаКлиенте
Процедура ДеревоОтчетовИВариантовПриАктивизацииСтроки(Элемент)
	
	ТекущиеДанные = Элементы.ДеревоОтчетовИВариантов.ТекущиеДанные; 
	Если ТекущиеДанные = Неопределено Или ТекущиеДанные.ПолучитьРодителя() = Неопределено Тогда
		Элементы.ДеревоОтчетовИВариантовОткрытьОтчет.Доступность = Ложь;
	Иначе
		Элементы.ДеревоОтчетовИВариантовОткрытьОтчет.Доступность = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция МожноВключитьОтчетВСписок(Отчет)
	
	УстановитьПривилегированныйРежим(Истина); 
	ОтчетСодержитсяВФункОпции = Ложь;
	Для Каждого ФункОпция Из Метаданные.ФункциональныеОпции Цикл 
		Если ФункОпция.Состав.Содержит(Отчет) Тогда
			Если Метаданные.Константы.Содержит(ФункОпция.Хранение) Тогда
				ОтчетСодержитсяВФункОпции = Истина;
				ЗначениеФункОпции = ПолучитьФункциональнуюОпцию(ФункОпция.Имя);
				Если ТипЗнч(ЗначениеФункОпции) = Тип("Булево") И ЗначениеФункОпции Тогда
					Возврат Истина;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Возврат НЕ ОтчетСодержитсяВФункОпции;	
	
КонецФункции

&НаКлиенте
Процедура ДополнительныеОтчеты(Команда)
	
	ПараметрыВыполненияКоманды = Новый Структура("Источник", ЭтаФорма);
	ДополнительныеОтчетыИОбработкиКлиент.ОткрытьФормуКомандДополнительныхОтчетовИОбработок(
			,
			ПараметрыВыполненияКоманды,
			ДополнительныеОтчетыИОбработкиКлиентСервер.ВидОбработкиДополнительныйОтчет(),
			ИмяПодсистемы);
	
КонецПроцедуры


