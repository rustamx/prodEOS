
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	КоличествоВсехИнструкций = РаботаСИнструкциями.КоличествоТиповыхИнструкций();
	КоличествоИнструкцийДляЗагрузки = РаботаСИнструкциями.КоличествоТиповыхИнструкцийДляЗагрузки();
	
	ПрописьЧисла = ЧислоПрописью(КоличествоВсехИнструкций, "Л = ru_RU", НСтр("ru = ',,,,,,,,0'; en = ',,,,,,,,0'"));
	ПрописьЧислаИПредмета = ЧислоПрописью(КоличествоВсехИнструкций, "Л = ru_RU", 
		НСтр("ru = 'типовая инструкция,типовые инструкции,типовых инструкций,ж,,,,,0'; en = 'model instruction,model instructions,model instructions,с,,,,,0'"));
	КоличествоВсехИнструкцийПрописью = СтрЗаменить(ПрописьЧислаИПредмета, ПрописьЧисла, 
		Формат(КоличествоВсехИнструкций, "ЧГ=") + " ");
	
	Элементы.ДекорацияВопрос.Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'Будет загружено %1 из %2 по работе с документами, процессами, задачами и электронной подписью.'; en = 'To be imported %1 of %2 that deal with working with documents, processes, tasks, and digital signatures.'"),
		КоличествоИнструкцийДляЗагрузки, КоличествоВсехИнструкцийПрописью
		);
	
КонецПроцедуры

&НаКлиенте
Процедура Загрузить(Команда)
	
	Состояние(НСтр("ru = 'Загрузка типовых инструкций.'; en = 'Importing model instructions'"));
	РаботаСИнструкциями.ЗагрузитьТиповыеИнструкции();
	Состояние(НСтр("ru = 'Загрузка типовых инструкций завершена.'; en = 'Import of model instructions completed.'"));
	Оповестить("Запись_Инструкции");
	ЭтаФорма.Закрыть();

КонецПроцедуры

&НаКлиенте
Процедура ДекорацияПодробнееНажатие(Элемент)
	
	ИнструкцииДляЗагрузки = РаботаСИнструкциями.ТиповыеИнструкцииДляЗагрузки();
	ТекстСообщения = "- " + СтрСоединить(ИнструкцииДляЗагрузки, Символы.ПС + "- ");
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Заголовок", НСтр("ru = 'Инструкции для загрузки'; en = 'Instructions to import'"));
	ПараметрыФормы.Вставить("ТекстСообщения", ТекстСообщения);
	ОткрытьФорму("ОбщаяФорма.Сообщение", ПараметрыФормы, ЭтаФорма,,,,, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если КоличествоИнструкцийДляЗагрузки = 0 Тогда
		ТекстПредупреждения = НСтр("ru = 'Все типовые инструкции уже загружены'; en = 'All model instructions have already been imported'");
		ПоказатьПредупреждение(Неопределено, ТекстПредупреждения);
		Отказ = Истина;
	КонецЕсли;

КонецПроцедуры


