// Возвращает папки писем, выбранные пользователем в форме настройки Текущие дела
//
// Возвращаемое значение:
//  Массив - массив папок, либо неопределено в случае отсутствия папок
//
Функция ВыбранныеПапкиПользователя() Экспорт
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ 
		|	ПапкиПисемТекущихДел.Папка КАК Папка
		|ИЗ
		|	РегистрСведений.ПапкиПисемТекущихДел КАК ПапкиПисемТекущихДел
		|ГДЕ
		|	ПапкиПисемТекущихДел.Пользователь = &Пользователь");
		
	Запрос.УстановитьПараметр("Пользователь", ПользователиКлиентСервер.ТекущийПользователь());
	
	Результат = Запрос.Выполнить();
	
	Если Результат.Пустой() Тогда
		Возврат Неопределено;
	Иначе
		Возврат Результат.Выгрузить().ВыгрузитьКолонку("Папка");
	КонецЕсли;	
	
КонецФункции

// Сохраняет выбранные папки писем пользователя
// 
Процедура СохранитьВыбранныеПапкиПользователя(Пользователь, Папки) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если Не ЗначениеЗаполнено(Пользователь) Тогда
		Возврат;
	КонецЕсли;
		
	НаборЗаписей = РегистрыСведений.ПапкиПисемТекущихДел.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Пользователь.Установить(Пользователь);
	
	Для каждого Папка Из Папки Цикл
		Запись = НаборЗаписей.Добавить();		
		Запись.Пользователь = Пользователь;                          				
		Запись.Папка = Папка;
	КонецЦикла; 
	
	НаборЗаписей.Записать(Истина);			
	
КонецПроцедуры	


