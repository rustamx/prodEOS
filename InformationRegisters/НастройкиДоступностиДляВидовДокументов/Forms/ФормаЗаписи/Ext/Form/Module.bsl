
&НаКлиенте
Процедура НастройкаДоступностиПоСостояниюНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Запись.ВидДокумента) Тогда 
		
		СтандартнаяОбработка = Ложь;
		
		Если ТипЗнч(Запись.ВидДокумента) = Тип("СправочникСсылка.ВидыВходящихДокументов") Тогда 
			
			ТипДокумента = ПредопределенноеЗначение("Перечисление.ТипыОбъектов.ВходящиеДокументы");
			
		ИначеЕсли ТипЗнч(Запись.ВидДокумента) = Тип("СправочникСсылка.ВидыИсходящихДокументов") Тогда 
			
			ТипДокумента = ПредопределенноеЗначение("Перечисление.ТипыОбъектов.ИсходящиеДокументы");
			
		ИначеЕсли ТипЗнч(Запись.ВидДокумента) = Тип("СправочникСсылка.ВидыВнутреннихДокументов") Тогда 
			
			ТипДокумента = ПредопределенноеЗначение("Перечисление.ТипыОбъектов.ВнутренниеДокументы");
			
		КонецЕсли;	
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("Отбор", Новый Структура("ТипДокумента", ТипДокумента));
		ПараметрыФормы.Вставить("ТекущаяСтрока", Запись.НастройкаДоступностиПоСостоянию);
		
		ОткрытьФорму("Справочник.НастройкиДоступностиПоСостоянию.ФормаВыбора", ПараметрыФормы, Элемент);
		
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ВидДокументаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Запись.НастройкаДоступностиПоСостоянию) Тогда 
		
		СтандартнаяОбработка = Ложь;
		
		ТипДокумента = ОбщегоНазначенияДокументооборотВызовСервера.ЗначениеРеквизитаОбъекта(
			Запись.НастройкаДоступностиПоСостоянию, "ТипДокумента");
		
		Если ТипДокумента = ПредопределенноеЗначение("Перечисление.ТипыОбъектов.ВходящиеДокументы") Тогда 
			
			ИмяФормыВыбора = "Справочник.ВидыВходящихДокументов.ФормаВыбора";
			
		ИначеЕсли ТипДокумента = ПредопределенноеЗначение("Перечисление.ТипыОбъектов.ИсходящиеДокументы") Тогда 
			
			ИмяФормыВыбора = "Справочник.ВидыИсходящихДокументов.ФормаВыбора";
			
		ИначеЕсли ТипДокумента = ПредопределенноеЗначение("Перечисление.ТипыОбъектов.ВнутренниеДокументы") Тогда 
			
			ИмяФормыВыбора = "Справочник.ВидыВнутреннихДокументов.ФормаВыбора";
			
		КонецЕсли;
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("ТекущаяСтрока", Запись.ВидДокумента);
		
		ОткрытьФорму(ИмяФормыВыбора, ПараметрыФормы, Элемент);
		
	КонецЕсли;	
	
КонецПроцедуры


