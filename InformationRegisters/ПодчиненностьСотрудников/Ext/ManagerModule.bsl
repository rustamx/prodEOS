
// Возвращает сведения о всех руководителях всех сотрудников в виде соответствия
// Ключ - пользователь-сотрудник
// Значение - массив пользователей-руководителей
//
Функция ПолучитьВсехРуководителей() Экспорт
	
	Руководители = Новый Соответствие;

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПодчиненностьСотрудников.Руководитель,
		|	ПодчиненностьСотрудников.Подчиненный
		|ИЗ
		|	РегистрСведений.ПодчиненностьСотрудников КАК ПодчиненностьСотрудников
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Пользователи КАК ПользователиРук
		|		ПО ПодчиненностьСотрудников.Руководитель = ПользователиРук.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Пользователи КАК ПользователиПодч
		|		ПО ПодчиненностьСотрудников.Подчиненный = ПользователиПодч.Ссылка
		|ГДЕ
		|	НЕ ПользователиПодч.ПометкаУдаления
		|	И НЕ ПользователиРук.ПометкаУдаления";

	Результат = Запрос.Выполнить();

	ВыборкаДетальныеЗаписи = Результат.Выбрать();

	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		
		МассивРуководителей = Руководители.Получить(ВыборкаДетальныеЗаписи.Подчиненный);
		Если МассивРуководителей = Неопределено Тогда
			МассивРуководителей = Новый Массив;
		КонецЕсли;	
		
		МассивРуководителей.Добавить(ВыборкаДетальныеЗаписи.Руководитель);
		Руководители.Вставить(ВыборкаДетальныеЗаписи.Подчиненный, МассивРуководителей);
		
	КонецЦикла;
	
	Возврат Руководители;
	
КонецФункции


