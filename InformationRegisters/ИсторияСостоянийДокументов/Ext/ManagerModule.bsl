#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

Функция ПолучитьСостояниеДокумента(Документ) Экспорт
	
	Если Документ.Пустая() Тогда
		Возврат Перечисления.СостоянияДокументов.ПустаяСсылка();
	Иначе
		Запрос = Новый Запрос("
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	Состояние
		|ИЗ
		|	РегистрСведений.ИсторияСостоянийДокументов.СрезПоследних( , Документ = &Документ)");
		
		Запрос.УстановитьПараметр("Документ", Документ);
		
		Выборка = Запрос.Выполнить().Выбрать();
		
		Если Выборка.Следующий() Тогда
			Возврат Выборка.Состояние;
		Иначе
			Возврат Перечисления.СостоянияДокументов.ПустаяСсылка();
		КонецЕсли;
	КонецЕсли;
	
КонецФункции

Функция ПолучитьСостояниеДокументаСТипомСтатуса(Документ) Экспорт
	
	Результат = Новый Структура("Состояние,ТипСтатуса", Перечисления.СостоянияДокументов.ПустаяСсылка(), 0);
	
	Если Не Документ.Пустая() Тогда
		Запрос = Новый Запрос("
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ИсторияСостоянийДокументов.Состояние КАК Состояние,
		|	ЕСТЬNULL(ра_ИндексыСтатусовВидовОбъектов.ТипСтатуса, 0) КАК ТипСтатуса
		|ИЗ
		|	РегистрСведений.ИсторияСостоянийДокументов.СрезПоследних( , Документ = &Документ) КАК ИсторияСостоянийДокументов
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ра_ИндексыСтатусовВидовОбъектов КАК ра_ИндексыСтатусовВидовОбъектов
		|		ПО ИсторияСостоянийДокументов.Документ.ВидДокумента = ра_ИндексыСтатусовВидовОбъектов.ВидДокумента
		|			И ИсторияСостоянийДокументов.Состояние = ра_ИндексыСтатусовВидовОбъектов.СостояниеДокумента");
		
		Запрос.УстановитьПараметр("Документ", Документ);
		
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			ЗаполнитьЗначенияСвойств(Результат, Выборка);
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#КонецЕсли