#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Заполнение регистра при первом запуске.
// 
Процедура НачальноеЗаполнение() Экспорт
	
	Набор = РегистрыСведений.РазрешенияДоступаОбщие.СоздатьНаборЗаписей();
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	ВидыДоступа.Ссылка,
		|	ВидыДоступа.Ссылка В (&ВидыИспользующиеРегистрацию) КАК ИспользуетРегистрацию
		|ИЗ
		|	ПланВидовХарактеристик.ВидыДоступа КАК ВидыДоступа
		|ГДЕ
		|	ВидыДоступа.Предопределенный");
	
	Запрос.УстановитьПараметр("ВидыИспользующиеРегистрацию", 
		НастройкиДоступаКлиентСервер.ВидыДоступаИспользующиеРегистрацию());
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		Стр = Набор.Добавить();
		Стр.ЗначениеДоступа = Выборка.Ссылка;
		Стр.Пользователь = Справочники.РабочиеГруппы.ВсеПользователи;
		Стр.УровеньДоступа = ?(Выборка.ИспользуетРегистрацию,
			Справочники.УровниДоступа.Регистрация,
			Справочники.УровниДоступа.Редактирование);
			
	КонецЦикла;
	
	Набор.Записать();
	
КонецПроцедуры

// Заполненяет разрешения по подразделениям.
// Каждому подразделению выдаются права на соответствующий разрез.
// 
// Параметры:
//   Подразделение - Подразделение, Неопределено - подразделение,
//   	по которому нужно заполнить разрешения. Если Неопределено, то по всем.
// 
Процедура ЗаполнитьРазрешенияПоПодразделениям(Подразделение = Неопределено) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	СтруктураПредприятия.Ссылка
		|ИЗ
		|	Справочник.СтруктураПредприятия КАК СтруктураПредприятия
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СтруктураПредприятия КАК СтруктураПредприятияПодч
		|		ПО СтруктураПредприятия.Ссылка = СтруктураПредприятияПодч.Родитель
		|ГДЕ
		|	СтруктураПредприятияПодч.Ссылка ЕСТЬ NULL ");
	
	Если Подразделение <> Неопределено Тогда
		Запрос.Текст = Запрос.Текст + Символы.ПС + "И СтруктураПредприятия.Ссылка = &Подразделение";
		Запрос.УстановитьПараметр("Подразделение", Подразделение);
	КонецЕсли;
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		Набор = РегистрыСведений.РазрешенияДоступаОбщие.СоздатьНаборЗаписей();
		Набор.Отбор.ЗначениеДоступа.Установить(Выборка.Ссылка);
		
		Стр = Набор.Добавить();
		Стр.ЗначениеДоступа = Выборка.Ссылка;
		Стр.Пользователь = Выборка.Ссылка;
		Стр.УровеньДоступа = Справочники.УровниДоступа.Редактирование;
		
		Набор.Записать();
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
