#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Добавляет запись в историю состояний протоколов мероприятий.
//
// Параметры:
//  Период			 - Дата											 - Дата записи.
//  ПунктПротокола	 - СправочникСсылка.ПротоколыМероприятий		 - Пункт протокола, к которому относится запись.
//  Состояние		 - ПеречислениеСсылка.СостоянияПротоколовМероприятий - Состояние пункта протокола.
//  Установил		 - БизнесПроцессСсылка, СправочникСсылка.Пользователи	 - Кто изменил состояние.
//  Мероприятие		 - СправочникСсылка.Мероприятия							 - Мероприятие, к которому относится пункт протокола.
//
Процедура ДобавитьЗапись(Период, ПунктПротокола, Состояние, Установил, Мероприятие) Экспорт
	
	МенеджерЗаписи = РегистрыСведений.ИсторияСостоянийПротоколовМероприятий.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.Период = Период;
	МенеджерЗаписи.ПунктПротокола = ПунктПротокола;
	МенеджерЗаписи.Состояние = Состояние;
	МенеджерЗаписи.Установил = Установил;
	МенеджерЗаписи.Мероприятие = Мероприятие;
	МенеджерЗаписи.Записать();
	
КонецПроцедуры

// Проверяет, доступна ли история состояний пункта протокола мероприятия.
//
// Параметры:
//  ПунктПротокола	 - СправочникСсылка.ПротоколыМероприятий - Пункт протокола, для которого выполняется проверка.
// 
// Возвращаемое значение:
//  Булево - Доступна ли история состояний для просмотра.
//
Функция ДоступнаИсторияСостояний(ПунктПротокола) Экспорт
	
	Если Не ЗначениеЗаполнено(ПунктПротокола) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ИсторияСостоянийПротоколовМероприятий.Период) КАК КоличествоЗаписей
		|ИЗ
		|	РегистрСведений.ИсторияСостоянийПротоколовМероприятий КАК ИсторияСостоянийПротоколовМероприятий
		|ГДЕ
		|	ИсторияСостоянийПротоколовМероприятий.ПунктПротокола = &ПунктПротокола";
	
	Запрос.УстановитьПараметр("ПунктПротокола", ПунктПротокола);
	
	Результат = Запрос.Выполнить();
	
	Если Результат.Пустой() Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Выборка = Результат.Выбрать();
	Выборка.Следующий();
	Возврат Выборка.КоличествоЗаписей >= 2;
	
КонецФункции

#КонецОбласти

#КонецЕсли
