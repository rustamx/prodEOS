#Область СлужебныйПрограммныйИнтерфейс

// Добавляет оценку вероятности достижения контрольной точки
Процедура Добавить(Дата, КТ, Автор, Оценка, Комментарий) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	НачатьТранзакцию();
	
	Попытка
				
		// Удаляем оценки, добавленные ранее на этой неделе
		Запрос = Новый Запрос;
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	ОценкиКонтрольныхТочек.Период
			|ИЗ
			|	РегистрСведений.ОценкиКонтрольныхТочек КАК ОценкиКонтрольныхТочек
			|ГДЕ
			|	ОценкиКонтрольныхТочек.КонтрольнаяТочка = &КонтрольнаяТочка
			|	И ОценкиКонтрольныхТочек.Период >= &НачалоНедели
			|	И ОценкиКонтрольныхТочек.Период <= &КонецНедели";
		
		Запрос.УстановитьПараметр("КонтрольнаяТочка", КТ);	
		Запрос.УстановитьПараметр("НачалоНедели", НачалоНедели(Дата));	
		Запрос.УстановитьПараметр("КонецНедели", КонецНедели(Дата));	
		
		Выборка = Запрос.Выполнить().Выбрать();
		
		Пока Выборка.Следующий() Цикл
			
			НаборЗаписей = РегистрыСведений.ОценкиКонтрольныхТочек.СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.Период.Установить(Выборка.Период);
			НаборЗаписей.Отбор.КонтрольнаяТочка.Установить(КТ);
			НаборЗаписей.Очистить();
			НаборЗаписей.Записать();
		
		КонецЦикла;

		// Записываем новую оценку
		МенеджерЗаписи = РегистрыСведений.ОценкиКонтрольныхТочек.СоздатьМенеджерЗаписи();
		
		МенеджерЗаписи.Период = Дата;
		МенеджерЗаписи.КонтрольнаяТочка = КТ;
		МенеджерЗаписи.Автор = Автор;
		МенеджерЗаписи.Оценка = Оценка;
		МенеджерЗаписи.Комментарий = Комментарий;
		
		МенеджерЗаписи.Записать();
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		
		ЗаписьЖурналаРегистрации(НСтр("ru = 'Установка оценок контрольных точек'; en = 'Set estimates for milestones'"),
			УровеньЖурналаРегистрации.Ошибка,,,
			ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		ОтменитьТранзакцию();
		ВызватьИсключение;
		
	КонецПопытки;	
	
КонецПроцедуры

#КонецОбласти
