// Добавляет сведения о дочернем процессе
Процедура Добавить(ДочернийПроцесс, СвязующаяЗадача, РодительскийПроцесс = Неопределено) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если Не ЗначениеЗаполнено(СвязующаяЗадача) Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(РодительскийПроцесс) Тогда
		РодительскийПроцесс = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СвязующаяЗадача, "БизнесПроцесс");
	КонецЕсли;
	
	МенеджерЗаписи = РегистрыСведений.ДочерниеБизнесПроцессы.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.ДочернийПроцесс = ДочернийПроцесс;
	МенеджерЗаписи.СвязующаяЗадача = СвязующаяЗадача;
	МенеджерЗаписи.РодительскийПроцесс = РодительскийПроцесс;
	МенеджерЗаписи.Записать();
	
КонецПроцедуры	

// Удаляет сведения о дочернем процессе
Процедура Удалить(ДочернийПроцесс) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	НаборЗаписей = РегистрыСведений.ДочерниеБизнесПроцессы.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.ДочернийПроцесс.Установить(ДочернийПроцесс);
	НаборЗаписей.Записать();
	
КонецПроцедуры

// Выполняет начальное заполнение сведений о дочерних процессах
Процедура Заполнить() Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	НаборЗаписей = РегистрыСведений.ДочерниеБизнесПроцессы.СоздатьНаборЗаписей();
	НаборЗаписей.Записать();
	
	Для каждого МетаданныеБизнесПроцесса Из Метаданные.БизнесПроцессы Цикл
		
		Запрос = Новый Запрос;
		
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	БизнесПроцессы.Ссылка,
			|	БизнесПроцессы.ВедущаяЗадача
			|ИЗ
			|	%БизнесПроцесс% КАК БизнесПроцессы";
		
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "%БизнесПроцесс%", МетаданныеБизнесПроцесса.ПолноеИмя());

		Результат = Запрос.Выполнить();
		
		ВыборкаДетальныеЗаписи = Результат.Выбрать();

		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			
			Добавить(ВыборкаДетальныеЗаписи.Ссылка, ВыборкаДетальныеЗаписи.ВедущаяЗадача);
			
		КонецЦикла;
			
	КонецЦикла;	

КонецПроцедуры


