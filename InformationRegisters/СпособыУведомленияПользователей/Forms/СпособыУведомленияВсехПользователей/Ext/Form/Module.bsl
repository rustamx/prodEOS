#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	ДанныеСпособовУведомления = РегистрыСведений.СпособыУведомленияПользователей.ПолучитьСпособыУведомленияВсехПользователей();
	Для Каждого ДанныеСпособаУведомления Из ДанныеСпособовУведомления Цикл
		СпособУведомления = СпособыУведомления.Добавить();
		СпособУведомления.ВидКонтактнойИнформации = ДанныеСпособаУведомления.Значение;
		СпособУведомления.Представление = ДанныеСпособаУведомления.Представление;
		СпособУведомления.Использовать = ДанныеСпособаУведомления.Пометка;
		Если СпособУведомления.ВидКонтактнойИнформации.Тип = Перечисления.ТипыКонтактнойИнформации.АдресЭлектроннойПочты Тогда
			СпособУведомления.ИндексКартинки = 3;
		ИначеЕсли СпособУведомления.ВидКонтактнойИнформации.Тип = Перечисления.ТипыКонтактнойИнформации.Телефон Тогда
			СпособУведомления.ИндексКартинки = 5;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если ОбщегоНазначенияДокументооборотКлиент.ПередЗакрытием(
		Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка, Модифицированность) Тогда
		Возврат;
	КонецЕсли;
	
	Если Модифицированность Тогда
		
		Отказ = Истина;
		ОписаниеОповещения = Новый ОписаниеОповещения("ПередЗакрытиемЗавершение", ЭтотОбъект);
		ТекстВопроса = НСтр("ru = 'Способы уведомления всех пользователей были изменены. Сохранить изменения?'; en = 'Notification methods for all users were changed. Save changes?'");
		
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНетОтмена);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытиемЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		СохранитьНаКлиенте();
	ИначеЕсли Результат = КодВозвратаДиалога.Нет Тогда
		Модифицированность = Ложь;
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Готово(Команда)
	
	СохранитьНаКлиенте();
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	Модифицированность = Ложь;
	Закрыть();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура СохранитьНаКлиенте()
	
	СохранитьНаСервере();
	Оповестить("ИзмененыНастройкиУведомлений");
	Закрыть();
	
КонецПроцедуры

&НаСервере
Процедура СохранитьНаСервере()
	
	ДанныеСпособовУведомления = Новый СписокЗначений;
	Для Каждого СпособУведомления Из СпособыУведомления Цикл
		ДанныеСпособовУведомления.Добавить(
			СпособУведомления.ВидКонтактнойИнформации,
			СпособУведомления.Представление,
			СпособУведомления.Использовать);
	КонецЦикла;
	РегистрыСведений.СпособыУведомленияПользователей.УстановитьСпособыУведомленияВсехПользователей(
		ДанныеСпособовУведомления);
	
	Модифицированность = Ложь;
	
КонецПроцедуры

#КонецОбласти
