
// Устанавливает значение индекса для переданного объекта.
//
//Параметры:
// Объект - СправочникСсылка.Контрагенты,
//           СправочникСсылка.СтруктураПредприятия,
//           СправочникСсылка.ВидыВнутреннихДокументов,
//           СправочникСсылка.Организации,
//           СправочникСсылка.ВидыВходящихДокументов,
//           СправочникСсылка.ВопросыДеятельности,
//           СправочникСсылка.ВидыИсходящихДокументов - ссылка на объект, для которого устанавливаем индекс нумерации.
// Индекс - Строка - устанавливаемый индекс
//
Процедура ЗаписатьИндексНумерации(Объект, Индекс) Экспорт
	
	Запись = РегистрыСведений.ИндексыНумерации.СоздатьМенеджерЗаписи();
	Запись.Объект = Объект;
	Запись.Прочитать();
	
	Запись.Объект = Объект;
	Запись.Индекс = Индекс;
	Запись.Записать(Истина);
	
КонецПроцедуры

// Возвращает значение индекса нумерации переданного объекта.
//
//Параметры:
// Объект - СправочникСсылка.Контрагенты,
//           СправочникСсылка.СтруктураПредприятия,
//           СправочникСсылка.ВидыВнутреннихДокументов,
//           СправочникСсылка.Организации,
//           СправочникСсылка.ВидыВходящихДокументов,
//           СправочникСсылка.ВопросыДеятельности,
//           СправочникСсылка.ВидыИсходящихДокументов - ссылка на объект, для которого получаем индекс нумерации.
//
Функция ПолучитьИндексНумерации(Объект) Экспорт
	
	Запись = РегистрыСведений.ИндексыНумерации.СоздатьМенеджерЗаписи();
	Запись.Объект = Объект;
	Запись.Прочитать();
	
	Возврат СокрЛП(Запись.Индекс);
	
КонецФункции

// Удаляет значение индекса для переданного объекта.
//
//Параметры:
// Объект - СправочникСсылка.Контрагенты,
//           СправочникСсылка.СтруктураПредприятия,
//           СправочникСсылка.ВидыВнутреннихДокументов,
//           СправочникСсылка.Организации,
//           СправочникСсылка.ВидыВходящихДокументов,
//           СправочникСсылка.ВопросыДеятельности,
//           СправочникСсылка.ВидыИсходящихДокументов - ссылка на объект, для которого удаляем индекс нумерации.
//
Процедура УдалитьИндексНумерации(Объект) Экспорт
	
	Запись = РегистрыСведений.ИндексыНумерации.СоздатьМенеджерЗаписи();
	Запись.Объект = Объект;
	Запись.Удалить();
	
КонецПроцедуры


