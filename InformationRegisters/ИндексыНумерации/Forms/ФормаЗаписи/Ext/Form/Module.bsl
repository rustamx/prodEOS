
&НаСервере
Процедура ЗаполнитьСписокТипов(СписокТипов) 
	
	Типы = Метаданные.РегистрыСведений.ИндексыНумерации.Измерения.Объект.Тип.Типы();
	Для Каждого ТипОбъекта Из Типы Цикл
		ОбъектСсылка = Новый(ТипОбъекта);
		
		Представление = ОбъектСсылка.Метаданные().ПредставлениеОбъекта;
	 	Если ПустаяСтрока(Представление) Тогда
			Представление = ОбъектСсылка.Метаданные().Синоним;
		КонецЕсли;
		
        СписокТипов.Добавить("СправочникСсылка." + ОбъектСсылка.Метаданные().Имя, Представление);
	КонецЦикла;	
	СписокТипов.СортироватьПоПредставлению();
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗаполнитьСписокТипов(Элементы.ТипОбъектаИндекса.СписокВыбора);
	
	Если Параметры.Свойство("ТипОбъекта") И ЗначениеЗаполнено(Параметры.ТипОбъекта) Тогда 
		Если Не ЗначениеЗаполнено(Параметры.ЗначениеКопирования) Тогда 
			Запись.Объект = Новый(Параметры.ТипОбъекта);
		КонецЕсли;	
	КонецЕсли;	
	
	Если Параметры.Свойство("Объект") И ЗначениеЗаполнено(Параметры.Объект) Тогда 
		МенеджерЗаписи = РегистрыСведений.ИндексыНумерации.СоздатьМенеджерЗаписи();
		МенеджерЗаписи.Объект = Параметры.Объект;
		МенеджерЗаписи.Прочитать();
		Если МенеджерЗаписи.Выбран() Тогда 
			ЗначениеВРеквизитФормы(МенеджерЗаписи, "Запись");
		Иначе
			Запись.Объект = Параметры.Объект;
		КонецЕсли;	
		Элементы.ТипОбъектаИндекса.ТолькоПросмотр = Истина;
		Элементы.Объект.ТолькоПросмотр = Истина;
	КонецЕсли;	
	
	Если Запись.Объект = Неопределено Тогда
		Элементы.Объект.Доступность = Ложь;
	Иначе
		ТипОбъектаИндекса = "СправочникСсылка." + Запись.Объект.Метаданные().Имя;
		Элементы.Объект.Доступность = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТипОбъектаИндексаПриИзменении(Элемент)
	
	Если ПустаяСтрока(ТипОбъектаИндекса) Тогда
		Элементы.Объект.Доступность = Ложь;
		Запись.Объект = Неопределено;
	Иначе
		Элементы.Объект.Доступность = Истина;	
		Запись.Объект = Новый(ТипОбъектаИндекса);
	КонецЕсли;
	
КонецПроцедуры


