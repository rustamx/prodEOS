
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает массив пользователей с установленным признаком отображения графика нагрузки
//
Функция ПользователиСПризнакомОтображенияГрафикаНагрузки() Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	НастройкиТекущихДел.Пользователь КАК Пользователь
		|ИЗ
		|	РегистрСведений.НастройкиТекущихДел КАК НастройкиТекущихДел
		|ГДЕ
		|	НастройкиТекущихДел.Пользователь.ПометкаУдаления = ЛОЖЬ
		|	И НастройкиТекущихДел.Пользователь.Недействителен = ЛОЖЬ
		|	И (НастройкиТекущихДел.Виджет1_1 = ЗНАЧЕНИЕ(Справочник.Виджеты.МоиДела)
		|			ИЛИ НастройкиТекущихДел.Виджет1_2 = ЗНАЧЕНИЕ(Справочник.Виджеты.МоиДела)
		|			ИЛИ НастройкиТекущихДел.Виджет1_3 = ЗНАЧЕНИЕ(Справочник.Виджеты.МоиДела)
		|			ИЛИ НастройкиТекущихДел.Виджет1_4 = ЗНАЧЕНИЕ(Справочник.Виджеты.МоиДела)
		|			ИЛИ НастройкиТекущихДел.Виджет2_1 = ЗНАЧЕНИЕ(Справочник.Виджеты.МоиДела)
		|			ИЛИ НастройкиТекущихДел.Виджет2_2 = ЗНАЧЕНИЕ(Справочник.Виджеты.МоиДела)
		|			ИЛИ НастройкиТекущихДел.Виджет2_3 = ЗНАЧЕНИЕ(Справочник.Виджеты.МоиДела)
		|			ИЛИ НастройкиТекущихДел.Виджет2_4 = ЗНАЧЕНИЕ(Справочник.Виджеты.МоиДела)
		|			ИЛИ НастройкиТекущихДел.Виджет3_1 = ЗНАЧЕНИЕ(Справочник.Виджеты.МоиДела)
		|			ИЛИ НастройкиТекущихДел.Виджет3_2 = ЗНАЧЕНИЕ(Справочник.Виджеты.МоиДела)
		|			ИЛИ НастройкиТекущихДел.Виджет3_3 = ЗНАЧЕНИЕ(Справочник.Виджеты.МоиДела)
		|			ИЛИ НастройкиТекущихДел.Виджет3_4 = ЗНАЧЕНИЕ(Справочник.Виджеты.МоиДела))
		|
		|СГРУППИРОВАТЬ ПО
		|	НастройкиТекущихДел.Пользователь";

	Результат = Запрос.Выполнить();

	Если Результат.Пустой() Тогда
		Возврат Новый Массив;
	Иначе
		Возврат Результат.Выгрузить().ВыгрузитьКолонку("Пользователь");
	КонецЕсли;
	
КонецФункции

// Возвращает настройки текущих дел.
// В случае отсутствия настроек создает их со значениями по умолчанию.
//
Функция ПолучитьНастройки() Экспорт
	
	Настройки = Новый Структура;
	
	МетаданныеНастройкиТекущихДел = Метаданные.РегистрыСведений.НастройкиТекущихДел;
	
	Для Каждого СтрИзмерение Из МетаданныеНастройкиТекущихДел.Измерения Цикл
		Настройки.Вставить(СтрИзмерение.Имя, Неопределено);
	КонецЦикла;
	
	Для Каждого СтрРесурс Из МетаданныеНастройкиТекущихДел.Ресурсы Цикл
		Настройки.Вставить(СтрРесурс.Имя, Неопределено);
	КонецЦикла;
	
	Для Каждого СтрРеквизит Из МетаданныеНастройкиТекущихДел.Реквизиты Цикл
		Настройки.Вставить(СтрРеквизит.Имя, Неопределено);
	КонецЦикла;
	
	Пользователь = ПользователиКлиентСервер.ТекущийПользователь();
	
	ЗаписьНастроек = РегистрыСведений.НастройкиТекущихДел.СоздатьМенеджерЗаписи();
	ЗаписьНастроек.Пользователь = Пользователь;
	ЗаписьНастроек.Прочитать();
	
	Если Не ЗаписьНастроек.Выбран() Тогда
		
		РасположениеВиджетовПоУмолчанию = РасположениеВиджетовПоУмолчанию();
		
		ЗаполнитьЗначенияСвойств(ЗаписьНастроек, РасположениеВиджетовПоУмолчанию);
		ЗаписьНастроек.Пользователь = Пользователь;
		ЗаписьНастроек.Записать();
		
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(Настройки, ЗаписьНастроек);
	
	Возврат Настройки;
	
КонецФункции

// Записывает настройки переданные настройки для текущего пользователя.
//
Процедура ЗаписатьНастройки(Настройки) Экспорт
	
	Пользователь = ПользователиКлиентСервер.ТекущийПользователь();
	
	ЗаписьНастроек = СоздатьМенеджерЗаписи();
	ЗаписьНастроек.Пользователь = Пользователь;
	ЗаписьНастроек.Прочитать();
	
	ЗаполнитьЗначенияСвойств(ЗаписьНастроек, Настройки);
	
	Если Не ЗаписьНастроек.Выбран() Тогда
		ЗаписьНастроек.Пользователь = Пользователь;
	КонецЕсли;
	
	ЗаписьНастроек.Записать();
	
КонецПроцедуры

// Возвращает ссылку на виджет из настройки по индексу
//
Функция ВиджетПоИндексу(ИндексВиджета) Экспорт
	
	НастройкиТекущихДел = ПолучитьНастройки();
	Возврат НастройкиТекущихДел["Виджет" + ИндексВиджета];
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Возвращает расположение виджетов по умолчанию в форме Текущие дела
//
Функция РасположениеВиджетовПоУмолчанию()
	
	РасположениеВиджетов = Новый Структура;
	
	КоличествоСтр = 3;
	КоличествоСт = 4;
	
	ИндексСтр = 1;
	Пока ИндексСтр <= КоличествоСтр Цикл
		ИндексСт = 1;
		Пока ИндексСт <= КоличествоСт Цикл
			ИндексВиджета = "" + ИндексСтр + "_" + ИндексСт;
			РасположениеВиджетов.Вставить("Виджет" + ИндексВиджета);
			ИндексСт = ИндексСт + 1;
		КонецЦикла;
		ИндексСтр = ИндексСтр + 1;
	КонецЦикла;
	
	ЭтоРуководительПодразделения = РольДоступна("РаботаСЗадачамиСотрудниковПодразделения");
	ЭтоРуководительПроектов = РольДоступна("ДобавлениеИзменениеПроектов");
	ЭтоПолноправныйПользователь = РольДоступна("ПолныеПрава");
	
	Если ЭтоРуководительПодразделения Или ЭтоРуководительПроектов Или ЭтоПолноправныйПользователь Тогда
		РасположениеВиджетов.Виджет1_1 = Справочники.Виджеты.МоиЗадачи;
		РасположениеВиджетов.Виджет1_2 = Справочники.Виджеты.Почта;
		Если ПолучитьФункциональнуюОпцию("ИспользоватьРабочийКалендарь") Тогда
			РасположениеВиджетов.Виджет1_3 = Справочники.Виджеты.Календарь;
		Иначе
			РасположениеВиджетов.Виджет1_3 = Справочники.Виджеты.Мероприятия;
		КонецЕсли;
		РасположениеВиджетов.Виджет1_4 = Справочники.Виджеты.Форум;
		РасположениеВиджетов.Виджет2_1 = Справочники.Виджеты.ЗадачиОтдела;
		РасположениеВиджетов.Виджет2_2 = Справочники.Виджеты.Контроль;
		РасположениеВиджетов.Виджет2_3 = Справочники.Виджеты.МоиДокументы;
		РасположениеВиджетов.Виджет2_4 = Справочники.Виджеты.МоиФайлы;
		РасположениеВиджетов.Виджет3_1 = Справочники.Виджеты.ЕжедневныеОтчеты;
		РасположениеВиджетов.Виджет3_2 = Справочники.Виджеты.МоиДела;
	Иначе
		РасположениеВиджетов.Виджет1_1 = Справочники.Виджеты.МоиЗадачи;
		РасположениеВиджетов.Виджет1_2 = Справочники.Виджеты.Почта;
		Если ПолучитьФункциональнуюОпцию("ИспользоватьРабочийКалендарь") Тогда
			РасположениеВиджетов.Виджет1_3 = Справочники.Виджеты.Календарь;
		Иначе
			РасположениеВиджетов.Виджет1_3 = Справочники.Виджеты.Мероприятия;
		КонецЕсли;
		РасположениеВиджетов.Виджет1_4 = Справочники.Виджеты.Форум;
		РасположениеВиджетов.Виджет2_1 = Справочники.Виджеты.МоиДокументы;
		РасположениеВиджетов.Виджет2_2 = Справочники.Виджеты.МоиФайлы;
		РасположениеВиджетов.Виджет3_1 = Справочники.Виджеты.ЕжедневныеОтчеты;
		РасположениеВиджетов.Виджет3_2 = Справочники.Виджеты.МоиДела;
	КонецЕсли;
	
	Возврат РасположениеВиджетов;
	
КонецФункции

#КонецОбласти

#КонецЕсли
