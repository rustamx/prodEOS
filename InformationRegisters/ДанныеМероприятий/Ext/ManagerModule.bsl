#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Обновляет данные мероприятия в регистре сведений.
//
// Параметры:
//  Мероприятие - СправочникСсылка.Мероприятие - Мероприятие.
//
Процедура ОбновитьДанныеМероприятия(Мероприятие) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если Не ЗначениеЗаполнено(Мероприятие) Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ОбщегоНазначения.СсылкаСуществует(Мероприятие) Тогда
		Возврат;
	КонецЕсли;
	
	МенеджерЗаписи = РегистрыСведений.ДанныеМероприятий.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.Мероприятие = Мероприятие;
	МенеджерЗаписи.Участники = ПолучитьПредставлениеУчастниковМероприятия(Мероприятие);
	МенеджерЗаписи.Записать();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Возвращает текстовое представление участников мероприятия.
//
// Параметры:
//  Мероприятие - СправочникСсылка.Мероприятие - Мероприятие.
//
// Возвращаемое значение:
//  Строка - Текстовое описание участников мероприятия.
//
Функция ПолучитьПредставлениеУчастниковМероприятия(Мероприятие)
	
	ПредставлениеУчастников = "";
	УчастникиВПредставлении = Новый Массив;
	
	РеквизитыМероприятия = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Мероприятие, "Организатор, Председатель, Секретарь");
	ДобавитьУчастникаВПредставление(ПредставлениеУчастников, РеквизитыМероприятия.Организатор, УчастникиВПредставлении);
	ДобавитьУчастникаВПредставление(ПредставлениеУчастников, РеквизитыМероприятия.Председатель, УчастникиВПредставлении);
	ДобавитьУчастникаВПредставление(ПредставлениеУчастников, РеквизитыМероприятия.Секретарь, УчастникиВПредставлении);
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	УчастникиМероприятия.Исполнитель
		|ИЗ
		|	РегистрСведений.УчастникиМероприятия КАК УчастникиМероприятия
		|ГДЕ
		|	УчастникиМероприятия.Мероприятие = &Мероприятие";
	
	Запрос.УстановитьПараметр("Мероприятие", Мероприятие);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		Если СтрДлина(ПредставлениеУчастников) + СтрДлина(Выборка.Исполнитель) > 1000 Тогда
			ПредставлениеУчастников = ПредставлениеУчастников + " " + НСтр("ru = 'и другие'; en = 'and more'");
			Прервать;
		КонецЕсли;
		
		ДобавитьУчастникаВПредставление(ПредставлениеУчастников, Выборка.Исполнитель, УчастникиВПредставлении);
		
	КонецЦикла;
	
	Возврат ПредставлениеУчастников;
	
КонецФункции

// Включает участника в представление, если он указан и его еще нет в представлении.
//
Процедура ДобавитьУчастникаВПредставление(ПредставлениеУчастников, Участник, УчастникиВПредставлении)
	
	Если Не ЗначениеЗаполнено(Участник) Тогда
		Возврат;
	КонецЕсли;
	
	Если УчастникиВПредставлении.Найти(Участник) <> Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ДобавитьЗначениеКСтрокеЧерезРазделитель(
		ПредставлениеУчастников, ", ", Участник);
	УчастникиВПредставлении.Добавить(Участник);
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли


