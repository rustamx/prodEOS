
#Область ОбработчикиСобытийФормы

&НаСервереБезКонтекста
Процедура УстановитьВозвращен(КлючЗаписи)
	
	МенеджерЗаписи = РегистрыСведений.ЖурналПередачиДокументов.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.Период = КлючЗаписи.Период;
	МенеджерЗаписи.Документ = КлючЗаписи.Документ;
	МенеджерЗаписи.ТипЭкземпляра = КлючЗаписи.ТипЭкземпляра;
	МенеджерЗаписи.НомерЭкземпляра = КлючЗаписи.НомерЭкземпляра;
	МенеджерЗаписи.Прочитать();
	
	Если МенеджерЗаписи.Возвращен Тогда 
		Возврат;
	КонецЕсли;	
	
	МенеджерЗаписи.Возвращен = Истина;
	МенеджерЗаписи.ДатаВозврата = ТекущаяДата();
	МенеджерЗаписи.Записать();
	
КонецПроцедуры	

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("ПоказатьДержателейДокумента") Тогда 
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(Список.Отбор, "Возвращен", Ложь);
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(Список.Отбор, "Документ", Параметры.ПоказатьДержателейДокумента);
			
		ЭтаФорма.Заголовок = НСтр("ru = 'Держатели документа'; en = 'Document holders'");
		ЭтаФорма.РежимОткрытияОкна = РежимОткрытияОкнаФормы.БлокироватьОкноВладельца;
		Элементы.Список.ИзменятьСоставСтрок = Ложь;
	Иначе	
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(Список.Отбор, "Документ", Параметры.Документ);
	КонецЕсли;
	
	ПоказатьУдаленные();
	
КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеДанныхИзНастроекНаСервере(Настройки)
	
	ПоказыватьУдаленные = Настройки["ПоказыватьУдаленные"];
	ПоказатьУдаленные();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОтметитьВозврат(Команда)
	
	ТекущаяСтрока = Элементы.Список.ТекущаяСтрока;
	Если ТекущаяСтрока = Неопределено Тогда 
		Возврат;
	КонецЕсли;	
	
	Если ТипЗнч(ТекущаяСтрока) <> Тип("РегистрСведенийКлючЗаписи.ЖурналПередачиДокументов") Тогда 
		ПоказатьПредупреждение(, НСтр("ru = 'Команда не может быть выполнена для указанного объекта.'; en = 'Cannot execute the command for the specified object.'"));
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	Если ТекущиеДанные.Возвращен Тогда 
		ПоказатьПредупреждение(, НСтр("ru = 'Документ уже возвращен!'; en = 'The document is already returned!'"));
		Возврат;
	КонецЕсли;
	
	УстановитьВозвращен(ТекущаяСтрока);
	Оповестить("ИзмененЖурналПередачи", ТекущиеДанные.Документ);
	ОповеститьОбИзменении(ТекущаяСтрока);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьОтчеты(Команда)
	
	Раздел = ПредопределенноеЗначение("Перечисление.РазделыОтчетов.ЖурналПередачиСписок");
	
	ЗаголовокФормы = НСтр("ru = 'Отчеты по журналу передачи'; en = 'Transfer journal reports'");
	
	РазделГипперСсылка = НастройкиВариантовОтчетовДокументооборот.ПолучитьРазделОтчетаПоИмени("ДокументыИФайлы");

	ПараметрыФормы = Новый Структура("Раздел, ЗаголовокФормы, НеОтображатьИерархию, РазделГипперСсылка", 
		Раздел, ЗаголовокФормы, Истина, РазделГипперСсылка);
	
	ОткрытьФорму(
		"Обработка.ВсеОтчеты.Форма.ФормаПоКатегориям",
		ПараметрыФормы,
		ЭтаФорма, 
		"ЖурналПередачиСписок");

КонецПроцедуры

&НаКлиенте
Процедура ПоказыватьУдаленные(Команда)
	
	ПоказыватьУдаленные = Не ПоказыватьУдаленные;
	
	ПоказатьУдаленные();
	
КонецПроцедуры

&НаСервере
Процедура ПоказатьУдаленные()
	
	Если ПоказыватьУдаленные Тогда
		ОбщегоНазначенияКлиентСервер.УдалитьЭлементыГруппыОтбораДинамическогоСписка(Список, "Документ.ПометкаУдаления");
	Иначе	
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список, "Документ.ПометкаУдаления", Ложь);
	КонецЕсли;	
	
	Элементы.ПоказыватьУдаленные.Пометка = ПоказыватьУдаленные;
	
КонецПроцедуры

#КонецОбласти
