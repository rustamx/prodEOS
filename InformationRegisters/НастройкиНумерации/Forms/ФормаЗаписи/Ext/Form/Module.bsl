
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ТипыОбъектовВходящиеДокументы = Перечисления.ТипыОбъектов.ВходящиеДокументы;
	ТипыОбъектовИсходящиеДокументы = Перечисления.ТипыОбъектов.ИсходящиеДокументы;
	ТипыОбъектовВнутренниеДокументы = Перечисления.ТипыОбъектов.ВнутренниеДокументы;
	
	НумероватьАвтоматически = Перечисления.СпособыНумерации.Автоматически;
	НумероватьВручную = Перечисления.СпособыНумерации.Вручную;
	
	ЗаполнитьТаблицуИзмерений();
	
	НачальнаяЗапись = Новый Структура("ТипДокумента, ВидДокумента, Организация, Подразделение, ВопросДеятельности, Контрагент, Проект");
	ЗаполнитьЗначенияСвойств(НачальнаяЗапись, Запись);
	
	ЭтоНовый = Параметры.Ключ.Пустой();
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	Для Каждого Строка Из ТаблицаИзмерений Цикл
		Запись[Строка.Имя] = Строка.Значение;
	КонецЦикла;	
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	ЗаполнитьТаблицуИзмерений();
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	Если Не ЗначениеЗаполнено(Запись.ТипДокумента) Тогда 
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			НСтр("ru = 'Не указан ""Тип документа""'; en = '""Document class"" is not specified'"),,"ТаблицаИзмерений[0].Значение",,Отказ);
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Нумеровать) Тогда 
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			НСтр("ru = 'Не указан ""Способ нумерации""'; en = '""Numeration method"" is not specified'"),,"Нумеровать",,Отказ);
	КонецЕсли;
	
	ИзмеренияИзменились = Ложь;
	Для Каждого Эл Из НачальнаяЗапись Цикл
		Если Эл.Значение <> Запись[Эл.Ключ] Тогда 
			ИзмеренияИзменились = Истина;
		КонецЕсли;	
	КонецЦикла;	
	
	Если ЭтоНовый Или ИзмеренияИзменились Тогда 
		Если ЕстьДубль() Тогда 
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				НСтр("ru = 'Для указанных полей ""Действует для"" уже задана настройка нумерации'; en = 'Numeration settings has already been set for specified fields ""Valid for"".'"),, "ТаблицаИзмерений",, Отказ);
			Возврат;	
		КонецЕсли;	
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура НумероватьНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Список = Новый СписокЗначений;
	Список.Добавить(НумероватьАвтоматически, НСтр("ru = 'Автоматически'; en = 'Automatically'"));
	Список.Добавить(НумероватьВручную, НСтр("ru = 'Вручную'; en = 'Manually'"));
	
	ОписаниеОповещения = Новый ОписаниеОповещения(
		"НумероватьНачалоВыбораПродолжение",
		ЭтотОбъект);

	ПоказатьВыборИзСписка(ОписаниеОповещения, Список, Элемент);
		
КонецПроцедуры

&НаКлиенте
Процедура НумероватьНачалоВыбораПродолжение(ВыбранныйЭлемент, Параметры) Экспорт 

	Если ВыбранныйЭлемент <> Неопределено Тогда 
		Запись.СпособНумерации = ВыбранныйЭлемент.Значение;
		
		ОписаниеОповещения = Новый ОписаниеОповещения(
			"СформироватьПолеНумеровать",
			ЭтотОбъект);

		Если Запись.СпособНумерации = НумероватьАвтоматически Тогда 
			ПараметрыФормы =  Новый Структура;
			ПараметрыФормы.Вставить("ТекущаяСтрока", Запись.Нумератор);
			
			ОткрытьФорму("Справочник.Нумераторы.ФормаВыбора",ПараметрыФормы,ЭтаФорма,,,,
				ОписаниеОповещения, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		Иначе
			ВыполнитьОбработкуОповещения(ОписаниеОповещения, НумероватьВручную);
		КонецЕсли;	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СформироватьПолеНумеровать(ВыбранноеЗначение, Параметры) Экспорт 

	Если Не ЗначениеЗаполнено(ВыбранноеЗначение) Тогда 
		Запись.СпособНумерации = Неопределено
	ИначеЕсли ВыбранноеЗначение = НумероватьВручную Тогда  
		Запись.Нумератор = Неопределено;
	Иначе	
		Запись.Нумератор = ВыбранноеЗначение;
	КонецЕсли;	

	Нумеровать = Нумерация.СформироватьПолеНумеровать(Запись.СпособНумерации, Запись.Нумератор);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗначениеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ТекущиеДанные = Элементы.ТаблицаИзмерений.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	ТипДокументаЗначение = ТаблицаИзмерений[0].Значение;
	
	ОписаниеОповещения = Новый ОписаниеОповещения(
		"ЗначениеНачалоВыбораПродолжение",
		ЭтотОбъект,
		Новый Структура("ТекущиеДанные", ТекущиеДанные));

	Если ТекущиеДанные.Имя = "ВидДокумента" Тогда 
		
		Если Не ЗначениеЗаполнено(ТипДокументаЗначение) Или ТекущиеДанные.Значение = Неопределено Тогда 
			СтандартнаяОбработка = Ложь;

			Список = Новый СписокЗначений;
			Список.Добавить("СправочникСсылка.ВидыВходящихДокументов",   НСтр("ru = 'Вид входящего документа'; en = 'Incoming document type'"));
			Список.Добавить("СправочникСсылка.ВидыИсходящихДокументов",  НСтр("ru = 'Вид исходящего документа'; en = 'Outgoing document type'"));
			Список.Добавить("СправочникСсылка.ВидыВнутреннихДокументов", НСтр("ru = 'Вид внутреннего документа'; en = 'Internal document type'"));
			
			ПоказатьВыборИзСписка(ОписаниеОповещения, Список, Элемент);
		КонецЕсли;
		
	ИначеЕсли ТекущиеДанные.Имя = "ТипДокумента" Тогда 
		
		СтандартнаяОбработка = Ложь;
		
		Список = Новый СписокЗначений;
		Список.Добавить(ТипыОбъектовВходящиеДокументы);
		Список.Добавить(ТипыОбъектовИсходящиеДокументы);
		Список.Добавить(ТипыОбъектовВнутренниеДокументы);
		
		ПоказатьВыборИзСписка(ОписаниеОповещения, Список, Элемент);
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ЗначениеНачалоВыбораПродолжение(ВыбранныйЭлемент, Параметры) Экспорт 
	
	Если ВыбранныйЭлемент = Неопределено Тогда 
		Возврат;
	КонецЕсли;	
	
	ТекущиеДанные = Параметры.ТекущиеДанные;
	
	Если ТекущиеДанные.Имя = "ВидДокумента" Тогда
		
		ОписаниеТипов = Новый ОписаниеТипов(ВыбранныйЭлемент.Значение);
		ТекущиеДанные.Значение = ОписаниеТипов.ПривестиЗначение(ТекущиеДанные.Значение);
		
		ОписаниеОповещения = Новый ОписаниеОповещения(
			"ОбработкаВыбораВидаДокумента",
			ЭтотОбъект,
			Новый Структура("ТекущиеДанные", ТекущиеДанные));

		Если ВыбранныйЭлемент.Значение = "СправочникСсылка.ВидыВходящихДокументов" Тогда 
			ТаблицаИзмерений[0].Значение = ТипыОбъектовВходящиеДокументы;
			ОткрытьФорму("Справочник.ВидыВходящихДокументов.ФормаВыбора",,,,,, 
				ОписаниеОповещения, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		ИначеЕсли ВыбранныйЭлемент.Значение = "СправочникСсылка.ВидыИсходящихДокументов" Тогда 
			ТаблицаИзмерений[0].Значение = ТипыОбъектовИсходящиеДокументы;
			ОткрытьФорму("Справочник.ВидыИсходящихДокументов.ФормаВыбора",,,,,, 
				ОписаниеОповещения, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		ИначеЕсли ВыбранныйЭлемент.Значение = "СправочникСсылка.ВидыВнутреннихДокументов" Тогда 
			ТаблицаИзмерений[0].Значение = ТипыОбъектовВнутренниеДокументы;
			ОткрытьФорму("Справочник.ВидыВнутреннихДокументов.ФормаВыбора",,,,,, 
				ОписаниеОповещения, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		КонецЕсли;
	Иначе 
		ТекущиеДанные.Значение = ВыбранныйЭлемент.Значение;
	
		Если ВыбранныйЭлемент.Значение = ТипыОбъектовВходящиеДокументы  Тогда 
			ИмяТипа = "СправочникСсылка.ВидыВходящихДокументов";
		ИначеЕсли ВыбранныйЭлемент.Значение = ТипыОбъектовИсходящиеДокументы Тогда 
			ИмяТипа = "СправочникСсылка.ВидыИсходящихДокументов";
		ИначеЕсли ВыбранныйЭлемент.Значение = ТипыОбъектовВнутренниеДокументы  Тогда 
			ИмяТипа = "СправочникСсылка.ВидыВнутреннихДокументов";
		КонецЕсли;
		
		ОписаниеТипов = Новый ОписаниеТипов(ИмяТипа);
		
		НайденныеСтроки = ТаблицаИзмерений.НайтиСтроки(Новый Структура("Имя", "ВидДокумента"));
		Если НайденныеСтроки.Количество() > 0 Тогда 
			НайденнаяСтрока = НайденныеСтроки[0];
			НайденнаяСтрока.Значение = ОписаниеТипов.ПривестиЗначение(НайденнаяСтрока.Значение);
		КонецЕсли;
	КонецЕсли;	
			
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбораВидаДокумента(ВыбранноеЗначение, Параметры) Экспорт 

	Если ВыбранноеЗначение <> Неопределено Тогда 
		ТаблицаИзмерений[1].Значение = ВыбранноеЗначение;
	КонецЕсли;	

КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТаблицуИзмерений()
	
	ТаблицаИзмерений.Очистить();
	
	Измерения = Метаданные.РегистрыСведений.НастройкиНумерации.Измерения;
	Для Каждого Измерение Из Измерения Цикл
		ИмяИзмерения = Измерение.Имя;
		
		Если ИмяИзмерения = "Организация" И Не ПолучитьФункциональнуюОпцию("ИспользоватьУчетПоОрганизациям") Тогда 
			Продолжить;
		ИначеЕсли ИмяИзмерения = "ВопросДеятельности" И Не ПолучитьФункциональнуюОпцию("ИспользоватьВопросыДеятельности") Тогда 
			Продолжить;
		ИначеЕсли ИмяИзмерения = "Проект" И Не ПолучитьФункциональнуюОпцию("ВестиУчетПоПроектам") Тогда 
			Продолжить;
		ИначеЕсли ИмяИзмерения = "ВидДокумента" И Не ПолучитьФункциональнуюОпцию("ИспользоватьВидыДокументов") Тогда 
			Продолжить;
		КонецЕсли;
		
		НоваяСтрока = ТаблицаИзмерений.Добавить();
		НоваяСтрока.Имя = ИмяИзмерения;
		НоваяСтрока.Представление = ?(ПустаяСтрока(Измерение.Синоним), ИмяИзмерения, Измерение.Синоним);
		НоваяСтрока.Тип = Измерение.Тип;
		НоваяСтрока.Значение = Запись[ИмяИзмерения];
	КонецЦикла;	
	
	Нумеровать = Нумерация.СформироватьПолеНумеровать(Запись.СпособНумерации, Запись.Нумератор);
	
КонецПроцедуры	

&НаСервере
Функция ЕстьДубль() 
	
	УстановитьПривилегированныйРежим(Истина);
	
	МенеджерЗаписи = РегистрыСведений.НастройкиНумерации.СоздатьМенеджерЗаписи();
	ЗаполнитьЗначенияСвойств(МенеджерЗаписи, Запись);
	
	МенеджерЗаписи.Прочитать();
	Если МенеджерЗаписи.Выбран() Тогда 
		Возврат Истина;	
	КонецЕсли;	
		
	Возврат Ложь;
		
КонецФункции

