#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПриЗаписи(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		
		ОбработанныеОбъекты = Новый Соответствие;
		ТипыОбъектовВходящихВМеханизмПрав = 
			ДокументооборотПраваДоступаПовтИсп.ТипыСсылокИспользующихДоступПоДескрипторам();
		
		Для Каждого Запись Из ЭтотОбъект Цикл
			
			ТекущийОбъект = Запись.Объект;
			Если ОбработанныеОбъекты.Получить(ТекущийОбъект) = Неопределено Тогда
				ТипОбъекта = ТипЗнч(ТекущийОбъект);
				Если ТипыОбъектовВходящихВМеханизмПрав.Найти(ТипОбъекта) <> Неопределено Тогда
					
					Если РегистрыСведений.ДескрипторыДляОбъектов.ЕстьДескрипторыОбъекта(ТекущийОбъект) Тогда
						ДокументооборотПраваДоступа.ОпределитьДескрипторыОбъекта(ТекущийОбъект);
					КонецЕсли;
					
				КонецЕсли;
			КонецЕсли;
			
			ОбработанныеОбъекты.Вставить(ТекущийОбъект);
			
		КонецЦикла;
		
		Возврат;
		
	КонецЕсли;
	
	ОбъектОтбора = ЭтотОбъект.Отбор.Объект.Значение;
	Если ЗначениеЗаполнено(ОбъектОтбора) И ОбщегоНазначения.ЭтоБизнесПроцесс(ОбъектОтбора.Метаданные()) Тогда
		
		// Запись в РС УчастникиПроцессов
		НаборИзменен = Ложь;
		ТаблицаНабора = РегистрыСведений.УчастникиПроцессов.ПолучитьУчастников(ОбъектОтбора);
		СтруктураПоиска = Новый Структура("Участник");
		
		Для Каждого СтрокаРГ Из ЭтотОбъект Цикл
			ЗаполнитьЗначенияСвойств(СтруктураПоиска, СтрокаРГ);
			НайденныеСтроки = ТаблицаНабора.НайтиСтроки(СтруктураПоиска);
			Если НайденныеСтроки.Количество() = 0 Тогда
				НаборИзменен = Истина;
				ЗаполнитьЗначенияСвойств(ТаблицаНабора.Добавить(), СтрокаРГ);
			КонецЕсли;
		КонецЦикла;
		
		Если НаборИзменен Тогда
			РегистрыСведений.УчастникиПроцессов.ЗаписатьНаборПоПроцессу(ОбъектОтбора, ТаблицаНабора);
		КонецЕсли;
		
	КонецЕсли;
		
КонецПроцедуры

Процедура ПередЗаписью(Отказ, Замещение)
	 
	Для Каждого Запись Из ЭтотОбъект Цикл
		Если ТипЗнч(Запись.Объект) = Тип("ДокументСсылка.ra_Nesootvetstvie") Тогда
			Запись.ра_Несоответствие = Запись.Объект;
		ИначеЕсли ТипЗнч(Запись.Объект) = Тип("ДокументСсылка.ra_ZayavkaNaOcenkuSootvetstviya") Тогда
			Запись.ра_ЗаявкаНаОценкуСоответствия = Запись.Объект;
		ИначеЕсли ТипЗнч(Запись.Объект) = Тип("ДокументСсылка.ra_ZayavkaNaKontrolnuyuOperaciyu") Тогда
			Запись.ра_ЗаявкаНаКонтрольнуюОперацию = Запись.Объект;
		КонецЕсли;	
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
