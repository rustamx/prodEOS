
&НаСервере
Функция ЭлементыДляСохранения()
	
	СохраняемыеЭлементы = Новый Структура;
	СохраняемыеЭлементы.Вставить("ВидРабот", ВидРабот);
	Возврат СохранениеВводимыхЗначений.СформироватьТаблицуСохраняемыхЭлементов(СохраняемыеЭлементы);
	
КонецФункции

&НаСервере
Процедура ЗаписатьВводимыеЗначения()
	
	// Сохранение вводимых значений
	СохранениеВводимыхЗначений.ОбновитьСпискиВыбора(ЭтаФорма, ЭлементыДляСохранения(), ЭтаФорма.ИмяФормы);
	
КонецПроцедуры	

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("ДатаОтчета") И ЗначениеЗаполнено(Параметры.ДатаОтчета) Тогда  
		ДатаДобавления = Параметры.ДатаОтчета;
	Иначе	
		ДатаДобавления = ТекущаяДатаСеанса();
	КонецЕсли;	
	
	Если Параметры.Свойство("Объект") И ЗначениеЗаполнено(Параметры.Объект) Тогда  
		Источник = Параметры.Объект;
	КонецЕсли;	
	
	Если Параметры.Свойство("Пользователь") И ЗначениеЗаполнено(Параметры.Пользователь) Тогда  
		Пользователь = Параметры.Пользователь;
	Иначе	
		Пользователь = ПользователиКлиентСервер.ТекущийПользователь();
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Источник) И ПолучитьФункциональнуюОпцию("ВестиУчетПоПроектам") Тогда 
		
		Если ТипЗнч(Источник) = Тип("ЗадачаСсылка.ЗадачаИсполнителя") Тогда 
			Проект = Источник.Проект;
			ПроектнаяЗадача = Источник.ПроектнаяЗадача;
			
		ИначеЕсли ТипЗнч(Источник) = Тип("СправочникСсылка.Проекты") Тогда
			Проект = Источник;
			ПроектнаяЗадача = Неопределено;
			
		ИначеЕсли ТипЗнч(Источник) = Тип("СправочникСсылка.ПроектныеЗадачи") Тогда
			Проект = Источник.Владелец;
			ПроектнаяЗадача = Источник;
			
		ИначеЕсли ДелопроизводствоКлиентСервер.ЭтоДокумент(Источник) 
			Или ВстроеннаяПочтаКлиентСервер.ЭтоПисьмо(Источник)
			Или ТипЗнч(Источник) = Тип("СправочникСсылка.Мероприятия") 
			Или ТипЗнч(Источник) = Тип("СправочникСсылка.Файлы") Тогда 
			
			Проект = Источник.Проект;
			ПроектнаяЗадача = Неопределено;
			
		КонецЕсли; 
		
	КонецЕсли;	
	
	Если Параметры.Свойство("ВидыРабот") Тогда 
		ВидыРабот = Параметры.ВидыРабот;
		
		Если ВидыРабот.Количество() = 1 Тогда
			ВидРабот = ВидыРабот[0].Значение;
		ИначеЕсли ВидыРабот.Количество() > 1 Тогда 
			Для Каждого Вид Из ВидыРабот Цикл
				Элементы.ВидРабот.СписокВыбора.Добавить(Вид.Значение);
			КонецЦикла;	
			Элементы.ВидРабот.КнопкаСпискаВыбора = Истина;
		Иначе	
			// Сохранение вводимых значений
			СохранениеВводимыхЗначений.ЗаполнитьСписокВыбора(ЭтаФорма, ЭлементыДляСохранения(), ЭтаФорма.ИмяФормы);
		КонецЕсли;
	КонецЕсли;
	
	Если Параметры.Свойство("ДлительностьРаботы") Тогда 
		Длительность = Параметры.ДлительностьРаботы;
	КонецЕсли;
	
	Если Параметры.Свойство("НачалоРаботы") Тогда 
		Начало = Параметры.НачалоРаботы;
	КонецЕсли;
	
	Если Параметры.Свойство("ОкончаниеРаботы") Тогда 
		Окончание = Параметры.ОкончаниеРаботы;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Источник) Тогда 
		Если ВстроеннаяПочтаКлиентСервер.ЭтоИсходящееПисьмо(Источник) Тогда
			ОписаниеРаботы = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Составление письма ""%1""'; en = 'Drafting the email ""%1""'"),
			Источник.Тема);
			
		ИначеЕсли ВстроеннаяПочтаКлиентСервер.ЭтоВходящееПисьмо(Источник) Тогда
			ОписаниеРаботы = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Чтение письма ""%1""'; en = 'Reading the email ""%1""'"),
			Источник.Тема);
			
		Иначе		
			ОписаниеРаботы = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Работа над ""%1""'; en = 'Work on ""%1""'"),
			Источник);
			
		КонецЕсли;
	КонецЕсли;	
	
	ДлительностьРаботы = УчетВремениКлиентСервер.ЧислоВСтроку(Длительность);
	ПроектЗадача = РаботаСПроектамиКлиентСервер.ПредставлениеПроектаЗадачи(Проект, ПроектнаяЗадача);
	
	СпособУказанияВремени = ХранилищеОбщихНастроек.Загрузить("НастройкиУчетаВремени", "СпособУказанияВремени");
	Если Не ЗначениеЗаполнено(СпособУказанияВремени) Тогда
		СпособУказанияВремени = Перечисления.СпособыУказанияВремени.Длительность;
	КонецЕсли;	
	
	Если СпособУказанияВремени = Перечисления.СпособыУказанияВремени.Длительность Тогда 
		Элементы.ДлительностьРаботы.Видимость = Истина;
		Элементы.Начало.Видимость = Ложь;
		Элементы.Окончание.Видимость = Ложь;
	Иначе
		Элементы.ДлительностьРаботы.Видимость = Ложь;
		Элементы.Начало.Видимость = Истина;
		Элементы.Окончание.Видимость = Истина;
	КонецЕсли;
	
	Элементы.ДлительностьРаботы.СписокВыбора.ЗагрузитьЗначения(МассивВыбораВремени());
	
	Если Не ЗначениеЗаполнено(ВидРабот) Тогда
		ВидРабот = УчетВремени.ПолучитьОсновнойВидРабот();
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	СпособУказанияВремени = ХранилищеОбщихНастроек.Загрузить("НастройкиУчетаВремени", "СпособУказанияВремени");
	Если Не ЗначениеЗаполнено(СпособУказанияВремени) Тогда
		СпособУказанияВремени = Перечисления.СпособыУказанияВремени.Длительность;
	КонецЕсли;
	
	Если СпособУказанияВремени = Перечисления.СпособыУказанияВремени.Длительность Тогда 
		Если Не ЗначениеЗаполнено(ДлительностьРаботы) Или ДлительностьРаботы = "00:00" Тогда 
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				НСтр("ru = 'Не заполнено поле ""Длительность""'; en = 'Field ""Duration"" is empty'"),,
				"ДлительностьРаботы",, 
				Отказ);
		КонецЕсли;
	Иначе
		Если Не ЗначениеЗаполнено(Начало) Тогда 
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				НСтр("ru = 'Не заполнено поле ""Начало""'; en = 'Field ""Start"" is empty'"),,
				"Начало",, 
				Отказ);
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(Окончание) Тогда 
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				НСтр("ru = 'Не заполнено поле ""Окончание""'; en = 'Field ""End"" is empty'"),,
				"Окончание",, 
				Отказ);
		КонецЕсли;
			
		Если ЗначениеЗаполнено(Начало) И ЗначениеЗаполнено(Окончание) И Начало > Окончание Тогда 
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				НСтр("ru = 'Время окончания меньше, чем время начала'; en = 'End time is less than start time'"),,
				"Окончание",, 
				Отказ);
		КонецЕсли;	
	КонецЕсли;
	
	Если ПолучитьФункциональнуюОпцию("ВестиУчетПоПроектам") Тогда 
		
		ТекстСообщения = "";
		РезультатПроверки = РаботаСПроектами.ПроверитьЗаписьОФактическихТрудозатратах(
			Проект,
			ПроектнаяЗадача,
			Источник,
			Пользователь,
			ТекстСообщения);
		Если Не РезультатПроверки Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				ТекстСообщения,,
				"ПроектЗадача",, 
				Отказ);	
		КонецЕсли;	
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПроектЗадачаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	РаботаСПроектамиКлиент.ВыбратьПроектЗадачу(Элемент, Проект, ПроектнаяЗадача);
	
КонецПроцедуры

&НаКлиенте
Процедура ПроектЗадачаОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Если ТипЗнч(ВыбранноеЗначение) = Тип("Структура") Тогда 	
		Проект = ВыбранноеЗначение.Проект;
		ПроектнаяЗадача = ВыбранноеЗначение.ПроектнаяЗадача;
		ПроектЗадача = РаботаСПроектамиКлиентСервер.ПредставлениеПроектаЗадачи(
			ВыбранноеЗначение.Проект, 
			ВыбранноеЗначение.ПроектнаяЗадача,
			ВыбранноеЗначение.ЕстьПроектныеЗадачи);
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ПроектЗадачаОткрытие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Если ЗначениеЗаполнено(ПроектнаяЗадача) Тогда 
		ПоказатьЗначение(, ПроектнаяЗадача);
	ИначеЕсли ЗначениеЗаполнено(Проект) Тогда 
		ПоказатьЗначение(, Проект);
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ПроектЗадачаОчистка(Элемент, СтандартнаяОбработка)
	
	Проект = Неопределено;
	ПроектнаяЗадача = Неопределено;
	
КонецПроцедуры

&НаКлиенте
Процедура Добавить(Команда)
	
	ОчиститьСообщения();
	Если Не ПроверитьЗаполнение() Тогда 
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ВидРабот) Тогда
		ЗаписатьВводимыеЗначения();
	КонецЕсли;
	
	Если СпособУказанияВремени = ПредопределенноеЗначение("Перечисление.СпособыУказанияВремени.Длительность") Тогда 
		Длительность = УчетВремениКлиентСервер.ЧислоИзСтроки(ДлительностьРаботы);
	ИначеЕсли ЗначениеЗаполнено(Начало) И ЗначениеЗаполнено(Окончание) Тогда 
		Длительность = Окончание - Начало;
	КонецЕсли;	
	
	ПараметрыОтчета = Новый Структура();
	ПараметрыОтчета.Вставить("ДатаОтчета", ДатаДобавления);
	ПараметрыОтчета.Вставить("ВидРаботы", ВидРабот);
	ПараметрыОтчета.Вставить("ОписаниеРаботы", ОписаниеРаботы);
	ПараметрыОтчета.Вставить("ДлительностьРаботы", Длительность);
	ПараметрыОтчета.Вставить("НачалоРаботы", Начало);
	ПараметрыОтчета.Вставить("ОкончаниеРаботы", Окончание);
	ПараметрыОтчета.Вставить("Объект", Источник);
	ПараметрыОтчета.Вставить("Проект", Проект);
	ПараметрыОтчета.Вставить("ПроектнаяЗадача", ПроектнаяЗадача);
	
	Закрыть(ПараметрыОтчета);
		
КонецПроцедуры

&НаКлиенте
Процедура НеДобавлять(Команда)
	
	Закрыть(Неопределено);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция МассивВыбораВремени()
	
	МассивВыбора = Новый Массив;
	
	МассивВыбора.Добавить("00:15");
	МассивВыбора.Добавить("00:30");
	МассивВыбора.Добавить("00:45");
	МассивВыбора.Добавить("01:00");
	МассивВыбора.Добавить("01:30");
	МассивВыбора.Добавить("02:00");
	МассивВыбора.Добавить("03:00");
	МассивВыбора.Добавить("04:00");
	МассивВыбора.Добавить("05:00");
	
	Возврат МассивВыбора;
	
КонецФункции

&НаКлиенте
Процедура ПроектЗадачаАвтоПодбор(Элемент, Текст, ДанныеВыбора, Ожидание, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Текст) Тогда 
		СтандартнаяОбработка = Ложь;
		ДанныеВыбора = РаботаСПроектами.СформироватьДанныеВыбораПроектаЗадачи(Текст);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПроектЗадачаОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Текст) Тогда 
		ДанныеВыбораПроектаЗадачи = РаботаСПроектами.СформироватьДанныеВыбораПроектаЗадачи(Текст);
		
		Если ДанныеВыбораПроектаЗадачи.Количество() = 1 Тогда 
			ВыбранноеЗначение = ДанныеВыбораПроектаЗадачи[0].Значение;
			
			Проект = ВыбранноеЗначение.Проект;
			ПроектнаяЗадача = ВыбранноеЗначение.ПроектнаяЗадача;
			ПроектЗадача = РаботаСПроектамиКлиентСервер.ПредставлениеПроектаЗадачи(Проект, ПроектнаяЗадача);
		Иначе	
			СтандартнаяОбработка = Ложь;
			ДанныеВыбора = ДанныеВыбораПроектаЗадачи;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПроектЗадачаПриИзменении(Элемент)
	
	Если Не ЗначениеЗаполнено(ПроектЗадача) Тогда 
		Проект = Неопределено;
		ПроектнаяЗадача = Неопределено;
	КонецЕсли;	
	
КонецПроцедуры


