// УправлениеДоступом

// Заполняет структуру сведений о полях доступа 
Процедура ЗаполнитьСведенияОПоляхДоступа(СведенияОПолях) Экспорт 
	
	СведенияОПолях.ОбъектДоступа1 = "Проект";
	СведенияОПолях.ОбъектДоступа2 = "Пользователь";
	
КонецПроцедуры

// Возвращает запрос для расчета прав доступа по дескрипторам регистра
// 
// Параметры:
//  
//  Дескрипторы - Массив - массив дескрипторов, чьи права нужно рассчитать
//  ИдРегистра - Ссылка - идентификатор объекта метаданных, назначенный переданным дескрипторам
//  МенеджерОбъектаДоступа - РегистрСведенийМенеджер, РегистрНакопленияМенеджер - менеджер регистра
// 
// Возвращаемое значение - Запрос - запрос, который выберет права доступа для переданного массива дескрипторов
// 
Функция ЗапросДляРасчетаПрав(Дескрипторы, ИдРегистра, МенеджерРегистра) Экспорт
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	ДескрипторыДоступаРегистров.Ссылка КАК Дескриптор,
		|	ДескрипторыДоступаРегистров.ОбъектДоступа2 КАК Пользователь,
		|	ИСТИНА КАК Чтение,
		|	ИСТИНА КАК Добавление,
		|	ИСТИНА КАК Изменение,
		|	ИСТИНА КАК Удаление,
		|	ЛОЖЬ КАК УправлениеПравами
		|ПОМЕСТИТЬ НовыеПрава
		|ИЗ
		|	Справочник.ДескрипторыДоступаРегистров КАК ДескрипторыДоступаРегистров
		|ГДЕ
		|	ДескрипторыДоступаРегистров.Ссылка В(&Дескрипторы)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ДескрипторыДоступаРегистров.Ссылка,
		|	Проекты.Руководитель,
		|	ИСТИНА,
		|	ЛОЖЬ,
		|	ЛОЖЬ,
		|	ЛОЖЬ,
		|	ЛОЖЬ
		|ИЗ
		|	Справочник.ДескрипторыДоступаРегистров КАК ДескрипторыДоступаРегистров
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Проекты КАК Проекты
		|		ПО ДескрипторыДоступаРегистров.ОбъектДоступа1 = Проекты.Ссылка
		|ГДЕ
		|	ДескрипторыДоступаРегистров.Ссылка В(&Дескрипторы)
		|	И Проекты.Руководитель <> ЗНАЧЕНИЕ(Справочник.Пользователи.ПустаяСсылка)");
	
	ДокументооборотПраваДоступа.ДополнитьТекстЗапросаПоПравам(Запрос.Текст, Истина);
	
	Возврат Запрос;
	
КонецФункции

Процедура ЗаполнитьПротоколРасчетаПрав(ПротоколРасчетаПрав, ЗапросПоПравам) Экспорт
	
	ЗаписьПротокола = Новый Структура("Элемент, Описание",
		"Пользователь", НСтр("ru = 'Пользователь'; en = 'User'"));
	ПротоколРасчетаПрав.Добавить(ЗаписьПротокола);
	
	ЗаписьПротокола = Новый Структура("Элемент, Описание",
		"РуководительПроекта", НСтр("ru = 'Руководитель проекта'; en = 'Project manager'"));
	ПротоколРасчетаПрав.Добавить(ЗаписьПротокола);
	
КонецПроцедуры

// Конец УправлениеДоступом

