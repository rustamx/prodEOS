#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Процедура ДобавитьПолномочия(Владелец, Полномочия) Экспорт
	
	НачатьТранзакцию();
	
	// Сохранение информации о правах ролей до внесения изменений.
	ПользователиДляПроверкиПрав = РегистрыСведений.ПользователиВКонтейнерах.ПользователиВКонтейнере(Владелец);
	ПраваДоИзменений = ПраваПоПолномочиям(ПользователиДляПроверкиПрав);
	
	// Добавление Полномочий.
	Запись = РегистрыСведений.ПолномочияПользователей.СоздатьМенеджерЗаписи();
	Запись.Владелец = Владелец;
	Запись.Полномочия = Полномочия;
	Запись.Записать();
	
	// Обновление прав доступа.
	СравнитьПраваРолейИВыполнитьПересчетПрав(ПользователиДляПроверкиПрав, ПраваДоИзменений);
	
	ЗафиксироватьТранзакцию();
	
КонецПроцедуры

Процедура УдалитьПолномочия(Владелец, Полномочия) Экспорт
	
	НачатьТранзакцию();
	
	// Сохранение информации о правах ролей до внесения изменений.
	ПользователиДляПроверкиПрав = РегистрыСведений.ПользователиВКонтейнерах.ПользователиВКонтейнере(Владелец);
	ПраваДоИзменений = ПраваПоПолномочиям(ПользователиДляПроверкиПрав);
	
	// Удаление полномочия для самого владельца.
	НаборЗаписей = РегистрыСведений.ПолномочияПользователей.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Полномочия.Установить(Полномочия);
	НаборЗаписей.Отбор.Владелец.Установить(Владелец);
	НаборЗаписей.Записать();
	
	// Обновление прав по дескрипторам.
	СравнитьПраваРолейИВыполнитьПересчетПрав(ПользователиДляПроверкиПрав, ПраваДоИзменений);
	
	ЗафиксироватьТранзакцию();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПраваПоПолномочиям(Пользователи)
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	ПользователиВКонтейнерах.Пользователь КАК Пользователь,
		|	ПраваРолей.ОбъектМетаданных КАК ОбъектМетаданных,
		|	ИСТИНА КАК Чтение,
		|	ПраваРолей.Добавление КАК Добавление,
		|	ПраваРолей.Изменение КАК Изменение,
		|	ПраваРолей.ЧтениеБезОграничения КАК ЧтениеБезОграничения,
		|	ПраваРолей.ИзменениеБезОграничения КАК ИзменениеБезОграничения
		|ПОМЕСТИТЬ ПраваПользователей
		|ИЗ
		|	РегистрСведений.ПраваРолей КАК ПраваРолей
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ПрофилиГруппДоступа.Роли КАК ПрофилиГруппДоступаРоли
		|		ПО ПраваРолей.Роль = ПрофилиГруппДоступаРоли.Роль
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ПолномочияПользователей КАК ПолномочияПользователей
		|		ПО (ПрофилиГруппДоступаРоли.Ссылка = ПолномочияПользователей.Полномочия)
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ПользователиВКонтейнерах КАК ПользователиВКонтейнерах
		|		ПО (ПолномочияПользователей.Владелец = ПользователиВКонтейнерах.Контейнер)
		|ГДЕ
		|	ПользователиВКонтейнерах.Пользователь В(&Пользователи)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВложенныйЗапрос.ОбъектМетаданных КАК ОбъектМетаданных,
		|	МИНИМУМ(ВложенныйЗапрос.Чтение) КАК Чтение,
		|	МИНИМУМ(ВложенныйЗапрос.Добавление) КАК Добавление,
		|	МИНИМУМ(ВложенныйЗапрос.Изменение) КАК Изменение,
		|	МИНИМУМ(ВложенныйЗапрос.ЧтениеБезОграничения) КАК ЧтениеБезОграничения,
		|	МИНИМУМ(ВложенныйЗапрос.ИзменениеБезОграничения) КАК ИзменениеБезОграничения
		|ИЗ
		|	(ВЫБРАТЬ
		|		Пользователи.Ссылка КАК Пользователь,
		|		ИдентификаторыОбъектовМетаданных.Ссылка КАК ОбъектМетаданных,
		|		МАКСИМУМ(НЕ ПраваПользователей.ОбъектМетаданных ЕСТЬ NULL ) КАК Чтение,
		|		МАКСИМУМ(ЕСТЬNULL(ПраваПользователей.Добавление, ЛОЖЬ)) КАК Добавление,
		|		МАКСИМУМ(ЕСТЬNULL(ПраваПользователей.Изменение, ЛОЖЬ)) КАК Изменение,
		|		МАКСИМУМ(ЕСТЬNULL(ПраваПользователей.ЧтениеБезОграничения, ЛОЖЬ)) КАК ЧтениеБезОграничения,
		|		МАКСИМУМ(ЕСТЬNULL(ПраваПользователей.ИзменениеБезОграничения, ЛОЖЬ)) КАК ИзменениеБезОграничения
		|	ИЗ
		|		Справочник.ИдентификаторыОбъектовМетаданных КАК ИдентификаторыОбъектовМетаданных
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Пользователи КАК Пользователи
		|			ПО (ИСТИНА)
		|			ЛЕВОЕ СОЕДИНЕНИЕ ПраваПользователей КАК ПраваПользователей
		|			ПО ИдентификаторыОбъектовМетаданных.Ссылка = ПраваПользователей.ОбъектМетаданных
		|				И (Пользователи.Ссылка = ПраваПользователей.Пользователь)
		|	ГДЕ
		|		ИдентификаторыОбъектовМетаданных.Ссылка В(&ОбъектыМетаданныхМеханизмаПрав)
		|		И Пользователи.Ссылка В(&Пользователи)
		|	
		|	СГРУППИРОВАТЬ ПО
		|		Пользователи.Ссылка,
		|		ИдентификаторыОбъектовМетаданных.Ссылка) КАК ВложенныйЗапрос
		|
		|СГРУППИРОВАТЬ ПО
		|	ВложенныйЗапрос.ОбъектМетаданных");
	
	Запрос.УстановитьПараметр("Пользователи", Пользователи);
	Запрос.УстановитьПараметр("ОбъектыМетаданныхМеханизмаПрав",
		ДокументооборотПраваДоступаПовтИсп.ВсеОбъектыМеханизмаПравДоступа());
	
	ТаблицаПрав = Запрос.Выполнить().Выгрузить();
	
	Возврат ТаблицаПрав;
	
КонецФункции

Процедура СравнитьПраваРолейИВыполнитьПересчетПрав(Пользователи, СтарыеПрава);
	
	Если Не ДокументооборотПраваДоступаПовтИсп.ВключеноИспользованиеПравДоступа() Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	СтарыеПрава.ОбъектМетаданных,
		|	СтарыеПрава.Чтение,
		|	СтарыеПрава.Добавление,
		|	СтарыеПрава.Изменение,
		|	СтарыеПрава.ЧтениеБезОграничения,
		|	СтарыеПрава.ИзменениеБезОграничения
		|ПОМЕСТИТЬ СтарыеПрава
		|ИЗ
		|	&СтарыеПрава КАК СтарыеПрава
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	НовыеПрава.ОбъектМетаданных,
		|	НовыеПрава.Чтение,
		|	НовыеПрава.Добавление,
		|	НовыеПрава.Изменение,
		|	НовыеПрава.ЧтениеБезОграничения,
		|	НовыеПрава.ИзменениеБезОграничения
		|ПОМЕСТИТЬ НовыеПрава
		|ИЗ
		|	&НовыеПрава КАК НовыеПрава
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	НовыеПрава.ОбъектМетаданных КАК ОбъектМетаданных
		|ИЗ
		|	НовыеПрава КАК НовыеПрава
		|		ЛЕВОЕ СОЕДИНЕНИЕ СтарыеПрава КАК СтарыеПрава
		|		ПО НовыеПрава.ОбъектМетаданных = СтарыеПрава.ОбъектМетаданных
		|			И НовыеПрава.Чтение = СтарыеПрава.Чтение
		|			И НовыеПрава.Добавление = СтарыеПрава.Добавление
		|			И НовыеПрава.Изменение = СтарыеПрава.Изменение
		|			И НовыеПрава.ЧтениеБезОграничения = СтарыеПрава.ЧтениеБезОграничения
		|			И НовыеПрава.ИзменениеБезОграничения = СтарыеПрава.ИзменениеБезОграничения
		|ГДЕ
		|	СтарыеПрава.ОбъектМетаданных ЕСТЬ NULL");
	
	Запрос.УстановитьПараметр("СтарыеПрава", СтарыеПрава);
	Запрос.УстановитьПараметр("НовыеПрава",  ПраваПоПолномочиям(Пользователи));
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ДокументооборотПраваДоступа.РассчитатьПраваОбъектовТаблицы(Выборка.ОбъектМетаданных);
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
