
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ТипКонтейнеровПользователей = Метаданные.ОпределяемыеТипы.КонтейнерПользователей.Тип;
	
	Если Параметры.Свойство("Полномочия") И ЗначениеЗаполнено(Параметры.Полномочия) Тогда 
		
		Полномочия = Параметры.Полномочия;
		Заголовок = СтрШаблон(НСтр("ru = 'Кому выданы полномочия ""%1""'; en = 'Who issued the privileges ""%1""'"), Полномочия);
		
		Список.Отбор.Элементы.Очистить();
		
		НовыйОтбор = Список.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		НовыйОтбор.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Полномочия");
		НовыйОтбор.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
		НовыйОтбор.ПравоеЗначение = Полномочия;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийТабличногоПоляСписок

&НаКлиенте
Процедура СписокПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)

	Отказ = Истина;

	РаботаСПользователямиКлиент.ВыбратьКонтейнерПользователей(Элемент, Неопределено);

КонецПроцедуры

&НаКлиенте
Процедура СписокПередУдалением(Элемент, Отказ)

	Отказ = Истина;

	УдалитьЗаписьОПолномочиях();

КонецПроцедуры

&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)

	СтандартнаяОбработка = Ложь;

	ТекДанные = Элементы.Список.ТекущиеДанные;

	ПоказатьЗначение( ,ТекДанные.Владелец);

КонецПроцедуры

&НаКлиенте
Процедура СписокОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если Не ТипКонтейнеровПользователей.СодержитТип(ТипЗнч(ВыбранноеЗначение)) Тогда
		Возврат
	КонецЕсли;
	
	ДобавитьПолномочияНаСервере(ВыбранноеЗначение, Полномочия);
	Элементы.Список.Обновить();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ДобавитьПолномочияНаСервере(Владелец, Полномочия)
	
	РегистрыСведений.ПолномочияПользователей.ДобавитьПолномочия(Владелец, Полномочия);
	
КонецПроцедуры


&НаКлиенте
Процедура УдалитьЗаписьОПолномочиях()
	
	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ОбработчикЗавершения = Новый ОписаниеОповещения("ОбработатьРешениеПользователя", ЭтаФорма);
	
	ПоказатьВопрос(ОбработчикЗавершения, НСтр("ru = 'Полномочия будут удалены. Продолжить?'; en = 'Privileges will be removed. Do you want to continue?'"), РежимДиалогаВопрос.ДаНет);

КонецПроцедуры 

&НаКлиенте
Процедура ОбработатьРешениеПользователя(Результат, ДопПараметры) Экспорт
	
	Если Не Результат = КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	
	УдалитьПолномочияНаСервере(ТекущиеДанные.Владелец, ТекущиеДанные.Полномочия);
	
	Элементы.Список.Обновить();
	
КонецПроцедуры

&НаСервере
Процедура УдалитьПолномочияНаСервере(Владелец, Полномочия)
	
	РегистрыСведений.ПолномочияПользователей.УдалитьПолномочия(Владелец, Полномочия);
	
КонецПроцедуры

#КонецОбласти
