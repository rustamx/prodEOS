
// Помещает указанное сообщение в очередь для указанной интегрированной системы
//
Процедура ПоместитьСообщениеВОчередь(ИнтегрированнаяСистема, Сообщение) Экспорт
	
	МенеджерЗаписиОчередей = РегистрыСведений.ОчередиСообщенийОбменаСИнтегрированнымиСистемами.СоздатьМенеджерЗаписи();
	МенеджерЗаписиОчередей.ИнтегрированнаяСистема = ИнтегрированнаяСистема;
	МенеджерЗаписиОчередей.Прочитать();
	
	Если МенеджерЗаписиОчередей.Выбран() Тогда
		Сообщение.Очередь = МенеджерЗаписиОчередей.Очередь;
	Иначе		
		// Если очереди для этой интегрированной системы еще нет, то она создается
		МенеджерЗаписиОчередей.ИнтегрированнаяСистема = ИнтегрированнаяСистема;
		
		НоваяОчередь = Справочники.ОчередиСообщенийИнтегрированныхСистем.СоздатьЭлемент();
		НоваяОчередь.Наименование = 
				Строка(МенеджерЗаписиОчередей.ИнтегрированнаяСистема);
		НоваяОчередь.Записать();
		
		МенеджерЗаписиОчередей.Очередь = НоваяОчередь.Ссылка;
		МенеджерЗаписиОчередей.Записать();
		
		Сообщение.Очередь = НоваяОчередь.Ссылка;
	КонецЕсли;
	
	Сообщение.Записать();
	
КонецПроцедуры

// Получает количество сообщений в очереди для указанной интегрированной системы
//
Функция ПолучитьКоличествоСообщенийВОчереди(ИнтегрированнаяСистема) Экспорт
	                                                                                  
	УстановитьПривилегированныйРежим(Истина);
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	КОЛИЧЕСТВО(СообщенияИнтегрированныхСистем.Ссылка) КАК Колво
		|ИЗ
		|	Справочник.СообщенияИнтегрированныхСистем КАК СообщенияИнтегрированныхСистем
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ОчередиСообщенийОбменаСИнтегрированнымиСистемами КАК ОчередиСообщенийОбменаСИнтегрированнымиСистемами
		|		ПО СообщенияИнтегрированныхСистем.Очередь = ОчередиСообщенийОбменаСИнтегрированнымиСистемами.Очередь
		|ГДЕ
		|	ОчередиСообщенийОбменаСИнтегрированнымиСистемами.ИнтегрированнаяСистема = &ИнтегрированнаяСистема
		|	И СообщенияИнтегрированныхСистем.ПометкаУдаления = ЛОЖЬ";
	Запрос.УстановитьПараметр("ИнтегрированнаяСистема", ИнтегрированнаяСистема);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Колво;
	Иначе
		Возврат 0;
	КонецЕсли;
			
КонецФункции


