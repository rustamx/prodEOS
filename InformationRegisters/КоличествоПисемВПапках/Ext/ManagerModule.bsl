// Удаляет все записи с указанной папкой
Процедура ОчиститьЗаписиПоПапке(Папка) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Набор = РегистрыСведений.КоличествоПисемВПапках.СоздатьНаборЗаписей();
	Набор.Отбор.Папка.Установить(Папка);
	Набор.Записать(Истина);
	
КонецПроцедуры

// Добавляет запись
Процедура ДобавитьЗапись(Папка, Пользователь, Всего, Прочтено) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запись = РегистрыСведений.КоличествоПисемВПапках.СоздатьМенеджерЗаписи();
	Запись.Папка = Папка;
	Запись.Пользователь = Пользователь;
	Запись.Всего = Всего;
	Запись.Прочтено = Прочтено;
	Запись.Записать(Истина);
	
КонецПроцедуры

// Возвращает структуру записи регистра, Если Запись не найдена, то возвращает Неопределено.
Функция ПолучитьЗапись(Папка, Пользователь) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запись = РегистрыСведений.КоличествоПисемВПапках.СоздатьМенеджерЗаписи();
	Запись.Папка = Папка;
	Запись.Пользователь = Пользователь;
	Запись.Прочитать();
	
	Если Не Запись.Выбран() Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Возврат Новый Структура("Папка, Пользователь, Всего, Прочтено",
		Запись.Папка,
		Запись.Пользователь,
		Запись.Всего,
		Запись.Прочтено);
	
КонецФункции


