
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	УчетныеЗаписиЭлектроннойПочты.Ссылка
		|ИЗ
		|	Справочник.УчетныеЗаписиЭлектроннойПочты КАК УчетныеЗаписиЭлектроннойПочты
		|ГДЕ
		|	УчетныеЗаписиЭлектроннойПочты.ВариантИспользования = ЗНАЧЕНИЕ(Перечисление.ВариантыИспользованияПочты.Встроенная)
		|	И НЕ УчетныеЗаписиЭлектроннойПочты.ПометкаУдаления";
		
	МассивУчетныхЗаписей = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
	КоличествоУчетныхЗаписей = МассивУчетныхЗаписей.Количество();
	КоличествоУчетныхЗаписей_ЕдиницаИзмеренеия = 
		ОбщегоНазначенияДокументооборотКлиентСервер.ПредметИсчисленияПрописью(
			КоличествоУчетныхЗаписей,
			НСтр("ru = 'учетная запись'; en = 'account'") + "," + НСтр("ru = 'учетные записи'; en = 'accounts'") + "," + НСтр("ru = 'учетных записей'; en = 'accounts'"));
			
	ЧислоЗаданий = ?(Цел(КоличествоУчетныхЗаписей/50) = КоличествоУчетныхЗаписей/50,
		КоличествоУчетныхЗаписей/50, Цел(КоличествоУчетныхЗаписей/50) + 1);
	ЧислоЗаданий = Мин(ЧислоЗаданий, 10);
	
	ЧислоЗаданий_ЕдиницаИзмеренеия = 
		ОбщегоНазначенияДокументооборотКлиентСервер.ПредметИсчисленияПрописью(
			ЧислоЗаданий,
			НСтр("ru = 'поток'; en = 'thread'") + "," + НСтр("ru = 'потока'; en = 'threads'") + "," + НСтр("ru = 'потоков'; en = 'threads'"));
			
	Элементы.РекомендуемоеКоличество.Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'В программе %1 %2.
              |Рекомендуется использовать %3 %4 приемки почты.';
              |en = 'There are %1 %2.
              |It is recommended that you use the %3 %4 email receiving.'"),
			КоличествоУчетныхЗаписей,
			КоличествоУчетныхЗаписей_ЕдиницаИзмеренеия,
			ЧислоЗаданий,
			ЧислоЗаданий_ЕдиницаИзмеренеия);
	
КонецПроцедуры

&НаКлиенте
Процедура ОК(Команда)
	
	Закрыть(ЧислоЗаданий);
	
КонецПроцедуры


