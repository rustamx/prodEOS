
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ДатаОтложенногоСтарта = Параметры.ДатаОтложенногоСтарта;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ОбновитьОтображениеИнтервала();
	
	ОбновитьСписокВыбораВремени();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ДатаОтложенногоСтартаПриИзменении(Элемент)
	
	ОбновитьОтображениеИнтервала();
	
	ОбновитьСписокВыбораВремени();
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаОтложенногоСтартаВремяПриИзменении(Элемент)
	
	ОбновитьОтображениеИнтервала();
	
	ОбновитьСписокВыбораВремени();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Готово(Команда)
	
	Если ДатаОтложенногоСтарта < ТекущаяДата() Тогда
		
		ТекстСообщения = НСтр("ru = 'Дата и время отложенного старта должны быть больше текущей даты.'; en = 'Deferred start date and time must be greater than current date.'");
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			ТекстСообщения,,
			"ДатаОтложенногоСтарта");
			
		Возврат;
		
	КонецЕсли;
	
	Закрыть(ДатаОтложенногоСтарта);
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	Закрыть();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Заполняет заголовок декорации ДекорацияИнтервал.
//
&НаКлиенте
Процедура ОбновитьОтображениеИнтервала()
	
	Если Не ЗначениеЗаполнено(ДатаОтложенногоСтарта) Тогда
		
		Элементы.ДекорацияИнтервал.Заголовок = "";
		
		Возврат;
		
	КонецЕсли;
	
	ТекущаяДата = ТекущаяДата();
	
	СимволНаправления = ?(ТекущаяДата < ДатаОтложенногоСтарта, "+", "-");
	
	Если ТекущаяДата > ДатаОтложенногоСтарта Тогда
		РазностьДат = ДелопроизводствоКлиентСервер.РазностьДатВДнях(ТекущаяДата, ДатаОтложенногоСтарта);
	Иначе
		РазностьДат = ДелопроизводствоКлиентСервер.РазностьДатВДнях(ДатаОтложенногоСтарта, ТекущаяДата);
	КонецЕсли;
	
	Элементы.ДекорацияИнтервал.Заголовок = 
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("(%1 %2)",
			СимволНаправления,
			РазностьДат);
			
КонецПроцедуры

// Заполняет список выбора для элемента формы ДатаОтложенногоСтартаВремя значениями
// времени.
//
&НаКлиенте
Процедура ОбновитьСписокВыбораВремени()
	
	Элементы.ДатаОтложенногоСтартаВремя.СписокВыбора.Очистить();
	
	ПустаяДата = НачалоДня(ДатаОтложенногоСтарта);
	
	Для Инд = 1 По 48 Цикл
		
		Элементы.ДатаОтложенногоСтартаВремя.СписокВыбора.Добавить(ПустаяДата, Формат(ПустаяДата, "ДФ=ЧЧ:мм"));
		
		ПустаяДата = ПустаяДата + 1800;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти
