
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("Отбор") И Параметры.Отбор.Свойство("Транспорт") Тогда
		Элементы.Транспорт.Видимость = Ложь;
	КонецЕсли;	
	
	Если Параметры.Свойство("Отбор") И Параметры.Отбор.Свойство("Адресат") Тогда
		Элементы.Адресат.Видимость = Ложь;
		Адресат = Параметры.Отбор.Адресат;
	КонецЕсли;	
	
	Если Параметры.Свойство("Заголовок") Тогда
		Заголовок = Параметры.Заголовок;
	КонецЕсли;	
	
	Если Параметры.Свойство("ЭтоПредставлениеВСВД") Тогда
		ЭтоПредставлениеВСВД = Параметры.ЭтоПредставлениеВСВД;
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	
	Если ЭтоПредставлениеВСВД Тогда
		
		Отказ = Истина;
		
		ЗначенияЗаполненияПараметр = Новый Структура("Адресат", Адресат);
		ПараметрыФормы = Новый Структура("ЗначенияЗаполнения, Заголовок, ЭтоПредставлениеВСВД", 
			ЗначенияЗаполненияПараметр, Заголовок, ЭтоПредставлениеВСВД);
		ОткрытьФорму("РегистрСведений.УчастникиСВД.ФормаЗаписи", ПараметрыФормы);
		
	КонецЕсли;	
	
КонецПроцедуры

&НаСервере
Функция ПолучитьКлюч(Адресат, Транспорт)
	
	ПараметрыКлюча = Новый Структура("Адресат, Транспорт", 
		Адресат, Транспорт);
	Ключ = РегистрыСведений.УчастникиСВД.СоздатьКлючЗаписи(ПараметрыКлюча);
	Возврат Ключ;
	
КонецФункции

&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если ЭтоПредставлениеВСВД Тогда
		
		СтандартнаяОбработка = Ложь;
		
		Ключ = ПолучитьКлюч(Элементы.Список.ТекущиеДанные.Адресат, Элементы.Список.ТекущиеДанные.Транспорт);
		ПараметрыФормы = Новый Структура("Ключ, ЭтоПредставлениеВСВД", Ключ, ЭтоПредставлениеВСВД);
		
		ПараметрыФормы.Вставить("Заголовок", Заголовок);
		
		ОткрытьФорму("РегистрСведений.УчастникиСВД.ФормаЗаписи", ПараметрыФормы);
		
	КонецЕсли;	

КонецПроцедуры


