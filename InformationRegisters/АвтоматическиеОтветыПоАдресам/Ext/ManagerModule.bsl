
// Добавляет запись о факте отправки автоответа указанным правилом на указанный адрес.
// Параметры:
//	Правило - ссылка на правило обработки писем
//	Адрес - email, на который направлен автоответ
//	Письмо - ссылкан а исходящее письмо-автоответ
Процедура ДобавитьЗапись(Правило, Адрес, Письмо) Экспорт
	
	МенеджерЗаписи = РегистрыСведений.АвтоматическиеОтветыПоАдресам.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.Правило = Правило;
	МенеджерЗаписи.Адрес = Адрес;
	МенеджерЗаписи.Письмо = Письмо;
	МенеджерЗаписи.Записать();
	
КонецПроцедуры

// Проверяет наличие отправленного автоответа указанным правилом на указанный адрес
// Параметры:
//	Правило - ссылка на правило обработки писем
//	Адрес - адрес электронной почты
Функция ЕстьЗаписиПоПравилуИАдресу(Правило, Адрес) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	АвтоматическиеОтветыПоАдресам.Письмо
		|ИЗ
		|	РегистрСведений.АвтоматическиеОтветыПоАдресам КАК АвтоматическиеОтветыПоАдресам
		|ГДЕ
		|	АвтоматическиеОтветыПоАдресам.Правило = &Правило
		|	И АвтоматическиеОтветыПоАдресам.Адрес = &Адрес";
	Запрос.УстановитьПараметр("Правило", Правило);
	Запрос.УстановитьПараметр("Адрес", Адрес);
	
	Возврат Не Запрос.Выполнить().Пустой();
	
КонецФункции

// Удаляет все записи об автоответах по указанному правилу
// Параметры:
//	Правило - ссылка на правило обработки писем
Процедура УдалитьЗаписиПоПравилу(Правило) Экспорт
	
	НаборЗаписей = РегистрыСведений.АвтоматическиеОтветыПоАдресам.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Правило.Установить(Правило);
	НаборЗаписей.Отбор.Правило.Использование = Истина;
	НаборЗаписей.Записать();
	
КонецПроцедуры

// Проверяет наличие записей в регистре по письму.
// Наличие записи означает что данное письмо является автоответом.
// Параметры:
//	Письмо - ссылка на письмо
Функция ПисьмоЯвляетсяАвтоОтветом(Письмо) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	АвтоматическиеОтветыПоАдресам.Правило
		|ИЗ
		|	РегистрСведений.АвтоматическиеОтветыПоАдресам КАК АвтоматическиеОтветыПоАдресам
		|ГДЕ
		|	АвтоматическиеОтветыПоАдресам.Письмо = &Письмо";
	Запрос.УстановитьПараметр("Письмо", Письмо);
	
	Возврат Не Запрос.Выполнить().Пустой();
	
КонецФункции


