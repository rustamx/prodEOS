#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Получает скрипт (выражение на встроенном языке) по схеме и идентификатору.
//
// Параметры:
//  Схема - СправочникСсылка.СхемыКомплексныхПроцессов - ссылка на схему, в которой
//          используется текущей скрипт.
//  ИдентификаторСкрипта - Уникальный Идентификатор - Идентификатор скрипта в разрезе схемы.
//
// Возвращаемое значение:
//  Строка - скрипт (выражение на встроенном языке).
//
Функция СкриптСхемы(Схема, ИдентификаторСкрипта) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Скрипт = "";
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	СкриптыСхемКомплексныхПроцессов.Скрипт
		|ИЗ
		|	РегистрСведений.СкриптыСхемКомплексныхПроцессов КАК СкриптыСхемКомплексныхПроцессов
		|ГДЕ
		|	СкриптыСхемКомплексныхПроцессов.Схема = &Схема
		|	И СкриптыСхемКомплексныхПроцессов.ИдентификаторСкрипта = &ИдентификаторСкрипта";
	
	Запрос.УстановитьПараметр("Схема", Схема);
	Запрос.УстановитьПараметр("ИдентификаторСкрипта", ИдентификаторСкрипта);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		Скрипт = Выборка.Скрипт;
	КонецЕсли;
	
	Возврат Скрипт;
	
КонецФункции

// Записывает скрипт в базу.
// Если у пользователя нет прав, то будет выдано исключение.
//
// Параметры:
//  Схема - СправочникСсылка.СхемыКомплексныхПроцессов - ссылка на схему, в которой используется текущей Скрипт.
//  ИдентификаторСкрипта - УникальныйИдентификатор - Идентификатор скрипта в разрезе схемы.
//  Скрипт - Строка - скрипт (выражение на встроенном языке) схемы.
//
Процедура ЗаписатьСкриптСхемы(Схема, ИдентификаторСкрипта, Скрипт) Экспорт
	
	Запись = СоздатьМенеджерЗаписи();
	Запись.Схема = Схема;
	Запись.ИдентификаторСкрипта = ИдентификаторСкрипта;
	Запись.Скрипт = Скрипт;
	
	Запись.Записать();
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
