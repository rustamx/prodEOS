
&НаСервере
Процедура ПриЗагрузкеПользовательскихНастроекНаСервере(Настройки)
	
	Если ЗначениеЗаполнено(Контрагент) Тогда 
		ПараметрКонтрагент = Новый ПараметрКомпоновкиДанных("Контрагент"); 
		ЭлементНастроек = Отчет.КомпоновщикНастроек.Настройки.ПараметрыДанных.НайтиЗначениеПараметра(ПараметрКонтрагент);
		Если ЭлементНастроек <> Неопределено Тогда 
			ЭлементНастроек.Значение = Контрагент;
			ЭлементНастроек.Использование = Истина;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеВариантаНаСервере(Настройки)
	
	Если ЗначениеЗаполнено(Контрагент) Тогда 
		ПараметрКонтрагент = Новый ПараметрКомпоновкиДанных("Контрагент"); 
		
		ЭлементНастроек = Отчет.КомпоновщикНастроек.Настройки.ПараметрыДанных.НайтиЗначениеПараметра(ПараметрКонтрагент);
		Если ЭлементНастроек <> Неопределено Тогда 
			ЭлементНастроек.Значение = Контрагент;
			ЭлементНастроек.Использование = Истина;
		КонецЕсли;
		
		ПараметрЗаголовок = Новый ПараметрКомпоновкиДанных("Заголовок");
		ЭлементНастроек = Отчет.КомпоновщикНастроек.Настройки.ПараметрыВывода.НайтиЗначениеПараметра(ПараметрЗаголовок);
		Если ЭлементНастроек <> Неопределено Тогда 
			ЭтаФорма.Заголовок = ЭлементНастроек.Значение;
			ЭлементНастроек.Использование = Истина;
			НаименованиеКонтрагента = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Контрагент, "Наименование");
			Если СтрНайти(ЭлементНастроек.Значение, НаименованиеКонтрагента) = 0 Тогда 
				ЭлементНастроек.Значение = ЭлементНастроек.Значение 
					+ ": " + ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Контрагент, "Наименование");
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	ПараметрКонтрагент = Новый ПараметрКомпоновкиДанных("Контрагент");
	ЭлементНастроек = Отчет.КомпоновщикНастроек.Настройки.ПараметрыДанных.НайтиЗначениеПараметра(ПараметрКонтрагент);
	Если ЭлементНастроек = Неопределено Или ЭлементНастроек.Использование = Ложь Тогда 
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			НСтр("ru = 'Не включен параметр ""Контрагент""'; en = 'Parameter ""Counterparty"" is not specified'"),,
			"Отчет.КомпоновщикНастроек.ПользовательскиеНастройки",,Отказ);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("Контрагент") И ЗначениеЗаполнено(Параметры.Контрагент) Тогда 
		Контрагент = Параметры.Контрагент;
	КонецЕсли;
	
КонецПроцедуры


