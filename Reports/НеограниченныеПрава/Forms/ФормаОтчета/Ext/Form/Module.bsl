// Форма принимает параметры:
//
//		ОбъектМетаданных - ИдентификаторыОбъектовМетаданных - идентификатор объекта, 
//			по правам на который нужно построить отчет.
//
//		ОбъектДанных - ссылка - объект, по правам на который нужно построить отчет.
//

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Параметры.ОбъектМетаданных) Тогда
		ОбъектМетаданных = Параметры.ОбъектМетаданных;
	ИначеЕсли ЗначениеЗаполнено(Параметры.ОбъектДанных) Тогда
		ОбъектМетаданных = ОбщегоНазначения.ИдентификаторОбъектаМетаданных(
			Параметры.ОбъектДанных.Метаданные());
	КонецЕсли;
	
	ИнициализироватьПользовательскиеНастройкиОтчета();
	Параметры.ПользовательскиеНастройки = Отчет.КомпоновщикНастроек.ПользовательскиеНастройки;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если ЗначениеЗаполнено(ОбъектМетаданных) Тогда
		
		ТекущийЭлемент = Элементы.Результат;
		Отображение = Элементы.Результат.ОтображениеСостояния;
		Отображение.Видимость                      = Истина;
		Отображение.ДополнительныйРежимОтображения = ДополнительныйРежимОтображения.Неактуальность;
		Отображение.Картинка                       = БиблиотекаКартинок.ДлительнаяОперация48;
		Отображение.Текст                          = НСтр("ru = 'Отчет формируется...'; en = 'Generating report...'");
		
		ПодключитьОбработчикОжидания("ЗапуститьФормирование", 0.1, Истина);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Сформировать(Команда)
	
	Если Не ЗначениеЗаполнено(ОбъектМетаданных) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			НСтр("ru = 'Не заполнены данные'; en = 'Data is not specified'"),,
			"ОбъектМетаданных");
		Возврат;
	КонецЕсли;
	
	ЗапуститьФормирование();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ЗапуститьФормирование()
	
	ИнициализироватьПользовательскиеНастройкиОтчета();
	СкомпоноватьРезультат();
	Результат.ПоказатьУровеньГруппировокСтрок(0);
	ТекущийЭлемент = Элементы.СформироватьОтчет;
	
КонецПроцедуры

// Инициализирует пользовательские настройки компоновщика по переданным параметрам
//
&НаСервере
Процедура ИнициализироватьПользовательскиеНастройкиОтчета()
	
	НовыеПараметры = Новый Соответствие;
	НовыеПараметры.Вставить(Новый ПараметрКомпоновкиДанных("ОбъектМетаданных"), ОбъектМетаданных);
	
	// Устанавливаем заголовок
	Для Каждого ПараметрВывода Из Отчет.КомпоновщикНастроек.Настройки.ПараметрыВывода.Элементы Цикл
		Если ПараметрВывода.Параметр = Новый ПараметрКомпоновкиДанных("Заголовок") Тогда
			ПараметрВывода.Значение = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Полномочия, дающие неограниченные права на:
				|%1';
				|en = 'Privileges providing unlimited permissions for: 
				|%1'"),
				ОбъектМетаданных);
		КонецЕсли;
	КонецЦикла;
	
	// Устанавливаем в пользовательские поля
	Для Каждого Элемент Из Отчет.КомпоновщикНастроек.ПользовательскиеНастройки.Элементы Цикл
		Значение = НовыеПараметры[Элемент.Параметр];
		Если Значение<>Неопределено Тогда
			Элемент.Использование = Истина;
			Элемент.Значение      = Значение;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти
