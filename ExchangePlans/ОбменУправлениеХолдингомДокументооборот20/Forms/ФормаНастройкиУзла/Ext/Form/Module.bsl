#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	ОбменДаннымиСервер.ФормаНастройкиУзлаПриСозданииНаСервере(ЭтаФорма, "ОбменУправлениеХолдингомДокументооборот20");
	
	ВременнаяТаблица = ЗаявкиНаОперации.Выгрузить();
	ЗаявкиНаОперации.Очистить();
	
	ПоддерживаемыеТипы = ПланыОбмена.ОбменУправлениеХолдингомДокументооборот20.ПоддерживаемыеТипыОпераций();
	Для Каждого КлючИЗначение из ПоддерживаемыеТипы Цикл
		
		Строка = ЗаявкиНаОперации.Добавить();
		Строка.ТипОперации = КлючИЗначение.Ключ;
		Строка.ТипОперацииПредставление = КлючИЗначение.Значение;
		
		СтруктураПоиска = Новый Структура("ТипОперации", Строка.ТипОперации);
		НастроенныеСтроки = ВременнаяТаблица.НайтиСтроки(СтруктураПоиска);
		Если НастроенныеСтроки.Количество() <> 0 Тогда
			Строка.ВидДокумента = НастроенныеСтроки[0].ВидДокумента;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если ОбщегоНазначенияДокументооборотКлиент.ПередЗакрытием(
		Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка, Модифицированность) Тогда
		Возврат;
	КонецЕсли;
	
	ОбменДаннымиКлиент.ФормаНастройкиПередЗакрытием(Отказ, ЭтаФорма, ЗавершениеРаботы);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаОК(Команда)
	
	// Удалим незаполненные строки.
	Н = 0;
	Пока Н < ЗаявкиНаОперации.Количество() Цикл
		Строка = ЗаявкиНаОперации.Получить(Н);
		Если ЗначениеЗаполнено(Строка.ВидДокумента) Тогда
			Н = Н + 1;
		Иначе
			ЗаявкиНаОперации.Удалить(Н);
		КонецЕсли;
	КонецЦикла;
	
	ОграничиватьВыгрузкуОрганизациями = Организации.Количество() <> 0;
	ВыгружатьДоговоры = Договоры.Количество() <> 0;
	ВыгружатьЗаявкиНаОперации = ЗаявкиНаОперации.Количество() <> 0;
	
	ОбменДаннымиКлиент.ФормаНастройкиУзлаКомандаЗакрытьФорму(ЭтаФорма);
	
КонецПроцедуры

#КонецОбласти
