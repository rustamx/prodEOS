#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	ра_ОбщегоНазначения.ЗаполнитьВидДокумента(ЭтотОбъект);
		
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, ДанныеЗаполнения);
	КонецЕсли;
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ra_Signal") Тогда
		
		EhtapVyyavleniya			= ДанныеЗаполнения.EhtapVyyavleniya;
		VidObektaNesootvetstviya 	= ДанныеЗаполнения.VidObektaNesootvetstviya;
		VidKontrolnoyOperacii       = ДанныеЗаполнения.VidKontrolnoyOperacii;
		Proekt                      = ДанныеЗаполнения.Proekt;
		KontrolnoeMeropriyatie      = ДанныеЗаполнения.KontrolnoeMeropriyatie;
		
	КонецЕсли;
	
	VidObektaKontrolya = KontrolnoeMeropriyatie.VidPredmetaNesootvetstviya;
	
	Zayavitel = ПользователиКлиентСервер.ТекущийПользователь();
	OrganizaciyaZayavitel = Zayavitel.ра_Организация;
	PodrazdelenieZayavitel = Zayavitel.Подразделение;
	
	ЗаполнитьПоДаннымПользователя();
	
	PodrazdelenieKontroler = RukovoditelKontrolera.Подразделение;
					
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Документы.ra_ZayavkaNaKontrolnuyuOperaciyu.АктуализироватьМассивОбязательныхРеквизитов(ПроверяемыеРеквизиты, ЭтотОбъект);
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(PredshestvuyushchayaKO) Тогда
		PoryadkovyjNomerPredyavleniya = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(
			PredshestvuyushchayaKO, "PoryadkovyjNomerPredyavleniya") + 1;
	Иначе
		PoryadkovyjNomerPredyavleniya = 1;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(PodrazdelenieKontroler) Тогда
		PodrazdelenieKontroler = RukovoditelKontrolera.Подразделение;
	КонецЕсли;
		
	Если Не ЗначениеЗаполнено(PervichnayaDataPnK) Тогда
		PervichnayaDataPnK = DataPredyavleniyaNaKontrol;
	КонецЕсли;
	
	Если ОценкаПроизводительностиВызовСервераПовтИсп.ВыполнятьЗамерыПроизводительности() Тогда
		ДополнительныеСвойства.Вставить("ВремяНачала", ОценкаПроизводительностиКлиентСервер.НачатьЗамерВремени());
	КонецЕсли;
	
	ЭтоНовый = ЭтоНовый();
	
	ДополнительныеСвойства.Вставить("ЭтоНовый", ЭтоНовый);
	
	// ТСК Близнюк С.И.; 17.12.2018; task#2009{
	Если ПометкаУдаления Тогда
		ДополнительныеСвойства.Вставить("НеСоздаватьФайлСВерсией");
	КонецЕсли;
	// ТСК Близнюк С.И.; 17.12.2018; task#2009}
	
	Если VvodInformaciiOZavershivshemsyaMeropriyatii Тогда
		СостояниеПроведен = Перечисления.СостоянияДокументов.Подписан;
	Иначе
		СостояниеПроведен = Перечисления.СостоянияДокументов.ra_НаПодтверждении;
	КонецЕсли;
		
	Если ЭтоНовый Тогда
		Если РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
			ДополнительныеСвойства.Вставить("Состояние", СостояниеПроведен);
			ДополнительныеСвойства.Вставить("ОтправитьНаПодтверждение", Не VvodInformaciiOZavershivshemsyaMeropriyatii);
		Иначе
			ДополнительныеСвойства.Вставить("Состояние", Перечисления.СостоянияДокументов.ra_Проект);
		КонецЕсли;
	Иначе
		Если Не Проведен И РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
			ДополнительныеСвойства.Вставить("Состояние", СостояниеПроведен);
			ДополнительныеСвойства.Вставить("ОтправитьНаПодтверждение", Не VvodInformaciiOZavershivshemsyaMeropriyatii);
		КонецЕсли;
	КонецЕсли;
		
	ра_ОбщегоНазначения.ДокументыКачестваПередЗаписью(ЭтотОбъект);
		
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ДополнительныеСвойства.Свойство("НужноПометитьНаУдалениеБизнесСобытия") Тогда
		БизнесСобытияВызовСервера.ПометитьНаУдалениеСобытияПоИсточнику(Ссылка);
	КонецЕсли;
		
	Если ДополнительныеСвойства.Свойство("Состояние") Тогда
			
		Делопроизводство.ЗаписатьСостояниеДокумента(
			Ссылка,
			ТекущаяДатаСеанса(),
			ДополнительныеСвойства.Состояние,
			ПользователиКлиентСервер.ТекущийПользователь());
		
	КонецЕсли;
	
	Если ДополнительныеСвойства.Свойство("ОтправитьНаПодтверждение") И ДополнительныеСвойства.ОтправитьНаПодтверждение Тогда
		
		БизнесСобытияВызовСервера.ЗарегистрироватьСобытие(
			Ссылка, 
			Справочники.ВидыБизнесСобытий.ра_ПодтверждениеКО);
			
	КонецЕсли;
		
	// ТСК Близнюк С.И.; 17.12.2018; task#2009{
	Если Не ДополнительныеСвойства.Свойство("НеСоздаватьФайлСВерсией") Тогда
		Документы.ra_Nesootvetstvie.СоздатьФайлСВерсией(ЭтотОбъект);
	КонецЕсли;
	// ТСК Близнюк С.И.; 17.12.2018; task#2009}
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Процедура ДобавитьУчастниковРабочейГруппыВНабор(ТаблицаНабора) Экспорт
	
	Документы.ra_ZayavkaNaKontrolnuyuOperaciyu.ДобавитьУчастниковРабочейГруппыВНабор(ТаблицаНабора, ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ЗаполнитьПоДаннымПользователя()
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Пользователь", Zayavitel);
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 2
	|	ra_DostupnyeProekty.Proekt КАК Проект
	|ИЗ
	|	РегистрСведений.ra_DostupnyeProekty КАК ra_DostupnyeProekty
	|ГДЕ
	|	ra_DostupnyeProekty.Polzovatel = &Пользователь
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 2
	|	ra_DostupnyePloshchadki.Ploshchadka КАК Площадка
	|ИЗ
	|	РегистрСведений.ra_DostupnyePloshchadki КАК ra_DostupnyePloshchadki
	|ГДЕ
	|	ra_DostupnyePloshchadki.Polzovatel = &Пользователь";
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	Выборка = РезультатЗапроса[0].Выбрать();
	Если Выборка.Следующий() И Выборка.Количество() = 1 Тогда
		Proekt = Выборка.Проект;
	КонецЕсли;
	
	Выборка = РезультатЗапроса[1].Выбрать();
	Если Выборка.Следующий() И Выборка.Количество() = 1 Тогда
		Ploshchadka = Выборка.Площадка;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Proekt) Тогда
	
		Запрос = Новый Запрос("
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 2
		|	ra_Obekty.Ссылка КАК Obekt
		|ИЗ
		|	Справочник.ra_Obekty КАК ra_Obekty
		|ГДЕ
		|	Владелец = &Proekt
		|	И НЕ ПометкаУдаления");
		
		Запрос.УстановитьПараметр("Proekt", Proekt);
		
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() И Выборка.Количество() = 1 Тогда
			Obekt = Выборка.Obekt;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
