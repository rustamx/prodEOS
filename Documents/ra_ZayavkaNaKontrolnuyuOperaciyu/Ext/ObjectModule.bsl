#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	ра_ОбщегоНазначения.ЗаполнитьВидДокумента(ЭтотОбъект);
		
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, ДанныеЗаполнения);
	КонецЕсли;
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ra_Signal") Тогда
		
		EhtapVyyavleniya			= ДанныеЗаполнения.EhtapVyyavleniya;
		VidObektaNesootvetstviya 	= ДанныеЗаполнения.VidObektaNesootvetstviya;
		VidKontrolnoyOperacii       = ДанныеЗаполнения.VidKontrolnoyOperacii;
		Proekt                      = ДанныеЗаполнения.Proekt;
		KontrolnoeMeropriyatie      = ДанныеЗаполнения.KontrolnoeMeropriyatie;
		
	КонецЕсли;
	
	VidObektaKontrolya = KontrolnoeMeropriyatie.VidPredmetaNesootvetstviya;
	
	Zayavitel = ПользователиКлиентСервер.ТекущийПользователь();
	OrganizaciyaZayavitel = Zayavitel.ра_Организация;
	PodrazdelenieZayavitel = Zayavitel.Подразделение;
	
	ЗаполнитьПоДаннымПользователя();
	
	PodrazdelenieKontroler = RukovoditelKontrolera.Подразделение;
					
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Документы.ra_ZayavkaNaKontrolnuyuOperaciyu.АктуализироватьМассивОбязательныхРеквизитов(ПроверяемыеРеквизиты, ЭтотОбъект);
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(PredshestvuyushchayaKO) Тогда
		PoryadkovyjNomerPredyavleniya = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(
			PredshestvuyushchayaKO, "PoryadkovyjNomerPredyavleniya") + 1;
	Иначе
		PoryadkovyjNomerPredyavleniya = 1;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(PervichnayaDataPnK) Тогда
		PervichnayaDataPnK = DataPredyavleniyaNaKontrol;
	КонецЕсли;
	
	Если ОценкаПроизводительностиВызовСервераПовтИсп.ВыполнятьЗамерыПроизводительности() Тогда
		ДополнительныеСвойства.Вставить("ВремяНачала", ОценкаПроизводительностиКлиентСервер.НачатьЗамерВремени());
	КонецЕсли;
	
	ДополнительныеСвойства.Вставить("ЭтоНовый", ЭтоНовый());
	
	ра_ОбщегоНазначения.ДокументыКачестваПередЗаписью(ЭтотОбъект);
	
	//Если Не ДополнительныеСвойства.Свойство("НеРегистрироватьБизнесСобытия") Тогда
	//	
	//	ПредыдущийКонтролер = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Ссылка, "Kontroler");
	//	ТекущийКонтролер = Kontroler;
	//	
	//	Если ПредыдущийКонтролер <> ТекущийКонтролер И ЗначениеЗаполнено(ТекущийКонтролер) Тогда
	//		
	//		ДополнительныеСвойства.Вставить("ИзменениеКонтролера", Истина);
	//								
	//	КонецЕсли;
	//			
	//КонецЕсли;
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ДополнительныеСвойства.Свойство("НужноПометитьНаУдалениеБизнесСобытия") Тогда
		БизнесСобытияВызовСервера.ПометитьНаУдалениеСобытияПоИсточнику(Ссылка);
	КонецЕсли;
		
	Если ДополнительныеСвойства.ЭтоНовый Тогда
			
		Делопроизводство.ЗаписатьСостояниеДокумента(
			Ссылка,
			ТекущаяДатаСеанса(),
			Перечисления.СостоянияДокументов.ra_Проект,
			ПользователиКлиентСервер.ТекущийПользователь());
		
	КонецЕсли;
	
	//Если ДополнительныеСвойства.Свойство("ИзменениеКонтролера") И ДополнительныеСвойства.ИзменениеКонтролера Тогда
	//	
	//	Исполнители = Новый Массив;
	//	Исполнители.Добавить(Kontroler);
	//	
	//	ПараметрыПроцесса = Новый Структура("Исполнители", Исполнители);
	//	
	//	КонтекстСобытия = Новый ХранилищеЗначения(ПараметрыПроцесса);
	//	
	//	БизнесСобытияВызовСервера.ЗарегистрироватьСобытие(
	//		Ссылка, 
	//		Справочники.ВидыБизнесСобытий.ра_ИзменениеКонтролераКО,
	//		КонтекстСобытия);
	//		
	//КонецЕсли;
	//
	//Если ДополнительныеСвойства.Свойство("SendingForApproval") И ДополнительныеСвойства.SendingForApproval Тогда
	//	
	//	Исполнители = РегистрыСведений.ra_UchastnikiKontrolnyhOperaciy.УчастникиКонтрольнойОперации(Ссылка);
	//	
	//	ПараметрыПроцесса = Новый Структура("Исполнители", Исполнители);
	//	
	//	КонтекстСобытия = Новый ХранилищеЗначения(ПараметрыПроцесса);
	//		
	//	БизнесСобытияВызовСервера.ЗарегистрироватьСобытие(
	//		Ссылка, 
	//		Справочники.ВидыБизнесСобытий.ра_СогласованиеКО,
	//		КонтекстСобытия);
	//			
	//КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Процедура ДобавитьУчастниковРабочейГруппыВНабор(ТаблицаНабора) Экспорт
	
	Документы.ra_ZayavkaNaKontrolnuyuOperaciyu.ДобавитьУчастниковРабочейГруппыВНабор(ТаблицаНабора, ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ЗаполнитьПоДаннымПользователя()
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Пользователь", Zayavitel);
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 2
	|	ra_DostupnyeProekty.Proekt КАК Проект
	|ИЗ
	|	РегистрСведений.ra_DostupnyeProekty КАК ra_DostupnyeProekty
	|ГДЕ
	|	ra_DostupnyeProekty.Polzovatel = &Пользователь
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 2
	|	ra_DostupnyePloshchadki.Ploshchadka КАК Площадка
	|ИЗ
	|	РегистрСведений.ra_DostupnyePloshchadki КАК ra_DostupnyePloshchadki
	|ГДЕ
	|	ra_DostupnyePloshchadki.Polzovatel = &Пользователь";
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	Выборка = РезультатЗапроса[0].Выбрать();
	Если Выборка.Следующий() И Выборка.Количество() = 1 Тогда
		Proekt = Выборка.Проект;
	КонецЕсли;
	
	Выборка = РезультатЗапроса[1].Выбрать();
	Если Выборка.Следующий() И Выборка.Количество() = 1 Тогда
		Ploshchadka = Выборка.Площадка;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Proekt) Тогда
	
		Запрос = Новый Запрос("
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 2
		|	ra_Obekty.Ссылка КАК Obekt
		|ИЗ
		|	Справочник.ra_Obekty КАК ra_Obekty
		|ГДЕ
		|	Владелец = &Proekt
		|	И НЕ ПометкаУдаления");
		
		Запрос.УстановитьПараметр("Proekt", Proekt);
		
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() И Выборка.Количество() = 1 Тогда
			Obekt = Выборка.Obekt;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
