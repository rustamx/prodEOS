
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("Объект") Тогда
		ЗначениеВРеквизитФормы(Параметры.Объект.ПолучитьОбъект(), "Объект");
	КонецЕсли;
		
	Если Параметры.Ключ.Пустая() Тогда
		
		// При создании произвольного ЭД на основании Присоединенного файла, структура данных файла помещается во
		// временное хранилище в обработке заполнения (в модуле объекта произвольного ЭД). Т.к. основание в созданном
		// произвольном ЭД может быть изменено пользователем, то адрес временного хранилища данных файла надо сохранить
		// в реквизите формы, что бы после записи документа можно было по этим данным создать элемент ЭДПрисоединенныеФайлы.
		
		ДокументОснование = Неопределено;
		Если Параметры.Свойство("ДокументОснование", ДокументОснование) И ЗначениеЗаполнено(ДокументОснование) Тогда
			ЗаполнитьПоДокументуОснованию(ДокументОснование);
		Иначе
			Объект.ВидЭД = Перечисления.ВидыЭД.ПроизвольныйЭД;
			Объект.ВерсияРегламентаЭДО = Перечисления.ВерсииРегламентаОбмена1С.Версия10;
			Объект.ТипЭлементаВерсииЭД = Перечисления.ТипыЭлементовВерсииЭД.ПервичныйЭД;
			Объект.СостояниеЭДО = Перечисления.СостоянияВерсийЭД.НеСформирован;
			Объект.УникальныйИД = Новый УникальныйИдентификатор;
			Если НЕ ЗначениеЗаполнено(Объект.Контрагент) Тогда
				Объект.Контрагент = ОбменСКонтрагентамиПовтИсп.ПолучитьПустуюСсылку("Контрагенты");
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	ЗначениеКопирования = Неопределено;
	Если Параметры.Свойство("ЗначениеКопирования", ЗначениеКопирования)
		И ЗначениеЗаполнено(ЗначениеКопирования) Тогда
		
		Объект.ТребуетсяПодтверждение = ТребуетсяОтветнаяПодписьПоСоглашению(
			Объект.ВидЭД, Объект.Организация, Объект.Контрагент);						
	КонецЕсли;
	
	СопроводительнаяЗаписка = Объект.Текст;
	Если Объект.ДокументыОснования.Количество() = 1 
		И ЗначениеЗаполнено(Объект.ДокументыОснования[0].ДокументОснование) Тогда
		
		ТекстДокументОснование = ЗаполнитьТекстДокументаОснования(Объект.ДокументыОснования[0].ДокументОснование);
	КонецЕсли;
	
	Элементы.ЖурналСобытийЭДО.Доступность = Пользователи.ЭтоПолноправныйПользователь();
	ПерезаполнитьКомментарии();
	
	ОбновитьДанныеОВложении();
	
	ЭДОтклонен = (ЭлектронныйДокументСсылка.СтатусЭД = Перечисления.СтатусыЭД.Отклонен
			ИЛИ ЭлектронныйДокументСсылка.СтатусЭД = Перечисления.СтатусыЭД.ОтклоненПолучателем
			ИЛИ ЭлектронныйДокументСсылка.СтатусЭД = Перечисления.СтатусыЭД.ОшибкаПередачи);
	ОбновитьИнформациюПодвала();
	ОбновитьЗаголовокФормы();
	УстановитьДоступностьЭлементов();
	
	Элементы.ФормаПеренаправить.Видимость = Ложь;
	Элементы.ГруппаКомандСтандартные.Видимость = Ложь;
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	// До записи документа вложение хранится во временном хранилище, после записи документа
	// создается новый элемент справочника ЭДПрисоединенныеФайлы, в который записывается данные из временного хранилища.
	ДанныеВложенияОбновлены = ДобавитьВложениеИзВременногоХранилищаПослеЗаписиДокумента();

	ОбновитьЗаголовокФормы();
	
	Если ОбновитьРеквизитыВложения И НЕ ДанныеВложенияОбновлены Тогда
		ДобавитьОбновитьПрисоединенныеФайлыКСообщению();
	КонецЕсли;
	УстановитьДоступностьЭлементов();
	ОбновитьИнформациюПодвала();
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	Если ЭтоАдресВременногоХранилища(АдресВременногоХранилищаВложения) Тогда
		ДанныеФайла = ДанныеФайлаВложения(АдресВременногоХранилищаВложения, ЭлектронныйДокументСсылка, УникальныйИдентификатор);
		Если ДанныеФайла.ФайлРедактируется Тогда
			Отказ = Истина;
			ОписаниеОповещения = Новый ОписаниеОповещения("ПередЗаписьюПродолжитьПослеОсвобожденияВложения", ЭтотОбъект);
			ПроверитьЗахватНаРедактирование(ОписаниеОповещения, ДанныеФайла);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Если НЕ ЕстьСоглашение(ТекущийОбъект) Тогда
		Отказ = Истина;
	ИначеЕсли ЗначениеЗаполнено(Объект.Ссылка) Тогда
		// Если вложение записано, то при изменении перечисленных ниже реквизитов
		// надо принудительно перезаполнить их во вложении.
		Если  Не СтатусПередан Тогда
			ОбновитьРеквизитыВложения = (ТекущийОбъект.Контрагент <> Объект.Ссылка.Контрагент
				ИЛИ ТекущийОбъект.Организация <> Объект.Ссылка.Организация
				ИЛИ ТекущийОбъект.Номер <> Объект.Ссылка.Номер
				ИЛИ ТекущийОбъект.Дата <> Объект.Ссылка.Дата
				ИЛИ ТекущийОбъект.Текст <> Объект.Ссылка.Текст
				ИЛИ ТекущийОбъект.ДокументыОснования.Количество() <> Объект.Ссылка.ДокументыОснования.Количество());
		КонецЕсли;
	КонецЕсли;
	
	ТекущийОбъект.Текст = СокрЛП(СопроводительнаяЗаписка);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ОбновитьСостояниеЭД" Тогда
		ВыполнитьОбработкуОповещенияНаСервере();
		ОбновитьОтображениеДанных();
	ИначеЕсли ИмяСобытия = "ПроведенаПроверкаЭП" Тогда
		Для Каждого ЭД Из Параметр Цикл
			Если ЭД = ЭлектронныйДокументСсылка Тогда
				ОбновитьОтображениеДанных();
				ЗаполнитьТаблицуЭП();
				Прервать;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	
	ЗаполнитьПоДокументуОснованию(ВыбранноеЗначение);
	ЗаполнитьПоДокументуОснованию(Объект.ДокументыОснования[0].ДокументОснование);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура КонтрагентПриИзменении(Элемент)
	
	ПроверитьВозможностьИзмененияУчастникаОбмена("Контрагент");
	
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	
	ПроверитьВозможностьИзмененияУчастникаОбмена("Организация");
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаПриИзменении(Элемент)
	
	ОбновитьЗаголовокФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура НомерПриИзменении(Элемент)
	
	ОбновитьЗаголовокФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура ТребуетсяПодтверждениеПриИзменении(Элемент)
	
	Модифицированность = Истина;
	ОбновитьСтатусСостояниеЭД();
	
КонецПроцедуры

&НаКлиенте
Процедура ЭПВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если СтрНайти(Поле.Имя, "ЭПКомуВыданСертификат")>0 Тогда
		ДобавитьСертификатВДоверенные(Элемент.ТекущиеДанные);
		Если Элемент.ТекущиеДанные <> Неопределено И НЕ Элемент.ТекущиеДанные.ОтсутствуетВСписке Тогда
			ПоказатьСертификат(Элемент.ТекущиеДанные.НомерСтроки, Элемент.ТекущиеДанные.Отпечаток);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЭППриАктивизацииСтроки(Элемент)
	
	Если Элементы.ЭП.ТекущиеДанные <> Неопределено Тогда
		Элементы.ДоверятьСертификату.Доступность = Элементы.ЭП.ТекущиеДанные.ОтсутствуетВСписке;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТекстВложениеНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ДанныеФайла = ДанныеФайлаВложения(АдресВременногоХранилищаВложения, ЭлектронныйДокументСсылка, УникальныйИдентификатор);
	
	Если НЕ ПроверитьДействиеРазрешено(ДанныеФайла) Тогда
		Возврат;
	КонецЕсли;
	
	ФайлРедактируется = ДанныеФайла.ФайлРедактируется И ДанныеФайла.ФайлРедактируетТекущийПользователь;
	ПрисоединенныеФайлыКлиент.ОткрытьФайл(ДанныеФайла, ФайлРедактируется);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбъектДополнительнаяИнформацияПриИзменении(Элемент)
	
	Модифицированность = Истина;
	Оповестить("ОбновитьСостояниеЭД");
	
КонецПроцедуры

&НаКлиенте
Процедура НадписьДокументОснованиеОтсутствуетНажатие(Элемент)
	
	СписокТиповДокументовОснований = СписокТиповДокументовОснованийПроизвольногоДокумента();
	Если СписокТиповДокументовОснований.Количество() > 0 Тогда
		ОбработкаПродолжения = Новый ОписаниеОповещения("ВыборДокументаОснованияПродолжить", ЭтотОбъект);
		Если СписокТиповДокументовОснований.Количество() = 1 Тогда
			ВыполнитьОбработкуОповещения(ОбработкаПродолжения, СписокТиповДокументовОснований[0].Значение);
		Иначе
			ЗаголовокСпискаВыбора = НСтр("ru = 'Выберите тип документа основания...'; en = 'Select type of source document...'");
			ЭлементПоУмолчанию = СписокТиповДокументовОснований.НайтиПоЗначению("ПроизвольныйЭД");
			СписокТиповДокументовОснований.ПоказатьВыборЭлемента(ОбработкаПродолжения, ЗаголовокСпискаВыбора, ЭлементПоУмолчанию);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДокументОснованиеНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Если ЗначениеЗаполнено(Объект.ДокументыОснования[0].ДокументОснование) Тогда
		ДокументОснование = Объект.ДокументыОснования[0].ДокументОснование;
		Если ТипЗнч(ДокументОснование) = Тип("СправочникСсылка.ВерсииФайлов") Тогда
			Файл = ОбщегоНазначенияДокументооборотВызовСервера.ЗначениеРеквизитаОбъекта(ДокументОснование,"Владелец");
			ДокументОснование = ОбщегоНазначенияДокументооборотВызовСервера.ЗначениеРеквизитаОбъекта(Файл ,"ВладелецФайла");
		КонецЕсли;

		ПоказатьЗначение(, ДокументОснование);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ГиперссылкаОчиститьОснованиеНажатие(Элемент)
	
	ТекстВопроса = НСтр("ru = 'Убрать документ-основание?'; en = 'Remove source document?'");
	ОписаниеОповещения = Новый ОписаниеОповещения("ОбработатьОтветНаВопросОчисткиДокументаОснования", ЭтотОбъект);
	ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОтменитьОтклонение(Команда)
	
	Если ЭДОтклонен Тогда
		ОбработкаПродолжения = Новый ОписаниеОповещения("ОбработатьОтменуОтклоненияЭД", ЭтотОбъект);
		ПроверитьЗаписьДокумента(, ОбработкаПродолжения);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПодписатьИОтправить(Команда)
	
	ОчиститьСообщения();
	
	Если ПроверитьЗаполнение() И (ЗначениеЗаполнено(ЭлектронныйДокументСсылка)
									ИЛИ ЭтоАдресВременногоХранилища(АдресВременногоХранилищаВложения)) Тогда
		
		ДанныеФайла = ДанныеФайлаВложения(АдресВременногоХранилищаВложения, ЭлектронныйДокументСсылка, УникальныйИдентификатор);
		ОбработкаПродолженияПослеПроверки = Новый ОписаниеОповещения("ЗакончитьПодписаниеДокумента", ЭтотОбъект, Истина);
		ОбработкаПродолжения = Новый ОписаниеОповещения("ПроверитьЗаписьДокумента", ЭтотОбъект, ОбработкаПродолженияПослеПроверки);
		ПроверитьЗахватНаРедактирование(ОбработкаПродолжения, ДанныеФайла);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПодписатьДокумент(Команда)
	
	Если ПроверитьЗаполнение() И (ЗначениеЗаполнено(ЭлектронныйДокументСсылка)
									ИЛИ ЭтоАдресВременногоХранилища(АдресВременногоХранилищаВложения)) Тогда
		ДанныеФайла = ДанныеФайлаВложения(АдресВременногоХранилищаВложения, ЭлектронныйДокументСсылка, УникальныйИдентификатор);
		ОбработкаПродолженияПослеПроверки = Новый ОписаниеОповещения("ЗакончитьПодписаниеДокумента", ЭтотОбъект, Ложь);
		ОбработкаПродолжения = Новый ОписаниеОповещения("ПроверитьЗаписьДокумента", ЭтотОбъект, ОбработкаПродолженияПослеПроверки);
		ПроверитьЗахватНаРедактирование(ОбработкаПродолжения, ДанныеФайла);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Отклонить(Команда)
	
	Если НЕ ЭДОтклонен И ЗначениеЗаполнено(ЭлектронныйДокументСсылка) Тогда
		ОбработкаПродолжения = Новый ОписаниеОповещения("ОтклонитьАннулироватьЭД", ЭтотОбъект, Истина);
		ПроверитьЗаписьДокумента(, ОбработкаПродолжения);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьПодписи(Команда)
	
	ОчиститьСообщения();
	Если ЗначениеЗаполнено(ЭлектронныйДокументСсылка) Тогда
		ОбменСКонтрагентамиСлужебныйКлиент.ОпределитьСтатусыПодписей(ЭлектронныйДокументСсылка);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьСертификат(Команда)
	
	Если Элементы.ЭП.ТекущиеДанные <> Неопределено Тогда
		ПоказатьСертификат(Элементы.ЭП.ТекущиеДанные.НомерСтроки, Элементы.ЭП.ТекущиеДанные.Отпечаток);
	Иначе
		ОчиститьСообщения();
		ТекстОшибки = НСтр("ru = 'Выберите сертификат в списке установленных подписей.'; en = 'Select the certificate from the list of installed signatures.'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДоверятьЭтомуСертификату(Команда)
	
	ДобавитьСертификатВДоверенные(Элементы.ЭП.ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьКомментарий(Команда)
	
	Если ЗначениеЗаполнено(ЭлектронныйДокументСсылка) Тогда
		СтруктураПараметров = Новый Структура("Описание", Комментарий);
		ИзменитьЗначенияРеквизитовНаСервере(ЭлектронныйДокументСсылка, СтруктураПараметров);
		Комментарий = "";
		Оповестить("ОбновитьСостояниеЭД");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Перенаправить(Команда)
	
	ОбработкаПродолжения = Новый ОписаниеОповещения("ОбработатьПеренаправлениеЭД", ЭтотОбъект);
	ПроверитьЗаписьДокумента(, ОбработкаПродолжения);
	
КонецПроцедуры

&НаКлиенте
Процедура ЖурналСобытийЭДО(Команда)
	
	ПараметрыФормы = Новый Структура;
	
	Отбор = Новый Структура;
	Отбор.Вставить("ПрисоединенныйФайл", ЭлектронныйДокументСсылка);
	
	ПараметрыФормы.Вставить("Отбор", Отбор);
	ПараметрыФормы.Вставить("РежимОткрытияОкна", РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	ОткрытьФорму("РегистрСведений.ЖурналСобытийЭД.ФормаСписка", ПараметрыФормы, ЭтотОбъект);
	
КонецПроцедуры

///////////////////////////////////////////////////////////////////////////////////
// Обработчики команд вложения

&НаКлиенте
Процедура СохранитьВложениеВФайл(Команда)
	
	Если ЗначениеЗаполнено(ЭлектронныйДокументСсылка) Тогда
		ДанныеФайла = ОбменСКонтрагентамиСлужебныйВызовСервера.ПолучитьДанныеФайла(ЭлектронныйДокументСсылка, УникальныйИдентификатор);
		ПрисоединенныеФайлыКлиент.СохранитьВместеСЭП(ЭлектронныйДокументСсылка, ДанныеФайла, УникальныйИдентификатор);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Редактировать(Команда)
	
	ДанныеФайла = ДанныеФайлаВложения(АдресВременногоХранилищаВложения, ЭлектронныйДокументСсылка, УникальныйИдентификатор);
	
	Если НЕ ПроверитьДействиеРазрешено(ДанныеФайла) Тогда
		Возврат;
	КонецЕсли;
	
	Если (ДанныеФайла.ФайлРедактируется И НЕ ДанныеФайла.ФайлРедактируетТекущийПользователь)
		ИЛИ ДанныеФайла.Зашифрован ИЛИ ДанныеФайла.ПодписанЭП Тогда
		// Файл может быть изменен в другом сеансе.
		Если ДанныеФайла.Ссылка <> Неопределено Тогда
			ОповеститьОбИзменении(ДанныеФайла.Ссылка);
		КонецЕсли;
		Возврат;
	КонецЕсли;
	
	Если НЕ ДанныеФайла.ФайлРедактируется Тогда
		ЗанятьФайлДляРедактированияСервер(ДанныеФайла);
		
		Если ДанныеФайла.Ссылка = Неопределено Тогда
			АдресВременногоХранилищаВложения = ПоместитьВоВременноеХранилище(ДанныеФайла, УникальныйИдентификатор);
		Иначе
			ОповеститьОбИзменении(ДанныеФайла.Ссылка);
		КонецЕсли;
		УстановитьДоступностьЭлементов();
	КонецЕсли;
	
	ПрисоединенныеФайлыКлиент.ОткрытьФайл(ДанныеФайла, Истина);
	ВложениеРедактируется = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗакончитьРедактирование(Команда)
	
	ДанныеФайла = ДанныеФайлаВложения(АдресВременногоХранилищаВложения, ЭлектронныйДокументСсылка, УникальныйИдентификатор);
	
	Если НЕ ПроверитьДействиеРазрешено(ДанныеФайла)
		ИЛИ (НЕ ДанныеФайла.ФайлРедактируется ИЛИ НЕ ДанныеФайла.ФайлРедактируетТекущийПользователь) Тогда
		Возврат;
	КонецЕсли;
	
	ОбработкаПродолжения = Новый ОписаниеОповещения("ЗакончитьРедактированиеЗавершить", ЭтотОбъект);
	ДополнительныеПараметры = Новый Структура("ДанныеФайла, ОбработкаПродолжения", ДанныеФайла, ОбработкаПродолжения);
	ОписаниеОповещения = Новый ОписаниеОповещения("ЗакончитьРедактированиеВыполненоПомещение", ЭтотОбъект, ДополнительныеПараметры);
	ПрисоединенныеФайлыСлужебныйКлиент.ПоместитьРедактируемыйФайлНаДискеВХранилище(ОписаниеОповещения, ДанныеФайла, УникальныйИдентификатор);
	
	УдалитьВременныйФайлВложения(ДанныеФайла);
	
КонецПроцедуры

&НаКлиенте
Процедура Освободить(Команда)
	
	ОсвободитьФайл();
	УстановитьДоступностьЭлементов();
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьВложениеИзПрисоединенныхФайлов(Команда)
	
	ПрисоединенныеФайлыКлиент.ОткрытьФормуВыбораФайлов(Объект.ДокументыОснования[0].ДокументОснование, ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьВложениеИзФайлаНаДиске(Команда)
	
	ВыбранныйФайл   = "";
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ДобавитьВложениеИзФайлаНаДискеЗавершить", ЭтотОбъект);
	НачатьПомещениеФайла(ОписаниеОповещения, , ВыбранныйФайл, Истина, УникальныйИдентификатор);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Вложение

&НаКлиенте
Процедура УдалитьВременныйФайлВложения(ДанныеФайла)
	
	РабочийКаталогПользователя = ФайловыеФункцииСлужебныйКлиент.РабочийКаталогПользователя();
	ПолноеИмяФайлаНаКлиенте = РабочийКаталогПользователя + ДанныеФайла.ОтносительныйПуть + ДанныеФайла.ИмяФайла;
	УдалитьФайлы(ПолноеИмяФайлаНаКлиенте);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ДанныеФайлаВложения(АдресВременногоХранилищаВложения, ЭлектронныйДокументСсылка, УникальныйИдентификатор)
	
	ДанныеФайла = Неопределено;
	Если ЭтоАдресВременногоХранилища(АдресВременногоХранилищаВложения) Тогда
		// Файл во временном хранилище
		ДанныеФайла = ПолучитьИзВременногоХранилища(АдресВременногоХранилищаВложения);
		ДанныеФайла.Вставить("Ссылка", Неопределено);
	ИначеЕсли ЗначениеЗаполнено(ЭлектронныйДокументСсылка) Тогда
		// Файл записан в ИБ
		ДанныеФайла = ОбменСКонтрагентамиСлужебныйВызовСервера.ПолучитьДанныеФайла(ЭлектронныйДокументСсылка, УникальныйИдентификатор);
		ДанныеФайла.Вставить("Ссылка", ЭлектронныйДокументСсылка);
	КонецЕсли;
	
	Возврат ДанныеФайла;
	
КонецФункции

&НаКлиенте
Функция ПроверитьДействиеРазрешено(ДанныеФайла, Знач ТекущееДействие = "")
	
	Если ДанныеФайла = Неопределено Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если ТекущееДействие = "ПометкаУдаления" И ДанныеФайла.ФайлРедактируется Тогда
		
		Если ДанныеФайла.ФайлРедактируетТекущийПользователь Тогда
			ТекстПредупреждения = НСтр("ru = 'Действие недоступно, так как файл занят для редактирования.'; en = 'The action is not available because the file is locked for editing.'");
		Иначе
			ТекстПредупреждения = НСтр("ru = 'Действие недоступно, так как файл занят для редактирования
			                                 |другим пользователем.'; en = 'The action is not available because the file is locked for editing by another user.'");
		КонецЕсли;
		
		ПоказатьПредупреждение(, ТекстПредупреждения);
		Возврат Ложь;
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

&НаСервереБезКонтекста
Процедура ЗанятьФайлДляРедактированияСервер(ДанныеФайла)
	
	Если ДанныеФайла.Ссылка = Неопределено Тогда
		ДанныеФайла.Редактирует                        = Пользователи.АвторизованныйПользователь();
		ДанныеФайла.ФайлРедактируется                  = Истина;
		ДанныеФайла.ФайлРедактируетТекущийПользователь = Истина;
	Иначе
		ПрисоединенныеФайлыСлужебный.ЗанятьФайлДляРедактированияСервер(ДанныеФайла.Ссылка);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОсвободитьФайл()
	
	ДанныеФайла = ДанныеФайлаВложения(АдресВременногоХранилищаВложения, ЭлектронныйДокументСсылка, УникальныйИдентификатор);
	
	Если НЕ ПроверитьДействиеРазрешено(ДанныеФайла) Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ ДанныеФайла.ФайлРедактируется
	 ИЛИ НЕ ДанныеФайла.ФайлРедактируетТекущийПользователь Тогда
		Возврат;
	КонецЕсли;
	
	ОсвободитьФайлНаСервере(ДанныеФайла);
	Если ДанныеФайла.Ссылка = Неопределено Тогда
		АдресВременногоХранилищаВложения = ПоместитьВоВременноеХранилище(ДанныеФайла, УникальныйИдентификатор);
	Иначе
		ОповеститьОбИзменении(ДанныеФайла.Ссылка);
	КонецЕсли;
	УдалитьВременныйФайлВложения(ДанныеФайла);
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ОсвободитьФайлНаСервере(ДанныеФайла)
	
	Если ДанныеФайла.Ссылка = Неопределено Тогда
		ДанныеФайла.Редактирует                        = Справочники.Пользователи.ПустаяСсылка();
		ДанныеФайла.ФайлРедактируется                  = Ложь;
		ДанныеФайла.ФайлРедактируетТекущийПользователь = Ложь;
	Иначе
		ПрисоединенныеФайлыСлужебный.ОсвободитьФайл(ДанныеФайла.Ссылка);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗакончитьРедактированиеВыполненоПомещение(ИнформацияОФайле, ДополнительныеПараметры) Экспорт
	
	Если ИнформацияОФайле <> Неопределено И ИнформацияОФайле.ФайлПомещенВХранилище Тогда
		ДанныеФайла = ДополнительныеПараметры.ДанныеФайла;
		Если ДанныеФайла.Ссылка = Неопределено Тогда
			ДанныеФайла.Редактирует                        = ПредопределенноеЗначение("Справочник.Пользователи.ПустаяСсылка");
			ДанныеФайла.ФайлРедактируется                  = Ложь;
			ДанныеФайла.ФайлРедактируетТекущийПользователь = Ложь;
			ДанныеФайла.СсылкаНаДвоичныеДанныеФайла        = ИнформацияОФайле.АдресФайлаВоВременномХранилище;
			ЗаполнитьЗначенияСвойств(ДанныеФайла, ИнформацияОФайле);
			Если ТипЗнч(ИнформацияОФайле.АдресФайлаВоВременномХранилище) = Тип("ДвоичныеДанные") Тогда
				ДвоичныеДанные = ИнформацияОФайле.АдресФайлаВоВременномХранилище;
			Иначе
				ДвоичныеДанные = ПолучитьИзВременногоХранилища(ИнформацияОФайле.АдресФайлаВоВременномХранилище);
			КонецЕсли;
			ДанныеФайла.Размер = ДвоичныеДанные.Размер();
			АдресВременногоХранилищаВложения = ПоместитьВоВременноеХранилище(ДанныеФайла, УникальныйИдентификатор);
		Иначе
			ПоместитьФайлВХранилищеИОсвободить(ДанныеФайла.Ссылка, ИнформацияОФайле);
			ОповеститьОбИзменении(ДанныеФайла.Ссылка);
		КонецЕсли;
		УдалитьВременныйФайлВложения(ДанныеФайла);
		ОбновитьДанныеОВложении();
		УстановитьДоступностьЭлементов();
		ОписаниеОповещения = Неопределено;
		Если ДополнительныеПараметры.Свойство("ОбработкаПродолжения", ОписаниеОповещения) Тогда
			// Завершение редактирования файла инициировано пользователем, при подписании вложения.
			ВыполнитьОбработкуОповещения(ОписаниеОповещения, Истина);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗакончитьРедактированиеЗавершить(Результат, ДополнительныеПараметры) Экспорт
	
	Записать();
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ПоместитьФайлВХранилищеИОсвободить(Знач ПрисоединенныйФайл, Знач ИнформацияОФайле)
	
	ПрисоединенныеФайлыСлужебный.ПоместитьФайлВХранилищеИОсвободить(ПрисоединенныйФайл, ИнформацияОФайле);
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьВложениеИзФайлаНаДискеЗавершить(ФайлПомещен, АдресВХранилище, ВыбранныйФайл, ДополнительныеПараметры) Экспорт
	
	СтруктураФайла  = Новый Структура;
	Если ФайлПомещен Тогда
		ФайлКлиента = Новый Файл(ВыбранныйФайл);
		РабочийКаталогПользователя = ФайловыеФункцииСлужебныйКлиент.РабочийКаталогПользователя();
		ОтносительныйПуть = Строка(УникальныйИдентификатор);
		ОтносительныйПуть = ОбщегоНазначенияКлиентСервер.ЗаменитьНедопустимыеСимволыВИмениФайла(ОтносительныйПуть, "");
		ОтносительныйПуть = ОбщегоНазначенияКлиентСервер.ДобавитьКонечныйРазделительПути(ОтносительныйПуть);
		
		СтруктураФайла  = Новый Структура;
		
		СтруктураФайла = ОбщегоНазначенияКлиентСервер.РазложитьПолноеИмяФайла(ВыбранныйФайл);
		СтруктураФайла.ПолноеИмя = РабочийКаталогПользователя + ОтносительныйПуть + СтруктураФайла.Имя;
		СтруктураФайла.Путь = РабочийКаталогПользователя + ОтносительныйПуть;
		
		СтруктураФайла.Вставить("ИмяФайла",                           СтруктураФайла.Имя);
		СтруктураФайла.Вставить("Наименование",                       СтруктураФайла.ИмяБезРасширения);
		СтруктураФайла.Вставить("Расширение",                         ОбщегоНазначенияКлиентСервер.РасширениеБезТочки(СтруктураФайла.Расширение));
		СтруктураФайла.Вставить("Редактирует",                        Неопределено);
		СтруктураФайла.Вставить("ПодписанЭП",                         Ложь);
		СтруктураФайла.Вставить("Зашифрован",                         Ложь);
		СтруктураФайла.Вставить("ФайлРедактируется",                  Ложь);
		СтруктураФайла.Вставить("ОтносительныйПуть",                  ОтносительныйПуть);
		СтруктураФайла.Вставить("СсылкаНаДвоичныеДанныеФайла",        АдресВХранилище);
		СтруктураФайла.Вставить("ФайлРедактируетТекущийПользователь", Ложь);

		СвойстваФайла = Новый Структура("Размер,УниверсальноеВремяИзменения");
		
		ДействияСФайлом = Новый Массив;
		Действие = Новый Структура;
		Действие.Вставить("Действие", "ПолучитьСвойства");
		Действие.Вставить("Свойства", СвойстваФайла);
		Действие.Вставить("Файл", ФайлКлиента);
		ДействияСФайлом.Добавить(Действие);
		
		СтруктураФайла.Вставить("СвойстваФайла",СвойстваФайла);
		
		ФайловыеФункцииСлужебныйКлиент.ОбработатьФайл(Новый ОписаниеОповещения(
		"ДобавитьВложениеИзФайлаНаДискеЗавершитьПродолжение", ЭтотОбъект, СтруктураФайла),
		ДействияСФайлом, УникальныйИдентификатор);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьВложениеИзФайлаНаДискеЗавершитьПродолжение(РезультатДействий, СтруктураФайла) Экспорт
	
	СтруктураФайла.Вставить("Размер",                             СтруктураФайла.СвойстваФайла.Размер);
	СтруктураФайла.Вставить("ДатаМодификацииУниверсальная",       СтруктураФайла.СвойстваФайла.УниверсальноеВремяИзменения);
	СтруктураФайла.Удалить("СвойстваФайла");
	АдресВременногоХранилищаВложения = ПоместитьВоВременноеХранилище(СтруктураФайла, УникальныйИдентификатор);
	ОбновитьДанныеОВложении();
	Модифицированность = Истина;
	
КонецПроцедуры
&НаКлиенте
Процедура ПередЗаписьюПродолжитьПослеОсвобожденияВложения(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Истина Тогда
		Записать();
	Иначе
		ТекстСообщения = НСтр("ru = 'Во время выполнения операции произошла ошибка. Действие отменено.
			|Подробности см. в журнале регистрации.';
			|en = 'An error occurred while performing the operation. Action canceled.
			|For details, see Events log.'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ДобавитьВложениеИзВременногоХранилищаПослеЗаписиДокумента()
	
	ДанныеОбновлены = Ложь;
	Если ЭтоАдресВременногоХранилища(АдресВременногоХранилищаВложения) Тогда
		СтруктураФайла = ПолучитьИзВременногоХранилища(АдресВременногоХранилищаВложения);
		АдресВременногоХранилищаВложения = Неопределено;
		Если ТипЗнч(СтруктураФайла) = Тип("Структура") И СтруктураФайла.Свойство("СсылкаНаДвоичныеДанныеФайла") Тогда
			Если ЗначениеЗаполнено(ЭлектронныйДокументСсылка) Тогда
				СтруктураФайла.Вставить("АдресФайлаВоВременномХранилище", СтруктураФайла.СсылкаНаДвоичныеДанныеФайла);
				СтруктураФайла.Вставить("АдресВременногоХранилищаТекста", "");
				СтруктураФайла.Вставить("ИмяБезРасширения",               СтруктураФайла.Наименование);
				
				ПрисоединенныеФайлы.ОбновитьПрисоединенныйФайл(ЭлектронныйДокументСсылка, СтруктураФайла);
			КонецЕсли;
			ДобавитьОбновитьПрисоединенныеФайлыКСообщению(СтруктураФайла.СсылкаНаДвоичныеДанныеФайла);
			ДанныеОбновлены = Истина;
		КонецЕсли;
	КонецЕсли;
	
	Возврат ДанныеОбновлены;
	
КонецФункции

&НаСервере
Процедура ДобавитьОбновитьПрисоединенныеФайлыКСообщению(АдресВХранилище = Неопределено)
	
	ПараметрыСоглашения = ОбменСКонтрагентамиСлужебный.ОпределитьНастройкиОбменаЭДПоИсточнику(Объект.Ссылка);
	Если ЗначениеЗаполнено(ПараметрыСоглашения) Тогда
		ИдентификаторОрганизации = ПараметрыСоглашения.ИдентификаторОрганизации;
		ИдентификаторКонтрагента = ПараметрыСоглашения.ИдентификаторКонтрагента;
		СоглашениеЭД             = ПараметрыСоглашения.СоглашениеЭД;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ПараметрыСоглашения) И НЕ ЗначениеЗаполнено(СоглашениеЭД) Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураДокумента =  Новый Структура;
	Если ЗначениеЗаполнено(ЭлектронныйДокументСсылка) Тогда
		ДобавленныйФайл = ЭлектронныйДокументСсылка;
	Иначе
		Если НЕ (ЗначениеЗаполнено(ЭлектронныйДокументИмяФайлаБезРасширения)
			И ЭтоАдресВременногоХранилища(АдресВХранилище)) Тогда
			Возврат;
		КонецЕсли;
		
		ПараметрыФайла = Новый Структура();
		ПараметрыФайла.Вставить("Автор", Пользователи.АвторизованныйПользователь());
		ПараметрыФайла.Вставить("ВладелецФайлов", Объект.Ссылка);
		ПараметрыФайла.Вставить("ИмяБезРасширения", ЭлектронныйДокументИмяФайлаБезРасширения);
		ПараметрыФайла.Вставить("РасширениеБезТочки", ЭлектронныйДокументРасширение);
		ПараметрыФайла.Вставить("ВремяИзмененияУниверсальное");
				
		СсылкаНаФайл = Справочники.ЭДПрисоединенныеФайлы.ПолучитьСсылку(Новый УникальныйИдентификатор(Объект.УникальныйИД));
		
		ДобавленныйФайл = ПрисоединенныеФайлы.ДобавитьПрисоединенныйФайл(ПараметрыФайла, АдресВХранилище,,,СсылкаНаФайл);
		
		НомерДокумента = ОбменСКонтрагентамиВнутренний.ВернутьИдЭД(ДобавленныйФайл);
		СтруктураДокумента.Вставить("ТипЭлементаВерсииЭД", Перечисления.ТипыЭлементовВерсииЭД.ПервичныйЭД);
		СтруктураДокумента.Вставить("НаправлениеЭД", Перечисления.НаправленияЭД.Исходящий);
		СтруктураДокумента.Вставить("СтатусЭД", Перечисления.СтатусыЭД.Сформирован);
		СтруктураДокумента.Вставить("Отправитель", ИдентификаторОрганизации);
		СтруктураДокумента.Вставить("Получатель", ИдентификаторКонтрагента);	
		СтруктураДокумента.Вставить("НомерЭД", НомерДокумента);	
		СтруктураДокумента.Вставить("УникальныйИД", НомерДокумента);

	КонецЕсли;
		
	ОбменСКонтрагентамиСлужебный.ИзменитьПоСсылкеПрисоединенныйФайл(ДобавленныйФайл, СтруктураДокумента, Ложь);
	
	Прочитать();
	ОбновитьДанныеОВложении();
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьДанныеОВложении()
	
	Если ЭтоАдресВременногоХранилища(АдресВременногоХранилищаВложения) Тогда
		СтруктураФайла = ПолучитьИзВременногоХранилища(АдресВременногоХранилищаВложения);
		Если ТипЗнч(СтруктураФайла) = Тип("Структура") И СтруктураФайла.Свойство("СсылкаНаДвоичныеДанныеФайла") Тогда
			ЭлектронныйДокументРасширение  = ОбщегоНазначенияКлиентСервер.РасширениеБезТочки(СтруктураФайла.Расширение);
			ЭлектронныйДокументИмяФайла    = СокрЛП(СтруктураФайла.ИмяФайла);
			ЭлектронныйДокументИмяФайлаБезРасширения = СокрЛП(СтруктураФайла.Наименование);
			ЭлектронныйДокументПиктограмма = ФайловыеФункцииСлужебныйКлиентСервер.ПолучитьИндексПиктограммыФайла(СтруктураФайла.Расширение);
			Элементы.ТекстВложение.Ширина  = СтрДлина(ЭлектронныйДокументИмяФайла);
		КонецЕсли;
	ИначеЕсли ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ЗапросВложений = Новый Запрос;
		ЗапросВложений.УстановитьПараметр("ВладелецФайла", Объект.Ссылка);
		ЗапросВложений.Текст =
		"ВЫБРАТЬ
		|	ЭДПрисоединенныеФайлы.Ссылка КАК Ссылка,
		|	ЭДПрисоединенныеФайлы.Наименование КАК ИмяФайла,
		|	ЭДПрисоединенныеФайлы.Расширение КАК Расширение,
		|	ЭДПрисоединенныеФайлы.Редактирует КАК Редактирует
		|ИЗ
		|	Справочник.ЭДПрисоединенныеФайлы КАК ЭДПрисоединенныеФайлы
		|ГДЕ
		|	ЭДПрисоединенныеФайлы.ВладелецФайла = &ВладелецФайла
		|	И ЭДПрисоединенныеФайлы.ТипЭлементаВерсииЭД = ЗНАЧЕНИЕ(Перечисление.ТипыЭлементовВерсииЭД.ПервичныйЭД)
		|	И НЕ ЭДПрисоединенныеФайлы.ПометкаУдаления";
		Результат = ЗапросВложений.Выполнить().Выбрать();
		
		Если Результат.Следующий() Тогда
			ЭлектронныйДокументСсылка      = Результат.Ссылка;
			ЭлектронныйДокументРасширение  = ОбщегоНазначенияКлиентСервер.РасширениеБезТочки(Результат.Расширение);
			ЭлектронныйДокументИмяФайла    = СокрЛП(Результат.ИмяФайла) + "." + ЭлектронныйДокументРасширение;
			ЭлектронныйДокументИмяФайлаБезРасширения = СокрЛП(Результат.ИмяФайла);
			ЭлектронныйДокументПиктограмма = ФайловыеФункцииСлужебныйКлиентСервер.ПолучитьИндексПиктограммыФайла(Результат.Расширение);
			Элементы.ТекстВложение.Ширина  = СтрДлина(ЭлектронныйДокументИмяФайла);
			Если ЗначениеЗаполнено(Результат.Редактирует) И Результат.Редактирует = ПараметрыСеанса.АвторизованныйПользователь Тогда
				ВложениеРедактируется = Истина;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	УстановитьДоступностьЭлементов();
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьЗахватНаРедактирование(ОбработкаПродолжения, ДанныеФайла)
	
	Если ДанныеФайла.ФайлРедактируется Тогда
		Если ДанныеФайла.ФайлРедактируетТекущийПользователь Тогда
			// Вложение захвачено для редактирования текущим пользователем
			ДополнительныеПараметры = Новый Структура("ДанныеФайла, ОбработкаПродолжения", ДанныеФайла, ОбработкаПродолжения);
			ОписаниеОповещения = Новый ОписаниеОповещения("ПроверитьЗахватНаРедактированиеПередПодписаниемПродолжить", ЭтотОбъект, ДополнительныеПараметры);
			ТекстВопроса = НСтр("ru = 'Вложение ""%1"" захвачено для редактирования.
				|Для продолжения надо завершить редактирование.';
				|en = 'Attachment ""%1"" is locked for editing.
				|You must finish editing in order to proceed.'");
			ТекстВопроса = СтрЗаменить(ТекстВопроса, "%1", ЭлектронныйДокументИмяФайла);
			СписокКнопок = Новый СписокЗначений;
			СписокКнопок.Добавить("ЗакончитьРедактирование", НСтр("ru = 'Закончить редактирование'; en = 'Finish editing'"));
			СписокКнопок.Добавить("Освободить",              НСтр("ru = 'Отменить редактирование'; en = 'Cancel edit'"));
			СписокКнопок.Добавить("Отменить",                НСтр("ru = 'Отменить'; en = 'Cancel'"));
			ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, СписокКнопок);
		Иначе
			ТекстПредупреждения = НСтр("ru = 'Действие недоступно, так как файл занят для редактирования
				|другим пользователем.'; en = 'The action is not available because the file is locked for editing by another user.'");
			ПоказатьПредупреждение(, ТекстПредупреждения);
		КонецЕсли;
	Иначе
		ВыполнитьОбработкуОповещения(ОбработкаПродолжения);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьЗахватНаРедактированиеПередПодписаниемПродолжить(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = "Освободить" Тогда
		ОсвободитьФайл();
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОбработкаПродолжения);
	ИначеЕсли Результат = "ЗакончитьРедактирование" Тогда
		ДанныеФайла = ДополнительныеПараметры.ДанныеФайла;
		ОписаниеОповещения = Новый ОписаниеОповещения("ЗакончитьРедактированиеВыполненоПомещение", ЭтотОбъект, ДополнительныеПараметры);
		ПрисоединенныеФайлыСлужебныйКлиент.ПоместитьРедактируемыйФайлНаДискеВХранилище(ОписаниеОповещения, ДанныеФайла, УникальныйИдентификатор);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

///////////////////////////////////////////////////////////////////////////////////

#Область Основание

&НаКлиенте
Процедура ПроверитьВозможностьИзмененияУчастникаОбмена(ИмяРеквизита)
	
	Если Объект.ДокументыОснования.Количество() = 1 
		И ЗначениеЗаполнено(Объект.ДокументыОснования[0].ДокументОснование) Тогда
		
		НаименованиеУчастникаВОсновании = Неопределено;
		УчастникВОсновании = Неопределено;
		Если ЗначениеЗаполнено(Объект[ИмяРеквизита])
			И УчастникОбменаВОбъектеИОснованииНеСовпадает(ИмяРеквизита, НаименованиеУчастникаВОсновании, УчастникВОсновании) Тогда
			ТекстВопроса = НСтр("ru = '%1 в основании (%2) отличается от выбранного значения.
				|Очистить основание?';
				|en = '%1 in the source (%2) differs from the selected value.
				|Clear the source?'");
			ТекстВопроса = СтрШаблон(ТекстВопроса, ИмяРеквизита, НаименованиеУчастникаВОсновании);
			СтруктураПараметров = Новый Структура("ИмяРеквизита, ЗначениеРеквизита", ИмяРеквизита, УчастникВОсновании);
			ОписаниеОповещения = Новый ОписаниеОповещения("ОбработатьОтветОбОчисткеОснования", ЭтотОбъект, СтруктураПараметров);
			ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
		КонецЕсли;
	Иначе
		УстановитьТребуетсяПодтверждениеПоСоглашению();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьОтветОбОчисткеОснования(Результат, СтруктураПараметров) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		Объект.ДокументыОснования.Очистить();
		УстановитьТребуетсяПодтверждениеПоСоглашению();
		УстановитьДоступностьЭлементов();
	Иначе
		Объект[СтруктураПараметров.ИмяРеквизита] = СтруктураПараметров.ЗначениеРеквизита;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция УчастникОбменаВОбъектеИОснованииНеСовпадает(ИмяРеквизита, НаименованиеУчастникаВОсновании, УчастникВОсновании)
	
	РеквизитыОснования = ОбменСКонтрагентамиСлужебный.ЗаполнитьПараметрыЭДПоИсточнику(Объект.ДокументыОснования[0].ДокументОснование);
	УчастникВОсновании = Неопределено;
	РеквизитыОснования.Свойство(ИмяРеквизита, УчастникВОсновании);
	Если ЗначениеЗаполнено(УчастникВОсновании) Тогда
		НаименованиеУчастникаВОсновании = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(УчастникВОсновании, "Наименование");
		Результат = Объект[ИмяРеквизита] <> УчастникВОсновании;
	Иначе
		Результат = Ложь;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура ВыборДокументаОснованияПродолжить(Результат, Неопределен) Экспорт
	
	Если Результат <> Неопределено Тогда
		ОбработкаПродолжения = Новый ОписаниеОповещения("ВыборДокументаОснованияЗавершить", ЭтотОбъект);
		СтруктураОтбора = Новый Структура;
		Если ЗначениеЗаполнено(Объект.Организация) Тогда
			СтруктураОтбора.Вставить("Организация", Объект.Организация);
		КонецЕсли;
		Если ЗначениеЗаполнено(Объект.Контрагент) Тогда
			СтруктураОтбора.Вставить("Контрагент", Объект.Контрагент);
			Если ЗначениеЗаполнено(Объект.ДоговорКонтрагента) Тогда
				СтруктураОтбора.Вставить("ДоговорКонтрагента", Объект.ДоговорКонтрагента);
			КонецЕсли;
		КонецЕсли;
		ПараметрыОткрытия = Новый Структура;
		ПараметрыОткрытия.Вставить("Отбор", СтруктураОтбора);
		ОткрытьФорму(Результат.Значение + ".ФормаВыбора", ПараметрыОткрытия, ЭтаФорма, УникальныйИдентификатор, , , ОбработкаПродолжения);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыборДокументаОснованияЗавершить(Результат, Неопределен) Экспорт
	
	Если ЗначениеЗаполнено(Результат) И Результат <> Объект.Ссылка Тогда
		ЗаполнитьПоДокументуОснованию(Результат);
		ОбновитьИнтерфейс();
		Модифицированность = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция СписокТиповДокументовОснованийПроизвольногоДокумента()
	
	Основания = Метаданные.ОпределяемыеТипы.ОснованияЭлектронныхДокументов.Тип.Типы();
	СписокТипов = Новый СписокЗначений;
	Для Каждого ТекущееОснование Из Основания Цикл
		СписокТипов.Добавить(Метаданные.НайтиПоТипу(ТекущееОснование).ПолноеИмя(), Метаданные.НайтиПоТипу(ТекущееОснование).Синоним);
	КонецЦикла;
	
	Возврат СписокТипов;
	
КонецФункции

&НаКлиенте
Процедура ОбработатьОтветНаВопросОчисткиДокументаОснования(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		Объект.ДокументыОснования.Очистить();
		Модифицированность = Истина;
		УстановитьДоступностьЭлементов();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПоДокументуОснованию(ДокументОснование)
	
	Если ЗначениеЗаполнено(ДокументОснование) Тогда
		Документ = РеквизитФормыВЗначение("Объект");
		Документ.Заполнить(ДокументОснование);
		ЗначениеВРеквизитФормы(Документ, "Объект");
		ТекстДокументОснование = ЗаполнитьТекстДокументаОснования(ДокументОснование);
		АдресХранилища = ОбменСКонтрагентамиСлужебный.ПараметрИзПараметрыКлиентаНаСервере(ДокументОснование);
		Если ЭтоАдресВременногоХранилища(АдресХранилища) Тогда
			АдресВременногоХранилищаВложения = АдресХранилища;
			// У документа уже есть сохраненное вложение, надо будет в нем обновить двоичные данные.
			ОбновитьДвоичныеДанныеФайла = ЗначениеЗаполнено(ЭлектронныйДокументСсылка);
		КонецЕсли;
		Модифицированность = Истина;
	КонецЕсли;
	ОбновитьДанныеОВложении();
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ЗаполнитьТекстДокументаОснования(СсылкаНаОснование)
	
	СсылкаНаОснованиеДляОтображения = СсылкаНаОснование;
	Если ТипЗнч(СсылкаНаОснование) = Тип("СправочникСсылка.ВерсииФайлов") Тогда
		СсылкаНаОснованиеДляОтображения = СсылкаНаОснование.Владелец.ВладелецФайла;
	КонецЕсли;
	
	ТекстОснование = ПредставлениеОснования(СсылкаНаОснованиеДляОтображения);
	
	Возврат ТекстОснование;
	
КонецФункции

&НаСервереБезКонтекста
Функция ПредставлениеОснования(СсылкаНаОснование)
	
	ТекстДокументОснование = "";
	Если ЗначениеЗаполнено(СсылкаНаОснование) Тогда
		Если Найти(СсылкаНаОснование.Метаданные().ПолноеИмя(), "Справочник") > 0 Тогда
			Структура = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(СсылкаНаОснование, "Наименование");
			ТекстДокументОснование = НСтр("ru = '%1 %2'; en = '%1 %2'");
			ТекстДокументОснование = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстДокументОснование,
				ТипЗнч(СсылкаНаОснование), СокрЛП(Структура.Наименование));
		Иначе
			Структура = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(СсылкаНаОснование, "Номер, Дата");
			ТекстДокументОснование = НСтр("ru = '%1 №%2 от %3'; en = '%1 №%2 of %3'");
			ТекстДокументОснование = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстДокументОснование,
				ТипЗнч(СсылкаНаОснование), СокрЛП(Структура.Номер), Формат(Структура.Дата, "ДЛФ=D"));
		КонецЕсли;
	КонецЕсли;
	
	Возврат ТекстДокументОснование;
	
КонецФункции

#КонецОбласти

///////////////////////////////////////////////////////////////////////////////////

#Область СертификатыИПодписи

&НаКлиенте
Процедура ЗакончитьПодписаниеДокумента(Неопределен, ФлагОтправить) Экспорт
	
	МассивЭД = Новый Массив;
	МассивЭД.Добавить(ЭлектронныйДокументСсылка);
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("МассивПодписей",   МассивОтпечатков());
	ДополнительныеПараметры.Вставить("МассивНаОтправку", МассивЭД);
	ДополнительныеПараметры.Вставить("ФлагОтправить",    ФлагОтправить);
	
	ОбработчикОповещения = Новый ОписаниеОповещения("ЗакончитьПодписаниеОповещение", ЭтотОбъект, ДополнительныеПараметры);
	Действия = "УтвердитьПодписать" + ?(ФлагОтправить, "Отправить", "");
	ОбменСКонтрагентамиСлужебныйКлиент.ОбработатьЭД(Новый Массив, Действия, , МассивЭД, ОбработчикОповещения);
	
КонецПроцедуры

&НаСервере
Функция МассивОтпечатков()
	
	Подписи = РеквизитФормыВЗначение("ЭП");
	МассивОтпечатков = Подписи.ВыгрузитьКолонку("Отпечаток");
	Возврат МассивОтпечатков;
	
КонецФункции

&НаКлиенте
Функция СравнитьПодписи(МассивОтпечатковДо)
	
	ДокументПодписан = Ложь;
	МассивОтпечатковПосле = МассивОтпечатков();
	
	Для Каждого Отпечаток Из МассивОтпечатковПосле Цикл
		Если МассивОтпечатковДо.Найти(Отпечаток) = Неопределено Тогда
			ДокументПодписан = Истина;
		КонецЕсли;
	КонецЦикла;
	
	Возврат ДокументПодписан;
	
КонецФункции

&НаКлиенте
Процедура ЗакончитьПодписаниеОповещение(Результат, ДополнительныеПараметры) Экспорт
	
	Подписан = СравнитьПодписи(ДополнительныеПараметры.МассивПодписей);
	
	Прочитать();
	ПоказатьРезультатПодписи(Подписан);
	ФлагОтправить = Ложь;
	Если ДополнительныеПараметры.Свойство("ФлагОтправить", ФлагОтправить) И ФлагОтправить = Истина Тогда
		ДополнительныеПараметры.Вставить("Подписан", Подписан);
		ПослеОтправкиПЭД(Результат, ДополнительныеПараметры);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьРезультатПодписи(Подписан)
	
	ТекстСостояния = НСтр("ru = 'Подписано: (0)'; en = 'Signed: (0)'");
	ТекстЗаголовка = НСтр("ru = 'Обмен электронными документами'; en = 'Electronic document interchange'");
	Если Подписан Тогда
		ПараметрыДокумента = Новый Структура;
		ПараметрыДокумента.Вставить("Подписан", Истина);
		ОбновитьСтатусДокумента(ПараметрыДокумента);
		Прочитать();
		УстановитьДоступностьЭлементов();
		ТекстСостояния = НСтр("ru = 'Подписано: (1)'; en = 'Signed: (1)'");
	КонецЕсли;
	
	Оповестить("ОбновитьСостояниеЭД");
	ПоказатьОповещениеПользователя(ТекстЗаголовка, , ТекстСостояния);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТаблицуЭП()
	
	Если Не ЗначениеЗаполнено(СоглашениеЭД)
		ИЛИ Не СоглашениеЭД.ПроверятьСертификатыПодписей Тогда
		ТаблицаЭП = РеквизитФормыВЗначение("ЭП");
		ТаблицаЭП.Очистить();
		Для Каждого ТекСтрока Из ЭлектронныйДокументСсылка.ЭлектронныеПодписи Цикл
			НоваяСтрока = ТаблицаЭП.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекСтрока);
			ЗаполнитьСтатусПодписи(НоваяСтрока, ТекСтрока);
		КонецЦикла;
		ЗначениеВРеквизитФормы(ТаблицаЭП, "ЭП");
		Возврат;
	КонецЕсли;
	
	МассивОтпечатковОжидаемыхСертификатов = ОбменСКонтрагентамиСлужебный.ОтпечаткиОжидаемыхСертификатов(Объект.Ссылка);
	
	ТаблицаЭП = РеквизитФормыВЗначение("ЭП");
	ТаблицаЭП.Очистить();
	Для Каждого ТекСтрока Из ЭлектронныйДокументСсылка.ЭлектронныеПодписи Цикл
		НоваяСтрока = ТаблицаЭП.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекСтрока);
		Если МассивОтпечатковОжидаемыхСертификатов.Найти(ТекСтрока.Отпечаток) = Неопределено Тогда
			НоваяСтрока.ОтсутствуетВСписке = Истина;
			НоваяСтрока.ВыводКартинки = 1;
		Иначе
			НоваяСтрока.ВыводКартинки = 0;
		КонецЕсли;
		ЗаполнитьСтатусПодписи(НоваяСтрока, ТекСтрока);
	КонецЦикла;
	ЗначениеВРеквизитФормы(ТаблицаЭП, "ЭП");
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСтатусПодписи(НоваяСтрока, ТекСтрока)
	
	Если ЗначениеЗаполнено(ТекСтрока.ДатаПроверкиПодписи) Тогда
		НоваяСтрока.ПодписьВерна = ?(ТекСтрока.ПодписьВерна, НСтр("ru = 'Верна'; en = 'Valid'"), НСтр("ru = 'Неверна'; en = 'Invalid'"))
			+ " (" + ТекСтрока.ДатаПроверкиПодписи + ")";
	Иначе
		НоваяСтрока.ПодписьВерна = НСтр("ru = 'Не проверена'; en = 'Not validated'");
	КонецЕсли
	
КонецПроцедуры

&НаСервере
Функция АдресДанныхСертификата(НомерСтроки)
	
	СсылкаНаХранилищеДанныхСертификата = Неопределено;
	Если ЗначениеЗаполнено(ЭлектронныйДокументСсылка) Тогда
		ДвоичныеДанныеСертификата = ЭлектронныйДокументСсылка.ЭлектронныеПодписи[НомерСтроки-1].Сертификат.Получить();
		СсылкаНаХранилищеДанныхСертификата = ПоместитьВоВременноеХранилище(ДвоичныеДанныеСертификата, УникальныйИдентификатор);
	КонецЕсли;
	
	Возврат СсылкаНаХранилищеДанныхСертификата;
	
КонецФункции

&НаКлиенте
Процедура ПоказатьСертификат(НомерСтроки, Отпечаток)
	
	АдресДанныхСертификата = АдресДанныхСертификата(НомерСтроки);
	СтруктураСертификата = ОбменСКонтрагентамиСлужебныйВызовСервера.СвойстваСертификата(АдресДанныхСертификата);
	
	Если СтруктураСертификата <> Неопределено Тогда
		ПараметрыФормы = Новый Структура("СтруктураСертификата, Отпечаток, АдресСертификата",
			СтруктураСертификата, Отпечаток, АдресДанныхСертификата);
		ПараметрыФормы = Новый Структура();
		ПараметрыФормы.Вставить("СтруктураСертификата", СтруктураСертификата);
		ПараметрыФормы.Вставить("Отпечаток",            Отпечаток);
		ПараметрыФормы.Вставить("АдресСертификата",     АдресДанныхСертификата);
			
		ОткрытьФорму("ОбщаяФорма.Сертификат", ПараметрыФормы);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьСертификатПодписиВСоглашение(Отпечаток, СертификатДобавлен)
	
	Если НЕ (СоглашениеОпределялось И ЗначениеЗаполнено(СоглашениеЭД) И ЗначениеЗаполнено(ЭлектронныйДокументСсылка)) Тогда
		Возврат;
	КонецЕсли;
	
	НайденнаяСтрока = ЭлектронныйДокументСсылка.ЭлектронныеПодписи.Найти(Отпечаток, "Отпечаток");
	Если НЕ НайденнаяСтрока = Неопределено Тогда
		СоглашениеОбъект = СоглашениеЭД.ПолучитьОбъект();
		НоваяСтрока = СоглашениеОбъект.СертификатыПодписейКонтрагента.Добавить();
		НоваяСтрока.Сертификат = НайденнаяСтрока.Сертификат;
		НоваяСтрока.Отпечаток  = Отпечаток;
		СоглашениеОбъект.Записать();
		СертификатДобавлен = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьСертификатВДоверенные(ДанныеПодписи)
	
	Если ДанныеПодписи <> Неопределено И ДанныеПодписи.ОтсутствуетВСписке Тогда
		Текст = НСтр("ru = 'Добавить сертификат %1 в список ожидаемых сертификатов контрагента?'; en = 'Add certificate %1 to the list of expected counterparty certificates?'");
		ТекстВопроса = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Текст, ДанныеПодписи.КомуВыданСертификат);
		ДопПараметры = Новый Структура("ТекущиеДанные, ДанныеПодписи", Элементы.ЭП.ТекущиеДанные, ДанныеПодписи);
		ОписаниеОповещения = Новый ОписаниеОповещения("ОбработатьОтветНаВопросПоСертификату", ЭтотОбъект, ДопПараметры);
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьОтветНаВопросПоСертификату(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Нет Тогда
		ТекущиеДанные = ДополнительныеПараметры.ТекущиеДанные;
		ПоказатьСертификат(ТекущиеДанные.НомерСтроки,ТекущиеДанные.Отпечаток);
	Иначе
		// Добавим сертификат в Соглашение.
		ДанныеПодписи = ДополнительныеПараметры.ДанныеПодписи;
		СертификатДобавлен = Ложь;
		ДобавитьСертификатПодписиВСоглашение(ДанныеПодписи.Отпечаток, СертификатДобавлен);
		Если НЕ СертификатДобавлен Тогда 
			ТекстСообщения = НСтр("ru = 'Ошибка добавления сертификата подписи в список ожидаемых сертификатов.'; en = 'Error adding the signing certificate to the list of expected certificates.'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		Иначе
			ЗаполнитьТаблицуЭП();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

///////////////////////////////////////////////////////////////////////////////////

#Область Прочие

&НаСервере
Процедура УстановитьДоступностьЭлементов()
	
	НаправлениеИсходящий = Истина;
	
	СтатусПередан = ОбменСКонтрагентамиСлужебный.СтатусОбъектаПередан(ЭлектронныйДокументСсылка);
	
	ДОЗакрыт = ОбменСКонтрагентамиСлужебныйВызовСервера.ДОЗакрытПринудительно(Объект.Ссылка);
		
	ЕстьВложения = ЗначениеЗаполнено(ЭлектронныйДокументСсылка)
					ИЛИ ЭтоАдресВременногоХранилища(АдресВременногоХранилищаВложения);
	
	Элементы.Подписать.Видимость = Ложь;
	Элементы.ПодписатьИОтправитьЭД.Видимость = Ложь;
	
	Элементы.СохранитьВложениеВФайл.Доступность = ЗначениеЗаполнено(ЭлектронныйДокументСсылка);
	Элементы.ФормаОбработкаЭлектронныеДокументыПечатьЭД.Доступность = ЗначениеЗаполнено(ЭлектронныйДокументСсылка);
	
	Если ЗначениеЗаполнено(Объект.Ссылка) И НаправлениеИсходящий
		И (ЭлектронныйДокументСсылка.Пустая()
			ИЛИ ЭлектронныйДокументСсылка.СтатусЭД = Перечисления.СтатусыЭД.Сформирован
			ИЛИ ЭлектронныйДокументСсылка.СтатусЭД = Перечисления.СтатусыЭД.Подписан
			ИЛИ ЭлектронныйДокументСсылка.СтатусЭД = Перечисления.СтатусыЭД.ПодготовленКОтправке) Тогда
		МожноРедактироватьПередаваемыеПараметры = Истина;
	Иначе
		МожноРедактироватьПередаваемыеПараметры = НЕ ЗначениеЗаполнено(Объект.Ссылка); // Если документ еще не записан, то можно редактировать.
	КонецЕсли;
	
	// Вложение
	Если НаправлениеИсходящий Тогда
		Элементы.КоманднаяПанельВложения.Видимость = Истина;
		
		ПрисоединенныеФайлыОснования = Новый Массив;
		Если Объект.ДокументыОснования.Количество() = 1
			И ЗначениеЗаполнено(Объект.ДокументыОснования[0].ДокументОснование) Тогда
			ПрисоединенныеФайлы.ПолучитьПрикрепленныеФайлыКОбъекту(Объект.ДокументыОснования[0].ДокументОснование,
				ПрисоединенныеФайлыОснования);
		КонецЕсли;
		Если ПрисоединенныеФайлыОснования.Количество() > 0 Тогда
			ГруппаКомандДобавитьЗаменить = Элементы.ГруппаКомандДобавитьЗаменить;
			Элементы.ДобавитьВложениеИзФайлаНаДискеОдиночная.Видимость = Ложь;
		Иначе
			ГруппаКомандДобавитьЗаменить = Элементы.ДобавитьВложениеИзФайлаНаДискеОдиночная;
			Элементы.ГруппаКомандДобавитьЗаменить.Видимость = Ложь;
		КонецЕсли;
		ГруппаКомандДобавитьЗаменить.Видимость = Истина;
		Если ЕстьВложения Тогда
			ДанныеФайла = ДанныеФайлаВложения(АдресВременногоХранилищаВложения, ЭлектронныйДокументСсылка, УникальныйИдентификатор);
			Если ДанныеФайла.ФайлРедактируется Тогда
				ТекстРедактируетФайл = НСтр("ru = 'Файл захвачен на редактирование пользователем %1'; en = 'File is locked for editing by %1'");
				ТекстРедактируетФайл = СтрЗаменить(ТекстРедактируетФайл, "%1", ДанныеФайла.Редактирует);
				Элементы.ТекстРедактируетФайл.ЦветТекста = ЦветаСтиля[?(ДанныеФайла.ФайлРедактируетТекущийПользователь,
					"ФайлЗанятыйТекущимПользователем", "ТекстЗапрещеннойЯчейкиЦвет")];
			Иначе
				ТекстРедактируетФайл = "";
			КонецЕсли;
			ГруппаКомандДобавитьЗаменить.Заголовок = СтрЗаменить(ГруппаКомандДобавитьЗаменить.Заголовок,
				НСтр("ru = 'Добавить'; en = 'Add'"), НСтр("ru = 'Заменить на'; en = 'Replace with'"));
			Если МожноРедактироватьПередаваемыеПараметры И НЕ ДанныеФайла.ПодписанЭП Тогда
				ГруппаКомандДобавитьЗаменить.Доступность = Истина;
				Элементы.Редактировать.Доступность = НЕ ДанныеФайла.ФайлРедактируется;
				Элементы.ЗакончитьРедактирование.Доступность = ДанныеФайла.ФайлРедактируется
					И ДанныеФайла.ФайлРедактируетТекущийПользователь;
				Элементы.Освободить.Доступность = ДанныеФайла.ФайлРедактируется И ДанныеФайла.ФайлРедактируетТекущийПользователь;
			Иначе
				ГруппаКомандДобавитьЗаменить.Доступность = Ложь;
				Элементы.Редактировать.Доступность = Ложь;
				Элементы.ЗакончитьРедактирование.Доступность = Ложь;
				Элементы.Освободить.Доступность = Ложь;
			КонецЕсли;
		Иначе
			ГруппаКомандДобавитьЗаменить.Заголовок = СтрЗаменить(ГруппаКомандДобавитьЗаменить.Заголовок, НСтр("ru = 'Заменить на'; en = 'Replace with'"), НСтр("ru = 'Добавить'; en = 'Add'"));
			ГруппаКомандДобавитьЗаменить.Доступность = МожноРедактироватьПередаваемыеПараметры;
			Элементы.Редактировать.Доступность = Ложь;
			Элементы.ЗакончитьРедактирование.Доступность = Ложь;
			Элементы.Освободить.Доступность = Ложь;
		КонецЕсли;
	Иначе
		Элементы.КоманднаяПанельВложения.Видимость = Ложь;
		Элементы.ТекстРедактируетФайл.Видимость = Ложь;
	КонецЕсли;
	Элементы.ВложенияСтраницы.ТекущаяСтраница = ?(ЕстьВложения, Элементы.ВложенияСтраницаОтобразитьФайл, Элементы.ВложенияСтраницаОтсутствуетФайл);
	
	Если Объект.ДокументыОснования.Количество() = 1
		И ЗначениеЗаполнено(Объект.ДокументыОснования[0].ДокументОснование) Тогда
		Элементы.ДокументОснованиеСтраницы.ТекущаяСтраница = Элементы.ДокументОснованиеСтраницаОтобразитьДокумент;
		Элементы.ДокументОснование.Ширина = СтрДлина(ТекстДокументОснование);
	Иначе
		Элементы.ДокументОснованиеСтраницы.ТекущаяСтраница = Элементы.ДокументОснованиеСтраницаДобавить;
	КонецЕсли;
	
	Элементы.ТребуетсяПодтверждение.ТолькоПросмотр = НЕ МожноРедактироватьПередаваемыеПараметры;
	Элементы.ТекстВложение.ТолькоПросмотр = НЕ МожноРедактироватьПередаваемыеПараметры;
	Элементы.ОбъектДополнительнаяИнформация.ТолькоПросмотр = НЕ МожноРедактироватьПередаваемыеПараметры;
	Элементы.ФормаПеренаправить.Доступность = ЕстьВложения;
	Элементы.ГруппаПодвалСтраницыКомментарии.Картинка = ОбщегоНазначенияКлиентСервер.КартинкаКомментария(ВсеКомментарии);
	Элементы.ГруппаКомментарииГоризонтальная.Доступность = ЕстьВложения;
	Элементы.СтраницаСопроводительнаяЗаписка.Картинка = ОбщегоНазначенияКлиентСервер.КартинкаКомментария(СопроводительнаяЗаписка);
	
	Элементы.ФормаОтменитьОтклонение.Видимость = НаправлениеИсходящий;
	
	Если ЭДОтклонен Тогда
		
		Если ЕстьВложения Тогда
			ПричинаОтклонения = Объект.ПричинаОтклонения;
			Элементы.ПричинаОтклонения.Видимость = Истина;
		КонецЕсли;
		Элементы.ФормаОтклонить.Доступность = Ложь;
		Элементы.ФормаОтменитьОтклонение.Доступность = (ЭлектронныйДокументСсылка.СтатусЭД = Перечисления.СтатусыЭД.Отклонен);
	Иначе
		Элементы.ПричинаОтклонения.Видимость = Ложь;
		Элементы.ФормаОтклонить.Доступность = ?((СтатусПередан ИЛИ НЕ ЕстьВложения Или ДОЗакрыт), Ложь, Истина);
		Элементы.ФормаОтменитьОтклонение.Доступность = Ложь;
	КонецЕсли;
	
	ЗаголовокПодписи = НСтр("ru = 'Подписи'; en = 'Signatures'");
	Если ЗначениеЗаполнено(ЭлектронныйДокументСсылка) Тогда
		КоличествоЭП = ЭлектронныйДокументСсылка.ЭлектронныеПодписи.Количество();
		Если КоличествоЭП > 0 Тогда
			ЗаголовокПодписи = СтрЗаменить(НСтр("ru = 'Подписи (%1)'; en = 'Signatures (%1)'"), "%1", КоличествоЭП);
		КонецЕсли;
	КонецЕсли;
	Элементы.СтраницаПодписиИСтатусы.Заголовок = ЗаголовокПодписи;
	
	Элементы.Организация.ТолькоПросмотр = Не МожноРедактироватьПередаваемыеПараметры;
	Элементы.Контрагент.ТолькоПросмотр  = Не МожноРедактироватьПередаваемыеПараметры;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьЗаголовокФормы()
	
	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ТекстЗаголовка = НСтр("ru = 'Исходящий произвольный документ %1 от %2'; en = 'Arbitrary outgoing document %1 of %2'");
		Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		ТекстЗаголовка, Объект.Номер, Объект.Дата);
	Иначе
		Заголовок = НСтр("ru = 'Исходящий произвольный документ (создание)'; en = 'Arbitrary outgoing document (create)'");
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ЗаписатьНовыйДокумент(ФлагЗаписи)
	
	ОбъектДокумент = РеквизитФормыВЗначение("Объект");
	Если ЕстьСоглашение(ОбъектДокумент) И Записать() Тогда
		ФлагЗаписи = Истина;
		ОбновитьИнформациюПодвала();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьСтатусДокумента(ПараметрыДокумента)
	
	СправочникОбъект = ЭлектронныйДокументСсылка.ПолучитьОбъект();
	
	Если ПараметрыДокумента.Свойство("МассивНаОтправку") И ПараметрыДокумента.МассивНаОтправку.Количество() > 0 Тогда
		ЭД = ПараметрыДокумента.МассивНаОтправку[0];
	КонецЕсли;
	
	Если ПараметрыДокумента.Свойство("Подписан") И ПараметрыДокумента.Подписан Тогда
		СправочникОбъект.СтатусЭД = ОбменСКонтрагентамиСлужебный.ПолучитьДопустимыйСтатусЭД(Перечисления.СтатусыЭД.ПодготовленКОтправке, ЭлектронныйДокументСсылка);
	КонецЕсли;
	
	Если (СоглашениеОпределялось И ЗначениеЗаполнено(СоглашениеЭД))
		ИЛИ (НЕ СоглашениеОпределялось И ЕстьСоглашение(Объект.Ссылка)) Тогда
		СправочникОбъект.Записать();
		ОбновитьИнформациюПодвала();
		Прочитать();
		ОбновитьЗаголовокФормы();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеОтправкиПЭД(Результат, ДополнительныеПараметры)
	
	Прочитать();
	
	Количество = 0;
	КолПодготовленных = 0;
	КолОтправленных = 0;
	Если ТипЗнч(Результат) = Тип("Структура") Тогда
		Если Результат.Свойство("КолОтправленных", КолОтправленных)
			И ТипЗнч(КолОтправленных) = Тип("Число")
			И КолОтправленных > 0 Тогда
			
			ТекстСостояния = НСтр("ru = 'Отправлено: (%1)'; en = 'Sent: (%1)'");
			Количество = КолОтправленных;
		ИначеЕсли Результат.Свойство("КолПодготовленных", КолПодготовленных)
			И ТипЗнч(КолПодготовленных) = Тип("Число")
			И КолПодготовленных > 0 Тогда
			
			ТекстСостояния = НСтр("ru = 'Подготовлено к отправке: (%1)'; en = 'Prepared for sending: (%1)'");
			Количество = КолПодготовленных;
		КонецЕсли;
	КонецЕсли;
	
	Если Количество > 0 Тогда
		ТекстСостояния = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСостояния, Количество);
		ТекстЗаголовка = НСтр("ru = 'Обмен электронными документами'; en = 'Electronic document interchange'");
		ПоказатьОповещениеПользователя(ТекстЗаголовка, , ТекстСостояния);
	КонецЕсли;
	
	ОбновитьСтатусДокумента(ДополнительныеПараметры);
	Оповестить("ОбновитьСостояниеЭД");
	
КонецПроцедуры

&НаСервере
Функция ЕстьСоглашение(ОбъектДокумент)
	
	ВыводитьСообщение = НЕ ЗначениеЗаполнено(Объект.Ссылка) ИЛИ Модифицированность;
	НастройкиЭД = ОбменСКонтрагентамиСлужебный.ОпределитьНастройкиОбменаЭДПоИсточнику(ОбъектДокумент, ВыводитьСообщение,,ЭлектронныйДокументСсылка,Объект.ВидЭД); 
	СоглашениеОпределялось = Истина;
	Если ЗначениеЗаполнено(НастройкиЭД) Тогда
		ОбъектДокумент.ДополнительныеСвойства.Вставить("ЕстьСоглашение", Истина);
		ОбъектДокумент.НастройкаЭДО = НастройкиЭД.СоглашениеЭД;
		ОбъектДокумент.ПрофильНастроекЭДО = НастройкиЭД.ПрофильНастроекЭДО;
		Возврат Истина;
	Иначе
		ОбъектДокумент.НастройкаЭДО = Справочники.СоглашенияОбИспользованииЭД.ПустаяСсылка();
		ОбъектДокумент.ПрофильНастроекЭДО = Справочники.ПрофилиНастроекЭДО.ПустаяСсылка();
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

&НаКлиенте
Процедура УстановитьТребуетсяПодтверждениеПоСоглашению()
	
	Если ЗначениеЗаполнено(Объект.Организация)
		И ЗначениеЗаполнено(Объект.Контрагент) Тогда
			
		Объект.ТребуетсяПодтверждение = ТребуетсяОтветнаяПодписьПоСоглашению(
			Объект.ВидЭД, Объект.Организация, Объект.Контрагент);
	
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ТребуетсяОтветнаяПодписьПоСоглашению(ВидЭД, Организация, Контрагент)
	
	Требуется = Ложь;
	
	ПараметрыЭД = Новый Структура();
	ПараметрыЭД.Вставить("ВидЭД", 		  ВидЭД);
	ПараметрыЭД.Вставить("Организация",   Организация);
	ПараметрыЭД.Вставить("Контрагент",	  Контрагент);
	ПараметрыЭД.Вставить("НаправлениеЭД", Перечисления.НаправленияЭД.Исходящий);	
	
	НастройкиЭД = ОбменСКонтрагентамиСлужебный.ОпределитьНастройкиОбменаЭД(ПараметрыЭД);
				
	Если ЗначениеЗаполнено(НастройкиЭД) Тогда

		Запрос = Новый Запрос;
		Запрос.Текст =
		"ВЫБРАТЬ
		|	СоглашенияОбИспользованииЭДИсходящиеДокументы.ТребуетсяОтветнаяПодпись
		|ИЗ
		|	Справочник.СоглашенияОбИспользованииЭД.ИсходящиеДокументы КАК СоглашенияОбИспользованииЭДИсходящиеДокументы
		|ГДЕ
		|	СоглашенияОбИспользованииЭДИсходящиеДокументы.ИсходящийДокумент = ЗНАЧЕНИЕ(Перечисление.ВидыЭД.ПроизвольныйЭД)
		|	И СоглашенияОбИспользованииЭДИсходящиеДокументы.Ссылка = &СоглашениеЭД";
		
		Запрос.УстановитьПараметр("СоглашениеЭД", НастройкиЭД.СоглашениеЭД);
		
		Выборка = Запрос.Выполнить().Выбрать();
		
		Если Выборка.Следующий() Тогда
			Требуется = Выборка.ТребуетсяОтветнаяПодпись;
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Требуется;
	
КонецФункции

&НаСервере
Процедура ОбновитьСтатусСостояниеЭД()
	
	ТекстСостоянияЭДО = ОбменСКонтрагентамиКлиентСервер.ПолучитьТекстСостоянияЭД(Объект.Ссылка);
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьИнформациюПодвала()
	
	ОбновитьСтатусСостояниеЭД();
	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
		Если НЕ (СоглашениеОпределялось И ЗначениеЗаполнено(СоглашениеЭД)) Тогда
			ДокументОбъект = РеквизитФормыВЗначение("Объект");
			ЕстьСоглашение = ЕстьСоглашение(ДокументОбъект);
		Иначе
			ЕстьСоглашение = Истина;
		КонецЕсли;
		ЗаполнитьТаблицуЭП();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ИзменитьСтатусОтклонить()
	
	ТекущийСтатус = ?(ЭДОтклонен, Перечисления.СтатусыЭД.Отклонен, Перечисления.СтатусыЭД.Сформирован);
	СтруктураПараметров = Новый Структура("СтатусЭД", ТекущийСтатус);
	ОбменСКонтрагентамиСлужебный.ИзменитьПоСсылкеПрисоединенныйФайл(ЭлектронныйДокументСсылка, СтруктураПараметров, Ложь);
	Объект.СостояниеЭДО = Перечисления.СостоянияВерсийЭД.Отклонен;
	
КонецПроцедуры

&НаСервере
Процедура ВыполнитьОбработкуОповещенияНаСервере()
	
	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ЗначениеВРеквизитФормы(Объект.Ссылка.ПолучитьОбъект(), "Объект");
	КонецЕсли;
	
	ЗаполнитьТаблицуЭП();
	ОбновитьСтатусСостояниеЭД();
	ПерезаполнитьКомментарии();
	УстановитьДоступностьЭлементов();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтклонитьАннулироватьЭД(Результат, Отклонить = Ложь) Экспорт
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ВыполнитьДействияПослеОтклоненияЭД", ЭтотОбъект);
	ПараметрыЭД = Новый Структура("Организация, Отклонить, ОписаниеОповещения",
		Объект.Организация, Отклонить, ОписаниеОповещения);
	ОбменСКонтрагентамиСлужебныйКлиент.ОбработатьОтклонениеАннулированиеЭД(ЭлектронныйДокументСсылка, ПараметрыЭД);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьДействияПослеОтклоненияЭД(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Истина Тогда
		ЭДОтклонен = Истина;
		ИзменитьСтатусОтклонить();
		Оповестить("ОбновитьСостояниеЭД");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗакончитьПроверкуЗаписиДокумента(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		ФлагЗаписи = Ложь;
		ЗаписатьНовыйДокумент(ФлагЗаписи);
		Если ФлагЗаписи Тогда
			ОбработкаПродолжения = "";
			Если ТипЗнч(ДополнительныеПараметры) = Тип("Структура")
				И ДополнительныеПараметры.Свойство("ИмяПроцедуры", ОбработкаПродолжения)
				И ТипЗнч(ОбработкаПродолжения) = Тип("ОписаниеОповещения") Тогда
				ВыполнитьОбработкуОповещения(ОбработкаПродолжения);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьЗаписьДокумента(Неопределен, ОбработкаПродолжения) Экспорт
	
	Если Модифицированность ИЛИ Объект.Ссылка.Пустая() Тогда
		ТекстВопроса = НСтр("ru = 'Для продолжения операции необходимо записать документ.
			|Записать документ?';
			|en = 'To continue, you must save the document.
			|Save the document?'");
		ДопПараметры = Новый Структура("ИмяПроцедуры", ОбработкаПродолжения);
		ОписаниеОповещения = Новый ОписаниеОповещения("ЗакончитьПроверкуЗаписиДокумента", ЭтотОбъект, ДопПараметры);
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Да);
	Иначе
		ВыполнитьОбработкуОповещения(ОбработкаПродолжения);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьОтменуОтклоненияЭД(Неопределен, ДополнительныеПараметры) Экспорт
	
	Если ОтменитьОтклонениеНаСервере() Тогда
		Оповестить("ОбновитьСостояниеЭД");
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ОтменитьОтклонениеНаСервере()
	
	ЭДОтклонен = Ложь;
	Объект.ПричинаОтклонения = "";
	ЭтаФорма.Записать();
	ЭлектронныйДокумент = ЭлектронныйДокументСсылка.ПолучитьОбъект();
	ЭлектронныйДокумент.ЭлектронныеПодписи.Очистить();
	ЭлектронныйДокумент.Записать();
	ИзменитьСтатусОтклонить();
	
	Возврат Истина;
	
КонецФункции

&НаКлиенте
Процедура ОбработатьПеренаправлениеЭД(Неопределен, ДополнительныеПараметры) Экспорт
	
	Если ЗначениеЗаполнено(ЭлектронныйДокументСсылка) Тогда
		МассивЭД = Новый Массив;
		МассивЭД.Добавить(ЭлектронныйДокументСсылка);
		ОбменСКонтрагентамиСлужебныйКлиент.ИзменитьОтветственного(МассивЭД, Неопределено);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПерезаполнитьКомментарии()
	
	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ВсеКомментарии = "";
		Запрос = Новый Запрос;
		Запрос.Текст =
		"ВЫБРАТЬ
		|	ЖурналСобытийЭД.Пользователь.Представление КАК Пользователь,
		|	ЖурналСобытийЭД.Дата КАК Дата,
		|	ЖурналСобытийЭД.СтатусЭД,
		|	ЖурналСобытийЭД.Ответственный.Представление КАК Ответственный,
		|	ЖурналСобытийЭД.Комментарий
		|ИЗ
		|	РегистрСведений.ЖурналСобытийЭД КАК ЖурналСобытийЭД
		|ГДЕ
		|	ЖурналСобытийЭД.ВладелецЭД = &Ссылка
		|	И ЖурналСобытийЭД.Комментарий <> &ПустаяСтрока
		|
		|УПОРЯДОЧИТЬ ПО
		|	Дата";
			
		Запрос.УстановитьПараметр("Ссылка", Объект.Ссылка);
		Запрос.УстановитьПараметр("ПустаяСтрока", "");
		Результат = Запрос.Выполнить();
		Выборка = Результат.Выбрать();
		ШаблонКомментария = НСтр("ru = '%1, %2 (статус - %3, ответственный - %4):
			|%5';
			|en = '%1, %2 (state - %3, responsible - %4):
			|%5'");
		ПредыдущийКомментарий = "";
		ПервыйКомментарий = Истина;
		Массив = Новый Массив;
		Пока Выборка.Следующий() Цикл
			ТекущийКомментарий = СокрЛП(Выборка.Комментарий);
			Если ПредыдущийКомментарий = ТекущийКомментарий Тогда
				Продолжить;
			КонецЕсли;
			ПредыдущийКомментарий = ТекущийКомментарий;
			СтрокаКомментария = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонКомментария,
					Выборка.Дата, Выборка.Пользователь, Выборка.СтатусЭД, Выборка.Ответственный, ТекущийКомментарий);
			Массив.Добавить(СтрокаКомментария);
			ПервыйКомментарий = Ложь;
		КонецЦикла;
		Если Массив.Количество() > 0 Тогда
			ПервыйКомментарий = Истина;
			Для Сч = -Массив.Количество() + 1 По 0 Цикл
				СтрокаКомментария = Массив[-Сч];
				ВсеКомментарии = ВсеКомментарии
					+ СтрокаКомментария
					+ ?(ПервыйКомментарий, Символы.ПС + "------------------------------------", "")
					+ Символы.ПС
					+ Символы.ПС;
				ПервыйКомментарий = Ложь;
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ИзменитьЗначенияРеквизитовНаСервере(Знач Ссылка, Знач СтруктураПараметров)
	
	Если СтруктураПараметров.Свойство("Описание") И ЗначениеЗаполнено(Ссылка.Ответственный) Тогда
		СтруктураПараметров.Вставить("Ответственный", Ссылка.Ответственный);
	КонецЕсли;
	ОбменСКонтрагентамиСлужебный.ИзменитьПоСсылкеПрисоединенныйФайл(Ссылка, СтруктураПараметров, Ложь);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
