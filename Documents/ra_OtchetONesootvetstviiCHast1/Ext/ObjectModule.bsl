#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, ДанныеЗаполнения);
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ra_Nesootvetstvie") Тогда
		Nesootvetstvie = ДанныеЗаполнения;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Nesootvetstvie) Тогда
		Документы.ra_OtchetONesootvetstviiCHast1.ЗаполнитьАктуальнымиДанными(ЭтотОбъект);
	КонецЕсли;
	
	ра_ОбщегоНазначения.ЗаполнитьВидДокумента(ЭтотОбъект);
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Документы.ra_OtchetONesootvetstviiCHast1.АктуализироватьМассивОбязательныхРеквизитов(ПроверяемыеРеквизиты, ЭтотОбъект);
	
	ра_ОбщегоНазначения.ПроверитьЗаполнениеТабличнойЧастиСогласующие(ЭтотОбъект, Отказ);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Dejstviya", Dejstviya.Выгрузить());
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВЫРАЗИТЬ(Действия.VremennyeSderzhivayushchieDejstviyaIKorrekciya КАК Документ.ra_VremennyeSderzhivayushchieDejstviyaIKorrekciya) КАК Действие
	|ПОМЕСТИТЬ Действия
	|ИЗ
	|	&Dejstviya КАК Действия
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПРЕДСТАВЛЕНИЕ(Действия.Действие) КАК Действие
	|ИЗ
	|	Действия КАК Действия
	|ГДЕ
	|	Действия.Действие.Otvetstvennyj = ЗНАЧЕНИЕ(Справочник.Пользователи.ПустаяСсылка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПРЕДСТАВЛЕНИЕ(Действия.Действие) КАК Действие
	|ИЗ
	|	Действия КАК Действия
	|ГДЕ
	|	Действия.Действие.VidVremennyhSderzhivayushchihDejstvijIKorrekcij В ИЕРАРХИИ (ЗНАЧЕНИЕ(Справочник.ra_VidyPoruchenij.Коррекция))";
	Результат = Запрос.ВыполнитьПакет();
	
	Выборка = Результат[Результат.ВГраница() - 1].Выбрать();
	Пока Выборка.Следующий() Цикл
		ТекстОшибки = НСтр("ru = 'Не указан исполнитель по документу ""%1""'; en = 'Responsible is not specified for ""%1""'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СтрШаблон(ТекстОшибки, Выборка.Действие),,,, Отказ);
	КонецЦикла;
	
	ТаблицаДокументов = Документы.ra_VremennyeSderzhivayushchieDejstviyaIKorrekciya.ДокументыКоррекцияСоСтатусом(Nesootvetstvie);
	
	Если ТаблицаДокументов.Количество() = 0 Тогда
		ТекстОшибки = НСтр("ru = 'Не введен ни один документ с типом Коррекция'; en = 'No documents with correction type'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки,,,, Отказ);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ra_VremennyeSderzhivayushchieDejstviyaIKorrekciya.Представление КАК Представление
	|ИЗ
	|	Документ.ra_VremennyeSderzhivayushchieDejstviyaIKorrekciya КАК ra_VremennyeSderzhivayushchieDejstviyaIKorrekciya
	|ГДЕ
	|	ra_VremennyeSderzhivayushchieDejstviyaIKorrekciya.Nesootvetstvie = &Nesootvetstvie
	|	И (ra_VremennyeSderzhivayushchieDejstviyaIKorrekciya.Otvetstvennyj = ЗНАЧЕНИЕ(Справочник.Пользователи.ПустаяСсылка)
	|	ИЛИ ra_VremennyeSderzhivayushchieDejstviyaIKorrekciya.CelevojSrok = ДАТАВРЕМЯ(1,1,1))
	|	И НЕ ra_VremennyeSderzhivayushchieDejstviyaIKorrekciya.ПометкаУдаления");
	
	Запрос.УстановитьПараметр("Nesootvetstvie", Nesootvetstvie);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		ТекстОшибки = НСтр("ru = 'Не заполнен документ ""%1""'; en = 'Document ""%1"" is not filled'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СтрШаблон(ТекстОшибки, Выборка.Представление),,,, Отказ);
	КонецЦикла;
	
	Если ОценкаПроизводительностиВызовСервераПовтИсп.ВыполнятьЗамерыПроизводительности() Тогда
		ДополнительныеСвойства.Вставить("ВремяНачала", ОценкаПроизводительностиКлиентСервер.НачатьЗамерВремени());
	КонецЕсли;
	
	ДополнительныеСвойства.Вставить("ЭтоНовый", ЭтоНовый());
	
	Если ПометкаУдаления Тогда
		ДополнительныеСвойства.Вставить("НеСоздаватьФайлСВерсией");
	КонецЕсли;
	
	РаботаСРабочимиГруппами.ОтключитьПерезаписьРабочейГруппыПредметаПроцесса(ЭтотОбъект);
	
	ра_ОбщегоНазначения.ДокументыКачестваПередЗаписью(ЭтотОбъект);
	
	Если НЕ Dejstviya.Количество() Тогда
		Документы.ra_OtchetONesootvetstviiCHast1.ЗаполнитьАктуальнымиДанными(ЭтотОбъект, Ложь);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ра_ОбщегоНазначения.ПерезаписатьРабочиеГруппыОбъектов(Nesootvetstvie, Истина);
	
	ДополнительныеСвойства.Вставить("ПропуститьОпределениеДескриптораДоступаИПроверкуПрав", Истина);
	
	Если ДополнительныеСвойства.Свойство("НужноПометитьНаУдалениеБизнесСобытия") Тогда
		БизнесСобытияВызовСервера.ПометитьНаУдалениеСобытияПоИсточнику(Ссылка);
	КонецЕсли;
	
	// ТСК Корнюшенков А.Ю. Искать текст "МаршрутыСогласованияЕОСК" 29.06.2018 {
	// настройка автозапуска процессов для документа "Уведомление о несоответствии"
	// Возможно, выполнена явная регистрация событий при загрузке объекта.
	Если Не ДополнительныеСвойства.Свойство("НеРегистрироватьБизнесСобытия") Тогда
		Если ДополнительныеСвойства.Свойство("ЭтоНовый") И ДополнительныеСвойства.ЭтоНовый Тогда
			БизнесСобытияВызовСервера.ЗарегистрироватьСобытие(Ссылка, Справочники.ВидыБизнесСобытий.СозданиеВнутреннегоДокумента);
		Иначе
			Если НЕ ра_ОбщегоНазначения.ЕстьНеЗавершенныеБизнесПроцессыПоДокументу(Ссылка) Тогда
				БизнесСобытияВызовСервера.ЗарегистрироватьСобытие(Ссылка, Справочники.ВидыБизнесСобытий.ИзменениеВнутреннегоДокумента);
				ДополнительныеСвойства.Вставить("СтартВыполнен", Истина);
				ДополнительныеСвойства.Вставить("Описание", "ok");
			Иначе
				ДополнительныеСвойства.Вставить("СтартВыполнен", Ложь);
				ДополнительныеСвойства.Вставить("Описание", НСтр("ru = 'Не запущен процесс согласования измененного уведомления, т.к. есть предыдущие незавершенные бизнес-процесы.'; en = 'The start of the business process is canceled. There are unfinished business processes'"));
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	// ТСК Корнюшенков А.Ю. Искать текст "МаршрутыСогласованияЕОСК" 29.06.2018 }
	
	Если НЕ ДополнительныеСвойства.Свойство("НеСоздаватьФайлСВерсией") Тогда
		Документы.ra_Nesootvetstvie.СоздатьФайлСВерсией(ЭтотОбъект);
	КонецЕсли;
	
	Если ДополнительныеСвойства.Свойство("Cancellation") И ДополнительныеСвойства.Cancellation Тогда
		
		ПараметрыПроверки = Новый Структура("ra_AktObUstraneniiNesootvetstviya",
			Перечисления.СостоянияДокументов.ra_Утвержден);
		
		РезультатыПроверки = Документы.ra_Nesootvetstvie.ПроверитьНаличиеПроизводныхДокументов(Nesootvetstvie,
			ПараметрыПроверки);
			
		Если РезультатыПроверки.Свойство("ra_AktObUstraneniiNesootvetstviya") И РезультатыПроверки.ra_AktObUstraneniiNesootvetstviya Тогда
			ВызватьИсключение НСтр("ru = 'Акт об устранении уже утвержден. Аннулирование невозможно.';
					|en = 'Nonconformity elimination act has already been approved. Cancellation is not possible.'");
		КонецЕсли;
					
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
		|	ra_OtchetONesootvetstviiCHast1Dejstviya.VremennyeSderzhivayushchieDejstviyaIKorrekciya КАК Ссылка
		|ПОМЕСТИТЬ ВТ_Документы
		|ИЗ
		|	Документ.ra_OtchetONesootvetstviiCHast1.Dejstviya КАК ra_OtchetONesootvetstviiCHast1Dejstviya
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ИсторияСостоянийДокументов.СрезПоследних КАК ИсторияСостоянийДокументов
		|		ПО ra_OtchetONesootvetstviiCHast1Dejstviya.VremennyeSderzhivayushchieDejstviyaIKorrekciya = ИсторияСостоянийДокументов.Документ
		|ГДЕ
		|	ИсторияСостоянийДокументов.Состояние <> ЗНАЧЕНИЕ(Перечисление.СостоянияДокументов.Исполнен)
		|	И НЕ ra_OtchetONesootvetstviiCHast1Dejstviya.VremennyeSderzhivayushchieDejstviyaIKorrekciya.ПометкаУдаления
		|	И ra_OtchetONesootvetstviiCHast1Dejstviya.Ссылка = &Ссылка
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ra_AktObUstraneniiNesootvetstviya.Ссылка
		|ИЗ
		|	Документ.ra_AktObUstraneniiNesootvetstviya КАК ra_AktObUstraneniiNesootvetstviya
		|ГДЕ
		|	ra_AktObUstraneniiNesootvetstviya.Nesootvetstvie = &Nesootvetstvie
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ra_OtchetONesootvetstviiCHast1.Ссылка
		|ИЗ
		|	Документ.ra_OtchetONesootvetstviiCHast1 КАК ra_OtchetONesootvetstviiCHast1
		|ГДЕ
		|	ra_OtchetONesootvetstviiCHast1.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
		|	ВЫБОР
		|		КОГДА БизнесПроцессОзнакомлениеПредметы.Ссылка.ВедущаяЗадача = ЗНАЧЕНИЕ(Задача.ЗадачаИсполнителя.ПустаяСсылка)
		|			ТОГДА БизнесПроцессОзнакомлениеПредметы.Ссылка
		|		ИНАЧЕ БизнесПроцессОзнакомлениеПредметы.Ссылка.ВедущаяЗадача.БизнесПроцесс
		|	КОНЕЦ КАК БизнесПроцесс
		|ИЗ
		|	БизнесПроцесс.Ознакомление.Предметы КАК БизнесПроцессОзнакомлениеПредметы
		|ГДЕ
		|	БизнесПроцессОзнакомлениеПредметы.Ссылка.Стартован
		|	И НЕ БизнесПроцессОзнакомлениеПредметы.Ссылка.Завершен
		|	И БизнесПроцессОзнакомлениеПредметы.Предмет В
		|			(ВЫБРАТЬ РАЗЛИЧНЫЕ
		|				ВТ_Документы.Ссылка
		|			ИЗ
		|				ВТ_Документы)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ВЫБОР
		|		КОГДА БизнесПроцессСогласованиеПредметы.Ссылка.ВедущаяЗадача = ЗНАЧЕНИЕ(Задача.ЗадачаИсполнителя.ПустаяСсылка)
		|			ТОГДА БизнесПроцессСогласованиеПредметы.Ссылка
		|		ИНАЧЕ БизнесПроцессСогласованиеПредметы.Ссылка.ВедущаяЗадача.БизнесПроцесс
		|	КОНЕЦ КАК БизнесПроцесс
		|ИЗ
		|	БизнесПроцесс.Согласование.Предметы КАК БизнесПроцессСогласованиеПредметы
		|ГДЕ
		|	БизнесПроцессСогласованиеПредметы.Ссылка.Стартован
		|	И НЕ БизнесПроцессСогласованиеПредметы.Ссылка.Завершен
		|	И БизнесПроцессСогласованиеПредметы.Предмет В
		|			(ВЫБРАТЬ РАЗЛИЧНЫЕ
		|				ВТ_Документы.Ссылка
		|			ИЗ
		|				ВТ_Документы)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ВТ_Документы.Ссылка КАК Документ
		|ИЗ
		|	ВТ_Документы КАК ВТ_Документы";
		
		Запрос.УстановитьПараметр("Ссылка", Ссылка);
		Запрос.УстановитьПараметр("Nesootvetstvie", Nesootvetstvie);
		
		РезультатЗапроса = Запрос.ВыполнитьПакет();
		
		МассивБизнесПроцессов = РезультатЗапроса[1].Выгрузить().ВыгрузитьКолонку("БизнесПроцесс");
		
		Для Каждого БизнесПроцесс Из МассивБизнесПроцессов Цикл
			ПричинаПрерывания = СтрШаблон("Отмена документа %1", Ссылка);
			БизнесПроцессыИЗадачиВызовСервера.ПрерватьБизнесПроцесс(БизнесПроцесс, ПричинаПрерывания);
		КонецЦикла;
				
		ВыборкаДокументы = РезультатЗапроса[2].Выбрать();
		
		Пока ВыборкаДокументы.Следующий() Цикл
			
			Если ТипЗнч(ВыборкаДокументы.Документ) = Тип("ДокументСсылка.ra_AktObUstraneniiNesootvetstviya")
				ИЛИ ТипЗнч(ВыборкаДокументы.Документ) = Тип("ДокументСсылка.ra_OtchetONesootvetstviiCHast1") Тогда
				
				Делопроизводство.ЗаписатьСостояниеДокумента(
					ВыборкаДокументы.Документ,
					ТекущаяДатаСеанса(),
					Перечисления.СостоянияДокументов.ra_Аннулирован,
					ПользователиКлиентСервер.ТекущийПользователь());
					
			ИначеЕсли ТипЗнч(ВыборкаДокументы.Документ) = Тип("ДокументСсылка.ra_VremennyeSderzhivayushchieDejstviyaIKorrekciya") Тогда
				
				Делопроизводство.ЗаписатьСостояниеДокумента(
					ВыборкаДокументы.Документ,
					ТекущаяДатаСеанса(),
					Перечисления.СостоянияДокументов.ra_Проект,
					ПользователиКлиентСервер.ТекущийПользователь());
					
			КонецЕсли;
			
		КонецЦикла;
										
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Процедура ДобавитьУчастниковРабочейГруппыВНабор(ТаблицаНабора) Экспорт
	
	Документы.ra_Nesootvetstvie.ДобавитьУчастниковРабочейГруппыВНабор(ТаблицаНабора, ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#КонецОбласти

#КонецЕсли
