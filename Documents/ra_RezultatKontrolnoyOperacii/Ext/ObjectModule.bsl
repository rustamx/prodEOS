#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ra_ZayavkaNaKontrolnuyuOperaciyu") Тогда
		
		ZayavkaNaKontrolnuyuOperaciyu = ДанныеЗаполнения;
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ra_UchastnikiKontrolnyhOperaciy.Organizaciya КАК Organizaciya,
		|	ra_UchastnikiKontrolnyhOperaciy.Podrazdelenie КАК Podrazdelenie,
		|	ra_UchastnikiKontrolnyhOperaciy.Dolzhnost КАК Dolzhnost,
		|	ra_UchastnikiKontrolnyhOperaciy.Otvetstvennyj КАК Otvetstvennyj
		|ИЗ
		|	РегистрСведений.ra_UchastnikiKontrolnyhOperaciy КАК ra_UchastnikiKontrolnyhOperaciy
		|ГДЕ
		|	ra_UchastnikiKontrolnyhOperaciy.ZayavkaNaKontrolnuyuOperaciyu = &ZayavkaNaKontrolnuyuOperaciyu";
		
		Запрос.УстановитьПараметр("ZayavkaNaKontrolnuyuOperaciyu", ZayavkaNaKontrolnuyuOperaciyu);
		
		Soglasuyushchie.Загрузить(Запрос.Выполнить().Выгрузить());
		
	КонецЕсли;
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, ДанныеЗаполнения);
	КонецЕсли;
	
	ра_ОбщегоНазначения.ЗаполнитьВидДокумента(ЭтотОбъект);
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Документы.ra_RezultatKontrolnoyOperacii.АктуализироватьМассивОбязательныхРеквизитов(ПроверяемыеРеквизиты, ЭтотОбъект);
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ОценкаПроизводительностиВызовСервераПовтИсп.ВыполнятьЗамерыПроизводительности() Тогда
		ДополнительныеСвойства.Вставить("ВремяНачала", ОценкаПроизводительностиКлиентСервер.НачатьЗамерВремени());
	КонецЕсли;
	
	ЭтоНовый = ЭтоНовый();
	ДополнительныеСвойства.Вставить("ЭтоНовый", ЭтоНовый);
	
	Если ЭтоНовый Тогда
		ДополнительныеСвойства.Вставить("Состояние", Перечисления.СостоянияДокументов.ra_Проект);
	КонецЕсли;
		
	ВводИнформацииОЗавершившемсяМероприятии = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(
		ZayavkaNaKontrolnuyuOperaciyu, "VvodInformaciiOZavershivshemsyaMeropriyatii");
		
	ДополнительныеСвойства.Вставить("ВводИнформацииОЗавершившемсяМероприятии", ВводИнформацииОЗавершившемсяМероприятии);
		
	ра_ОбщегоНазначения.ДокументыКачестваПередЗаписью(ЭтотОбъект);
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ДополнительныеСвойства.Свойство("НужноПометитьНаУдалениеБизнесСобытия") Тогда
		БизнесСобытияВызовСервера.ПометитьНаУдалениеСобытияПоИсточнику(Ссылка);
	КонецЕсли;
	
	Если ДополнительныеСвойства.Свойство("Состояние") Тогда
			
		Делопроизводство.ЗаписатьСостояниеДокумента(
			Ссылка,
			ТекущаяДатаСеанса(),
			ДополнительныеСвойства.Состояние,
			ПользователиКлиентСервер.ТекущийПользователь());
		
	КонецЕсли;
	
	//Упрощенный процесс КО (ввод информации о завершившемся мероприятии)
	Если ДополнительныеСвойства.ЭтоНовый И ДополнительныеСвойства.ВводИнформацииОЗавершившемсяМероприятии Тогда
		Запрос = Новый Запрос();
		Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
		|	ra_OpisaniePredmetaKontrolya.ID КАК ID
		|ПОМЕСТИТЬ ВТ_Идентификаторы
		|ИЗ
		|	РегистрСведений.ra_OpisaniePredmetaKontrolya КАК ra_OpisaniePredmetaKontrolya
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ra_ZayavkaNaKontrolnuyuOperaciyu КАК ra_ZayavkaNaKontrolnuyuOperaciyu
		|		ПО ra_OpisaniePredmetaKontrolya.ZayavkaNaKontrolnuyuOperaciyu = &ZayavkaNaKontrolnuyuOperaciyu
		|			И ra_ZayavkaNaKontrolnuyuOperaciyu.VvodInformaciiOZavershivshemsyaMeropriyatii = ИСТИНА
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
		|	ra_Nesootvetstvie.Ссылка КАК Ссылка
		|ИЗ
		|	Документ.ra_Nesootvetstvie КАК ra_Nesootvetstvie
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ra_Uvedomlenie КАК ra_Uvedomlenie
		|		ПО ra_Nesootvetstvie.Ссылка = ra_Uvedomlenie.Nesootvetstvie
		|			И (НЕ ra_Uvedomlenie.ПометкаУдаления)
		|ГДЕ
		|	ra_Nesootvetstvie.OpisaniePredmetaKontrolyaID В
		|			(ВЫБРАТЬ
		|				ВТ_Идентификаторы.ID
		|			ИЗ
		|				ВТ_Идентификаторы)
		|	И ra_Uvedomlenie.Ссылка ЕСТЬ NULL";
		
		Запрос.УстановитьПараметр("ZayavkaNaKontrolnuyuOperaciyu", ZayavkaNaKontrolnuyuOperaciyu);
		
		Выборка = Запрос.Выполнить().Выбрать();
		
		Пока Выборка.Следующий() Цикл
			
			БизнесСобытияВызовСервера.ЗарегистрироватьСобытие(
				Выборка.Ссылка,
				Справочники.ВидыБизнесСобытий.ра_РегистрацияНесоответствия);
				
		КонецЦикла;
				
	КонецЕсли;
	
	Если Не ДополнительныеСвойства.Свойство("НеСоздаватьФайлСВерсией") Тогда
		Документы.ra_Nesootvetstvie.СоздатьФайлСВерсией(ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Процедура ДобавитьУчастниковРабочейГруппыВНабор(ТаблицаНабора) Экспорт
	
	Документы.ra_ZayavkaNaKontrolnuyuOperaciyu.ДобавитьУчастниковРабочейГруппыВНабор(ТаблицаНабора, ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#КонецОбласти

#КонецЕсли