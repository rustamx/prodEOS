#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ra_ZayavkaNaKontrolnuyuOperaciyu") Тогда
		
		ZayavkaNaKontrolnuyuOperaciyu = ДанныеЗаполнения;
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ra_UchastnikiKontrolnyhOperaciy.Organizaciya КАК Organizaciya,
		|	ra_UchastnikiKontrolnyhOperaciy.Podrazdelenie КАК Podrazdelenie,
		|	ra_UchastnikiKontrolnyhOperaciy.Dolzhnost КАК Dolzhnost,
		|	ra_UchastnikiKontrolnyhOperaciy.Otvetstvennyj КАК Otvetstvennyj
		|ИЗ
		|	РегистрСведений.ra_UchastnikiKontrolnyhOperaciy КАК ra_UchastnikiKontrolnyhOperaciy
		|ГДЕ
		|	ra_UchastnikiKontrolnyhOperaciy.ZayavkaNaKontrolnuyuOperaciyu = &ZayavkaNaKontrolnuyuOperaciyu";
		
		Запрос.УстановитьПараметр("ZayavkaNaKontrolnuyuOperaciyu", ZayavkaNaKontrolnuyuOperaciyu);
		
		Soglasuyushchie.Загрузить(Запрос.Выполнить().Выгрузить());
		
	КонецЕсли;
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, ДанныеЗаполнения);
	КонецЕсли;
	
	ра_ОбщегоНазначения.ЗаполнитьВидДокумента(ЭтотОбъект);
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Документы.ra_RezultatKontrolnoyOperacii.АктуализироватьМассивОбязательныхРеквизитов(ПроверяемыеРеквизиты, ЭтотОбъект);
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	// ТСК Близнюк С.И.; 09.10.2018; task#1373{
	Если ОценкаПроизводительностиВызовСервераПовтИсп.ВыполнятьЗамерыПроизводительности() Тогда
		ВремяНачала = ОценкаПроизводительностиКлиентСервер.НачатьЗамерВремени();
		ДополнительныеСвойства.Вставить("ВремяНачала", ВремяНачала);	
	КонецЕсли;
	// ТСК Близнюк С.И.; 09.10.2018; task#1373}
	
	ДополнительныеСвойства.Вставить("ЭтоНовый", ЭтоНовый());
		
	ра_ОбщегоНазначения.ДокументыКачестваПередЗаписью(ЭтотОбъект);
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ДополнительныеСвойства.ЭтоНовый Тогда
		Запрос = Новый Запрос();
		Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
		|	ra_OpisaniePredmetaKontrolya.ID КАК ID
		|ПОМЕСТИТЬ ВТ_Идентификаторы
		|ИЗ
		|	РегистрСведений.ra_OpisaniePredmetaKontrolya КАК ra_OpisaniePredmetaKontrolya
		|ГДЕ
		|	ra_OpisaniePredmetaKontrolya.ZayavkaNaKontrolnuyuOperaciyu = &ZayavkaNaKontrolnuyuOperaciyu
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
		|	ra_Nesootvetstvie.Ссылка КАК Ссылка
		|ИЗ
		|	Документ.ra_Nesootvetstvie КАК ra_Nesootvetstvie
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ra_Uvedomlenie КАК ra_Uvedomlenie
		|		ПО ra_Nesootvetstvie.Ссылка = ra_Uvedomlenie.Nesootvetstvie
		|			И (НЕ ra_Uvedomlenie.ПометкаУдаления)
		|ГДЕ
		|	ra_Nesootvetstvie.OpisaniePredmetaKontrolyaID В
		|			(ВЫБРАТЬ
		|				ВТ_Идентификаторы.ID
		|			ИЗ
		|				ВТ_Идентификаторы)
		|	И ra_Uvedomlenie.Ссылка ЕСТЬ NULL";
		
		Запрос.УстановитьПараметр("ZayavkaNaKontrolnuyuOperaciyu", ZayavkaNaKontrolnuyuOperaciyu);
		
		Предметы = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
		
		Исполнители = Новый Массив;
		Исполнители.Добавить(
			ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ZayavkaNaKontrolnuyuOperaciyu, "Kontroler"));
		
		ПараметрыПроцесса = Новый Структура("Предметы, Исполнители", Предметы, Исполнители);
		
		КонтекстСобытия = Новый ХранилищеЗначения(ПараметрыПроцесса);
		
		БизнесСобытияВызовСервера.ЗарегистрироватьСобытие(
			Ссылка,
			Справочники.ВидыБизнесСобытий.ра_РегистрацияРезультатаКО,
			КонтекстСобытия);
		
	КонецЕсли;
			
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Процедура ДобавитьУчастниковРабочейГруппыВНабор(ТаблицаНабора) Экспорт
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#КонецОбласти

#КонецЕсли