#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	ра_ОбщегоНазначения.ЗаполнитьВидДокумента(ЭтотОбъект);
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, ДанныеЗаполнения);
	КонецЕсли;
	
	ТекущийПользователь = ПользователиКлиентСервер.ТекущийПользователь();
	// ТСК Ткаченко И.Л.; 06.08.2018; {
	DataRegistracii = НачалоДня(ТекущаяДата());
	Ispolnitel = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ТекущийПользователь, "ра_Организация");
	Status =  Перечисления.ra_StatusyZayavokNaOcenkuSootvetstviya.Zayavka;
	// ТСК Ткаченко И.Л.; 06.08.2018; }
	
	Если FormaOS = Перечисления.ra_FormyOcenkiSootvetstviya.PoryadokObyazatelnojSertifikacii Тогда 
		VidProdukciiVSootvetstviiSNP_071_18 = Справочники.ra_VidyProdukciiVSootvetstviiSNP_071_18.ЗначениеПоУмолчаниюДляСертификации;
	КонецЕсли;
	
	Если FormaOS = Перечисления.ra_FormyOcenkiSootvetstviya.Priemka Тогда
		СтруктураРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Ispolnitel, "ра_СпециализированнаяОрганизация,ра_ЭксплуатирующаяОрганизация");
		Если НЕ СтруктураРеквизитов["ра_СпециализированнаяОрганизация"]
				И НЕ СтруктураРеквизитов["ра_ЭксплуатирующаяОрганизация"]
				И Ссылка.Пустая() Тогда
			ВызватьИсключение НСтр("ru = 'Автор не числится в эксплуатирующей или в специализированной организации'; en = 'User is not associated to operating or specialized organization'");
		КонецЕсли;
	КонецЕсли;
		
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Документы.ra_ZayavkaNaOcenkuSootvetstviya.АктуализироватьМассивОбязательныхРеквизитов(ПроверяемыеРеквизиты, ЭтотОбъект);
	
	//Если FormaOS = Перечисления.ra_FormyOcenkiSootvetstviya.AttestacionnyeIspytaniya Тогда
	//	Если НЕ ЗначениеЗаполнено(VidIspytanij) Тогда 
	//		ТекстОшибки = НСтр("ru='Не указан вид испытания'; en = 'Not specified type of test'");
	//		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки, , "VidIspytanij");
	//	КонецЕсли;		
	//КонецЕсли;
	Если DataZayavki > DataRegistracii И (ЗначениеЗаполнено(DataZayavki) И ЗначениеЗаполнено(DataRegistracii)) Тогда 
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru='Дата регистрации не может быть раньше даты заявки'; en = 'Registration date must be later than the application date'"), , "DataZayavki", , Отказ)
	КонецЕсли;
	
	// ТСК Близнюк С.И.; 01.11.2018; task#1609{
	//Если SrokSertificataS > SrokSertificataPo Тогда 
	Если SrokSertificataS > SrokSertificataPo И НЕ 
		(SkhemaSertifikacii = Перечисления.ra_SkhemySertifikacii.C6_IspytaniyaPartii ИЛИ
		SkhemaSertifikacii = Перечисления.ra_SkhemySertifikacii.C7_IspytanieKazhdogoObrazca) Тогда
	// ТСК Близнюк С.И.; 01.11.2018; task#1609}
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru='Дата окончания действия сертификата не может быть раньше даты начала'; en = 'End of validity period must be later than begin of validity period'"), , "SrokSertificataPo", , Отказ)
	КонецЕсли;
		
	Если (SrokSertificataS >= SrokSertificataPo И ЗначениеЗаполнено(SrokSertificataS) И ЗначениеЗаполнено(SrokSertificataPo) И
		FormaOS = Перечисления.ra_FormyOcenkiSootvetstviya.PoryadokObyazatelnojSertifikacii И
		RezultatProvedeniya = Перечисления.ra_SootvetstvuetIliNet.Sootvetstvuet И НЕ 
		(SkhemaSertifikacii = Перечисления.ra_SkhemySertifikacii.C6_IspytaniyaPartii ИЛИ
		SkhemaSertifikacii = Перечисления.ra_SkhemySertifikacii.C7_IspytanieKazhdogoObrazca)) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru='Некорректные сроки сертификации'; en = 'Invalid certification dates'"), , "SrokSertificataS", , Отказ)
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	// ТСК Близнюк С.И.; 09.10.2018; task#1373{
	Если ОценкаПроизводительностиВызовСервераПовтИсп.ВыполнятьЗамерыПроизводительности() Тогда
		ВремяНачала = ОценкаПроизводительностиКлиентСервер.НачатьЗамерВремени();
		ДополнительныеСвойства.Вставить("ВремяНачала", ВремяНачала);	
	КонецЕсли;
	// ТСК Близнюк С.И.; 09.10.2018; task#1373}
	
	ДополнительныеСвойства.Вставить("ЭтоНовый", ЭтоНовый());
	
	Если ЭтоНовый() И Не ЗначениеЗаполнено(Дата) Тогда 
		Дата = ТекущаяДатаСеанса();
	КонецЕсли;
	
	ра_ОбщегоНазначения.ДокументыКачестваПередЗаписью(ЭтотОбъект);
	
	Если Status = Перечисления.ra_StatusyZayavokNaOcenkuSootvetstviya.Zakryta И Не ЗначениеЗаполнено(DataZaversheniyaOS) Тогда
		DataZaversheniyaOS = ТекущаяДатаСеанса();
	КонецЕсли;
			
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ДополнительныеСвойства.Свойство("НужноПометитьНаУдалениеБизнесСобытия") Тогда
		БизнесСобытияВызовСервера.ПометитьНаУдалениеСобытияПоИсточнику(Ссылка);
	КонецЕсли;
	
	// ТСК Корнюшенков А.Ю. 28.06.2018 {
	// настройка автозапуска процессов для документа "Уведомление о несоответствии"
	// Возможно, выполнена явная регистрация событий при загрузке объекта.
	Если Не ДополнительныеСвойства.Свойство("НеРегистрироватьБизнесСобытия") Тогда
		Если ДополнительныеСвойства.ЭтоНовый Тогда
			БизнесСобытияВызовСервера.ЗарегистрироватьСобытие(Ссылка, Справочники.ВидыБизнесСобытий.СозданиеВнутреннегоДокумента);
		Иначе
			Если НЕ ра_ОбщегоНазначения.ЕстьБизнесПроцессыПоДокументу(Ссылка) Тогда
				БизнесСобытияВызовСервера.ЗарегистрироватьСобытие(Ссылка, Справочники.ВидыБизнесСобытий.ИзменениеВнутреннегоДокумента);
				ДополнительныеСвойства.Вставить("СтартВыполнен", Истина);
				ДополнительныеСвойства.Вставить("Описание", "ok");
			Иначе
				ДополнительныеСвойства.Вставить("СтартВыполнен", Ложь);
				ДополнительныеСвойства.Вставить("Описание", НСтр("ru = 'Не запущен процесс согласования измененного уведомления, т.к. есть предыдущие незавершенные бизнес-процесы.'; en = 'The start of the business process is canceled. There are unfinished business processes.'"));
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	// ТСК Корнюшенков А.Ю. 28.06.2018 }
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Процедура ДобавитьУчастниковРабочейГруппыВНабор(ТаблицаНабора) Экспорт
	
	Организации = Новый Массив;
	Организации.Добавить(EkspluatiruyushchayaOrganizaciya);
	Организации.Добавить(Ispolnitel);
	Организации.Добавить(Izgotovitel);
	Организации.Добавить(Zayavitel);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организации", Организации);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Пользователи.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.Пользователи КАК Пользователи
	|ГДЕ
	|	Пользователи.ра_Организация В(&Организации)";
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		РаботаСРабочимиГруппами.ДобавитьУчастникаВТаблицуНабора(ТаблицаНабора, Выборка.Ссылка);
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#КонецОбласти

#КонецЕсли
