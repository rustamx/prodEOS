
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Элементы.ТипПолучателя.СписокВыбора.Добавить(НСтр("ru='Кому:'; en = 'To:'"));
	Элементы.ТипПолучателя.СписокВыбора.Добавить(НСтр("ru='Копия:'; en = 'Cc:'"));
	Элементы.ТипПолучателя.СписокВыбора.Добавить(НСтр("ru='Скрытая копия:'; en = 'Bcc:'"));
	
	ТипПолучателя = НСтр("ru='Кому:'; en = 'To:'");
	
	Если Параметры.Свойство("УникальныйИдентификаторРодительскойФормы") Тогда
		
		УникальныйИдентификаторРодительскойФормы = Параметры.УникальныйИдентификаторРодительскойФормы;
		
	КонецЕсли;	
	
	Если Параметры.Свойство("ЗаполнениеИзСтроки") Тогда
		
		ЗаполнениеИзСтроки = Параметры.ЗаполнениеИзСтроки;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ДобавитьПолучателей(Команда)
	
	МассивАдресов = РаботаСоСтроками.РазложитьСтрокуПочтовыхАдресов(ТекстСАдресами);
	
	МассивРезультата = Новый Массив;
	МассивВозврата = Новый Массив;
	МассивФамилий = Новый Массив;
	
	Для каждого ПочтовыйАдресИнфо Из МассивАдресов Цикл
		
		Если ЗначениеЗаполнено(ПочтовыйАдресИнфо.Адрес) Тогда
			
			СтруктураПочтовогоАдреса = Новый Структура;
			СтруктураПочтовогоАдреса.Вставить("ФИО", "");
			СтруктураПочтовогоАдреса.Вставить("Адрес", ПочтовыйАдресИнфо.Адрес);
			СтруктураПочтовогоАдреса.Вставить("Представление", ПочтовыйАдресИнфо.ОтображаемоеИмя);
			СтруктураПочтовогоАдреса.Вставить("ТипПолучателя", ТипПолучателя);
			МассивВозврата.Добавить(СтруктураПочтовогоАдреса);
			
		ИначеЕсли ЗначениеЗаполнено(ПочтовыйАдресИнфо.ФИО) Тогда
			
			СтруктураПочтовогоАдреса = Новый Структура;
			СтруктураПочтовогоАдреса.Вставить("ФИО", ПочтовыйАдресИнфо.ФИО);
			СтруктураПочтовогоАдреса.Вставить("Адрес", "");
			СтруктураПочтовогоАдреса.Вставить("Представление", "");
			СтруктураПочтовогоАдреса.Вставить("ТипПолучателя", ТипПолучателя);
			СтруктураПочтовогоАдреса.Вставить("Группа", ТипПолучателя);
			
			МассивФамилий.Добавить(СтруктураПочтовогоАдреса);
			
		КонецЕсли;
	
	КонецЦикла;
	
	ТекстОшибок = "";
	
	Если МассивВозврата.Количество() <> 0 Тогда
		ЗаполнитьКонтакты(МассивВозврата);
	КонецЕсли;
	
	Если МассивФамилий.Количество() <> 0 Тогда
		ЗаполнитьАдресатовПоФИО(МассивФамилий, ТекстОшибок);
	КонецЕсли;
	
	Для Каждого Значение Из МассивВозврата Цикл 
		МассивРезультата.Добавить(Значение)
	КонецЦикла;
	Для Каждого Значение Из МассивФамилий Цикл 
		МассивРезультата.Добавить(Значение)
	КонецЦикла;
	
	Если МассивРезультата.Количество() <> 0 Или ЗначениеЗаполнено(ТекстОшибок) Тогда
		ПараметрыОповещения = Новый Структура;
		ПараметрыОповещения.Вставить("МассивРезультата", МассивРезультата);
		ПараметрыОповещения.Вставить("ТекстОшибок", ТекстОшибок);
		ПараметрыОповещения.Вставить("ЗаполнениеИзСтроки", ЗаполнениеИзСтроки);
		Оповестить("ВставкаПолучателейПисьма_Добавить", ПараметрыОповещения, 
			УникальныйИдентификаторРодительскойФормы);
	КонецЕсли;
	
	Закрыть();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьКонтакты(МассивВозвратаАргумент)
	
	ИспользованныеАдреса = Новый Соответствие;
	МассивВозврата = Новый Массив;
	
	Для каждого СтруктураПочтовогоАдреса Из МассивВозвратаАргумент Цикл
		
		Если ИспользованныеАдреса.Получить(СтруктураПочтовогоАдреса.Адрес) = Неопределено Тогда
			
			ИспользованныеАдреса.Вставить(СтруктураПочтовогоАдреса.Адрес, 1);
			
			Адресат = ВстроеннаяПочтаСерверПовтИсп.ПолучитьПочтовогоАдресата(
				СтруктураПочтовогоАдреса.Адрес, СтруктураПочтовогоАдреса.Представление);
			ПредставлениеАдресата = ВстроеннаяПочтаСервер.ПолучитьПредставлениеИКонтактАдресата(Адресат);
			Контакт = ПредставлениеАдресата.Контакт;
			СтруктураПочтовогоАдреса.Вставить("Контакт", Контакт);
			СтруктураПочтовогоАдреса.Представление = ПредставлениеАдресата.Представление;
			
			МассивВозврата.Добавить(СтруктураПочтовогоАдреса);
			
		КонецЕсли;	
			
	КонецЦикла;
	
	МассивВозвратаАргумент = МассивВозврата;

КонецПроцедуры

&НаСервереБезКонтекста
Процедура ЗаполнитьАдресатовПоФИО(МассивВозвратаАргумент, ТекстОшибок)
	
	ИспользованныеАдреса = Новый Соответствие;
	МассивВозврата = Новый Массив;
	
	Для каждого СтруктураПочтовогоАдреса Из МассивВозвратаАргумент Цикл
		
		Если ИспользованныеАдреса.Получить(СтруктураПочтовогоАдреса.ФИО) = Неопределено Тогда
			
			ИспользованныеАдреса.Вставить(СтруктураПочтовогоАдреса.ФИО, 1);
			
			МассивАдресатов = ВстроеннаяПочтаСервер.ПолучитьПочтовогоАдресатаПоФИО(СтруктураПочтовогоАдреса.ФИО);
				
			Если ТипЗнч(МассивАдресатов) = Тип("Массив") Тогда 
				Если МассивАдресатов.Количество() = 1 Тогда 
					ПредставлениеАдресата = ВстроеннаяПочтаСервер.ПолучитьПредставлениеИКонтактАдресата(МассивАдресатов[0]);
					Контакт = ПредставлениеАдресата.Контакт;
					СтруктураПочтовогоАдреса.Вставить("Контакт", Контакт);
					СтруктураПочтовогоАдреса.Вставить("Адресат", МассивАдресатов[0]);
					СтруктураПочтовогоАдреса.Представление = ПредставлениеАдресата.Представление;
					СтруктураПочтовогоАдреса.Вставить("МассивАдресатов", МассивАдресатов);
					
					МассивВозврата.Добавить(СтруктураПочтовогоАдреса);
				
				ИначеЕсли МассивАдресатов.Количество() = 0 Тогда 
					ТекстОшибок = ТекстОшибок + СтрШаблон(
						НСтр("ru = 'Адресат ""%1"" не найден.'; en = 'Addressee ""%1"" is not found.'"),
						СтруктураПочтовогоАдреса.ФИО) + Символы.ПС;  	
					
				ИначеЕсли МассивАдресатов.Количество() > 1 Тогда 
					СтрокаНайденныхАдресатов = "";
					Для Каждого Адресата Из МассивАдресатов Цикл
						СтрокаНайденныхАдресатов = СтрокаНайденныхАдресатов + Адресата.Наименование + ", ";
					КонецЦикла;
					СтрокаНайденныхАдресатов = Лев(СтрокаНайденныхАдресатов, СтрДлина(СтрокаНайденныхАдресатов) - 2);
					
					ТекстОшибок = ТекстОшибок + СтрШаблон(
						НСтр("ru = 'Для адресата ""%1"" найдено несколько совпадений: %2.'; en = 'For the recipient ""%1"" multiple matches are found: %2.'"),
						СтруктураПочтовогоАдреса.ФИО, СтрокаНайденныхАдресатов) + Символы.ПС;
					
				КонецЕсли;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Если ЗначениеЗаполнено(ТекстОшибок) Тогда 
		ТекстОшибок = ТекстОшибок + Символы.ПС +
			НСтр("ru = 'Проверьте правильность написания перечисленных адресатов и повторите добавление пользователей'; en = 'Check the spelling of the listed recipients and repeat adding users'");
	КонецЕсли;
	
	МассивВозвратаАргумент = МассивВозврата;
	
КонецПроцедуры

#КонецОбласти
