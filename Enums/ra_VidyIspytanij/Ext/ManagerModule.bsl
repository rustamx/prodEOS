
#Область ОбработчикиСобытий

Процедура ОбработкаПолученияДанныхВыбора(ДанныеВыбора, Параметры, СтандартнаяОбработка)
	
	Если Не Параметры.Свойство("НеОтбирать") Тогда
		
		СтандартнаяОбработка = Ложь;
		
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	ra_VidyIspytanij.Ссылка КАК Ссылка,
		|	ПРЕДСТАВЛЕНИЕ(ra_VidyIspytanij.Ссылка) КАК Представление
		|ИЗ
		|	Перечисление.ra_VidyIspytanij КАК ra_VidyIspytanij
		|%1";
		
		ФормаОС = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Параметры.Отбор, "FormaOS");
		Если ФормаОС <> Неопределено Тогда
			Если ФормаОС = Перечисления.ra_FormyOcenkiSootvetstviya.AttestacionnyeIspytaniya Тогда
				ТекстЗапросаУсловие = 
				"ГДЕ
				|	ra_VidyIspytanij.Ссылка <> ЗНАЧЕНИЕ(Перечисление.ra_VidyIspytanij.NePrimenyaetsya)";
			Иначе
				ТекстЗапросаУсловие = 
				"ГДЕ
				|	ra_VidyIspytanij.Ссылка = ЗНАЧЕНИЕ(Перечисление.ra_VidyIspytanij.NePrimenyaetsya)";
			КонецЕсли;
			ТекстЗапроса = СтрШаблон(ТекстЗапроса, ТекстЗапросаУсловие);
		Иначе
			ТекстЗапроса = СтрШаблон(ТекстЗапроса, "");
		КонецЕсли;
		
		Запрос = Новый Запрос(ТекстЗапроса);				
		
		ДанныеВыбора = Новый СписокЗначений;
		
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			ДанныеВыбора.Добавить(Выборка.Ссылка, Выборка.Представление);
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
