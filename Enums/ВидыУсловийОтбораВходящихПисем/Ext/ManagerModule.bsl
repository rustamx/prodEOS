Функция ВычислитьТекстовоеОписаниеСпискаЗначений(ВидУсловия, СписокЗначений) Экспорт
	
	Представление = "";
	
	Если СписокЗначений.Количество() = 0
		Или (Не ЗначениеЗаполнено(СписокЗначений[0].Значение)
		И ВидУсловия <> Перечисления.ВидыУсловийОтбораВходящихПисем.ПолученоВТечениеУказанногоПериода) Тогда
		Возврат Представление;
	КонецЕсли;

	Если ВидУсловия = Перечисления.ВидыУсловийОтбораВходящихПисем.ТемаСодержитУказанныеСлова
		ИЛИ ВидУсловия = Перечисления.ВидыУсловийОтбораВходящихПисем.ТемаНачинаетсяС
		ИЛИ ВидУсловия = Перечисления.ВидыУсловийОтбораВходящихПисем.ТекстСодержитУказанныеСлова 
		ИЛИ ВидУсловия = Перечисления.ВидыУсловийОтбораВходящихПисем.АдресОтправителяСодержитУказанныеСлова 
		ИЛИ ВидУсловия = Перечисления.ВидыУсловийОтбораВходящихПисем.ОтправленоНаУказанныеАдреса
		ИЛИ ВидУсловия = Перечисления.ВидыУсловийОтбораВходящихПисем.ПолучательВходитВГруппы
		ИЛИ ВидУсловия = Перечисления.ВидыУсловийОтбораВходящихПисем.ОтправительВходитВГруппы Тогда
		
		Для Каждого ЭлементСписка Из СписокЗначений Цикл
			Если Не ЗначениеЗаполнено(Представление) Тогда
				Представление = Строка(ЭлементСписка.Значение);
			Иначе
				Представление = Представление + ";" + Строка(ЭлементСписка.Значение);
			КонецЕсли;
		КонецЦикла;
		
	ИначеЕсли ВидУсловия = Перечисления.ВидыУсловийОтбораВходящихПисем.ПолученоВТечениеУказанногоПериода Тогда	
		
		Если СписокЗначений.Количество() = 2 Тогда
			ДатаПосле = СписокЗначений[0].Значение;
			ДатаДо = СписокЗначений[1].Значение;
			
			Если ЗначениеЗаполнено(ДатаПосле) Тогда
			Представление = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'С %1'; en = 'From %1'"),
				Формат(ДатаПосле, "ДФ='dd.MM.yyyy HH:mm'"));
			Конецесли;
			
			Если ЗначениеЗаполнено(ДатаДо) Тогда
				Если ЗначениеЗаполнено(Представление) Тогда
					Представление = Представление + 
						СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
							НСтр("ru = ' и по %1'; en = ' and to %1'"),
							Формат(ДатаДо, "ДФ='dd.MM.yyyy HH:mm'"));
				Иначе
					Представление = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						НСтр("ru = 'По %1'; en = 'To %1'"),
						Формат(ДатаДо, "ДФ='dd.MM.yyyy HH:mm'"));	
				КонецЕсли;
			Конецесли;
		КонецЕсли;
	Иначе
		Представление = Строка(СписокЗначений[0].Значение);
	КонецЕсли;

	Возврат Представление;
	
КонецФункции

Функция ОтрицаниеУсловия(ВидУсловия) Экспорт
	
	Если ВидУсловия = Перечисления.ВидыУсловийОтбораВходящихПисем.АдресованоМне Тогда
		Возврат НСтр("ru = 'НЕ адресовано мне'; en = 'NOT addressed to me'");
	ИначеЕсли ВидУсловия = Перечисления.ВидыУсловийОтбораВходящихПисем.АдресованоТолькоМне Тогда
		Возврат НСтр("ru = 'Адресовано НЕ только мне'; en = 'Addressed NOT only to me:'");
	ИначеЕсли ВидУсловия = Перечисления.ВидыУсловийОтбораВходящихПисем.ОтправленоНаУказанныеАдреса Тогда
		Возврат НСтр("ru = 'НЕ отправлено ни на один из адресов:'; en = 'NOT sent to any of the addresses:'");	
	ИначеЕсли ВидУсловия = Перечисления.ВидыУсловийОтбораВходящихПисем.АдресОтправителяСодержитУказанныеСлова Тогда
		Возврат НСтр("ru = 'Адрес отправителя НЕ содержит:'; en = 'Sender address DOES NOT contain:'");
	ИначеЕсли ВидУсловия = Перечисления.ВидыУсловийОтбораВходящихПисем.ИмеетВложения Тогда
		Возврат НСтр("ru = 'НЕ имеет вложений'; en = 'DOES NOT have attachments'");
	ИначеЕсли ВидУсловия = Перечисления.ВидыУсловийОтбораВходящихПисем.ПолученоВТечениеУказанногоПериода Тогда
		Возврат НСтр("ru = 'Получено НЕ в течение указанного периода:'; en = 'Received NOT during the period:'");
	ИначеЕсли ВидУсловия = Перечисления.ВидыУсловийОтбораВходящихПисем.ТекстСодержитУказанныеСлова Тогда
		Возврат НСтр("ru = 'Текст НЕ содержит:'; en = 'Body DOES NOT contain:'");
	ИначеЕсли ВидУсловия = Перечисления.ВидыУсловийОтбораВходящихПисем.ТемаСодержитУказанныеСлова Тогда
		Возврат НСтр("ru = 'Тема НЕ содержит текст:'; en = 'Subject DOES NOT contain the text:'");
	ИначеЕсли ВидУсловия = Перечисления.ВидыУсловийОтбораВходящихПисем.ТемаНачинаетсяС Тогда
		Возврат НСтр("ru = 'Тема НЕ начинается с:'; en = 'Subject DOES NOT start with:'");	
	ИначеЕсли ВидУсловия = Перечисления.ВидыУсловийОтбораВходящихПисем.ОтправительВходитВГруппы Тогда
		Возврат НСтр("ru = 'Отправитель НЕ входит в группы:'; en = 'Sender DOES NOT belong to groups:'");
	ИначеЕсли ВидУсловия = Перечисления.ВидыУсловийОтбораВходящихПисем.ПолучательВходитВГруппы Тогда
		Возврат НСтр("ru = 'Получатель НЕ входит в группы:'; en = 'Recipient DOES NOT belong to groups:'");
	ИначеЕсли ВидУсловия = Перечисления.ВидыУсловийОтбораВходящихПисем.АдресОтправителяВСписке Тогда
		Возврат НСтр("ru = 'Адрес отправителя НЕ входит в список:'; en = 'Sender address is NOT in the list:'");
	КонецЕсли;
	
	Возврат СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		Нстр("ru = 'НЕ %1'; en = 'DO NOT %1'"),
		Строка(ВидУсловия));
	
КонецФункции


