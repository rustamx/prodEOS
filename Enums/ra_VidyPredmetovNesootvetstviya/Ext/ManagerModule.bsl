
#Область ОбработчикиСобытий

Процедура ОбработкаПолученияДанныхВыбора(ДанныеВыбора, Параметры, СтандартнаяОбработка)
	
	Если Не Параметры.Свойство("НеОтбирать") Тогда
		
		СтандартнаяОбработка = Ложь;
		
		ДанныеВыбора = Новый СписокЗначений;
		
		СхемаЗапроса = Новый СхемаЗапроса;
		
		ТекстЗапроса =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
		|	ra_VidyPredmetovNesootvetstviya.Ссылка,
		|	ПРЕДСТАВЛЕНИЕ(ra_VidyPredmetovNesootvetstviya.Ссылка) КАК Представление
		|ИЗ
		|	Перечисление.ra_VidyPredmetovNesootvetstviya КАК ra_VidyPredmetovNesootvetstviya
		|		%1";
		
		Если Параметры.Отбор.Свойство("EhtapVyyavleniyaNesootvetstvija") Тогда
		
			ТекстЗапросаСоединение = "
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ra_ZavisimostPredmetovOtEtapov КАК ra_ZavisimostPredmetovOtEtapov
			|		ПО ra_VidyPredmetovNesootvetstviya.Ссылка = ra_ZavisimostPredmetovOtEtapov.VidPredmetaNesootvetstviya";
					
			ТекстЗапроса = СтрШаблон(ТекстЗапроса, ТекстЗапросаСоединение);
			
		ИначеЕсли Параметры.Отбор.Свойство("KontrolnoeMeropriyatie") Тогда
			
			ТекстЗапросаСоединение = "
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ra_KontrolnyeMeropriyatiya КАК ra_KontrolnyeMeropriyatiya
			|		ПО ra_VidyPredmetovNesootvetstviya.Ссылка = ra_KontrolnyeMeropriyatiya.VidPredmetaNesootvetstviya
			|			И ra_KontrolnyeMeropriyatiya.Ссылка = &KontrolnoeMeropriyatie";
					
			ТекстЗапроса = СтрШаблон(ТекстЗапроса, ТекстЗапросаСоединение);
					
		Иначе
			ТекстЗапроса = СтрШаблон(ТекстЗапроса, "");
		КонецЕсли;
		
		СхемаЗапроса.УстановитьТекстЗапроса(ТекстЗапроса);
	
		ра_ОбщегоНазначения.ДобавитьУсловияОтбораПоПараметрам(Параметры, СхемаЗапроса.ПакетЗапросов[0].Операторы[0]);
		
		Запрос = ра_ОбщегоНазначения.ПолучитьЗапросПоСхемеЗапроса(СхемаЗапроса, Параметры);
				
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			ДанныеВыбора.Добавить(Выборка.Ссылка, Выборка.Представление);
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
