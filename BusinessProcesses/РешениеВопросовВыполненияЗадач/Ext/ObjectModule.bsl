#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#КонецОбласти

#Область ПрограммныйИнтерфейс_Предметы

// Проверяет права участников процесса на предметы этого процесса.
// Если у участников процесса отсутствуют права на предметы, то выводятся сообщения с привязкой
// к карточке процесса.
//
// Параметры
//  ПроцессОбъект - БизнесПроцессОбъект - процесс.
//  Отказ - Булево - в этот параметр помещается значение Истина, если кто-то из участников не имеет
//                   прав на предметы.
//  ПроверятьПриИзменении - Булево - если указано значение Истина, то проверка выполняется только если
//                          изменены участники или предметы процесса, иначе проверка выполняется всегда.
//
Процедура ПроверитьПраваУчастниковПроцессаНаПредметы(
	ПроцессОбъект, Отказ, ПроверятьПриИзменении) Экспорт
	
	// В процессах решения вопросов не поддерживается мультипредметность.
	// Поэтому нет проверки.
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ПриКопировании(ОбъектКопирования)
	
	Дата = ТекущаяДатаСеанса();
	Автор = ПользователиКлиентСервер.ТекущийПользователь();
	ДатаНачала = '00010101';
	ДатаЗавершения = '00010101';
	
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	Если ЭтоНовый() Тогда
		
		// Проверка на наличие обязательных параметров
		Если ТипЗнч(ДанныеЗаполнения) <> Тип("Структура") Тогда
			ТекстИсключения = НСтр("ru = 'Для создания нового процесса ""Решение вопросов"" необходимо использовать структуру с полями ""ВидВопроса"" и ""Задача"" в качестве данных заполнения.'; en = 'To start new process ""Issue solving"" one must use a structure with fields ""ВидВопроса"" and ""Задача"" as fill data.'");
			ВызватьИсключение ТекстИсключения;
		КонецЕсли;
		
		Если ДанныеЗаполнения.Свойство("ВидВопроса")  Тогда
			ВидВопроса = ДанныеЗаполнения.ВидВопроса;
		Иначе	
			ВидВопроса = Перечисления.ВидыВопросовВыполненияЗадач.Иное;
		КонецЕсли;
		
		Если Не ДанныеЗаполнения.Свойство("Задача")  Тогда
			ТекстИсключения = НСтр("ru = 'В данных заполнения не найдено поле ""Задача"".'; en = 'Field ""Задача"" is not found in fill data.'");
			ВызватьИсключение ТекстИсключения;
		КонецЕсли;
		
		ПредметРассмотрения = ДанныеЗаполнения.Задача;
		
		Дата = ТекущаяДатаСеанса();
		Автор = ПользователиКлиентСервер.ТекущийПользователь();
		Важность = Перечисления.ВариантыВажностиЗадачи.Обычная;
		
		РеквизитыПредметаРассмотрения = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
			ПредметРассмотрения,
			"СрокИсполнения, Выполнена, Автор, БизнесПроцесс, Проект, ПроектнаяЗадача");
		ИнициаторПроцесса = РеквизитыПредметаРассмотрения.Автор;
		
		Если ДанныеЗаполнения.Свойство("Кому") Тогда
			ИнициаторПроцесса = ДанныеЗаполнения.Кому;
		КонецЕсли;	
		
		ГлавнаяЗадача = ПредметРассмотрения;
		Проект = РеквизитыПредметаРассмотрения.Проект;
		ПроектнаяЗадача = РеквизитыПредметаРассмотрения.ПроектнаяЗадача;
		
		Мультипредметность.ПередатьПредметыПроцессу(
			ЭтотОбъект, РеквизитыПредметаРассмотрения.БизнесПроцесс);
		
		ОпределитьСрокИсполнения = Истина;
		
		Если ВидВопроса = Перечисления.ВидыВопросовВыполненияЗадач.ПереносСрока Тогда
			
			Если Не ЗначениеЗаполнено(РеквизитыПредметаРассмотрения.СрокИсполнения) Тогда
				ТекстИсключения = НСтр("ru = 'Нельзя согласовать перенос срока по задаче, у которой не установлен срок исполнения.'; en = 'It is impossible to approve the postponement of the task if due date is not set.'");
				ВызватьИсключение ТекстИсключения;
			КонецЕсли;
			
			Если РеквизитыПредметаРассмотрения.Выполнена Тогда
				ТекстИсключения = НСтр("ru = 'Нельзя согласовать перенос срока завершенной задачи.'; en = 'It is impossible to approve the postponement of the completed task.'");
				ВызватьИсключение ТекстИсключения;
			КонецЕсли;
			
			НовыйСрок = РеквизитыПредметаРассмотрения.СрокИсполнения + 86400;
			
			Если ТекущаяДатаСеанса() <= РеквизитыПредметаРассмотрения.СрокИсполнения Тогда
				СрокИсполнения = РеквизитыПредметаРассмотрения.СрокИсполнения;
				ОпределитьСрокИсполнения = Ложь;
			КонецЕсли;
			
		КонецЕсли;
		
		Если ОпределитьСрокИсполнения Тогда
			Если НЕ ПолучитьФункциональнуюОпцию("ИспользоватьГрафикиРаботы") Тогда
				СрокИсполнения = КонецДня(ТекущаяДатаСеанса() + 3600*24);
			Иначе
				ГрафикАвтораЗадачи = ГрафикиРаботы.ПолучитьГрафикРаботыПользователя(ИнициаторПроцесса);
				СрокИсполнения = ГрафикиРаботы.ПолучитьДатуОкончанияПериода(
					ГрафикАвтораЗадачи, 
					ТекущаяДатаСеанса(), 
					1);
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ОбычнаяЗапись = Истина;
	ТолькоЗаполнениеТаблицыПредметыЗадачИОбновлениеРабочейГруппыПроцессов = Ложь;
	
	Если ДополнительныеСвойства.Свойство("ВидЗаписи") Тогда
		
		ОбычнаяЗапись = Ложь;
		
		ТолькоЗаполнениеТаблицыПредметыЗадачИОбновлениеРабочейГруппыПроцессов = 
			(ДополнительныеСвойства.ВидЗаписи = 
			"ЗаписьСОбновлением_Предметов_ПредметовЗадач_Проекта_ОбщегоСпискаПроцессов_РабочихГруппПредметов_РабочихГруппПроцессов_ДопРеквизитовПоПредметам");
		
		Если Не ТолькоЗаполнениеТаблицыПредметыЗадачИОбновлениеРабочейГруппыПроцессов Тогда 
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	Если ОбычнаяЗапись Тогда
	
		Если Стартован
			И НЕ ЗначениеЗаполнено(ИнициаторПроцесса) Тогда
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				НСтр("ru = 'Поле ""Кому"" не заполнено'; en = '""To"" field is empty'"), ЭтотОбъект, "ИнициаторПроцесса", , Отказ);
				
			Возврат;
			
		КонецЕсли;
		
		// Обработка выполнения задачи
		Если ДополнительныеСвойства.Свойство("ТекущаяЗадача") Тогда
			Если ДополнительныеСвойства.ТекущаяЗадача.ТочкаМаршрута = 
				БизнесПроцессы.РешениеВопросовВыполненияЗадач.ТочкиМаршрута.ОзнакомлениеСРезультатомРассмотрения Тогда
				ОтправитьНаУточнение = ДополнительныеСвойства.ОтправитьНаУточнение;
			КонецЕсли;
		КонецЕсли;
		
		ПредыдущаяПометкаУдаления = Ложь;
		Если Не Ссылка.Пустая() Тогда
			ПредыдущаяПометкаУдаления = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Ссылка, "ПометкаУдаления");
		КонецЕсли;
		ДополнительныеСвойства.Вставить("ПредыдущаяПометкаУдаления", ПредыдущаяПометкаУдаления);
		
		ПометкаИБ = Ссылка.ПометкаУдаления;
		Если ПометкаУдаления <> ПометкаИБ И Не Ссылка.Пустая() Тогда
			// Отбираем файлы и пытаемся поставить им пометку удаления
			Запрос = Новый Запрос;
			Запрос.Текст =
				"ВЫБРАТЬ
				|	Файлы.Ссылка,
				|	Файлы.Редактирует
				|ИЗ
				|	Справочник.Файлы КАК Файлы
				|ГДЕ
				|	Файлы.ВладелецФайла = &Ссылка";
			
			Запрос.УстановитьПараметр("Ссылка", Ссылка);
			Результат = Запрос.Выполнить();
			Выборка = Результат.Выбрать();
			Пока Выборка.Следующий() Цикл
				Если Не Выборка.Редактирует.Пустая() Тогда
					ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						НСтр("ru = 'Вопрос %1 нельзя удалить, т.к. к нему прикреплен файл ""%2"", занятый для редактирования.'; en = 'Question %1 cannot be deleted because it has an attached file ""%2"" that is locked for editing'"),
						Строка(Ссылка),
						Строка(Выборка.Ссылка));
				КонецЕсли;
				ФайлОбъект = Выборка.Ссылка.ПолучитьОбъект();
				ФайлОбъект.Заблокировать();
				ФайлОбъект.УстановитьПометкуУдаления(ПометкаУдаления);
			КонецЦикла;
		КонецЕсли;
		Если ВидВопроса = Перечисления.ВидыВопросовВыполненияЗадач.ПереносСрока Тогда
			Наименование = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Перенос срока по задаче: %1'; en = 'Postponement of the task: %1'"),
				Строка(ПредметРассмотрения));
		Иначе
			Наименование = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Рассмотрение вопроса: %1'; en = 'Solving of issue: %1'"),
				Описание);
		КонецЕсли;
		
	КонецЕсли;
	
	Если ОбычнаяЗапись Или ТолькоЗаполнениеТаблицыПредметыЗадачИОбновлениеРабочейГруппыПроцессов Тогда
		
		// Обработка рабочей группы
		РаботаСБизнесПроцессамиВызовСервера.СформироватьРабочуюГруппу(ЭтотОбъект);
		
		// Заполнение табличной части ПредметыЗадач
		Мультипредметность.ЗаполнитьПредметыТочекДействийПроцесса(ЭтотОбъект);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ДополнительныеСвойства.Свойство("ВидЗаписи") Тогда
		Возврат;
	КонецЕсли;
	
	ПредыдущаяПометкаУдаления = Ложь;
	Если ДополнительныеСвойства.Свойство("ПредыдущаяПометкаУдаления") Тогда
		ПредыдущаяПометкаУдаления = ДополнительныеСвойства.ПредыдущаяПометкаУдаления;
	КонецЕсли;
	
	Если ПометкаУдаления <> ПредыдущаяПометкаУдаления Тогда
		ПротоколированиеРаботыПользователей.ЗаписатьПометкуУдаления(Ссылка, ПометкаУдаления);
	КонецЕсли;	
	
КонецПроцедуры

// Обработчики событий карты маршрута

Процедура СтартПередСтартом(ТочкаМаршрутаБизнесПроцесса, Отказ)
	
	ДатаНачала = ТекущаяДатаСеанса();
	
КонецПроцедуры

Процедура РассмотрениеИнициаторомПередСозданиемЗадач(
	ТочкаМаршрутаБизнесПроцесса,
	ФормируемыеЗадачи,
	СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Задача = Задачи.ЗадачаИсполнителя.СоздатьЗадачу();
	Задача.Дата = ТекущаяДатаСеанса();
	Задача.Автор = Автор;
	Задача.Описание = Описание;
	Задача.Важность = Важность;
	
	Мультипредметность.ЗадачаПередСозданием(ЭтотОбъект, Задача, ТочкаМаршрутаБизнесПроцесса);
	
	Задача.Проект = Проект;
	Задача.ПроектнаяЗадача = ПроектнаяЗадача;
	
	Задача.СрокИсполнения = СрокИсполнения;
	Задача.БизнесПроцесс  = ЭтотОбъект.Ссылка;
	Задача.ТочкаМаршрута  = ТочкаМаршрутаБизнесПроцесса;
	
	Если ТипЗнч(ИнициаторПроцесса) = Тип("СправочникСсылка.Пользователи") Тогда
		Задача.Исполнитель = ИнициаторПроцесса;
	Иначе
		Задача.РольИсполнителя = ИнициаторПроцесса;
	КонецЕсли;
	
	Если ВидВопроса = Перечисления.ВидыВопросовВыполненияЗадач.ПереносСрока Тогда
		Задача.Наименование = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru='Согласовать перенос срока по задаче: %1'; en = 'Approve postponement of task: %1'",
				ЛокализацияКонфигурации.КодЯзыкаИсполнителяЗадачи(Задача)),
			Строка(ПредметРассмотрения));
	Иначе
		Задача.Наименование = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru='Рассмотреть вопрос: %1'; en = 'Review issue: %1'",
				ЛокализацияКонфигурации.КодЯзыкаИсполнителяЗадачи(Задача)),
			Описание);
	КонецЕсли;
	
	РаботаСБизнесПроцессамиВызовСервера.СкопироватьЗначенияДопРеквизитов(Задача.БизнесПроцесс, Задача);
	
	ЗадачаСсылка = Задачи.ЗадачаИсполнителя.ПолучитьСсылку();
	Задача.УстановитьСсылкуНового(ЗадачаСсылка);
	
	ФормируемыеЗадачи.Добавить(Задача);

	Итерация = Итерация + 1;
	НоваяСтрока = ЦиклыРассмотрения.Добавить();
	НоваяСтрока.Цикл = Итерация;
	НоваяСтрока.Задача = ЗадачаСсылка;
	
	РаботаСБизнесПроцессами.ЗаписатьПроцесс(ЭтотОбъект, "ЗаписьСОбновлениемРезультатаВыполнения");
	
КонецПроцедуры

Процедура РассмотрениеИнициаторомПриВыполнении(ТочкаМаршрутаБизнесПроцесса, Задача, Отказ)
	
	УстановитьПривилегированныйРежим(Истина);
	
	ПринятоеРешение = Задача.РезультатВыполнения;
	
	РаботаСБизнесПроцессами.ЗаписатьПроцесс(ЭтотОбъект, "ЗаписьСОбновлениемРезультатаВыполнения");
	
КонецПроцедуры

Процедура ОзнакомлениеСРезультатомРассмотренияПередСозданиемЗадач(
	ТочкаМаршрутаБизнесПроцесса,
	ФормируемыеЗадачи,
	СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Задача = Задачи.ЗадачаИсполнителя.СоздатьЗадачу();
	Задача.Дата = ТекущаяДатаСеанса();
	
	Если ТипЗнч(ИнициаторПроцесса) = Тип("СправочникСсылка.Пользователи") Тогда
		Задача.Автор = ИнициаторПроцесса;
	Иначе
		Запрос = Новый Запрос;
		Запрос.Текст = 
			"ВЫБРАТЬ ПЕРВЫЕ 1
			|	ЗадачаИсполнителя.Ссылка КАК ЗадачаИсполнителя,
			|	ЗадачаИсполнителя.Исполнитель КАК Исполнитель
			|ИЗ
			|	Задача.ЗадачаИсполнителя КАК ЗадачаИсполнителя
			|ГДЕ
			|	ЗадачаИсполнителя.БизнесПроцесс = &БизнесПроцесс
			|	И ЗадачаИсполнителя.Выполнена
			|	И ЗадачаИсполнителя.ТочкаМаршрута = &ТочкаМаршрута
			|	И ЗадачаИсполнителя.Дата < &ДатаЗадачи
			|
			|УПОРЯДОЧИТЬ ПО
			|	ЗадачаИсполнителя.Дата УБЫВ";
		
		Запрос.УстановитьПараметр("БизнесПроцесс", Ссылка);
		Запрос.УстановитьПараметр("ТочкаМаршрута", 
			БизнесПроцессы.РешениеВопросовВыполненияЗадач.ТочкиМаршрута.РассмотрениеИнициатором);
		Запрос.УстановитьПараметр("ДатаЗадачи", Задача.Дата);
		
		Выборка = Запрос.Выполнить().Выбрать();
		Выборка.Следующий();
		Задача.Автор = Выборка.Исполнитель;
	КонецЕсли;
	
	Задача.Описание = Описание;
	Задача.Важность = Важность;
	
	Мультипредметность.ЗадачаПередСозданием(ЭтотОбъект, Задача, ТочкаМаршрутаБизнесПроцесса);
	
	Задача.Проект = Проект;
	Задача.ПроектнаяЗадача = ПроектнаяЗадача;
	
	Задача.БизнесПроцесс = ЭтотОбъект.Ссылка;
	Задача.ТочкаМаршрута = ТочкаМаршрутаБизнесПроцесса;
	
	Задача.Исполнитель = Автор;
	
	Если ВидВопроса = Перечисления.ВидыВопросовВыполненияЗадач.ПереносСрока Тогда
		Задача.Наименование = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru='Ознакомиться: перенос срока по задаче ""%1""'; en = 'Examine the postponement of the task ""%1""'",
				ЛокализацияКонфигурации.КодЯзыкаИсполнителяЗадачи(Задача)),
			Строка(ПредметРассмотрения));
	Иначе
		Задача.Наименование = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru='Ознакомиться с ответом на вопрос: %1'; en = 'Examine the answer to the issue: %1'",
				ЛокализацияКонфигурации.КодЯзыкаИсполнителяЗадачи(Задача)),
			Описание);
	КонецЕсли;
		
	ФормируемыеЗадачи.Добавить(Задача);
	РаботаСБизнесПроцессамиВызовСервера.СкопироватьЗначенияДопРеквизитов(Задача.БизнесПроцесс, Задача);
	
	ЗадачаСсылка = Задачи.ЗадачаИсполнителя.ПолучитьСсылку();
	Задача.УстановитьСсылкуНового(ЗадачаСсылка);
	
	НоваяСтрока = ЦиклыРассмотрения.Добавить();
	НоваяСтрока.Цикл = Итерация;
	НоваяСтрока.Задача = ЗадачаСсылка;
	
	РаботаСБизнесПроцессами.ЗаписатьПроцесс(ЭтотОбъект, "ЗаписьСОбновлениемРезультатаВыполнения");
	
КонецПроцедуры

Процедура ОзнакомлениеСРезультатомРассмотренияПриВыполнении(
	ТочкаМаршрутаБизнесПроцесса, Задача, Отказ)
	
	УстановитьПривилегированныйРежим(Истина);
	ПринятоеРешение = Задача.РезультатВыполнения;
	
	РаботаСБизнесПроцессами.ЗаписатьПроцесс(ЭтотОбъект, "ЗаписьСОбновлениемРезультатаВыполнения");
	
КонецПроцедуры

Процедура УсловиеТребуетсяУточнениеПроверкаУсловия(ТочкаМаршрутаБизнесПроцесса, Результат)
	
	Результат = ОтправитьНаУточнение;
	
КонецПроцедуры

Процедура ЗавершениеПриЗавершении(ТочкаМаршрутаБизнесПроцесса, Отказ)
	
	УстановитьПривилегированныйРежим(Истина);
	ДатаЗавершения = ТекущаяДатаСеанса();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Возвращает признак наличия метода ИзменитьРеквизитыНевыполненныхЗадач
//
Функция ЕстьМетодИзменитьРеквизитыНевыполненныхЗадач() Экспорт
	
	Возврат Истина;
	
КонецФункции

// Обновляет значения реквизитов невыполненных задач
// при изменении реквизитов бизнес-процесса.
//
Процедура ИзменитьРеквизитыНевыполненныхЗадач() Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	НачатьТранзакцию();
	Попытка
		Запрос = Новый Запрос;
		Запрос.Текст =
			"ВЫБРАТЬ
			|	Задачи.Ссылка
			|ИЗ
			|	Задача.ЗадачаИсполнителя КАК Задачи
			|ГДЕ
			|	Задачи.БизнесПроцесс = &БизнесПроцесс
			|	И Задачи.ПометкаУдаления = ЛОЖЬ
			|	И Задачи.Выполнена = ЛОЖЬ";
		
		Запрос.УстановитьПараметр("БизнесПроцесс", Ссылка);
		Результат = Запрос.Выполнить();
		
		ВыборкаДетальныеЗаписи = Результат.Выбрать();
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			ЗадачаОбъект = ВыборкаДетальныеЗаписи.Ссылка.ПолучитьОбъект();
			ЗаблокироватьДанныеДляРедактирования(ЗадачаОбъект.Ссылка);
			ЗадачаОбъект.Важность = Важность;
			ЗадачаОбъект.СрокИсполнения = СрокИсполнения;
			ЗадачаОбъект.Описание = Описание;
			
			ЗадачаОбъект.Проект = Проект;
			ЗадачаОбъект.ПроектнаяЗадача = ПроектнаяЗадача;
			
			Если ТипЗнч(ИнициаторПроцесса) = Тип("СправочникСсылка.Пользователи") Тогда 
				ЗадачаОбъект.Исполнитель = ИнициаторПроцесса;
				ЗадачаОбъект.РольИсполнителя = Неопределено;
			Иначе
				ЗадачаОбъект.Исполнитель = Неопределено;
				ЗадачаОбъект.РольИсполнителя = ИнициаторПроцесса;
			КонецЕсли;
			
			Если ЗадачаОбъект.ТочкаМаршрута = БизнесПроцессы.
				РешениеВопросовВыполненияЗадач.ТочкиМаршрута.РассмотрениеИнициатором Тогда
				
				Если ВидВопроса = Перечисления.ВидыВопросовВыполненияЗадач.ПереносСрока Тогда
					ЗадачаОбъект.Наименование = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						НСтр("ru='Согласовать перенос срока по задаче: %1'; en = 'Approve postponement of task: %1'",
							ЛокализацияКонфигурации.КодЯзыкаИсполнителяЗадачи(ЗадачаОбъект)),
						Строка(ПредметРассмотрения));
				Иначе
					ЗадачаОбъект.Наименование = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						НСтр("ru='Рассмотреть вопрос: %1'; en = 'Review issue: %1'",
							ЛокализацияКонфигурации.КодЯзыкаИсполнителяЗадачи(ЗадачаОбъект)),
						Описание);
				КонецЕсли;
				
			ИначеЕсли ЗадачаОбъект.ТочкаМаршрута = БизнесПроцессы.
				РешениеВопросовВыполненияЗадач.ТочкиМаршрута.ОзнакомлениеСРезультатомРассмотрения Тогда
				
				Если ВидВопроса = Перечисления.ВидыВопросовВыполненияЗадач.ПереносСрока Тогда
					ЗадачаОбъект.Наименование = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						НСтр("ru='Ознакомиться: перенос срока по задаче ""%1""'; en = 'Examine the postponement of the task ""%1""'",
							ЛокализацияКонфигурации.КодЯзыкаИсполнителяЗадачи(ЗадачаОбъект)),
						Строка(ПредметРассмотрения));
				Иначе
					ЗадачаОбъект.Наименование = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						НСтр("ru='Ознакомиться с ответом на вопрос: %1'; en = 'Examine the answer to the issue: %1'",
							ЛокализацияКонфигурации.КодЯзыкаИсполнителяЗадачи(ЗадачаОбъект)),
						Описание);
				КонецЕсли;
					
			КонецЕсли;
			
			ЗадачаОбъект.СрокИсполнения = СрокИсполнения;
			
			ЗадачаОбъект.Записать();
			
		КонецЦикла;
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		ВызватьИсключение;
	КонецПопытки;
	
КонецПроцедуры

// Проверяет что заполнены поля шаблона
Функция ПолучитьСписокНезаполненныхПолейНеобходимыхДляСтарта() Экспорт
	
	МассивПолей = Новый Массив;
	Возврат МассивПолей;
	
КонецФункции

// Возвращает описание задачи, специфичное для бизнес-процесса
Функция ПолучитьОписаниеУведомленияЗадачи(Задача, КодЯзыкаПолучателя) Экспорт
	
	Если Задача.БизнесПроцесс <> Ссылка Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Результат = Неопределено;
	
	Если Задача.ТочкаМаршрута = БизнесПроцессы.
		РешениеВопросовВыполненияЗадач.ТочкиМаршрута.РассмотрениеИнициатором Тогда
		
		НайденнаяСтрока = ЦиклыРассмотрения.Найти(Задача, "Задача");
		Если НайденнаяСтрока <> Неопределено Тогда 
			Если НайденнаяСтрока.Цикл > 1 Тогда
				Результат = НСТР("ru = 'Был задан уточняющий вопрос'; en = 'Clarifying question was asked'", КодЯзыкаПолучателя);
			КонецЕсли;
		КонецЕсли;
		
	ИначеЕсли Задача.ТочкаМаршрута = БизнесПроцессы.
		РешениеВопросовВыполненияЗадач.ТочкиМаршрута.ОзнакомлениеСРезультатомРассмотрения Тогда
		
		Если Задача.Выполнена Тогда
			Если ОтправитьНаУточнение Тогда
				Результат = НСТР("ru = 'Результат ознакомления с ответом: Задан уточняющий вопрос'; en = 'Result of examination of the answer: clarifying question was asked'", КодЯзыкаПолучателя);
			Иначе
				Результат = НСТР("ru = 'Результат ознакомления с ответом: Вопрос закрыт'; en = 'Result of examination of the answer: issue solved'", КодЯзыкаПолучателя);
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Если НЕ ЗначениеЗаполнено(ИнициаторПроцесса) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			"Поле ""Кому"" не заполнено", ЭтотОбъект,,"ИнициаторПроцессаСтрокой", Отказ);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
