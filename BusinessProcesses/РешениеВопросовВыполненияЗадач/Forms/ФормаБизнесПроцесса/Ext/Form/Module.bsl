
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЭтоНовый = Объект.Ссылка.Пустая();
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	// Обработчик подсистемы "Свойства"
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("Объект", Объект);
	ДополнительныеПараметры.Вставить("ИмяЭлементаДляРазмещения", "ГруппаДополнительныеРеквизиты");
	УправлениеСвойствами.ПриСозданииНаСервере(ЭтаФорма, ДополнительныеПараметры);
	Мультипредметность.УстановитьЗначенияДопРеквизитовИДоступностьЭлементовФормыПроцесса(ЭтаФорма, Объект);
	
	// Обработчик механизма "Последние объекты"
	РаботаСПоследнимиОбъектами.ЗаписатьОбращениеКОбъекту(Объект.Ссылка);
	
	// Обработчик подсистемы "Дополнительные отчеты и обработки"
	ДополнительныеОтчетыИОбработки.ПриСозданииНаСервере(ЭтаФорма);
	
	СтартПроцессовСервер.ЗаполнитьНастройкиСтартаВФормеПроцесса(ЭтаФорма);
	
	ОбновитьДоступностьЭлементовФормыПоСостояниюПроцесса();
	
	ИзменятьЗаданияЗаднимЧислом = ПолучитьФункциональнуюОпцию("ИзменятьЗаданияЗаднимЧислом");
	ИспользоватьВремяВСрокахЗадач = ПолучитьФункциональнуюОпцию("ИспользоватьДатуИВремяВСрокахЗадач");
	ВестиУчетПоПроектам = ПолучитьФункциональнуюОпцию("ВестиУчетПоПроектам");
	
	ФорматДатыДляКолонок = ?(ИспользоватьВремяВСрокахЗадач, 
		"ДФ='dd.MM.yy H:mm'",
		"ДФ='dd.MM.yy'");
	
	// Инициализация списка файлов
	ОтображатьУдаленныеФайлы = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
		ИмяФормы,
		"ОтображатьУдаленныеФайлы",
		Ложь);
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(
		Файлы,
		"ВладелецФайла",
		Объект.Ссылка);
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(
		Файлы,
		"ТекущийПользователь",
		ПользователиКлиентСервер.ТекущийПользователь());
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(
		Файлы,
		"ОтображатьУдаленные",
		ОтображатьУдаленныеФайлы);
	РаботаСФайламиВызовСервера.ЗаполнитьУсловноеОформлениеСпискаФайлов(Файлы);
	Элементы.ФайлыКонтекстноеМенюОтображатьУдаленные.Пометка = ОтображатьУдаленныеФайлы;
	
	// Инициализация списка Задачи
	Элементы.ГруппаЗадачи.Видимость = Ложь;
	Если Объект.Стартован Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(
			Задачи,
			"БизнесПроцесс",
			Объект.Ссылка);
			
		Элементы.ГруппаЗадачи.Видимость = Истина;
		
		Элементы.ЗадачиСрокИсполнения.Формат = ФорматДатыДляКолонок;
		Элементы.ЗадачиДатаИсполнения.Формат = ФорматДатыДляКолонок;
		
		Эл = БизнесПроцессыИЗадачиСервер.ЭлементУсловногоОформленияПоПредставлению(
			Задачи.УсловноеОформление, НСтр("ru = 'Принята к исполнению'; en = 'Accepted by performer'"));
		ЭлементОтбораДанных = Эл.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ЭлементОтбораДанных.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ПринятаКИсполнению");
		ЭлементОтбораДанных.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
		ЭлементОтбораДанных.ПравоеЗначение = Ложь;
		ЭлементОтбораДанных.Использование = Истина;
		ЭлементОформления = Эл.Оформление.Элементы.Найти("Шрифт");
		ЭлементОформления.Значение = Новый Шрифт(ШрифтыСтиля.ОбычныйШрифтТекста,,,Истина);
		ЭлементОформления.Использование = Истина;
		
		Эл = БизнесПроцессыИЗадачиСервер.ЭлементУсловногоОформленияПоПредставлению(
			Задачи.УсловноеОформление, НСтр("ru = 'Просрочена'; en = 'Is overdue'"));
		ЭлементОтбораДанных = Эл.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ЭлементОтбораДанных.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("СрокИсполнения");
		ЭлементОтбораДанных.ВидСравнения = ВидСравненияКомпоновкиДанных.Больше;
		ЭлементОтбораДанных.ПравоеЗначение = ТекущаяДатаСеанса();
		ЭлементОтбораДанных.Использование = Истина;
		ЭлементОформления = Эл.Оформление.Элементы.Найти("ЦветТекста");
		ЭлементОформления.Значение = ЦветаСтиля.ПросроченныеДанныеЦвет;
		ЭлементОформления.Использование = Истина;
		Поле = Эл.Поля.Элементы.Добавить();
		Поле.Поле = Новый ПолеКомпоновкиДанных("СрокИсполнения");
		
	КонецЕсли;
	
	// Если время в сроках задач не используется, скрываются соответствующие реквизиты
	Элементы.СрокИсполненияВремя.Видимость = ИспользоватьВремяВСрокахЗадач;
	
	// Сохранение вводимых значений
	СохранениеВводимыхЗначений.ОбновитьСпискиВыбора(ЭтаФорма, ЭлементыДляСохранения(), ЭтаФорма.ИмяФормы);
	
	Элементы.ФормаПометитьНаУдаление.Доступность =
		РаботаСБизнесПроцессамиВызовСервера.ПроверитьДоступностьПометкиУдаленияБизнесПроцесса(Объект.Ссылка);
	
	// Заполняем строковое представление участников
	Элементы.ИнициаторПроцесса.ТолькоПросмотр = ТолькоПросмотр
		Или Объект.ВидВопроса = Перечисления.ВидыВопросовВыполненияЗадач.ПереносСрока;
		
	Элементы.ГруппаИнфо.Видимость = Не ЭтоНовый;
	Если Объект.Стартован Тогда
		Если Объект.Завершен Тогда
			Длительность = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = '(выполнялся %1)'; en = '(was on execution for %1)'"),
				НРег(ДелопроизводствоКлиентСервер.РазностьДатВДнях(Объект.ДатаЗавершения, Объект.ДатаНачала)));
		Иначе
			Длительность = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = '(выполняется %1)'; en = '(on execution for %1)'"),
				НРег(ДелопроизводствоКлиентСервер.РазностьДатВДнях(ТекущаяДатаСеанса(), Объект.ДатаНачала)));
		КонецЕсли;
	КонецЕсли;
		
	Если ЭтоНовый Тогда
		Заголовок = НСтр("ru = 'Новый вопрос'; en = 'New issue'");
		АвтоЗаголовок = Ложь;
		Элементы.ОтветственныйИсполнитель.Видимость = Ложь;
		
		Если Элементы.ГруппаДополнительныеРеквизиты.ПодчиненныеЭлементы.Количество() = 0 Тогда
			Элементы.ГруппаСтраницы.ОтображениеСтраниц = ОтображениеСтраницФормы.Нет;
		КонецЕсли;
		
	КонецЕсли;
	
	Элементы.ГруппаСтарыйИНовыйСрок.Видимость = Ложь;
	Элементы.ГруппаВопросыПоЗадаче.Видимость = Ложь;
	Элементы.ГруппаПереносыСроков.Видимость = Ложь;
	
	Если Объект.ВидВопроса = Перечисления.ВидыВопросовВыполненияЗадач.ПереносСрока Тогда
		
		Элементы.ГруппаГлавное.Заголовок = НСтр("ru = 'Запрос'; en = 'Request'");
		
		Если ЭтоНовый Тогда
			Заголовок = НСтр("ru = 'Новый запрос на перенос срока'; en = 'New postponement request'");
			АвтоЗаголовок = Ложь;
			
			// Инициализация списка существующих вопросов по задаче и запросов на перенос сроков
			Элементы.ГруппаВопросыПоЗадаче.Видимость = Истина;
			Элементы.ГруппаПереносыСроков.Видимость = Истина;
			ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(
				ВопросыПоЗадаче,
				"ПредметРассмотрения",
				Объект.ПредметРассмотрения);
			ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(
				ПереносыСроков,
				"ПредметРассмотрения",
				Объект.ПредметРассмотрения);
			Элементы.ПереносыСроковДата.Формат = ФорматДатыДляКолонок;
			Элементы.ПереносыСроковНовыйСрок.Формат = ФорматДатыДляКолонок;
			Элементы.ВопросыПоЗадачеДата.Формат = ФорматДатыДляКолонок;
			
			// Подсчет количества уже существующих вопросов по задаче
			ПосчитатьКоличествоСуществующихВопросов();
			ПосчитатьКоличествоСуществующихЗапросовНаПереносСрока();
			
		КонецЕсли;
		
		Элементы.ГруппаСтарыйИНовыйСрок.Видимость = Истина;
		
		
		Элементы.ФормаСтартИЗакрыть.Заголовок = НСтр("ru = 'Отправить запрос и закрыть'; en = 'Send request and close'");
		
		СтарыйСрок = СтарыйСрокИсполненияПредметаРассмотрения(
			Объект.ПредметРассмотрения, Объект.Ссылка);
			
		ДлительностьПереноса = ПереносСроковВыполненияЗадачВызовСервера.ПолучитьПодписьДлительностьПереноса(
			Объект.Автор, 
			СтарыйСрок, 
			Объект.НовыйСрок);
		
		Элементы.Описание.ПодсказкаВвода = НСтр("ru = 'Обоснование запроса'; en = 'Request reason'");
		Элементы.Описание_БезПредметов.ПодсказкаВвода = Элементы.Описание.ПодсказкаВвода;
		Элементы.Описание.Подсказка = НСтр("ru = 'Обоснование запроса'; en = 'Request reason'");
		Элементы.Описание_БезПредметов.Подсказка = Элементы.Описание.Подсказка;
		
		Элементы.СтарыйСрокВремя.Видимость = ИспользоватьВремяВСрокахЗадач;
		Элементы.НовыйСрокВремя.Видимость = ИспользоватьВремяВСрокахЗадач;
		ДоступнаУстановкаНовогоСрока = ДоступнаУстановкаНовогоСрока(Объект.Ссылка);
		Элементы.НовыйСрокДата.ТолькоПросмотр = Не ДоступнаУстановкаНовогоСрока;
		Элементы.НовыйСрокВремя.ТолькоПросмотр = Не ДоступнаУстановкаНовогоСрока;
		
	КонецЕсли;
	
	ОбновитьВидимостьТаблицыФайлов(ЭтаФорма);
	Если Элементы.ГруппаСтарыйИНовыйСрок.Видимость Тогда
		ТекущийЭлемент = Элементы.НовыйСрокДата;
	КонецЕсли;
	
	ПроверятьОтсутствие = Отсутствия.ПредупреждатьОбОтсутствии();
	
	// СтандартныеПодсистемы.Печать
	УправлениеПечатью.ПриСозданииНаСервере(ЭтаФорма, Элементы.ФормаГруппаПечать);
	// Конец СтандартныеПодсистемы.Печать
	
	// СтандартныеПодсистемы.БазоваяФункциональность
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.БазоваяФункциональность
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
			
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПриЧтенииНаСервере(ЭтаФорма, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства

КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	ОчиститьСообщения();
	
	Если Объект.ВидВопроса = ПредопределенноеЗначение("Перечисление.ВидыВопросовВыполненияЗадач.Иное") Тогда
		Если НЕ ЗначениеЗаполнено(Объект.Описание) Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				НСтр("ru = 'Поле ""Вопрос"" не заполнено'; en = '""Issue"" field is not filled in'"),
				,
				"Объект.Описание",
				,
				Отказ);
			Возврат;
		КонецЕсли;
	Иначе
		// Проверка на правильность указания нового срока
		Если НЕ ЗначениеЗаполнено(Объект.НовыйСрок) Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				НСтр("ru = 'Не введен новый срок выполнения.'; en = 'New due date is not specified'"),
				,
				"Объект.НовыйСрок",
				,
				Отказ);
		КонецЕсли;
		
		// Проверка заполнения обоснования на перенос
		Если НЕ ЗначениеЗаполнено(Объект.Описание) Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				НСтр("ru = 'Не указано основание для переноса срока.'; en = 'Reason for postponement is not specified.'"),
				,
				"Объект.Описание",
				,
				Отказ);
		КонецЕсли;
	КонецЕсли;
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	// Пока процесс решения вопроса не стартован, каждая его запись будет сдвигать дату создания		
	Если (НЕ ПараметрыЗаписи.Свойство("Старт") ИЛИ НЕ ПараметрыЗаписи.Старт)
		И НЕ Объект.Стартован Тогда
		Объект.Дата = ТекущаяДата();
	КонецЕсли;
	
	ВыполнитьПроверкуНаЗанятыеФайлы = Ложь;
	
	// Перед стартом процесса проверяется занятость файлов и отсутствие исполнителя.
	Если (ПараметрыЗаписи.Свойство("Старт") И ПараметрыЗаписи.Старт)
		Или (Не Объект.Стартован И Модифицированность И ЗначениеЗаполнено(НастройкаСтарта)
			И НастройкаСтарта.Состояние = 
			ПредопределенноеЗначение("Перечисление.СостоянияПроцессовДляЗапуска.ГотовКСтарту"))
		Или ПараметрыЗаписи.Свойство("ФоновыйСтартПроцесса") Тогда
		
		Если НЕ ПараметрыЗаписи.Свойство("СтартоватьПриЗанятыхФайлах")
			И ЕстьЗанятыеНаРедактированиеФайлы(Объект.Ссылка) Тогда
			
			ОписаниеОповещения = Новый ОписаниеОповещения(
				"ПередЗаписьюПослеВопросаОЗанятыхНаРедактированиеФайлах", ЭтотОбъект, ПараметрыЗаписи);
				
			ТекстВопроса = НСтр("ru = 'Один из приложенных файлов занят для редактирования. Отправить вопрос автору?'; en = 'One of the attached files is locked for editing. Send a question to the author?'");
			
			ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет,,
				КодВозвратаДиалога.Нет, НСТР("ru = 'Отправка вопроса'; en = 'Question sending'"));
				
			Отказ = Истина;
			Возврат;
			
		КонецЕсли;
		
		Если Не ОтсутствияКлиент.ПроверитьОтсутствиеПоПроцессуПередЗаписью(
				ЭтаФорма, ПараметрыЗаписи, Отказ) Тогда
			Возврат;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписьюПослеВопросаОЗанятыхНаРедактированиеФайлах(Результат, ПараметрыЗаписи) Экспорт
	
	Если Результат <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыЗаписи.Вставить("СтартоватьПриЗанятыхФайлах", Истина);
	ПараметрыОповещения = Новый Структура();
	ПараметрыОповещения.Вставить("ИдентификаторФормы", УникальныйИдентификатор);
	ПараметрыОповещения.Вставить("ПараметрыЗаписиПроцесса", ПараметрыЗаписи);
	Оповестить("СтартПроцессаПослеВопроса", ПараметрыОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Оповестить("ОбновитьСписокПоследних");
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	РаботаСБизнесПроцессамиКлиент.ОбработкаОповещенияФормаБизнесПроцесса(
		ИмяСобытия, Параметр, Источник, ЭтаФорма);
	
	// Подсистема "Свойства"
	Если ИмяСобытия = "Процесс_ВводПричиныПрерывания" И Параметр.ВладелецФормы = ЭтаФорма Тогда
		КомандыРаботыСБизнесПроцессамиКлиент.ПрерватьБизнесПроцессИзФормыОбъектаОкончание(
			ЭтаФорма, Параметр);
	ИначеЕсли УправлениеСвойствамиКлиент.ОбрабатыватьОповещения(ЭтаФорма, ИмяСобытия, Параметр) Тогда
		ОбновитьЭлементыДополнительныхРеквизитов();
		
	КонецЕсли;
	
	Если ИмяСобытия = "Запись_Файл" И Параметр.Событие = "СозданФайл" Тогда
		Элементы.Файлы.Обновить();
		Файлы.Параметры.УстановитьЗначениеПараметра("ВладелецФайла", Объект.Ссылка);
		Если ТипЗнч(Параметр) = Тип("Структура") Тогда
			
			Элементы.Файлы.Обновить();
			ОбновитьВидимостьТаблицыФайлов(ЭтаФорма);
			
			Элементы.Файлы.ТекущаяСтрока = Параметр.Файл;
			
		КонецЕсли;	
	КонецЕсли;
	
	Если ИмяСобытия = "ИмпортФайловЗавершен" И ТипЗнч(Источник) = Тип("Структура")
		И Источник.ВладелецФайлов = Объект.Ссылка Тогда
		
		Элементы.Файлы.Обновить();
		ОбновитьВидимостьТаблицыФайлов(ЭтаФорма);
		
	КонецЕсли;
	
	Если ИмяСобытия = "Запись_Файл" И Параметр.Событие = "ДанныеФайлаИзменены" Тогда
		ВладелецФайла = Неопределено;
		Если ТипЗнч(Параметр) = Тип("Структура") И Параметр.Свойство("Владелец")
			 И ЗначениеЗаполнено(Параметр.Владелец)  Тогда
			ВладелецФайла = Параметр.Владелец;
		Иначе	
			ВладелецФайла = ОбщегоНазначенияДокументооборотВызовСервера.ЗначениеРеквизитаОбъекта(Источник, "ВладелецФайла");
		КонецЕсли;		
		
		Если ВладелецФайла = Объект.Ссылка Тогда
			Элементы.Файлы.Обновить();
			Файлы.Параметры.УстановитьЗначениеПараметра("ВладелецФайла", Объект.Ссылка);
			ОбновитьДоступностьКомандСпискаФайлов();
		КонецЕсли;	
	КонецЕсли;
	
	Если ИмяСобытия = "ИзменилсяФлаг"
		И ТипЗнч(Параметр[0]) = Тип("ЗадачаСсылка.ЗадачаИсполнителя") Тогда
		
		Элементы.Задачи.Обновить();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ОбработкаПроверкиЗаполнения(ЭтаФорма, Отказ, ПроверяемыеРеквизиты);
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	РаботаСБизнесПроцессамиВызовСервера.ПередЗаписьюНаСервереФормаБизнесПроцесса(
		Отказ, ТекущийОбъект, ПараметрыЗаписи, ЭтаФорма);
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	// Обработчик подсистемы "Свойства"
	УправлениеСвойствами.ПередЗаписьюНаСервере(ЭтаФорма, ТекущийОбъект);
	
	ПараметрыЗаписи.Вставить("ЭтоНовыйОбъект", Не ЗначениеЗаполнено(ТекущийОбъект.Ссылка));
	ТекущийОбъект.ДополнительныеСвойства.Вставить("ИдентификаторФормы", УникальныйИдентификатор);
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	Если ПараметрыЗаписи.Свойство("ЭтоНовыйОбъект") И ПараметрыЗаписи.ЭтоНовыйОбъект = Истина Тогда
		РаботаСПоследнимиОбъектами.ЗаписатьОбращениеКОбъекту(Объект.Ссылка);
	КонецЕсли;
	
	ОбновитьДоступностьЭлементовФормыПоСостояниюПроцесса();
	
	Если ЭтоНовый
		И Объект.ВидВопроса = Перечисления.ВидыВопросовВыполненияЗадач.ПереносСрока Тогда
		
		ПосчитатьКоличествоСуществующихВопросов();
		ПосчитатьКоличествоСуществующихЗапросовНаПереносСрока();
	КонецЕсли;
	
	ЗначениеПараметра = Файлы.Параметры.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("ВладелецФайла"));
	Если Не ЗначениеЗаполнено(ЗначениеПараметра.Значение) Тогда 
		Файлы.Параметры.УстановитьЗначениеПараметра("ВладелецФайла", Объект.Ссылка);
	КонецЕсли;
	
	// Обновление заголовка формы после записи
	Если НЕ ТекущийОбъект.Ссылка.Пустая() Тогда
		Заголовок = "";
		АвтоЗаголовок = Истина
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	ОчиститьСообщения();
	ОповеститьОбИзменении(Объект.Ссылка);
	Оповестить("БизнесПроцессИзменен", Объект.Ссылка, ЭтаФорма);
	
	Если ПараметрыЗаписи.Свойство("ЭтоНовыйОбъект") И ПараметрыЗаписи.ЭтоНовыйОбъект = Истина Тогда
		Оповестить("ОбновитьСписокПоследних");
		Оповестить("СозданНовыйВопросВыполненияЗадачи", Объект.ПредметРассмотрения);
	КонецЕсли;
	
	Если (ПараметрыЗаписи.Свойство("Старт") И ПараметрыЗаписи.Старт)
		Или (ПараметрыЗаписи.Свойство("ФоновыйСтартПроцесса") И ПараметрыЗаписи.ФоновыйСтартПроцесса) Тогда
		
		Если Объект.Предметы.Количество() = 0 Тогда 
			
			ИнформацияОЗапуске = Новый Структура;
			ИнформацияОЗапуске.Вставить("СсылкаНаБизнесПроцесс", Объект.Ссылка);
			ИнформацияОЗапуске.Вставить("СсылкаНаПредметБизнесПроцесса", Неопределено);
			Если ВестиУчетПоПроектам Тогда
				ИнформацияОЗапуске.Вставить("Проект", Объект.Проект);
				ИнформацияОЗапуске.Вставить("ПроектнаяЗадача", Объект.ПроектнаяЗадача);
			КонецЕсли;
			Оповестить("БизнесПроцессСтартован", ИнформацияОЗапуске);
			
		Иначе
		
			Для Каждого СтрокаПредмета из Объект.Предметы Цикл
				Если ЗначениеЗаполнено(СтрокаПредмета.Предмет) Тогда
					ДелопроизводствоКлиент.ОповеститьОбИзмененииОбъекта(СтрокаПредмета.Предмет);
				КонецЕсли;
				
				ИнформацияОЗапуске = Новый Структура();
				ИнформацияОЗапуске.Вставить("СсылкаНаБизнесПроцесс", Объект.Ссылка);
				ИнформацияОЗапуске.Вставить("СсылкаНаПредметБизнесПроцесса", СтрокаПредмета.Предмет);
				Если ВестиУчетПоПроектам Тогда
					ИнформацияОЗапуске.Вставить("Проект", Объект.Проект);
					ИнформацияОЗапуске.Вставить("ПроектнаяЗадача", Объект.ПроектнаяЗадача);
				КонецЕсли;
				Оповестить("БизнесПроцессСтартован", ИнформацияОЗапуске);
			КонецЦикла;
		
		КонецЕсли;
		
	ИначеЕсли ИзменитьРеквизитыНевыполненныхЗадач Тогда 
		Оповестить("ИзмененыРеквизитыНевыполненныхЗадач", Объект.Ссылка);
	КонецЕсли;
	
	РаботаСБизнесПроцессамиКлиент.ПоказатьОповещениеПослеЗаписиПроцесса(ЭтаФорма, ПараметрыЗаписи);
	
КонецПроцедуры

&НаСервере
Процедура ПриЗаписиНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Если ИзменитьРеквизитыНевыполненныхЗадач Тогда 
		ТекущийОбъект.ИзменитьРеквизитыНевыполненныхЗадач();
	КонецЕсли;
	
	// Сохранение вводимых значений
	СохранениеВводимыхЗначений.ОбновитьСпискиВыбора(ЭтаФорма, ЭлементыДляСохранения(), ЭтаФорма.ИмяФормы);
	
	// Фоновый старт процесса
	Если ПараметрыЗаписи.Свойство("ФоновыйСтартПроцесса")
		И ПараметрыЗаписи.ФоновыйСтартПроцесса Тогда
		
		РегистрыСведений.ПроцессыДляЗапуска.ДобавитьПроцессДляФоновогоСтарта(ТекущийОбъект.Ссылка);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ДекорацияОписаниеНажатие(Элемент)
	
	Если Объект.Состояние = ПредопределенноеЗначение(
		"Перечисление.СостоянияБизнесПроцессов.Прерван") Тогда
		
		КомандыРаботыСБизнесПроцессамиКлиент.ПоказатьПричинуПрерывания(ЭтаФорма);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПредметРассмотренияНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ПоказатьЗначение(, Объект.ПредметРассмотрения);
	
КонецПроцедуры

&НаКлиенте
Процедура НовыйСрокДатаПриИзменении(Элемент)
	
	НовыйСрокПриИзменении();
	
КонецПроцедуры

&НаКлиенте
Процедура НовыйСрокВремяПриИзменении(Элемент)
	
	НовыйСрокПриИзменении();
	
КонецПроцедуры

&НаКлиенте
Процедура СрокОтветаПриИзменении(Элемент)
	
	Если НЕ ИспользоватьВремяВСрокахЗадач Тогда
		Объект.СрокИсполнения = КонецДня(Объект.СрокИсполнения);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СрокОтветаВремяПриИзменении(Элемент)
	
	// Если в поле "Срок ответа" вводится только время, то в дату проставляется текущая дата.
	// При вводе только времени дата автоматически принимает значение 01.01.0001 <Дата>. Это значение исправляется.
	Если ЗначениеЗаполнено(Объект.СрокИсполнения) и Объект.СрокИсполнения < Дата(1,1,2) Тогда
		ДатаНачалаДня = НачалоДня(ТекущаяДата());
		ГодНачалаДня = Год(ДатаНачалаДня);
		МесяцНачалаДня = Месяц(ДатаНачалаДня);
		ДеньНачалаДня = День(ДатаНачалаДня);
		ЧасДаты = Час(Объект.СрокИсполнения);
		МинутаДаты = Минута(Объект.СрокИсполнения);
		СекундаДаты = Секунда(Объект.СрокИсполнения);
		Объект.СрокИсполнения = Дата(
			ГодНачалаДня,
			МесяцНачалаДня,
			ДеньНачалаДня,
			ЧасДаты,
			МинутаДаты,
			СекундаДаты);	
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////
// Обработчики поля ИнициаторПроцесса

&НаКлиенте
Процедура ИнициаторПроцессаПриИзменении(Элемент)
	
	РаботаСБизнесПроцессамиКлиент.УчастникПриИзменении(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ИнициаторПроцессаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	РаботаСБизнесПроцессамиКлиент.УчастникНачалоВыбора(
		Элемент, Объект.ИнициаторПроцесса, СтандартнаяОбработка, ЭтаФорма, "ИнициаторПроцесса");
	
КонецПроцедуры

&НаКлиенте
Процедура ИнициаторПроцессаОчистка(Элемент, СтандартнаяОбработка)
	
	РаботаСБизнесПроцессамиКлиент.УчастникОчистка(СтандартнаяОбработка,
		ЭтаФорма, ПараметрыИнициаторПроцесса);
	
КонецПроцедуры

&НаКлиенте
Процедура ИнициаторПроцессаОткрытие(Элемент, СтандартнаяОбработка)
	
	РаботаСБизнесПроцессамиКлиент.УчастникОткрытие(СтандартнаяОбработка,
		ЭтаФорма, ПараметрыИнициаторПроцесса);
	
КонецПроцедуры

&НаКлиенте
Процедура ИнициаторПроцессаОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	РаботаСБизнесПроцессамиКлиент.УчастникОбработкаВыбора(СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ИнициаторПроцессаАвтоПодбор(Элемент, Текст, ДанныеВыбора, Параметры, Ожидание, СтандартнаяОбработка)
	
	РаботаСБизнесПроцессамиКлиент.УчастникАвтоПодбор(Текст, ДанныеВыбора, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ИнициаторПроцессаОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, Параметры, СтандартнаяОбработка)
	
	РаботаСБизнесПроцессамиКлиент.
		УчастникОкончаниеВводаТекста(Текст, ДанныеВыбора, СтандартнаяОбработка,
			ЭтаФорма, ПараметрыИнициаторПроцесса);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормы_Файлы

&НаКлиенте
Процедура СписокФайловВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	КакОткрывать = ФайловыеФункцииКлиентПовтИсп.ПолучитьПерсональныеНастройкиРаботыСФайлами().ДействиеПоДвойномуЩелчкуМыши;
	Если КакОткрывать = "ОткрыватьКарточку" Тогда
		ПоказатьЗначение(, ВыбраннаяСтрока);
		Возврат;
	КонецЕсли;
	
	ДанныеФайла = РаботаСФайламиВызовСервера.ДанныеФайлаДляОткрытия(
		ВыбраннаяСтрока, 
		Неопределено, 
		ЭтаФорма.УникальныйИдентификатор);
	
	ПараметрыОбработчика = Новый Структура;
	ПараметрыОбработчика.Вставить("ДанныеФайла", ДанныеФайла);
	Обработчик = Новый ОписаниеОповещения("СписокВыборПослеВыбораРежимаРедактирования", ЭтотОбъект, ПараметрыОбработчика);
	
	РаботаСФайламиКлиент.ВыбратьРежимИРедактироватьФайл(Обработчик, ДанныеФайла, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокВыборПослеВыбораРежимаРедактирования(Результат, ПараметрыВыполнения) Экспорт
	
	РезультатОткрыть = "Открыть";
	РезультатРедактировать = "Редактировать";
	РезультатОткрытьКарточку = "ОткрытьКарточку";
	
	Обработчик = Новый ОписаниеОповещения("ОбновитьДоступностьКомандСпискаФайлов", ЭтотОбъект, ПараметрыВыполнения);
	
	Если Результат = РезультатРедактировать Тогда
		РаботаСФайламиКлиент.РедактироватьФайл(Обработчик, ПараметрыВыполнения.ДанныеФайла);
	ИначеЕсли Результат = РезультатОткрыть Тогда
		РаботаСФайламиКлиент.ОткрытьФайлСОповещением(Неопределено, ПараметрыВыполнения.ДанныеФайла, УникальныйИдентификатор); 
	ИначеЕсли Результат = РезультатОткрытьКарточку Тогда
		ПоказатьЗначение(, ПараметрыВыполнения.ДанныеФайла.Ссылка);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ФайлыПриАктивизацииСтроки(Элемент)
	
	ОбновитьДоступностьКомандСпискаФайлов();
	
КонецПроцедуры

&НаКлиенте
Процедура ФайлыПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	
	Отказ = Истина;
	
	Если Объект.Ссылка.Пустая() Тогда 
		Если НЕ Записать() Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	ВладелецФайла = Объект.Ссылка;
	ФайлОснование = Элементы.Файлы.ТекущаяСтрока;
	
	Если Не Копирование Тогда
		Попытка
			РежимСоздания = 1;
			РаботаСФайламиКлиент.ДобавитьФайл(Неопределено, ВладелецФайла, ЭтаФорма, РежимСоздания, Истина);
		Исключение
			Инфо = ИнформацияОбОшибке();
			ПоказатьПредупреждение(,СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			                 НСтр("ru = 'Ошибка создания нового файла: %1'; en = 'Error creating the file: %1'"),
			                 КраткоеПредставлениеОшибки(Инфо)));
		КонецПопытки;
	Иначе
		РаботаСФайламиКлиент.СкопироватьФайл(ВладелецФайла, ФайлОснование);
	КонецЕсли;
	Элементы.Файлы.Обновить();
	
КонецПроцедуры

&НаКлиенте
Процедура ФайлыПроверкаПеретаскивания(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Поле)
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ФайлыПеретаскивание(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Поле)
	
	СтандартнаяОбработка = Ложь;
	
	Если Элементы.Файлы.ТолькоПросмотр Тогда 
		Возврат;
	КонецЕсли;
	
	Если Объект.Ссылка.Пустая() Тогда 
		Если НЕ Записать() Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	ВладелецФайлаСписка = Объект.Ссылка;
	НеОткрыватьКарточкуПослеСозданияИзФайла = Истина;	
	РаботаСФайламиКлиент.ОбработкаПеретаскиванияВЛинейныйСписок(ПараметрыПеретаскивания, ВладелецФайлаСписка, ЭтаФорма, НеОткрыватьКарточкуПослеСозданияИзФайла);
	Элементы.Файлы.Обновить();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормы_Задачи

&НаКлиенте
Процедура ЗадачиВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если Элементы.Задачи.ТекущаяСтрока = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Элемент.ТекущийЭлемент = Элементы.ЗадачиНомерФлага Тогда
		РаботаСФлагамиОбъектовКлиент.ПереключитьФлагЗадачи(Элементы.Задачи.ТекущаяСтрока);
		Возврат;
	КонецЕсли;
	
	ПоказатьЗначение(, Элементы.Задачи.ТекущаяСтрока);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗадачиПередНачаломИзменения(Элемент, Отказ)
	
	ПоказатьЗначение(, Элементы.Задачи.ТекущиеДанные.Ссылка);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СтартоватьИЗакрыть(Команда)
	
	КомандыРаботыСБизнесПроцессамиКлиент.СтартоватьИЗакрыть(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура Остановить(Команда)
	
	КомандыРаботыСБизнесПроцессамиКлиент.ОстановитьБизнесПроцессИзФормыОбъекта(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ПрерватьБизнесПроцесс(Команда)
	
	КомандыРаботыСБизнесПроцессамиКлиент.ПрерватьБизнесПроцессИзФормыОбъекта(ЭтаФорма);
	
КонецПроцедуры        

&НаКлиенте
Процедура ПродолжитьБизнесПроцесс(Команда)
	
	КомандыРаботыСБизнесПроцессамиКлиент.ПродолжитьБизнесПроцессИзФормыОбъекта(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ПометитьНаУдаление(Команда)
	
	РаботаСБизнесПроцессамиКлиент.ПометитьНаУдалениеБизнесПроцесс(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьИЗакрыть(Команда)
	
	РаботаСБизнесПроцессамиКлиент.ЗаписатьИЗакрыть(Команда, ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ПраваДоступа(Команда)
	
	ДокументооборотПраваДоступаКлиент.ОткрытьФормуПравДоступа(ЭтаФорма);
	
КонецПроцедуры

// СтандартныеПодсистемы.Печать
&НаКлиенте
Процедура Подключаемый_ВыполнитьКомандуПечати(Команда)
	
	УправлениеПечатьюКлиент.ВыполнитьПодключаемуюКомандуПечати(Команда, ЭтаФорма, Объект);
	
КонецПроцедуры


// СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки
&НаКлиенте
Процедура Подключаемый_ВыполнитьНазначаемуюКоманду(Команда)
	
	Если НЕ ДополнительныеОтчетыИОбработкиКлиент.ВыполнитьНазначаемуюКомандуНаКлиенте(ЭтаФорма, Команда.Имя) Тогда
		РезультатВыполнения = Неопределено;
		ДополнительныеОтчетыИОбработкиВыполнитьНазначаемуюКомандуНаСервере(Команда.Имя, РезультатВыполнения);
	КонецЕсли;
	
КонецПроцедуры


// СтандартныеПодсистемы.БазоваяФункциональность
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат)
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Объект, Результат);
КонецПроцедуры


&НаКлиенте
Процедура ВставитьКартинкуИзБуфера(Команда)
	
	Если Объект.Ссылка.Пустая() Тогда 
		
		Если Не Записать() Тогда 
			Возврат;
		КонецЕсли;
		
	КонецЕсли;
	
	КомпонентаУстановлена = РаботаСКартинкамиКлиент.ПроинициализироватьКомпоненту();
	Если Не КомпонентаУстановлена Тогда
		
		Обработчик = Новый ОписаниеОповещения("ВставитьКартинкуИзБуфераЗавершение", ЭтотОбъект);		
		РаботаСКартинкамиКлиент.УстановитьКомпоненту(Обработчик);
		Возврат;
		
	КонецЕсли;
	
	ВставитьКартинкуИзБуфераЗавершение(Истина, Неопределено);
	
КонецПроцедуры

&НаКлиенте
Процедура ВставитьКартинкуИзБуфераЗавершение(Результат, ПараметрыВыполнения) Экспорт
	
	Если Результат = Истина Тогда
		
		ПутьКФайлу = КомпонентаПолученияКартинкиИзБуфера.ПолучитьКартинкуИзБуфера();
	
		Если Не ПустаяСтрока(ПутьКФайлу) Тогда
			
			НеОткрыватьКарточкуПослеСозданияИзФайла = Истина;
			РаботаСФайламиКлиент.СоздатьДокументНаОсновеФайла(
				ПутьКФайлу, Объект.Ссылка, ЭтаФорма, НеОткрыватьКарточкуПослеСозданияИзФайла);
				
		Иначе
			ПоказатьПредупреждение(,НСтр("ru = 'Буфер обмена не содержит картинки'; en = 'Ther is no picture in clipboard'"));
		КонецЕсли;
	
	КонецЕсли;	
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы_Файлы

&НаКлиенте
Процедура ОткрытьФайл(Команда)
	
	Если Элементы.Файлы.ТекущаяСтрока = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	ДанныеФайла = РаботаСФайламиВызовСервера.ДанныеФайлаДляОткрытия(Элементы.Файлы.ТекущаяСтрока, Неопределено, ЭтаФорма.УникальныйИдентификатор);
	КомандыРаботыСФайламиКлиент.Открыть(ДанныеФайла);
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьКак(Команда)
	
	Если Элементы.Файлы.ТекущаяСтрока = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	ДанныеФайла = РаботаСФайламиВызовСервера.ДанныеФайлаДляСохранения(Элементы.Файлы.ТекущаяСтрока, Неопределено, ЭтаФорма.УникальныйИдентификатор);
	КомандыРаботыСФайламиКлиент.СохранитьКак(ДанныеФайла, УникальныйИдентификатор);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьИзФайлаНаДиске(Команда)
	Если Элементы.Файлы.ТекущаяСтрока = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	ДанныеФайла = РаботаСФайламиВызовСервера.ДанныеФайлаИРабочийКаталог(Элементы.Файлы.ТекущаяСтрока);
	
	Обработчик = Новый ОписаниеОповещения("ОбновитьДоступностьКомандСпискаФайлов", ЭтотОбъект);
	РаботаСФайламиКлиент.ОбновитьИзФайлаНаДискеСОповещением(
		Обработчик,
		ДанныеФайла, ЭтаФорма.УникальныйИдентификатор);
		
КонецПроцедуры

&НаКлиенте
Процедура Редактировать(Команда)
	
	Если Объект.Ссылка.Пустая()
		И Элементы.ФайлыДобавленные.ТекущаяСтрока <> Неопределено Тогда
		Если ЭтоАдресВременногоХранилища(Элементы.ФайлыДобавленные.ТекущиеДанные.ПолныйПуть) Тогда 
			ТекущийФайлВСпискеДобавленных = ПолучитьИзВременногоХранилища(Элементы.ФайлыДобавленные.ТекущиеДанные.ПолныйПуть).Ссылка;
		Иначе			
			РаботаСФайламиКлиент.ЗапуститьПриложениеПоИмениФайла(
				Элементы.ФайлыДобавленные.ТекущиеДанные.ПолныйПуть);
		КонецЕсли;	
	Иначе
		
		Если Элементы.Файлы.ТекущаяСтрока = Неопределено Тогда 
			Возврат;
		КонецЕсли;
			
		Обработчик = Новый ОписаниеОповещения("ОбновитьДоступностьКомандСпискаФайлов", ЭтотОбъект);
		РаботаСФайламиКлиент.РедактироватьСОповещением(Обработчик, Элементы.Файлы.ТекущаяСтрока);
		
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура ЗакончитьРедактирование(Команда)
	
	Если Элементы.Файлы.ТекущаяСтрока = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	Обработчик = Новый ОписаниеОповещения("ОбновитьДоступностьКомандСпискаФайлов", ЭтотОбъект);
	
	ТекущиеДанные = Элементы.Файлы.ТекущиеДанные;
		
	ПараметрыОбновленияФайла = РаботаСФайламиКлиент.ПараметрыОбновленияФайла(Обработчик, 
		Элементы.Файлы.ТекущаяСтрока, ЭтаФорма.УникальныйИдентификатор);
	ПараметрыОбновленияФайла.ХранитьВерсии = ТекущиеДанные.ХранитьВерсии;
	ПараметрыОбновленияФайла.РедактируетТекущийПользователь = ТекущиеДанные.РедактируетТекущийПользователь;
	ПараметрыОбновленияФайла.Редактирует = ТекущиеДанные.Редактирует;
	РаботаСФайламиКлиент.ЗакончитьРедактированиеСОповещением(ПараметрыОбновленияФайла);
		
КонецПроцедуры

&НаКлиенте
Процедура Занять(Команда)
	
	Если Элементы.Файлы.ТекущаяСтрока = Неопределено Тогда 
		Возврат;
	КонецЕсли;

	Обработчик = Новый ОписаниеОповещения("ОбновитьДоступностьКомандСпискаФайлов", ЭтотОбъект);
	РаботаСФайламиКлиент.ЗанятьСОповещением(Обработчик, Элементы.Файлы.ТекущаяСтрока);	
	
КонецПроцедуры

&НаКлиенте
Процедура Освободить(Команда)
	
	Если Элементы.Файлы.ТекущаяСтрока = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	Обработчик = Новый ОписаниеОповещения("ОбновитьДоступностьКомандСпискаФайлов", ЭтотОбъект);
	
	ТекущиеДанные = Элементы.Файлы.ТекущиеДанные;
		
	ПараметрыОсвобожденияФайла = РаботаСФайламиКлиент.ПараметрыОсвобожденияФайла(Обработчик, 
		Элементы.Файлы.ТекущаяСтрока);
	ПараметрыОсвобожденияФайла.ХранитьВерсии = ТекущиеДанные.ХранитьВерсии;	
	ПараметрыОсвобожденияФайла.РедактируетТекущийПользователь = ТекущиеДанные.РедактируетТекущийПользователь;	
	ПараметрыОсвобожденияФайла.Редактирует = ТекущиеДанные.Редактирует;	
	РаботаСФайламиКлиент.ОсвободитьФайлСОповещением(ПараметрыОсвобожденияФайла);
		
КонецПроцедуры

&НаКлиенте
Процедура СохранитьИзменения(Команда)
	
	Если Элементы.Файлы.ТекущаяСтрока = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	Обработчик = Новый ОписаниеОповещения("ОбновитьДоступностьКомандСпискаФайлов", ЭтотОбъект);
	
	РаботаСФайламиКлиент.СохранитьИзмененияФайлаСОповещением(
		Обработчик,
		Элементы.Файлы.ТекущаяСтрока, 
		ЭтаФорма.УникальныйИдентификатор);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтображатьУдаленныеФайлы(Команда)
	
	ОтображатьУдаленныеФайлыСервер();
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьФайл(Команда)
	
	Если Объект.Ссылка.Пустая() Тогда 
		Если НЕ Записать() Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	ВладелецФайла = Объект.Ссылка;
	ФайлОснование = Элементы.Файлы.ТекущаяСтрока;
	
	Попытка
		РежимСоздания = 1;
		РаботаСФайламиКлиент.ДобавитьФайл(Неопределено, ВладелецФайла, ЭтаФорма, РежимСоздания, Истина);
	Исключение
		Инфо = ИнформацияОбОшибке();
		ПоказатьПредупреждение(,СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		                 НСтр("ru = 'Ошибка создания нового файла: %1'; en = 'Error creating the file: %1'"),
		                 КраткоеПредставлениеОшибки(Инфо)));
	КонецПопытки;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы_Задачи

&НаКлиенте
Процедура ЖелтыйФлаг(Команда)
	
	УстановитьФлаги(ПредопределенноеЗначение("Перечисление.ФлагиОбъектов.Желтый"));
	
КонецПроцедуры

&НаКлиенте
Процедура ЗеленыйФлаг(Команда)
	
	УстановитьФлаги(ПредопределенноеЗначение("Перечисление.ФлагиОбъектов.Зеленый"));
	
КонецПроцедуры

&НаКлиенте
Процедура КрасныйФлаг(Команда)
	
	УстановитьФлаги(ПредопределенноеЗначение("Перечисление.ФлагиОбъектов.Красный"));
	
КонецПроцедуры

&НаКлиенте
Процедура ЛиловыйФлаг(Команда)
	
	УстановитьФлаги(ПредопределенноеЗначение("Перечисление.ФлагиОбъектов.Лиловый"));
	
КонецПроцедуры

&НаКлиенте
Процедура ОранжевыйФлаг(Команда)
	
	УстановитьФлаги(ПредопределенноеЗначение("Перечисление.ФлагиОбъектов.Оранжевый"));
	
КонецПроцедуры

&НаКлиенте
Процедура СинийФлаг(Команда)
	
	УстановитьФлаги(ПредопределенноеЗначение("Перечисление.ФлагиОбъектов.Синий"));
	
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьФлаг(Команда)
	
	УстановитьФлаги(Неопределено);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ОбновитьДоступностьЭлементовФормыПоСостояниюПроцесса(ПрочитатьПараметрыСостояния = Ложь) Экспорт
	
	Если ПрочитатьПараметрыСостояния Тогда
		Прочитать();
		СтартПроцессовСервер.ЗаполнитьНастройкиСтартаВФормеПроцесса(ЭтаФорма);
	КонецЕсли;
	
	ЭтотУзел = РаботаСБизнесПроцессами.ЭтотУзелОбменаДляОбработкиПроцессов();
	
	ТолькоПросмотр =
		Объект.Состояние = Перечисления.СостоянияБизнесПроцессов.Прерван
		Или Объект.Состояние = Перечисления.СостоянияБизнесПроцессов.Остановлен
		Или Объект.Завершен
		Или Объект.ПометкаУдаления
		Или (ЗначениеЗаполнено(Объект.Ссылка) И ЭтотУзел <> Объект.УзелОбмена);
		
	Элементы.Файлы.ТолькоПросмотр = ТолькоПросмотр;
	
	// Заполнение группы ГруппаСостояниеПроцесса
	Элементы.ДекорацияСостояния.Видимость = Ложь;
	Элементы.ДекорацияОписание.Гиперссылка = Ложь;
	Элементы.ДекорацияОписание.ЦветТекста = ЦветаСтиля.ЦветТекстаФормы;
	Элементы.ГруппаСостояниеПроцесса.Видимость = Ложь;
	
	ПроцессСтартованФоново = Ложь;
	Если ЗначениеЗаполнено(НастройкаСтарта) Тогда
		ПроцессСтартованФоново = 
			Не ЗначениеЗаполнено(НастройкаСтарта.ДатаОтложенногоСтарта)
			И НастройкаСтарта.Состояние = Перечисления.СостоянияПроцессовДляЗапуска.ГотовКСтарту;
	КонецЕсли;
	
	Если Не ЭтоНовый Тогда
		Если Объект.Завершен Тогда
			Элементы.ГруппаСостояниеПроцесса.Видимость = Истина;
			
			Элементы.ДекорацияСостояния.Видимость = Истина;
			
			Элементы.ДекорацияСостояния.Картинка = 
				БиблиотекаКартинок.РезультатВыполненияПроцессовИЗадач;
				
			ПредставлениеРезультата = НСтр("ru = 'Рассмотрение завершено %1'; en = 'Reviewal completed %1'");
			
			ПредставлениеРезультата = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				ПредставлениеРезультата, Формат(Объект.ДатаЗавершения, "ДФ='dd.MM.yyyy HH:mm'"));
				
			Элементы.ДекорацияОписание.Заголовок = ПредставлениеРезультата;
			
			ТолькоПросмотр = Истина;
			
		ИначеЕсли Объект.Состояние = Перечисления.СостоянияБизнесПроцессов.Остановлен Тогда
			Элементы.ГруппаСостояниеПроцесса.Видимость = Истина;
			
			Элементы.ДекорацияСостояния.Видимость = Истина;
			Элементы.ДекорацияСостояния.Картинка = БиблиотекаКартинок.ЗнакПауза;
			
			Элементы.ДекорацияОписание.Заголовок = НСтр("ru = 'Остановлен'; en = 'Stopped'");
		ИначеЕсли Объект.Состояние = Перечисления.СостоянияБизнесПроцессов.Прерван Тогда
			Элементы.ГруппаСостояниеПроцесса.Видимость = Истина;
			
			Элементы.ДекорацияСостояния.Видимость = Истина;
			Элементы.ДекорацияСостояния.Картинка = БиблиотекаКартинок.ЗнакПрерванПроцесс;
			
			РаботаСБизнесПроцессамиВызовСервера.ОтобразитьНаФормеПричинуПрерывания(ЭтаФорма);
			Элементы.ДекорацияОписание.ЦветТекста = ЦветаСтиля.ИнформационнаяНадпись;
			Элементы.ДекорацияОписание.Гиперссылка = Истина;
		ИначеЕсли Объект.Стартован Тогда
			Элементы.ГруппаСостояниеПроцесса.Видимость = Истина;
			
			Элементы.ДекорацияСостояния.Видимость = Истина;
			Элементы.ДекорацияСостояния.Картинка = БиблиотекаКартинок.СформироватьОтчет;
			
			Если Объект.ВидВопроса = Перечисления.ВидыВопросовВыполненияЗадач.Иное Тогда
				ЗаголовокДекорации = НСтр("ru = 'Вопрос рассматривается с %1'; en = 'Issue is being reviewed with %1'");
			Иначе
				ЗаголовокДекорации = НСтр("ru = 'Запрос рассматривается с %1'; en = 'Request is being reviewed %1'");
			КонецЕсли;
			
			Элементы.ДекорацияОписание.Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				ЗаголовокДекорации, Формат(Объект.ДатаНачала, "ДФ='dd.MM.yyyy HH:mm'"));
		ИначеЕсли ЗначениеЗаполнено(НастройкаСтарта)
			И ЗначениеЗаполнено(НастройкаСтарта.Состояние)
			И Не ЗначениеЗаполнено(НастройкаСтарта.ДатаОтложенногоСтарта) Тогда
			
			Если НастройкаСтарта.Состояние =
				Перечисления.СостоянияПроцессовДляЗапуска.ГотовКСтарту Тогда
				
				Элементы.ГруппаСостояниеПроцесса.Видимость = Истина;
				
				Элементы.ДекорацияОписание.Заголовок = НСтр("ru = 'Ожидается старт процесса в фоновом задании.'; en = 'Awaiting for process start by background job.'");
				Элементы.ДекорацияОписание.Гиперссылка = Ложь;
				
			ИначеЕсли НастройкаСтарта.Состояние = 
				Перечисления.СостоянияПроцессовДляЗапуска.СтартОтменен Тогда
				
				Элементы.ГруппаСостояниеПроцесса.Видимость = Истина;
				
				Элементы.ДекорацияОписание.Заголовок =
					НастройкаСтарта.ПричинаОтменыСтарта;
				Элементы.ДекорацияОписание.ЦветТекста = ЦветаСтиля.ПоясняющийОшибкуТекст;
				Элементы.ДекорацияОписание.Гиперссылка = Истина;
				
			КонецЕсли;
		ИначеЕсли ЗначениеЗаполнено(Объект.Ссылка) Тогда
			Элементы.ГруппаСостояниеПроцесса.Видимость = Истина;
			
			Если Объект.ВидВопроса = Перечисления.ВидыВопросовВыполненияЗадач.Иное Тогда
				Элементы.ДекорацияОписание.Заголовок = НСтр("ru = 'Вопрос не отправлен'; en = 'Issue is not raised'");
			Иначе
				Элементы.ДекорацияОписание.Заголовок = НСтр("ru = 'Запрос не отправлен'; en = 'Request is not sent'");
			КонецЕсли;
			
			Элементы.ДекорацияОписание.ЦветТекста = ЦветаСтиля.ПоясняющийОшибкуТекст;
		КонецЕсли;
	КонецЕсли;
	
	// Установка видимости кнопок ФормаСтартИЗакрыть и ФормаЗаписатьИЗакрыть
	Если Объект.ПометкаУдаления
		Или Объект.Состояние = Перечисления.СостоянияБизнесПроцессов.Остановлен
		Или Объект.Состояние = Перечисления.СостоянияБизнесПроцессов.Прерван Тогда
		
		Элементы.ФормаСтартИЗакрыть.Видимость = Ложь;
		Элементы.ФормаЗаписатьИЗакрыть.Видимость = Истина;
		
	ИначеЕсли Объект.Стартован Или ПроцессСтартованФоново Тогда
		Элементы.ФормаСтартИЗакрыть.Видимость = Ложь;
		Элементы.ФормаЗаписатьИЗакрыть.Видимость = Истина;
	Иначе
		Элементы.ФормаСтартИЗакрыть.Видимость = Истина;
		Элементы.ФормаЗаписатьИЗакрыть.Видимость = Ложь;
	КонецЕсли;
	
	Элементы.ФормаЗаписатьИЗакрыть.КнопкаПоУмолчанию = 
		Элементы.ФормаЗаписатьИЗакрыть.Видимость;
		
	Элементы.ФормаСтартИЗакрыть.КнопкаПоУмолчанию = 
		Элементы.ФормаСтартИЗакрыть.Видимость;
	
	// Получение права на изменение процесса
	ПравоНаИзменениеБизнесПроцесса = Ложь;
	Если НЕ Объект.Ссылка.Пустая() Тогда
		ПравоНаИзменениеБизнесПроцесса = 
			ДокументооборотПраваДоступа.ПолучитьПраваПоОбъекту(Объект.Ссылка).Изменение;
	КонецЕсли;
	
	// Доступность команд Стартов и закрыть/Записать и закрыть
	Если Не ПравоНаИзменениеБизнесПроцесса
		И ЗначениеЗаполнено(Объект.Ссылка) Тогда
		
		Элементы.ФормаСтартИЗакрыть.Доступность = Ложь;
		Элементы.ФормаЗаписатьИЗакрыть.Доступность = Ложь;
	КонецЕсли;
	
	// Видимость команды Остановить
	Элементы.ФормаОстановить.Видимость = Объект.Стартован
		И ЗначениеЗаполнено(Объект.Ссылка)
		И НЕ Объект.Завершен
		И НЕ ЗначениеЗаполнено(Объект.ВедущаяЗадача)
		И ПравоНаИзменениеБизнесПроцесса
		И Объект.Состояние <> Перечисления.СостоянияБизнесПроцессов.Прерван
		И Объект.Состояние <> Перечисления.СостоянияБизнесПроцессов.Остановлен;
		
	// Видимость команды Продолжить
	Элементы.ФормаПродолжитьБизнесПроцесс.Видимость = Объект.Стартован
		И ЗначениеЗаполнено(Объект.Ссылка)
		И НЕ Объект.Завершен
		И НЕ ЗначениеЗаполнено(Объект.ВедущаяЗадача)
		И ПравоНаИзменениеБизнесПроцесса
		И Объект.Состояние <> Перечисления.СостоянияБизнесПроцессов.Прерван
		И Объект.Состояние = Перечисления.СостоянияБизнесПроцессов.Остановлен;
	
	// Видимость команды Прервать
	Элементы.ФормаПрервать.Видимость = Объект.Стартован
		И ЗначениеЗаполнено(Объект.Ссылка)
		И НЕ Объект.Завершен
		И НЕ ЗначениеЗаполнено(Объект.ВедущаяЗадача)
		И ПравоНаИзменениеБизнесПроцесса
		И Объект.Состояние <> Перечисления.СостоянияБизнесПроцессов.Прерван;
	
КонецПроцедуры

&НаСервере
Функция ЭлементыДляСохранения()
	
	СохраняемыеЭлементы = Новый Структура("ИнициаторПроцесса", Объект.ИнициаторПроцесса);
	
	Возврат СохранениеВводимыхЗначений.СформироватьТаблицуСохраняемыхЭлементов(СохраняемыеЭлементы);
	
КонецФункции

&НаСервере
Функция ПредставлениеРезультата() Экспорт
	
	Если Объект.ВидВопроса = Перечисления.ВидыВопросовВыполненияЗадач.Иное Тогда
		ПредставлениеРезультата = НСтр("ru = 'Вопрос закрыт %1'; en = 'Solved issue %1'");
	Иначе
		ПредставлениеРезультата = НСтр("ru = 'Рассмотрение запроса завершено %1'; en = 'Request revewal completed %1'");
	КонецЕсли;
	
	Возврат СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		ПредставлениеРезультата, Формат(Объект.ДатаЗавершения, "ДФ='dd.MM.yyyy HH:mm'"));
	
КонецФункции

&НаСервереБезКонтекста
Функция ДоступнаУстановкаНовогоСрока(ЗаявкаНаПереносСрока)
	
	УстановитьПривилегированныйРежим(Истина);
	
	ДоступнаУстановкаНовогоСрока = Истина;
	
	Если ЗначениеЗаполнено(ЗаявкаНаПереносСрока) Тогда
	
		Запрос = Новый Запрос;
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	ЗадачаИсполнителя.Ссылка
			|ИЗ
			|	Задача.ЗадачаИсполнителя КАК ЗадачаИсполнителя
			|ГДЕ
			|	ЗадачаИсполнителя.БизнесПроцесс = &БизнесПроцесс
			|	И ЗадачаИсполнителя.ТочкаМаршрута = &ТочкаМаршрута
			|	И ЗадачаИсполнителя.Выполнена = ЛОЖЬ";
		Запрос.УстановитьПараметр("БизнесПроцесс", ЗаявкаНаПереносСрока);
		Запрос.УстановитьПараметр("ТочкаМаршрута", 
			БизнесПроцессы.РешениеВопросовВыполненияЗадач.ТочкиМаршрута.РассмотрениеИнициатором);
			
		ДоступнаУстановкаНовогоСрока = Не Запрос.Выполнить().Пустой();
		
	КонецЕсли;
		
	Возврат ДоступнаУстановкаНовогоСрока;
	
КонецФункции

&НаСервереБезКонтекста
Функция СтарыйСрокИсполненияПредметаРассмотрения(ПредметРассмотрения, ЗаявкаНаПереносСрока)
	
	СтарыйСрок = Неопределено;
	
	Если ЗначениеЗаполнено(ЗаявкаНаПереносСрока) Тогда
		Запрос = Новый Запрос;
		Запрос.Текст = 
			"ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	ПереносыСроковВыполненияПроцессовИЗадач.СтарыйСрок
			|ИЗ
			|	РегистрСведений.ПереносыСроковВыполненияПроцессовИЗадач КАК ПереносыСроковВыполненияПроцессовИЗадач
			|ГДЕ
			|	ПереносыСроковВыполненияПроцессовИЗадач.Предмет = &ПредметРассмотрения
			|	И ПереносыСроковВыполненияПроцессовИЗадач.БизнесПроцессПереноса = &ЗаявкаНаПереносСрока";
		Запрос.УстановитьПараметр("ПредметРассмотрения", ПредметРассмотрения);
		Запрос.УстановитьПараметр("ЗаявкаНаПереносСрока", ЗаявкаНаПереносСрока);
		
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			СтарыйСрок = Выборка.СтарыйСрок;
		КонецЕсли;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(СтарыйСрок) Тогда
		СтарыйСрок = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(
			ПредметРассмотрения, "СрокИсполнения");
	КонецЕсли;
	
	Возврат СтарыйСрок;
	
КонецФункции

&НаКлиенте
Процедура НовыйСрокПриИзменении()
	
	Если ЗначениеЗаполнено(Объект.НовыйСрок) Тогда
		ДлительностьПереноса = ПереносСроковВыполненияЗадачВызовСервера.ПолучитьПодписьДлительностьПереноса(
				Объект.Автор, 
				СтарыйСрок, 
				Объект.НовыйСрок);
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(СтарыйСрок) Тогда
		ДлительностьПереноса = "";
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПосчитатьКоличествоСуществующихВопросов()
	
	КоличествоВопросов = 0;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ РешениеВопросовВыполненияЗадач.Ссылка) КАК Количество
		|ИЗ
		|	БизнесПроцесс.РешениеВопросовВыполненияЗадач КАК РешениеВопросовВыполненияЗадач
		|ГДЕ
		|	РешениеВопросовВыполненияЗадач.ПредметРассмотрения = &ПредметРассмотрения
		|	И РешениеВопросовВыполненияЗадач.ПометкаУдаления = ЛОЖЬ
		|	И РешениеВопросовВыполненияЗадач.ВидВопроса <> &ВидВопроса";
			
	Запрос.УстановитьПараметр("ПредметРассмотрения", Объект.ПредметРассмотрения);
	Запрос.УстановитьПараметр("ВидВопроса", Перечисления.ВидыВопросовВыполненияЗадач.ПереносСрока);
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();

	Если Выборка.Следующий() Тогда
		КоличествоВопросов = Выборка.Количество;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПосчитатьКоличествоСуществующихЗапросовНаПереносСрока()
	
	КоличествоЗапросовНаПереносСрока = 0;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ РешениеВопросовВыполненияЗадач.Ссылка) КАК Количество
		|ИЗ
		|	БизнесПроцесс.РешениеВопросовВыполненияЗадач КАК РешениеВопросовВыполненияЗадач
		|ГДЕ
		|	РешениеВопросовВыполненияЗадач.ПредметРассмотрения = &ПредметРассмотрения
		|	И РешениеВопросовВыполненияЗадач.ПометкаУдаления = ЛОЖЬ
		|	И РешениеВопросовВыполненияЗадач.ВидВопроса = &ВидВопроса";
			
	Запрос.УстановитьПараметр("ПредметРассмотрения", Объект.ПредметРассмотрения);
	Запрос.УстановитьПараметр("ВидВопроса", Перечисления.ВидыВопросовВыполненияЗадач.ПереносСрока);
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();

	Если Выборка.Следующий() Тогда
		КоличествоЗапросовНаПереносСрока = Выборка.Количество;
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ЕстьЗанятыеНаРедактированиеФайлы(ВопросСсылка)
	
	Запрос = Новый Запрос();
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	Файлы.Редактирует
		|ИЗ
		|	Справочник.Файлы КАК Файлы
		|ГДЕ
		|	Файлы.ВладелецФайла = &Ссылка
		|	И Файлы.Редактирует <> ЗНАЧЕНИЕ(Справочник.Пользователи.ПустаяСсылка)";
	Запрос.УстановитьПараметр("Ссылка", ВопросСсылка);
	РезультатЗапроса = Запрос.Выполнить();
	Возврат НЕ РезультатЗапроса.Пустой();
	
КонецФункции

&НаКлиенте
Процедура УстановитьФлаги(Флаг)
	
	Результат = РаботаСФлагамиОбъектовКлиент.УстановитьФлагиЗадачам(Элементы.Задачи.ВыделенныеСтроки, Флаг);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции_Файлы

&НаКлиенте
Процедура ОбновитьДоступностьКомандСпискаФайлов(Результат = Неопределено, ПараметрыВыполнения = Неопределено) Экспорт
	
	УстановитьДоступностьКоманд(Элементы.Файлы.ТекущиеДанные);
	
КонецПроцедуры	

&НаКлиенте
Процедура УстановитьДоступностьКоманды(Команда, Доступность)
	
	Команда.Доступность = Доступность;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьДоступностьКоманд(ТекущиеДанные)
	
	Если ТекущиеДанные = Неопределено Тогда 
		
		УстановитьДоступностьКоманды(Элементы.ФайлыКонтекстноеМенюОткрытьФайл, Ложь);
		УстановитьДоступностьКоманды(Элементы.ФайлыКонтекстноеМенюРедактировать, Ложь);
		УстановитьДоступностьКоманды(Элементы.ФайлыКонтекстноеМенюЗакончитьРедактирование, Ложь);
		УстановитьДоступностьКоманды(Элементы.ФайлыКонтекстноеМенюЗанять, Ложь);
		УстановитьДоступностьКоманды(Элементы.ФайлыКонтекстноеМенюСохранитьИзменения, Ложь);
		УстановитьДоступностьКоманды(Элементы.ФайлыКонтекстноеМенюСохранитьКак, Ложь);
		УстановитьДоступностьКоманды(Элементы.ФайлыКонтекстноеМенюОсвободить, Ложь);
		УстановитьДоступностьКоманды(Элементы.ФайлыКонтекстноеМенюОбновитьИзФайлаНаДиске, Ложь);	
	Иначе	
		
		РедактируетТекущийПользователь = ТекущиеДанные.РедактируетТекущийПользователь;
		Редактирует = ТекущиеДанные.Редактирует;
		
		УстановитьДоступностьКоманды(Элементы.ФайлыКонтекстноеМенюОткрытьФайл, Истина);
		УстановитьДоступностьКоманды(Элементы.ФайлыКонтекстноеМенюРедактировать, НЕ ТекущиеДанные.ПодписанЭП);
		УстановитьДоступностьКоманды(Элементы.ФайлыКонтекстноеМенюЗакончитьРедактирование, РедактируетТекущийПользователь);
		УстановитьДоступностьКоманды(Элементы.ФайлыКонтекстноеМенюЗанять, Редактирует.Пустая());
		УстановитьДоступностьКоманды(Элементы.ФайлыКонтекстноеМенюСохранитьИзменения, РедактируетТекущийПользователь);
		УстановитьДоступностьКоманды(Элементы.ФайлыКонтекстноеМенюСохранитьКак, Истина);
		УстановитьДоступностьКоманды(Элементы.ФайлыКонтекстноеМенюОсвободить, Не Редактирует.Пустая());
		УстановитьДоступностьКоманды(Элементы.ФайлыКонтекстноеМенюОбновитьИзФайлаНаДиске, Истина);		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОтображатьУдаленныеФайлыСервер()
	
	РаботаСБизнесПроцессамиВызовСервера.ФормаБизнесПроцессаОтображатьУдаленныеФайлы(ЭтаФорма);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция КоличествоФайлов(ВладелецФайла, ОтображатьУдаленные)
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Файлы.Ссылка
		|ИЗ
		|	Справочник.Файлы КАК Файлы
		|ГДЕ
		|	Файлы.ВладелецФайла = &ВладелецФайла
		|	И (&ОтображатьУдаленные
		|			ИЛИ НЕ Файлы.ПометкаУдаления)";
	Запрос.УстановитьПараметр("ВладелецФайла", ВладелецФайла);
	Запрос.УстановитьПараметр("ОтображатьУдаленные", ОтображатьУдаленные);
	
	Возврат Запрос.Выполнить().Выбрать().Количество();
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура ОбновитьВидимостьТаблицыФайлов(Форма)
	
	КоличествоФайлов = КоличествоФайлов(Форма.Объект.Ссылка, Форма.ОтображатьУдаленныеФайлы);
	
	ПредыдущийТекущийЭлемент = Форма.ТекущийЭлемент;
	
	Если КоличествоФайлов > 0 Тогда
		Форма.Элементы.ГруппаОписаниеИПредметы.ТекущаяСтраница = Форма.Элементы.ОписаниеСПредметами;
		Форма.ТекущийЭлемент = Форма.Элементы.Описание;
	Иначе
		Форма.Элементы.ГруппаОписаниеИПредметы.ТекущаяСтраница = Форма.Элементы.ОписаниеБезПредметов;
		Форма.ТекущийЭлемент = Форма.Элементы.Файлы;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции_ПодсистемаСвойств

&НаКлиенте
Процедура Подключаемый_РедактироватьСоставСвойств()
	
	УправлениеСвойствамиКлиент.РедактироватьСоставСвойств(ЭтаФорма, Объект.Ссылка);
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьЭлементыДополнительныхРеквизитов()
	
	УправлениеСвойствами.ОбновитьЭлементыДополнительныхРеквизитов(ЭтаФорма,
	РеквизитФормыВЗначение("Объект"));
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьЗависимостиДополнительныхРеквизитов()
      УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры
 
&НаКлиенте
Процедура Подключаемый_ПриИзмененииДополнительногоРеквизита(Элемент)
      УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры

&НаСервере
Процедура ДополнительныеОтчетыИОбработкиВыполнитьНазначаемуюКомандуНаСервере(ИмяЭлемента, РезультатВыполнения)
	
	ДополнительныеОтчетыИОбработки.ВыполнитьНазначаемуюКомандуНаСервере(ЭтаФорма, ИмяЭлемента, РезультатВыполнения);
	
КонецПроцедуры

#КонецОбласти
