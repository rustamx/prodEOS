
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	РаботаСБизнесПроцессамиВызовСервера.УстановитьФорматДаты(Элементы.ИсторияЦикловДатаИсполнения);
	РаботаСБизнесПроцессамиВызовСервера.УстановитьФорматДаты(Элементы.ИсторияИсполнителяДатаИсполнения);
	
	ЗадачаСсылка = Параметры.ЗадачаСсылка;
	БизнесПроцесс = ЗадачаСсылка.БизнесПроцесс;
	
	НомерИтерации = 0;
	Если ЗадачаСсылка.ТочкаМаршрута = БизнесПроцессы.Приглашение.ТочкиМаршрута.Ознакомиться Тогда 
		
		НайденнаяСтрока = БизнесПроцесс.РезультатыОзнакомлений.Найти(ЗадачаСсылка, "ЗадачаИсполнителя");
		Если НайденнаяСтрока <> Неопределено Тогда 
			НомерИтерации = НайденнаяСтрока.НомерИтерации;
		КонецЕсли;
		
		// заполнить дерево
		ДеревоИсторияЦиклов = РеквизитФормыВЗначение("ИсторияЦиклов");
		
		НомераИтераций = БизнесПроцесс.РезультатыПриглашения.Выгрузить(,"НомерИтерации");
		НомераИтераций.Свернуть("НомерИтерации",);
		НомераИтераций.Сортировать("НомерИтерации Убыв");
		
		Для Каждого СтрокаИтерации Из НомераИтераций Цикл
			Если СтрокаИтерации.НомерИтерации >= НомерИтерации Тогда 
				Продолжить;
			КонецЕсли;	
			
			СтрокаДереваЦикл = ДеревоИсторияЦиклов.Строки.Добавить();
			СтрокаДереваЦикл.Исполнитель = "Цикл " + СтрокаИтерации.НомерИтерации;
			СтрокаДереваЦикл.НомерИтерации = СтрокаИтерации.НомерИтерации;
			
			Для Каждого Строка Из БизнесПроцесс.РезультатыПриглашения Цикл
				Если СтрокаИтерации.НомерИтерации <> Строка.НомерИтерации Тогда 
					Продолжить;
				КонецЕсли;	
				
				СтрокаДереваИсполнитель = СтрокаДереваЦикл.Строки.Добавить();
				СтрокаДереваИсполнитель.ЗадачаИсполнителя = Строка.ЗадачаИсполнителя;
				СтрокаДереваИсполнитель.РезультатПриглашения = Строка.РезультатПриглашения;
				СтрокаДереваИсполнитель.ДатаИсполнения = Строка.ЗадачаИсполнителя.ДатаИсполнения;
				СтрокаДереваИсполнитель.РезультатВыполнения = Строка.ЗадачаИсполнителя.РезультатВыполнения;
				СтрокаДереваИсполнитель.НомерИтерации = Строка.НомерИтерации;
				
				Если ЗначениеЗаполнено(Строка.ЗадачаИсполнителя.Исполнитель) Тогда 
					СтрокаДереваИсполнитель.Исполнитель = Строка.ЗадачаИсполнителя.Исполнитель;
				Иначе
					СтрокаДереваИсполнитель.Исполнитель = Строка.ЗадачаИсполнителя.РольИсполнителя;
				КонецЕсли;	
				
			КонецЦикла;	
			
		КонецЦикла;	
		
		ЗначениеВРеквизитФормы(ДеревоИсторияЦиклов, "ИсторияЦиклов");
		
		
		Элементы.ГруппаИсторияЦиклов.Видимость = Истина;
		Элементы.ГруппаИсторияИсполнителя.Видимость = Ложь;
		Элементы.Закрыть.КнопкаПоУмолчанию = Истина;
		Заголовок = НСтр("ru = 'История приглашения'; en = 'Invitation history'");
		
	ИначеЕсли ЗадачаСсылка.ТочкаМаршрута = БизнесПроцессы.Приглашение.ТочкиМаршрута.Пригласить Тогда 
		  
		НайденнаяСтрока = БизнесПроцесс.РезультатыПриглашения.Найти(ЗадачаСсылка, "ЗадачаИсполнителя");
		Если НайденнаяСтрока <> Неопределено Тогда 
			НомерИтерации = НайденнаяСтрока.НомерИтерации;
		КонецЕсли;
		
		ДоступностьПоШаблону = ШаблоныБизнесПроцессов.ДоступностьПоШаблону(БизнесПроцесс);
		
		Если НомерИтерации <> БизнесПроцесс.НомерИтерации Или Не ДоступностьПоШаблону
			Или Не БизнесПроцессыИЗадачиСервер.БизнесПроцессАктивен(БизнесПроцесс) Тогда 
			Элементы.ДобавитьУчастника.Видимость = Ложь;
			Элементы.Закрыть.КнопкаПоУмолчанию = Истина;
			
			Элементы.КонтекстноеМенюДобавитьУчастника.Видимость = Ложь;
			Элементы.КонтекстноеМенюУдалитьУчастника.Видимость = Ложь;
			
			Элементы.ИсторияИсполнителя.ТолькоПросмотр = Истина;
		КонецЕсли;
		
		// заполнить дерево
		ДеревоИсторияИсполнителя = РеквизитФормыВЗначение("ИсторияИсполнителя");
		
		НомераИтераций = БизнесПроцесс.РезультатыПриглашения.Выгрузить(,"НомерИтерации");
		НомераИтераций.Свернуть("НомерИтерации",);
		НомераИтераций.Сортировать("НомерИтерации Убыв");
		
		Для Каждого СтрокаИтерации Из НомераИтераций Цикл
			Если СтрокаИтерации.НомерИтерации > НомерИтерации Тогда 
				Продолжить;
			КонецЕсли;	
			
			Если СтрокаИтерации.НомерИтерации = БизнесПроцесс.НомерИтерации Тогда 
				
				СтрокаДереваЦикл = ДеревоИсторияИсполнителя.Строки.Добавить();
				СтрокаДереваЦикл.Исполнитель = "Цикл " + СтрокаИтерации.НомерИтерации + "";
				СтрокаДереваЦикл.НомерИтерации = СтрокаИтерации.НомерИтерации;
				
				Для Каждого Строка Из БизнесПроцесс.Исполнители Цикл
					СтрокаДереваИсполнитель = СтрокаДереваЦикл.Строки.Добавить();
					СтрокаДереваИсполнитель.ЗадачаИсполнителя = Строка.ЗадачаИсполнителя;
					СтрокаДереваИсполнитель.НомерИтерации = БизнесПроцесс.НомерИтерации;
					
					Если ЗначениеЗаполнено(Строка.ЗадачаИсполнителя) Тогда 
						
						СтрокаДереваИсполнитель.ДатаИсполнения = Строка.ЗадачаИсполнителя.ДатаИсполнения;
						СтрокаДереваИсполнитель.РезультатВыполнения = Строка.ЗадачаИсполнителя.РезультатВыполнения;
						
						Если ЗначениеЗаполнено(Строка.ЗадачаИсполнителя.Исполнитель) Тогда 
							СтрокаДереваИсполнитель.Исполнитель = Строка.ЗадачаИсполнителя.Исполнитель;
						Иначе
							СтрокаДереваИсполнитель.Исполнитель = Строка.ЗадачаИсполнителя.РольИсполнителя;
						КонецЕсли;	
						
						НайденнаяСтрока = БизнесПроцесс.РезультатыПриглашения.Найти(Строка.ЗадачаИсполнителя, "ЗадачаИсполнителя");
						Если НайденнаяСтрока <> Неопределено Тогда 
							СтрокаДереваИсполнитель.РезультатПриглашения = НайденнаяСтрока.РезультатПриглашения;
						КонецЕсли;
						
					Иначе
						СтрокаДереваИсполнитель.Исполнитель = Строка.Исполнитель;
					КонецЕсли;	
				КонецЦикла;	
				
			Иначе
				
				СтрокаДереваЦикл = ДеревоИсторияИсполнителя.Строки.Добавить();
				СтрокаДереваЦикл.Исполнитель = "Цикл " + СтрокаИтерации.НомерИтерации;
				СтрокаДереваЦикл.НомерИтерации = СтрокаИтерации.НомерИтерации;
				
				Для Каждого Строка Из БизнесПроцесс.РезультатыПриглашения Цикл
					Если СтрокаИтерации.НомерИтерации <> Строка.НомерИтерации Тогда 
						Продолжить;
					КонецЕсли;	
					
					СтрокаДереваИсполнитель = СтрокаДереваЦикл.Строки.Добавить();
					СтрокаДереваИсполнитель.ЗадачаИсполнителя = Строка.ЗадачаИсполнителя;
					СтрокаДереваИсполнитель.РезультатПриглашения = Строка.РезультатПриглашения;
					СтрокаДереваИсполнитель.ДатаИсполнения = Строка.ЗадачаИсполнителя.ДатаИсполнения;
					СтрокаДереваИсполнитель.РезультатВыполнения = Строка.ЗадачаИсполнителя.РезультатВыполнения;
					СтрокаДереваИсполнитель.НомерИтерации = Строка.НомерИтерации;
					
					Если ЗначениеЗаполнено(Строка.ЗадачаИсполнителя.Исполнитель) Тогда 
						СтрокаДереваИсполнитель.Исполнитель = Строка.ЗадачаИсполнителя.Исполнитель;
					Иначе
						СтрокаДереваИсполнитель.Исполнитель = Строка.ЗадачаИсполнителя.РольИсполнителя;
					КонецЕсли;
					
				КонецЦикла;
				
			КонецЕсли;
			
		КонецЦикла;
		
		ЗначениеВРеквизитФормы(ДеревоИсторияИсполнителя, "ИсторияИсполнителя");
		
		Элементы.ГруппаИсторияЦиклов.Видимость = Ложь;
		Элементы.ГруппаИсторияИсполнителя.Видимость = Истина;
		Заголовок = НСтр("ru = 'Ход приглашения'; en = 'Invitation advancement'");
		
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ЭлементыДерева = ИсторияИсполнителя.ПолучитьЭлементы();
	
	Если ЭлементыДерева.Количество() > 0 Тогда 
		ЭлементДерева = ЭлементыДерева[0];
		Элементы.ИсторияИсполнителя.Развернуть(ЭлементДерева.ПолучитьИдентификатор());
	КонецЕсли;	
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ИсторияЦикловВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ТекущиеДанные = Элементы.ИсторияЦиклов.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ТекущиеДанные.ЗадачаИсполнителя) Тогда
		БизнесПроцессыИЗадачиКлиент.СписокЗадачВыбор(Элемент, ТекущиеДанные.ЗадачаИсполнителя, Поле, СтандартнаяОбработка);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыИсторияИсполнителя

&НаКлиенте
Процедура ИсторияИсполнителяВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ТекущиеДанные = Элементы.ИсторияИсполнителя.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(ТекущиеДанные.Исполнитель) = Тип("Строка") Тогда 
		СтандартнаяОбработка = Ложь;
		
		Если Элементы.ИсторияИсполнителя.Развернут(ТекущиеДанные.ПолучитьИдентификатор()) Тогда 
			Элементы.ИсторияИсполнителя.Свернуть(ТекущиеДанные.ПолучитьИдентификатор());
		Иначе
			Элементы.ИсторияИсполнителя.Развернуть(ТекущиеДанные.ПолучитьИдентификатор());
		КонецЕсли;	
		
		Возврат;
	КонецЕсли;
	
	Если ТекущиеДанные.Добавлена Тогда 
		Возврат;
	КонецЕсли;	
		
	СтандартнаяОбработка = Ложь;
	Если ЗначениеЗаполнено(ТекущиеДанные.ЗадачаИсполнителя) Тогда  
		БизнесПроцессыИЗадачиКлиент.СписокЗадачВыбор(Элемент, ТекущиеДанные.ЗадачаИсполнителя, Поле, СтандартнаяОбработка);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ИсторияИсполнителяПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	
	Отказ = Истина;
	
	ТекущиеДанные = Элементы.ИсторияИсполнителя.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	ЭлементыДерева = ИсторияИсполнителя.ПолучитьЭлементы();
	ЭлементДерева = ИсторияИсполнителя.НайтиПоИдентификатору(ТекущиеДанные.ПолучитьИдентификатор());
	
	Если ЭлементДерева = Неопределено Тогда 
		Возврат;
	КонецЕсли;	
	
	Если ЭлементыДерева.Индекс(ЭлементДерева) = 0 Тогда 
		ДобавитьСтрокуУчастника();
		Возврат;
	КонецЕсли;	
	
	РодительЭлементаДерева = ЭлементДерева.ПолучитьРодителя();
	Если ЭлементыДерева.Индекс(РодительЭлементаДерева) = 0 Тогда 
		ДобавитьСтрокуУчастника();
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ИсторияИсполнителяПередНачаломИзменения(Элемент, Отказ)
	
	ТекущиеДанные = Элементы.ИсторияИсполнителя.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(ТекущиеДанные.Исполнитель) = Тип("Строка") Тогда 
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Если ТекущиеДанные.Добавлена Тогда 
		Возврат;
	КонецЕсли;
	
	Отказ = Истина;
	Если ЗначениеЗаполнено(ТекущиеДанные.ЗадачаИсполнителя) Тогда  
		БизнесПроцессыИЗадачиКлиент.ОткрытьФормуВыполненияЗадачи(ТекущиеДанные.ЗадачаИсполнителя);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ИсторияИсполнителяПередУдалением(Элемент, Отказ)
	
	ТекущиеДанные = Элементы.ИсторияИсполнителя.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(ТекущиеДанные.Исполнитель) = Тип("Строка") Тогда 
		Отказ = Истина;
	КонецЕсли;
	
	Если Не ТекущиеДанные.Добавлена Тогда 
		ТекстПредупреждения = НСтр("ru = 'Можно удалить только строки, созданные самостоятельно!'; en = 'One can delete only the rows created personally!'");
		ПоказатьПредупреждение(, ТекстПредупреждения);
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ИсторияИсполнителяИсполнительНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ТекущиеДанные = Элементы.ИсторияИсполнителя.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда 
		Возврат;
	КонецЕсли;	
	
	Если ТипЗнч(ТекущиеДанные.Исполнитель) = Тип("Строка") Тогда 
		Возврат;
	КонецЕсли;
	
	РаботаСБизнесПроцессамиКлиент.УчастникНачалоВыбора(
		Элемент, ТекущиеДанные.Исполнитель, СтандартнаяОбработка, ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ИсторияИсполнителяИсполнительАвтоПодбор(Элемент, Текст, ДанныеВыбора, Ожидание, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Текст) Тогда 
		СтандартнаяОбработка = Ложь;
		ДополнениеТипа = Новый ОписаниеТипов("СправочникСсылка.ПолныеРоли");
		ДанныеВыбора = РаботаСПользователями.СформироватьДанныеВыбора(Текст, ДополнениеТипа);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ИсторияИсполнителяИсполнительОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Текст) Тогда 
		СтандартнаяОбработка = Ложь;
		ДополнениеТипа = Новый ОписаниеТипов("СправочникСсылка.ПолныеРоли");
		ДанныеВыбора = РаботаСПользователями.СформироватьДанныеВыбора(Текст, ДополнениеТипа);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ДобавитьУчастника(Команда)
	
	ДобавитьСтрокуУчастника();
	
КонецПроцедуры

&НаКлиенте
Процедура ОК(Команда)
	
	Если Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаИсторияЦиклов Тогда 
		Закрыть();
		Возврат;
	КонецЕсли;	
	
	ЭлементыДерева = ИсторияИсполнителя.ПолучитьЭлементы();
	Если ЭлементыДерева.Количество() = 0 Тогда 
		Закрыть();
		Возврат;
	КонецЕсли;	
	
	СтрокаДерева = ЭлементыДерева[0];
	ЭлементыСтрокиДерева = СтрокаДерева.ПолучитьЭлементы();
	
	ЕстьДобавленныеСтроки = Ложь;
	Для Каждого Строка Из ЭлементыСтрокиДерева Цикл
		Если Строка.Добавлена Тогда
			ЕстьДобавленныеСтроки = Истина;
			Прервать;
		КонецЕсли;
	КонецЦикла;	
	
	Если Не ЕстьДобавленныеСтроки Тогда 
		Закрыть();
		Возврат;
	КонецЕсли;	
	
	// проверка заполнения
	ОчиститьСообщения();
	
	Для Каждого Строка Из ЭлементыСтрокиДерева Цикл
		Если Строка.Добавлена И Не ЗначениеЗаполнено(Строка.Исполнитель) Тогда
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Не заполнено поле ""Участник"" в строке %1 списка!'; en = 'Field ""Attendee"" is not filled in at row %1 of the list!'"), 
					ЭлементыСтрокиДерева.Индекс(Строка)+1);
				
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,, 
					"ИсторияИсполнителя",,);
			Возврат;		
		КонецЕсли;
	КонецЦикла;
	
	// проверка дублей
	КоличествоИсполнителей = ЭлементыСтрокиДерева.Количество();
	Для Инд1 = 0 По КоличествоИсполнителей-2 Цикл
		Строка1 = ЭлементыСтрокиДерева[Инд1];
		
		Для Инд2 = Инд1+1 По КоличествоИсполнителей-1 Цикл
			Строка2 = ЭлементыСтрокиДерева[Инд2];
			
			Если Строка1.Исполнитель = Строка2.Исполнитель Тогда 
				
				ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Участник ""%1"" указан дважды в списке участников!'; en = 'Participant ""%1"" is specified twice in the participants list!'"), 
					Строка(Строка1.Исполнитель));
				
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
					ТекстСообщения,,
					"ИсторияИсполнителя",,);
					
				Возврат;
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ОКЗавершение", ЭтотОбъект);
	ТекстВопроса = НСтр("ru = 'В список участников были добавлены новые исполнители. Будет выполнено обновление процесса.
		|Продолжить?';
		|en = 'New performers were added to the participants list. Process will be updated.
		|Continue?'");
	
	ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Да);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ОбновитьБизнесПроцесс()
	
	ДеревоИсторияИсполнителя = РеквизитФормыВЗначение("ИсторияИсполнителя");
	Если ДеревоИсторияИсполнителя.Строки.Количество() = 0 Тогда 
		Возврат;
	КонецЕсли;
	
	ЗадачаСсылка = Параметры.ЗадачаСсылка;
	БизнесПроцесс = ЗадачаСсылка.БизнесПроцесс;
	
	Если Не БизнесПроцессыИЗадачиСервер.БизнесПроцессАктивен(БизнесПроцесс) Тогда 
		ВызватьИсключение НСтр("ru = 'Бизнес процесс не активен, данные запрещено изменять.
		|Обратитесь к администратору.';
		|en = 'Business process is inactive, it is forbidden to change the data.
		|Contact the administrator.'");
	КонецЕсли;
	
	НачатьТранзакцию();
	Попытка
		
		БизнесПроцессОбъект = БизнесПроцесс.ПолучитьОбъект();
		ЗаблокироватьДанныеДляРедактирования(БизнесПроцессОбъект.Ссылка);
		
		СтарыеУчастникиПроцесса = БизнесПроцессыИЗадачиВызовСервера.ТекущиеУчастникиПроцесса(БизнесПроцессОбъект);
		
		СтрокиДерева = ДеревоИсторияИсполнителя.Строки[0].Строки;
		Для Каждого Строка Из СтрокиДерева Цикл
			Если Строка.Добавлена Тогда 
				Индекс = СтрокиДерева.Индекс(Строка);
				НоваяСтрока = БизнесПроцессОбъект.Исполнители.Вставить(Индекс);
				НоваяСтрока.Исполнитель = Строка.Исполнитель;
			КонецЕсли;
		КонецЦикла;
		
		БизнесПроцессОбъект.Записать();
		
		УзелОбменаПроцесса = ОбщегоНазначенияДокументооборот.ЗначениеРеквизитаОбъектаВПривилегированномРежиме(
			БизнесПроцесс, "УзелОбмена");
		ЭтотУзелОбмена = РаботаСБизнесПроцессами.ЭтотУзелОбменаДляОбработкиПроцессов();
		
		Если УзелОбменаПроцесса = ЭтотУзелОбмена Тогда
			БизнесПроцессОбъект.ИзменитьРеквизитыНевыполненныхЗадач(СтарыеУчастникиПроцесса, Новый Структура);
		КонецЕсли;
		
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		ВызватьИсключение;
	КонецПопытки;
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьСтрокуУчастника()
	
	ЭлементыДерева = ИсторияИсполнителя.ПолучитьЭлементы();
	
	Если ЭлементыДерева.Количество() > 0 Тогда 
		СтрокаДерева = ЭлементыДерева[0];
		ЭлементыСтрокиДерева = СтрокаДерева.ПолучитьЭлементы();
		
		
		ВсеПройдены = Истина;
		Для Каждого Строка Из ЭлементыСтрокиДерева Цикл
			Если ЗначениеЗаполнено(Строка.ЗадачаИсполнителя) И Не Строка.Пройдена Тогда 
				ВсеПройдены = Ложь;
				Прервать;
			КонецЕсли;
		КонецЦикла;	
		
		Если ВсеПройдены Тогда 
			ТекстПредупреждения = НСтр("ru = 'Все исполнители завершили свои задачи, добавление строки невозможно!'; en = 'All performers have completed their tasks, it is not possible to add a new one!'");
			ПоказатьПредупреждение(, ТекстПредупреждения);
			Возврат;
		КонецЕсли;
		
		НоваяСтрока = ЭлементыСтрокиДерева.Добавить();
		НоваяСтрока.Добавлена = Истина;
		
		Элементы.ИсторияИсполнителя.ТекущаяСтрока = НоваяСтрока.ПолучитьИдентификатор();
		Элементы.ИсторияИсполнителя.ИзменитьСтроку();
	КонецЕсли;	
	
КонецПроцедуры	

&НаКлиенте
Процедура ОКЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> КодВозвратаДиалога.Да Тогда 
		Возврат;
	КонецЕсли;
	
	ОбновитьБизнесПроцесс();
	
	Оповестить("ИзмененСоставУчастников", БизнесПроцесс);
	Оповестить("ИзмененыРеквизитыНевыполненныхЗадач", БизнесПроцесс);
	
	ПоказатьОповещениеПользователя(
		"Изменение:", 
		ПолучитьНавигационнуюСсылку(БизнесПроцесс),
		Строка(БизнесПроцесс),
		БиблиотекаКартинок.Информация32);
	
	Закрыть();
	
КонецПроцедуры

#КонецОбласти
