
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	РаботаСБизнесПроцессамиВызовСервера.УстановитьФорматДаты(Элементы.ИсторияЦикловДатаИсполнения);
	РаботаСБизнесПроцессамиВызовСервера.УстановитьФорматДаты(Элементы.ИсторияИсполнителяДатаИсполнения);
	
	ЗадачаСсылка = Параметры.ЗадачаСсылка;
	БизнесПроцесс = ЗадачаСсылка.БизнесПроцесс;
	
	ВидПроцесса = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ЗадачаСсылка.БизнесПроцесс, "ВидПроцесса"); 
	СформироватьЗаголовкиИПодсказкиЭлементовФормы(ВидПроцесса);
	
	ТочкиМаршрута = Новый Массив;
	ТочкиМаршрута.Добавить(БизнесПроцессы.Утверждение.ТочкиМаршрута.Утвердить);
	
	Если ЗадачаСсылка.ТочкаМаршрута = БизнесПроцессы.Утверждение.ТочкиМаршрута.Ознакомиться Тогда 
		
		НайденнаяСтрока = БизнесПроцесс.РезультатыОзнакомлений.Найти(ЗадачаСсылка, "ЗадачаИсполнителя");
		Если НайденнаяСтрока <> Неопределено Тогда 
			НомерИтерации = НайденнаяСтрока.НомерИтерации;
		КонецЕсли;	
		
		ИсторияЦиклов.Параметры.УстановитьЗначениеПараметра("ТочкиМаршрута", ТочкиМаршрута);
		ИсторияЦиклов.Параметры.УстановитьЗначениеПараметра("БизнесПроцесс", БизнесПроцесс);
		ИсторияЦиклов.Параметры.УстановитьЗначениеПараметра("НомерИтерации", НомерИтерации);
		
		Элементы.ГруппаИсторияЦиклов.Видимость = Истина;
		Элементы.ГруппаИсторияИсполнителя.Видимость = Ложь;
		
	ИначеЕсли ЗадачаСсылка.ТочкаМаршрута = БизнесПроцессы.Утверждение.ТочкиМаршрута.Утвердить Тогда 
		
		НайденнаяСтрока = БизнесПроцесс.РезультатыУтверждения.Найти(ЗадачаСсылка, "ЗадачаИсполнителя");
		Если НайденнаяСтрока <> Неопределено Тогда 
			НомерИтерации = НайденнаяСтрока.НомерИтерации;
		КонецЕсли;	
		
		ИсторияИсполнителя.Параметры.УстановитьЗначениеПараметра("ТочкиМаршрута", ТочкиМаршрута);
		ИсторияИсполнителя.Параметры.УстановитьЗначениеПараметра("БизнесПроцесс", БизнесПроцесс);
		Если ЗадачаСсылка.Выполнена Тогда 
			ИсторияИсполнителя.Параметры.УстановитьЗначениеПараметра("НомерИтерации", НомерИтерации); 
		Иначе
			ИсторияИсполнителя.Параметры.УстановитьЗначениеПараметра("НомерИтерации", НомерИтерации - 1);
		КонецЕсли;	
		  
		ИсторияИсполнителя.Параметры.УстановитьЗначениеПараметра("Исполнитель", ЗадачаСсылка.Исполнитель);
		ИсторияИсполнителя.Параметры.УстановитьЗначениеПараметра(
			"РольИсполнителя",
			ЗадачаСсылка.РольИсполнителя);
		
		Элементы.ГруппаИсторияЦиклов.Видимость = Ложь;
		Элементы.ГруппаИсторияИсполнителя.Видимость = Истина;
		
		Если ЗначениеЗаполнено(ЗадачаСсылка.РольИсполнителя) Тогда 
			Заголовок = Заголовок + " (" + Строка(ЗадачаСсылка.РольИсполнителя) + " )";
		Иначе
			Заголовок = Заголовок + " (" + Строка(ЗадачаСсылка.Исполнитель) + " )";
		КонецЕсли;	
		
	КонецЕсли;	
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыИсторияИсполнителя

&НаКлиенте
Процедура ИсторияИсполнителяВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	БизнесПроцессыИЗадачиКлиент.СписокЗадачВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыИсторияЦиклов

&НаКлиенте
Процедура ИсторияЦикловВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	БизнесПроцессыИЗадачиКлиент.СписокЗадачВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура СформироватьЗаголовкиИПодсказкиЭлементовФормы(ВидПроцесса)
	
	Если ВидПроцесса = Перечисления.ВидыБизнесПроцессаУтверждение.Подписание Тогда
		Заголовок = НСтр("ru = 'История подписания'; en = 'Signing history'");
		Элементы.ИсторияЦикловИсполнитель.Заголовок = НСтр("ru = 'Подписант'; en = 'Signatory'");
		Элементы.ИсторияЦикловРезультатУтверждения.Заголовок = НСтр("ru = 'Результат подписи'; en = 'Signing result'");
		Элементы.ИсторияЦикловДатаИсполнения.Заголовок = НСтр("ru = 'Дата подписи'; en = 'Signature date'");
	Иначе
		Заголовок = НСтр("ru = 'История утверждения'; en = 'Confirmation history'");
		Элементы.ИсторияЦикловИсполнитель.Заголовок = НСтр("ru = 'Утверждающее лицо'; en = 'Confirming person'");
		Элементы.ИсторияЦикловРезультатУтверждения.Заголовок = НСтр("ru = 'Результат утверждения'; en = 'Confirmation result'");
		Элементы.ИсторияЦикловДатаИсполнения.Заголовок = НСтр("ru = 'Дата утверждения'; en = 'Confirmation date'");
	КонецЕсли;
		
	Элементы.ИсторияИсполнителяРезультатУтверждения.Заголовок =
		Элементы.ИсторияЦикловРезультатУтверждения.Заголовок;

	Элементы.ИсторияИсполнителяДатаИсполнения.Заголовок =
		Элементы.ИсторияЦикловДатаИсполнения.Заголовок;
		
КонецПроцедуры

#КонецОбласти
