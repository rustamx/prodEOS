
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ВариантРассмотренияВвестиТекстРезолюции = Перечисления.ВариантыРассмотрения.ВвестиТекстРезолюции;
	ВариантРассмотренияНаправитьНаИсполнение = Перечисления.ВариантыРассмотрения.НаправитьНаИсполнение;
	ВариантРассмотренияНаправитьНаОзнакомление = Перечисления.ВариантыРассмотрения.НаправитьНаОзнакомление;
	
	РаботаСБизнесПроцессамиВызовСервера.ФормаЗадачиПриСозданииНаСервере(ЭтаФорма, Объект);
	
	УчетВремени.ПроинициализироватьПараметрыУчетаВремени(
		ДатаНачалаХронометража,
		ДатаКонцаХронометража,
		ВключенХронометраж,
		ОпцияИспользоватьУчетВремени,
		Объект.Ссылка,
		ВидыРабот,
		СпособУказанияВремени,
		ЭтаФорма.Команды.ПереключитьХронометраж,
		ЭтаФорма.Элементы.ПереключитьХронометраж,
		ЭтаФорма.Элементы.УказатьТрудозатраты);
	
	БизнесПроцессыИЗадачиВызовСервера.ЗаписатьСобытиеОткрытаКарточкаИОбращениеКОбъекту(Объект.Ссылка);
	
	ЗаполнитьШаблоныКоманд();
	ОбновитьВидимостьКомандРассмотретьПоШаблону();
	
	ПраваПоОбъекту = ДокументооборотПраваДоступа.ПолучитьПраваПоОбъекту(Объект.Ссылка);
	Если Не ПраваПоОбъекту.Изменение Тогда
		ТолькоПросмотр = Истина;
		Элементы.ГруппаКомандыВыполнения.Доступность = Ложь;
		
		Элементы.ДеревоПриложений.ИзменятьПорядокСтрок = Ложь;
		Элементы.ДеревоПриложений.ИзменятьСоставСтрок = Ложь;
		
		Элементы.Перенаправить.Доступность = Ложь;
		Элементы.ФормаПринятьКИсполнению.Доступность = Ложь;
		Элементы.ФормаОтменитьПринятиеКИсполнению.Доступность = Ложь;
		Элементы.ИзменитьДатуВыполнения.Доступность = Ложь;
	КонецЕсли;
	
	Если Объект.СостояниеБизнесПроцесса <> Перечисления.СостоянияБизнесПроцессов.Активен
		Или Объект.Выполнена Тогда
		
		Элементы.ДеревоПриложений.ИзменятьПорядокСтрок = Ложь;
		Элементы.ДеревоПриложений.ИзменятьСоставСтрок = Ложь;
	КонецЕсли;
	
	Если ТолькоПросмотр = Истина Тогда 
		Элементы.ЗаполнитьПоШаблону.Доступность = Ложь;
	КонецЕсли;
	
	ВестиУчетПлановыхТрудозатратВБизнесПроцессах = ПолучитьФункциональнуюОпцию("ВестиУчетПлановыхТрудозатратВБизнесПроцессах");
	
	// Заполнение настроек процессов Исполнения и Ознакомления.
	СтрокаРеквизитовРассмотрения = 
		"ВариантРассмотрения, 
		|ШаблонИсполнения, ШаблонОзнакомления,
		|НаименованиеИсполнения, НаименованиеОзнакомления,
		|ОписаниеИсполнения, ОписаниеОзнакомления, ВажностьИсполнения, ВажностьОзнакомления,
		|ВариантИсполнения,
		|СрокИсполненияПроцессаИсполнения, СрокИсполненияПроцессаОзнакомления,
		|СрокОбработкиРезультатовИсполнения,
		|СрокОбработкиРезультатовДниИсполнения,
		|СрокОбработкиРезультатовЧасыИсполнения,
		|СрокОбработкиРезультатовМинутыИсполнения,
		|ВариантУстановкиСрокаОбработкиРезультатовИсполнения,
		|КоличествоИтерацийИсполнения,
		|ТрудозатратыПланИсполнителяОзнакомления,
		|КонтролерИсполнения, 
		|ПроверяющийИсполнения, ТрудозатратыПланПроверяющего,
		|ИсполнителиИсполнения, ИсполнителиОзнакомления";
		
	РеквизитыРассмотрения =
		ОбщегоНазначенияДокументооборот.ЗначенияРеквизитовОбъектаВПривилегированномРежиме(
			Объект.БизнесПроцесс, СтрокаРеквизитовРассмотрения);
			
	ЗаполнитьЗначенияСвойств(ЭтаФорма, РеквизитыРассмотрения,, "ИсполнителиИсполнения, ИсполнителиОзнакомления");
	ИсполнителиИсполнения.Загрузить(РеквизитыРассмотрения.ИсполнителиИсполнения.Выгрузить());
	ИсполнителиОзнакомления.Загрузить(РеквизитыРассмотрения.ИсполнителиОзнакомления.Выгрузить());
	
	Если Не ЗначениеЗаполнено(ВажностьИсполнения) Тогда 
		ВажностьИсполнения = Перечисления.ВариантыВажностиЗадачи.Обычная;
	КонецЕсли;
	Если Не ЗначениеЗаполнено(ВажностьОзнакомления) Тогда 
		ВажностьОзнакомления = Перечисления.ВариантыВажностиЗадачи.Обычная;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	КоличествоИсполнителейИсполнения = ИсполнителиИсполнения.Количество();
	КоличествоИсполнителейОзнакомления = ИсполнителиОзнакомления.Количество();
	
	СрокИсполненияДляПодчиненныхПроцессов = Неопределено;
	Если КоличествоИсполнителейИсполнения = 0
		Или КоличествоИсполнителейОзнакомления = 0 Тогда
		
		ТипыДокументов = МультипредметностьКлиентСервер.ПолучитьТипыДокументов();
		ОбрабатываемыеПредметы = МультипредметностьКлиентСервер.ПолучитьМассивПредметовОбъекта(Объект, ТипыДокументов, Истина);
		Для Каждого Предмет Из ОбрабатываемыеПредметы Цикл
			Если ЗначениеЗаполнено(Предмет.СрокИсполнения) Тогда 
				СрокИсполненияДляПодчиненныхПроцессов = КонецДня(Предмет.СрокИсполнения);
				Прервать;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(СрокИсполненияДляПодчиненныхПроцессов) Тогда
		Если КоличествоИсполнителейИсполнения = 0 Тогда
			
			ВариантИсполнения = Перечисления.ВариантыМаршрутизацииЗадач.Параллельно;
			
			СтрИсполнитель = ИсполнителиИсполнения.Добавить();
			СтрИсполнитель.ПорядокИсполнения = Перечисления.ПорядокВыполненияЗадач.ВместеСПредыдущим;
			
			СтрИсполнитель.СрокИсполнения = СрокИсполненияДляПодчиненныхПроцессов;
			СтрИсполнитель.ВариантУстановкиСрокаИсполнения = 
				Перечисления.ВариантыУстановкиСрокаИсполнения.ТочныйСрок;
			
		КонецЕсли;
		Если КоличествоИсполнителейОзнакомления = 0 Тогда
			
			СтрИсполнитель = ИсполнителиОзнакомления.Добавить();
			СтрИсполнитель.СрокИсполнения = СрокИсполненияДляПодчиненныхПроцессов;
			СтрИсполнитель.ВариантУстановкиСрокаИсполнения = 
				Перечисления.ВариантыУстановкиСрокаИсполнения.ТочныйСрок;
			
		КонецЕсли;
		
		КоличествоИтерацийИсполнения = 1;
	КонецЕсли;
	УстановитьПривилегированныйРежим(Ложь);
	
	ВестиУчетПлановыхТрудозатратВБизнесПроцессах = 
		ПолучитьФункциональнуюОпцию("ВестиУчетПлановыхТрудозатратВБизнесПроцессах");
	Элементы.ОписаниеТрудозатратПроцессаИсполнения.Видимость = ВестиУчетПлановыхТрудозатратВБизнесПроцессах;
	Элементы.ОписаниеТрудозатратПроцессаОзнакомления.Видимость = ВестиУчетПлановыхТрудозатратВБизнесПроцессах;
	ПолучитьДанныеПроекта();
	
	Если ИсполнителиИсполнения.Количество() > 1 Тогда 
		Элементы.НазначитьОтветственным.Доступность = Истина;
	Иначе
		Элементы.НазначитьОтветственным.Доступность = Ложь;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ВариантИсполнения) Тогда
		ВариантИсполнения = Перечисления.ВариантыМаршрутизацииЗадач.Параллельно;
	КонецЕсли;
	ПредыдущийВариантИсполнения = ВариантИсполнения;
	
	// Сроки исполнения
	УстановитьУсловноеОформлениеИстекшихСроков();
	ДатаОтсчетаДляРасчетаСроков = ТекущаяДата();
	ВозможенВыборТочнойДатыВСроках = Истина;
	
	ИспользоватьДатуИВремяВСрокахЗадач = 
		ПолучитьФункциональнуюОпцию("ИспользоватьДатуИВремяВСрокахЗадач");
		
	ПодсказкаВвода = СрокиИсполненияПроцессов.ПодсказкаВводаПоляСрокИсполнения(ЭтаФорма);
	Элементы.ИсполнителиСрокИсполненияПредставление.ПодсказкаВвода = ПодсказкаВвода;
	Элементы.СрокОбработкиРезультатовИсполненияПредставление.ПодсказкаВвода = ПодсказкаВвода;
	Элементы.ИсполнителиОзнакомленияСрокИсполненияПредставление.ПодсказкаВвода = ПодсказкаВвода;
	
	ШиринаПоляСрокИсполнения = СрокиИсполненияПроцессов.ШиринаПоляСрокИсполнения(ЭтаФорма);
	Элементы.ИсполнителиСрокИсполненияПредставление.Ширина = ШиринаПоляСрокИсполнения;
	Элементы.СрокОбработкиРезультатовИсполненияПредставление.Ширина = ШиринаПоляСрокИсполнения;
	Элементы.ИсполнителиОзнакомленияСрокИсполненияПредставление.Ширина = ШиринаПоляСрокИсполнения;
	
	ОбновитьСрокиИсполненияНаСервере(Истина);
	
	// Установка доступности
	УстановитьДоступность();
	УстановитьДоступностьПоШаблонуИсполнения();
	УстановитьДоступностьПоШаблонуОзнакомления();
	
	// Инструкции
	ПоказыватьИнструкции = ПолучитьФункциональнуюОпцию("ИспользоватьИнструкции");
	ПолучитьИнструкции();
	
	// Определение необходимости подписания резолюций ЭП
	ПодписатьРезолюцииЭП = Ложь;
	Для Каждого Предмет Из Объект.Предметы Цикл
		
		Если ТипЗнч(Предмет.Предмет) = Тип("СправочникСсылка.ВнутренниеДокументы")
			ИЛИ ТипЗнч(Предмет.Предмет) = Тип("СправочникСсылка.ВходящиеДокументы") Тогда
			
			ВидДокумента = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Предмет.Предмет, "ВидДокумента");
			ПодписыватьРезолюцииЭП = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ВидДокумента, 
				"ПодписыватьРезолюцииЭП");
				
			Если ТипЗнч(ПодписыватьРезолюцииЭП) = Тип("Булево") И ПодписыватьРезолюцииЭП Тогда
				
				ПодписатьРезолюцииЭП = Истина;
				Прервать;
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Если ПодписатьРезолюцииЭП Тогда
		Команды.Выполнено.Заголовок = НСтр("ru = 'Рассмотрено (ЭП)'; en = 'Revewed (DS)'");
		Команды.Выполнено.Подсказка = НСтр("ru = 'При нажатии кнопки ""Рассмотрено"" потребуется выбрать сертификат для подписи и ввести пароль'; en = 'Upon pressing ""Reviewed"" you will be prompted to choose the digital certificate and to enter the password'");
		Команды.Исполнить.Заголовок = НСтр("ru = 'Исполнить (ЭП)'; en = 'Perform (DS)'");
		Команды.Исполнить.Подсказка = НСтр("ru = 'При нажатии кнопки ""Исполнить"" потребуется выбрать сертификат для подписи и ввести пароль'; en = 'Upon pressing ""Execute"" you will be prompted to choose the digital certificate and to enter the password'");
		Команды.Ознакомить.Заголовок = НСтр("ru = 'Ознакомить (ЭП)'; en = 'Examine (DS)'");
		Команды.Ознакомить.Подсказка = НСтр("ru = 'При нажатии кнопки ""Ознакомить"" потребуется выбрать сертификат для подписи и ввести пароль'; en = 'Upon pressing ""Examined"" you will be prompted to choose the digital certificate and to enter the password'");
	КонецЕсли;
	
	РасширеннаяФорма = ОбщегоНазначения.ХранилищеНастроекДанныхФормЗагрузить(ЭтаФорма.ИмяФормы, 
		"РасширеннаяФорма", Ложь);
	ПоказатьРасширеннуюФорму();
	
	ПроверятьОтсутствие = Отсутствия.ПредупреждатьОбОтсутствии();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	РаботаСБизнесПроцессамиКлиентСервер.ЗаполнитьШаг(ИсполнителиИсполнения);
	Оповестить("ОбновитьСписокПоследних");
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если ОбщегоНазначенияДокументооборотКлиент.ПередЗакрытием(
		Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка, Модифицированность) Тогда
		Возврат;
	КонецЕсли;
	
	ОбщегоНазначенияВызовСервера.ХранилищеНастроекДанныхФормСохранить(ЭтаФорма.ИмяФормы, 
		"РасширеннаяФорма", РасширеннаяФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(РезультатВыбора, ИсточникВыбора)
	
	РаботаСБизнесПроцессамиКлиентСервер.ЗаполнитьШаг(ИсполнителиИсполнения);
	УстановитьДоступность();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ИзмененыРеквизитыНевыполненныхЗадач" И Параметр = Объект.БизнесПроцесс И Не Объект.Выполнена Тогда 
		
		ДатаИсполнения = Объект.ДатаИсполнения;
		Прочитать();
		Объект.ДатаИсполнения = ДатаИсполнения;
		
	ИначеЕсли ИмяСобытия = "Запись_Файл" И Параметр.Событие = "ДанныеФайлаИзменены" Тогда
		
		ОбновитьДеревоПриложений();
		
	ИначеЕсли ИмяСобытия = "ФайлЗанятДляРедактирования" Тогда
		
		ОбновитьДеревоПриложений();
		
	ИначеЕсли ИмяСобытия = "Запись_Файл" И Параметр.Событие = "СозданФайл" И Параметр.ИдентификаторРодительскойФормы = УникальныйИдентификатор Тогда
		
		МультипредметностьВызовСервера.ОбработатьДобавлениеПредметаЗадачи(
			Объект.Ссылка, Объект.БизнесПроцесс, Параметр.Файл, УникальныйИдентификатор);
			
		Прочитать();
		ОбновитьДеревоПриложений();
		
	ИначеЕсли ИмяСобытия = "ПерейтиКРезультатуРассмотренияЗадачи"
		И ТипЗнч(Параметр) = Тип("Структура")
		И Параметр.Свойство("ЗадачаСсылка")
		И Объект.Ссылка = Параметр.ЗадачаСсылка
		И Параметр.Свойство("ВариантРассмотрения") Тогда
		ВариантРассмотрения = Параметр.ВариантРассмотрения;
		ПоказатьРасширеннуюФорму();
		ГруппаСтраницыПриСменеСтраницы(Элементы.ГруппаСтраницы, Элементы.ГруппаСтраницы.ТекущаяСтраница);
		
	ИначеЕсли ИмяСобытия = "ИзменилсяФлаг"
		И Источник <> ЭтаФорма
		И Параметр.Найти(Объект.Ссылка) <> Неопределено Тогда
		
		РаботаСФлагамиОбъектовКлиентСервер.ОтобразитьФлагВФормеОбъекта(ЭтаФорма);
		
	ИначеЕсли ИмяСобытия = "СозданНовыйВопросВыполненияЗадачи" И Параметр = Объект.Ссылка Тогда
		
		БизнесПроцессыИЗадачиКлиентСервер.ЗаполнитьЗаголовокКомандыЗадатьВопрос(ЭтаФорма);
		
	ИначеЕсли ИмяСобытия = "ЗадачаИзменена" И Источник <> ЭтаФорма Тогда
		
		ПрочитатьДанныеЗадачиВФорму = Ложь;
		
		Если ТипЗнч(Параметр) = Тип("Массив") Тогда
			ПрочитатьДанныеЗадачиВФорму = Параметр.Найти(Объект.Ссылка) <> Неопределено;
		Иначе
			ПрочитатьДанныеЗадачиВФорму = (Параметр = Объект.Ссылка);
		КонецЕсли;
		
		Если ПрочитатьДанныеЗадачиВФорму Тогда
			Прочитать();
		КонецЕсли;
		
		КомандыРаботыСБизнесПроцессамиКлиент.ОбновитьДоступностьКомандПринятияКИсполнению(ЭтаФорма);
		
	ИначеЕсли ИмяСобытия = "Перенаправление_ЗадачаИсполнителя" И Источник = Объект.Ссылка Тогда
		Закрыть();
		
	ИначеЕсли ИмяСобытия = "СозданНовыйШаблонРезолюции" Тогда
		
		ОбновитьКомандыРассмотренияПоШаблону();
		
	ИначеЕсли ИмяСобытия = "ИзменилсяШаблонРезолюции" Тогда
		Если КоличествоКоманд <= 2 Тогда
			Для Ит = 0 По КоличествоКоманд - 1 Цикл 
				Если ЭтаФорма["Шаблон"+Ит] = Параметр.Шаблон Тогда 
					Элементы["РассмотретьПоШаблону"+Ит].Заголовок = Лев(Параметр.Наименование, 30);
					Прервать;
				КонецЕсли;
			КонецЦикла;
		Иначе
			ЗаменитьЗаголовокКоманды(Параметр);
		КонецЕсли;
		
	ИначеЕсли ИмяСобытия = "БылУдаленШаблонРезолюции" Тогда
		ОбновитьКомандыРассмотренияПоШаблону();
		
	ИначеЕсли ИмяСобытия = "ОбновитьТрудозатратыУчастниковПроцесса" И Источник = ЭтаФорма Тогда
		ЗаполнитьОписаниеТрудозатрат(ЭтаФорма);
	КонецЕсли;
	
	// Подсистема "Свойства"
	Если УправлениеСвойствамиКлиент.ОбрабатыватьОповещения(ЭтаФорма, ИмяСобытия, Параметр) Тогда
		ОбновитьЭлементыДополнительныхРеквизитов();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
		Если Мультипредметность.ИзмененыПредметыЗадачи(Объект.Ссылка) Тогда
			ОбновитьДеревоПриложенийСервер();
		КонецЕсли;
	КонецЕсли;
	
	РаботаСБизнесПроцессамиВызовСервера.ФормаЗадачиИсполнителяУстановитьВидимостьПредмета(ЭтаФорма);
	
	Если Не Объект.Выполнена Тогда
		Объект.ДатаИсполнения = ТекущаяДатаСеанса();
	КонецЕсли;
	
	РаботаСФлагамиОбъектовСервер.ОтобразитьФлагВФормеОбъекта(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	ОбщегоНазначенияДокументооборотКлиент.УдалитьПустыеСтрокиТаблицы(ИсполнителиИсполнения, "Исполнитель");
	ОбщегоНазначенияДокументооборотКлиент.УдалитьПустыеСтрокиТаблицы(ИсполнителиОзнакомления, "Исполнитель");
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	РаботаСБизнесПроцессами.ФормаЗадачиПередЗаписьюНаСервере(
		ЭтаФорма, Отказ, ТекущийОбъект, ПараметрыЗаписи);
	
	УстановитьПривилегированныйРежим(Истина);
	
	// сформируем дополнительные свойства для передачи данных объекту
	РеквизитыЗаполнения =
		"ВариантРассмотрения, 
		|ШаблонИсполнения, ШаблонОзнакомления,
		|НаименованиеИсполнения, НаименованиеОзнакомления,
		|ОписаниеИсполнения, ОписаниеОзнакомления, ВажностьИсполнения, ВажностьОзнакомления,
		|ВариантИсполнения,
		|СрокИсполненияПроцессаИсполнения, СрокИсполненияПроцессаОзнакомления,
		|СрокОбработкиРезультатовИсполнения,
		|СрокОбработкиРезультатовДниИсполнения,
		|СрокОбработкиРезультатовЧасыИсполнения,
		|СрокОбработкиРезультатовМинутыИсполнения,
		|ВариантУстановкиСрокаОбработкиРезультатовИсполнения,
		|КоличествоИтерацийИсполнения,
		|ТрудозатратыПланИсполнителяОзнакомления,
		|КонтролерИсполнения,
		|ПроверяющийИсполнения, ТрудозатратыПланПроверяющего";
		
	ДанныеЗаполнения = Новый Структура(РеквизитыЗаполнения);
	ЗаполнитьЗначенияСвойств(ДанныеЗаполнения, ЭтаФорма);
	
	ПараметрыЗаписи.Вставить("ДанныеЗаполнения", ДанныеЗаполнения);
	
	ЗаблокироватьДанныеДляРедактирования(ТекущийОбъект.БизнесПроцесс);
	РассмотрениеОбъект = ТекущийОбъект.БизнесПроцесс.ПолучитьОбъект();
	
	РассмотрениеОбъект.ДополнительныеСвойства.Вставить("ТекущаяЗадача",ТекущийОбъект.Ссылка);
	РассмотрениеОбъект.ДополнительныеСвойства.Вставить("ДанныеЗаполнения",ДанныеЗаполнения);
	РассмотрениеОбъект.ДополнительныеСвойства.Вставить("ИсполнителиИсполнения",ИсполнителиИсполнения.Выгрузить());
	РассмотрениеОбъект.ДополнительныеСвойства.Вставить("ИсполнителиОзнакомления",ИсполнителиОзнакомления.Выгрузить());
	РассмотрениеОбъект.ДополнительныеСвойства.Вставить("РезультатВыполнения",ТекущийОбъект.РезультатВыполнения);
	
	Если ПараметрыЗаписи.Свойство("ВыполнитьЗадачу") И ПараметрыЗаписи.ВыполнитьЗадачу Тогда
		РассмотрениеОбъект.ДополнительныеСвойства.Вставить("ВыполнитьЗадачу", Истина);
	КонецЕсли;

	РаботаСБизнесПроцессами.ЗаписатьПроцесс(РассмотрениеОбъект, "ЗаписьСОбработкойВыполненияЗадачи");

	РазблокироватьДанныеДляРедактирования(РассмотрениеОбъект.Ссылка);
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

&НаСервере
Процедура ПриЗаписиНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	ПараметрыФоновогоВыполнения = Новый Структура;
	
	Если ПараметрыЗаписи.Свойство("ВыполнитьЗадачуФоново")
		И ПараметрыЗаписи.ВыполнитьЗадачуФоново Тогда
		
		ПараметрыФоновогоВыполнения.Вставить("ДанныеЗаполнения", ПараметрыЗаписи.ДанныеЗаполнения);
		ПараметрыФоновогоВыполнения.Вставить("РезультатВыполнения", ТекущийОбъект.РезультатВыполнения);
		ПараметрыФоновогоВыполнения.Вставить("ИсполнителиИсполнения", ИсполнителиИсполнения.Выгрузить());
		ПараметрыФоновогоВыполнения.Вставить("ИсполнителиОзнакомления", ИсполнителиОзнакомления.Выгрузить());
		
		Если ПодписатьРезолюцииЭП Тогда
			
			ПодписиКРезолюциямПредметовОбработанная = Новый Структура;
			
			Для Каждого КлючИЗначение Из ПодписиКРезолюциямПредметов Цикл
				
				ТекЗначение = КлючИЗначение.Значение;
				
				Если ТипЗнч(ТекЗначение) = Тип("Строка") 
					И ЭтоАдресВременногоХранилища(ТекЗначение) Тогда
					ТекЗначение = ПолучитьИзВременногоХранилища(ТекЗначение);
				КонецЕсли; 
				
				ПодписиКРезолюциямПредметовОбработанная.Вставить(КлючИЗначение.Ключ, ТекЗначение);
				
			КонецЦикла;	
			
			ПараметрыФоновогоВыполнения.Вставить("ПодписиКРезолюциямПредметов", ПодписиКРезолюциямПредметовОбработанная);
			
			ПараметрыФоновогоВыполнения.Вставить("ДатаИсполнения", Объект.ДатаИсполнения);
		КонецЕсли;
		
	КонецЕсли;
	
	РаботаСБизнесПроцессами.ФормаЗадачиПриЗаписиНаСервере(
		ЭтаФорма, Отказ, ТекущийОбъект, ПараметрыЗаписи, ПараметрыФоновогоВыполнения);
	
	Если ТекущийОбъект.ДополнительныеСвойства.Свойство("ЗадачаВыполнена")
		И ТекущийОбъект.ДополнительныеСвойства.ЗадачаВыполнена
		И ПодписатьРезолюцииЭП
		И ТипЗнч(ПодписиКРезолюциямПредметов) = Тип("Структура") Тогда
		
		Для Каждого Предмет Из Объект.Предметы Цикл
			
			Предмет = Предмет.Предмет;
			Если ТипЗнч(Предмет) = Тип("СправочникСсылка.ВходящиеДокументы") 
				ИЛИ ТипЗнч(Предмет) = Тип("СправочникСсылка.ВнутренниеДокументы") Тогда
				
				Резолюции = РаботаСРезолюциями.ПолучитьРезолюциюПоДате(Предмет, Объект.ДатаИсполнения);
				Если Резолюции.Количество() = 0 Тогда
					Продолжить;
				КонецЕсли;
				
				ОбъектРезолюции = Резолюции[0].Ссылка.ПолучитьОбъект();
				
				Ключ = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					"Предмет_%1_%2",
					СтрЗаменить(Строка(Предмет.УникальныйИдентификатор()), "-", ""),
					Формат(Объект.ДатаИсполнения, "ДФ=yyyyMMddHHmmss"));
				
				Если Не ПодписиКРезолюциямПредметов.Свойство(Ключ) Тогда
					Продолжить;
				КонецЕсли;
				
				ДанныеПодписи = ПодписиКРезолюциямПредметов[Ключ];
				
				РаботаСЭП.ЗанестиИнформациюОПодписи(ОбъектРезолюции.Ссылка, ДанныеПодписи);
				ОбъектРезолюции.Подписана = Истина;
				ОбъектРезолюции.Записать();
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	РаботаСБизнесПроцессамиВызовСервера.ФормаЗадачиИсполнителяУстановитьВидимостьПредмета(ЭтаФорма);
	ПротоколированиеРаботыПользователей.ЗаписатьИзменение(Объект.Ссылка);
	Если ПараметрыЗаписи.Свойство("ЭтоНовыйОбъект") И ПараметрыЗаписи.ЭтоНовыйОбъект = Истина Тогда
		РаботаСФлагамиОбъектовСервер.СохранитьФлагОбъектаИзФормы(ЭтаФорма);
	КонецЕсли;
	
	ЗаполнитьПредставлениеСроковИсполненияОзнакомления(ЭтаФорма);
	ОбновитьПризнакиИстекшихСроков();
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	Оповестить("ЗадачаИзменена", Объект.Ссылка, ЭтаФорма);
	
КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеДанныхИзНастроекНаСервере(Настройки)

	Если Настройки["ПоказыватьИнструкции"] <> Неопределено
		И ПолучитьФункциональнуюОпцию("ИспользоватьИнструкции") Тогда
		ПолучитьИнструкции();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ГруппаСтраницыПриСменеСтраницы(Элемент, ТекущаяСтраница)
	
	Если Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.СтраницаЗадача Тогда 
		Элементы.ГруппаКомандыВыполнения.ТекущаяСтраница = Элементы.Рассмотрение;
		
		ВариантРассмотрения = ПредопределенноеЗначение("Перечисление.ВариантыРассмотрения.ВвестиТекстРезолюции");
		
	ИначеЕсли Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаНаИсполнение Тогда 
		Элементы.ГруппаКомандыВыполнения.ТекущаяСтраница = Элементы.Исполнение;
		
		Если Не ЗначениеЗаполнено(НаименованиеИсполнения) Тогда 
			НаименованиеИсполнения = МультипредметностьКлиентСервер.ПолучитьНаименованиеСПредметами(
				НСтр("ru = 'Исполнить'; en = 'Perform'"), Объект.Предметы); 
		КонецЕсли;
		
		ВариантРассмотрения = ПредопределенноеЗначение("Перечисление.ВариантыРассмотрения.НаправитьНаИсполнение");
		
	ИначеЕсли Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаОзнакомление Тогда 
		Элементы.ГруппаКомандыВыполнения.ТекущаяСтраница = Элементы.Ознакомление;
		
		Если Не ЗначениеЗаполнено(НаименованиеОзнакомления) Тогда 
			НаименованиеОзнакомления = МультипредметностьКлиентСервер.ПолучитьНаименованиеСПредметами(
				НСтр("ru = 'Ознакомиться'; en = 'Examine'"), Объект.Предметы); 
		КонецЕсли;
		
		ВариантРассмотрения = ПредопределенноеЗначение("Перечисление.ВариантыРассмотрения.НаправитьНаОзнакомление");
		
	КонецЕсли;
	
	ЗаполнитьПредставлениеСроковИсполненияОзнакомления(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ПредставлениеHTMLПриНажатии(Элемент, ДанныеСобытия, СтандартнаяОбработка)
	
	ОбзорЗадачКлиент.ПредставлениеHTMLПриНажатии(Элемент, ДанныеСобытия, СтандартнаяОбработка, ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ИнструкцияПриНажатии(Элемент, ДанныеСобытия, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	РаботаСИнструкциямиКлиент.ОткрытьСсылку(ДанныеСобытия.Href, ДанныеСобытия.Element, Элемент.Документ);
	
КонецПроцедуры

&НаКлиенте
Процедура КоличествоИтерацийПриИзменении(Элемент)
	
	РаботаСБизнесПроцессамиКлиент.КоличествоИтерацийПриИзменении(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ОписаниеТрудозатратПроцессаИсполненияНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Настройки = Новый Структура;
	Настройки.Вставить("ЕдиницаИзмеренияТрудозатрат", ЕдиницаТрудозатрат);
	Настройки.Вставить("ВариантМаршрутизацииЗадач", ВариантИсполнения);
	Настройки.Вставить("ТолькоПросмотр", ТолькоПросмотр);
	Настройки.Вставить("Участники", Новый Массив);
	
	Настройки.Вставить("ИмяРеквизитаОбъекта", "");
	Настройки.Вставить("ИмяТаблицыИсполнители", "ИсполнителиИсполнения");
	
	Для Каждого СтрИсполнитель ИЗ ИсполнителиИсполнения Цикл
		
		ТрудозатратыИсполнителя = РаботаСБизнесПроцессамиКлиент.
			СтруктураСтрокиТрудозатратУчастникаПроцесса(
				НСтр("ru = 'Исполнитель'; en = 'Performer'"),
				"ТрудозатратыПланИсполнителя",
				СтрИсполнитель.ТрудозатратыПланИсполнителя,
				СтрИсполнитель.Исполнитель,
				СтрИсполнитель.Шаг,
				СтрИсполнитель.НомерСтроки);
		Настройки.Участники.Добавить(ТрудозатратыИсполнителя);
		
	КонецЦикла;
	
	ТрудозатратыКонтролера = РаботаСБизнесПроцессамиКлиент.
		СтруктураСтрокиТрудозатратУчастникаПроцесса(
			НСтр("ru = 'Контролер'; en = 'Monitor'"),
			"ТрудозатратыПланКонтролера",
			ТрудозатратыПланКонтролера,
			КонтролерИсполнения);
	Настройки.Участники.Добавить(ТрудозатратыКонтролера);
	
	ТрудозатратыПроверяющего = РаботаСБизнесПроцессамиКлиент.
		СтруктураСтрокиТрудозатратУчастникаПроцесса(
			НСтр("ru = 'Проверяющий'; en = 'Verifier'"),
			"ТрудозатратыПланПроверяющего",
			ТрудозатратыПланПроверяющего,
			ПроверяющийИсполнения);
	Настройки.Участники.Добавить(ТрудозатратыПроверяющего);
	
	РаботаСБизнесПроцессамиКлиент.НастроитьТрудозатратУчастниковПроцесса(ЭтаФорма, Настройки);
	
КонецПроцедуры

&НаКлиенте
Процедура ОписаниеТрудозатратПроцессаОзнакомленияНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Настройки = Новый Структура;
	Настройки.Вставить("ЕдиницаИзмеренияТрудозатрат", ЕдиницаТрудозатрат);
	Настройки.Вставить("ТолькоПросмотр", ТолькоПросмотр);
	Настройки.Вставить("Участники", Новый Массив);
	
	Настройки.Вставить("ИмяРеквизитаОбъекта", "");
	
	ТрудозатратыИсполнителей = РаботаСБизнесПроцессамиКлиент.
		СтруктураСтрокиТрудозатратУчастникаПроцесса(
			НСтр("ru = 'Исполнители'; en = 'Performers'"),
			"ТрудозатратыПланИсполнителяОзнакомления",
			ТрудозатратыПланИсполнителяОзнакомления);
	Настройки.Участники.Добавить(ТрудозатратыИсполнителей);
	
	РаботаСБизнесПроцессамиКлиент.НастроитьТрудозатратУчастниковПроцесса(ЭтаФорма, Настройки);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы_ВариантИсполнения

&НаКлиенте
Процедура ВариантИсполненияПриИзменении(Элемент)
	
	РаботаСБизнесПроцессамиКлиент.ВариантИсполненияПриИзмененииПроцессаИсполнения(
		ЭтаФорма, Элементы.ИсполнителиИсполнения,
		ВариантИсполнения, ПредыдущийВариантИсполнения, ИсполнителиИсполнения);
	
КонецПроцедуры

&НаКлиенте
Процедура ВариантИсполненияОчистка(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы_Проверяющий

&НаКлиенте
Процедура ПроверяющийПриИзменении(Элемент)
	
	ОбновитьСрокиИсполненияОтложенно("ПроверяющийИсполнения");

КонецПроцедуры

&НаКлиенте
Процедура ПроверяющийНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	РаботаСБизнесПроцессамиКлиент.УчастникНачалоВыбора(
		Элемент, ПроверяющийИсполнения, СтандартнаяОбработка, ЭтаФорма, "ПроверяющийИсполнения");
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверяющийАвтоПодбор(Элемент, Текст, ДанныеВыбора, Ожидание, СтандартнаяОбработка)
	
	РаботаСБизнесПроцессамиКлиент.УчастникАвтоПодбор(Текст, ДанныеВыбора, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверяющийОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Текст) Тогда
		СтандартнаяОбработка = Ложь;
		ДополнениеТипа = Новый ОписаниеТипов("СправочникСсылка.ПолныеРоли");
		ДанныеВыбора = РаботаСПользователями.СформироватьДанныеВыбора(Текст, ДополнениеТипа);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы_СрокОбработкиРезультатовПредставление

&НаКлиенте
Процедура СрокОбработкиРезультатовПредставлениеПриИзменении(Элемент)
	
	ДопПараметры = СрокиИсполненияПроцессовКлиент.ДопПараметрыДляИзмененияСрокаПоПредставлению();
	ДопПараметры.Форма = ЭтаФорма;
	ДопПараметры.Поле = "СрокОбработкиРезультатовИсполненияПредставление";
	ДопПараметры.НаименованиеИзмененногоРеквизита = "СрокОбработкиРезультатов";
	ДопПараметры.Исполнитель = ПроверяющийИсполнения;
	
	СрокиИсполненияПроцессовКлиент.ИзменитьСрокИсполненияУчастникаПроцессаПоПредставлению(
		СрокОбработкиРезультатовИсполнения,
		СрокОбработкиРезультатовДниИсполнения,
		СрокОбработкиРезультатовЧасыИсполнения,
		СрокОбработкиРезультатовМинутыИсполнения,
		ВариантУстановкиСрокаОбработкиРезультатовИсполнения,
		СрокОбработкиРезультатовИсполненияПредставление,
		ДопПараметры);
	
КонецПроцедуры

&НаКлиенте
Процедура СрокОбработкиРезультатовПредставлениеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ПараметрыВыбораСрока = СрокиИсполненияПроцессовКлиент.ПараметрыВыбораСрокаУчастникаПроцесса();
	ПараметрыВыбораСрока.Форма = ЭтаФорма;
	ПараметрыВыбораСрока.ИмяРеквизитаСрокИсполнения = "СрокОбработкиРезультатовИсполнения";
	ПараметрыВыбораСрока.ИмяРеквизитаСрокИсполненияДни = "СрокОбработкиРезультатовДниИсполнения";
	ПараметрыВыбораСрока.ИмяРеквизитаСрокИсполненияЧасы = "СрокОбработкиРезультатовЧасыИсполнения";
	ПараметрыВыбораСрока.ИмяРеквизитаСрокИсполненияМинуты = "СрокОбработкиРезультатовМинутыИсполнения";
	ПараметрыВыбораСрока.ИмяРеквизитаВариантУстановкиСрока = "ВариантУстановкиСрокаОбработкиРезультатовИсполнения";
	ПараметрыВыбораСрока.ИмяРеквизитаПредставлениеСрока = "СрокОбработкиРезультатовИсполненияПредставление";
	ПараметрыВыбораСрока.СрокиПредшественников = ИсполнителиИсполнения;
	ПараметрыВыбораСрока.НаименованиеСрокаУчастника = "СрокОбработкиРезультатов";
	ПараметрыВыбораСрока.Участник = ПроверяющийИсполнения;
	
	СрокиИсполненияПроцессовКлиент.ВыбратьСрокУчастникаПроцесса(ПараметрыВыбораСрока);
	
КонецПроцедуры

&НаКлиенте
Процедура СрокОбработкиРезультатовПредставлениеРегулирование(Элемент, Направление, СтандартнаяОбработка)
	
	СрокиИсполненияПроцессовКлиент.ИзменитьОтносительныйСрокУчастникаПроцесса(
		ЭтаФорма,
		СрокОбработкиРезультатовИсполнения,
		СрокОбработкиРезультатовДниИсполнения,
		СрокОбработкиРезультатовЧасыИсполнения,
		СрокОбработкиРезультатовМинутыИсполнения,
		СрокОбработкиРезультатовИсполненияПредставление,
		ВариантУстановкиСрокаОбработкиРезультатовИсполнения,
		Направление,
		"СрокОбработкиРезультатов");
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы_Контролер

&НаКлиенте
Процедура КонтролерПриИзменении(Элемент)
	
	ОбновитьСрокиИсполненияОтложенно("КонтролерИсполнения");

КонецПроцедуры

&НаКлиенте
Процедура КонтролерНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	РаботаСБизнесПроцессамиКлиент.УчастникНачалоВыбора(
		Элемент, КонтролерИсполнения, СтандартнаяОбработка, ЭтаФорма, "КонтролерИсполнения");
	
КонецПроцедуры

&НаКлиенте
Процедура КонтролерАвтоПодбор(Элемент, Текст, ДанныеВыбора, Ожидание, СтандартнаяОбработка)
	
	РаботаСБизнесПроцессамиКлиент.УчастникАвтоПодбор(Текст, ДанныеВыбора, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура КонтролерОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Текст) Тогда
		СтандартнаяОбработка = Ложь;
		ДополнениеТипа = Новый ОписаниеТипов("СправочникСсылка.ПолныеРоли");
		ДанныеВыбора = РаботаСПользователями.СформироватьДанныеВыбора(Текст, ДополнениеТипа);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормы_ДеревоПриложений
 
&НаКлиенте
Процедура ДеревоПриложенийВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ТекущиеДанные = ДеревоПриложений.НайтиПоИдентификатору(ВыбраннаяСтрока);
	Если ЗначениеЗаполнено(ТекущиеДанные.ИмяПредмета) И НЕ ЗначениеЗаполнено(ТекущиеДанные.Ссылка) Тогда
		ОчиститьСообщения();
		СообщениеОбОшибке = "";
		
		ПараметрыОбработчикаОповещения = Новый Структура();
		ПараметрыОбработчикаОповещения.Вставить("СообщениеОбОшибке", СообщениеОбОшибке);
		
		ОписаниеОповещения = Новый ОписаниеОповещения(
			"ДеревоПриложенийВыборПродолжение",
			ЭтотОбъект,
			ПараметрыОбработчикаОповещения);         
			
		Если Не МультипредметностьКлиент.ДобавитьПредметЗадачи(ЭтаФорма, СообщениеОбОшибке, 
			ТекущиеДанные.ИмяПредмета, ТекущиеДанные.Ссылка, СтандартнаяОбработка, ОписаниеОповещения) Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				СообщениеОбОшибке,, "ДеревоПриложений");
			Возврат;
		КонецЕсли;
	Иначе
		РаботаСБизнесПроцессамиКлиент.ДеревоПриложенийВыбор(
			ЭтаФорма, Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоПриложенийВыборПродолжение(Результат, Параметры) Экспорт
	
	Если Результат = Истина Тогда
		ОбновитьДеревоПриложений();
		УстановитьПредметСервер();	
	Иначе
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			Параметры.СообщениеОбОшибке,, "ДеревоПриложений");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоПриложенийПриАктивизацииСтроки(Элемент)
	
	РаботаСБизнесПроцессамиКлиент.УстановитьДоступностьКомандРаботыСФайлами(ЭтаФорма, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоПриложенийПередНачаломИзменения(Элемент, Отказ)
	
	Отказ = Истина;
	
	ОткрытьКарточкуНаКлиенте();
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоПриложенийПроверкаПеретаскивания(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Поле)
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоПриложенийПеретаскивание(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Поле)
	
	СтандартнаяОбработка = Ложь;
	
	Если ЭтаФорма.ТолькоПросмотр Или Элементы.ДеревоПриложений.ТолькоПросмотр Или Объект.Выполнена Тогда 
		Возврат;
	КонецЕсли;
	
	ВладелецФайлаСписка = Объект.БизнесПроцесс;
	НеОткрыватьКарточкуПослеСозданияИзФайла = Истина;
	РаботаСФайламиКлиент.ОбработкаПеретаскиванияВЛинейныйСписок(ПараметрыПеретаскивания, ВладелецФайлаСписка, ЭтаФорма, НеОткрыватьКарточкуПослеСозданияИзФайла);
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоПриложенийПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	
	Отказ = Истина;
	
	СписокВыбора = Новый СписокЗначений;
	СписокВыбора.Добавить("Предмет", НСтр("ru = 'Предмет'; en = 'Subject'"));
	СписокВыбора.Добавить("Файл", НСтр("ru = 'Файл'; en = 'File'"));
	
	ОписаниеОповещения = 
		Новый ОписаниеОповещения("ДеревоПриложенийПередНачаломДобавления_Завершение", ЭтаФорма);
	
	СписокВыбора.ПоказатьВыборЭлемента(ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоПриложенийПередНачаломДобавления_Завершение(РезультатВыбора, ДопПараметры) Экспорт
	
	Если РезультатВыбора = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Предмет = Неопределено;
	
	Если РезультатВыбора.Значение = "Файл" Тогда
		Предмет = ПредопределенноеЗначение("Справочник.Файлы.ПустаяСсылка");
	КонецЕсли;
	
	ДеревоПриложенийДобавлениеНаКлиенте(Предмет);
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоПриложенийПередУдалением(Элемент, Отказ)
	
	Отказ = Истина;
	ДеревоПриложенийУдалениеНаКлиенте();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормы_ИсполнителиИсполнения

&НаКлиенте
Процедура ИсполнителиИсполненияПриАктивизацииСтроки(Элемент)
	
	РаботаСБизнесПроцессамиКлиент.ИсполнителиИсполненияПриАктивизацииСтроки(
		ЭтаФорма, Элементы.ИсполнителиИсполнения,
		Элементы.ИсполнителиСрокИсполненияПредставление, ИсполнителиИсполнения,
		ДоступностьПоШаблонуИсполнения);
	
КонецПроцедуры

&НаКлиенте
Процедура ИсполнителиИсполненияПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	РаботаСБизнесПроцессамиКлиент.ИсполнителиПриНачалеРедактирования(
		ЭтаФорма, НоваяСтрока,
		Элементы.ИсполнителиИсполнения,
		ИсполнителиИсполнения,
		ВариантИсполнения,
		"ПорядокИсполнения");
	
КонецПроцедуры

&НаКлиенте
Процедура ИсполнителиИсполненияПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	РаботаСБизнесПроцессамиКлиент.ИсполнителиПриОкончанииРедактирования(
		ЭтаФорма, НоваяСтрока, ОтменаРедактирования, Элементы.ИсполнителиИсполнения, ИсполнителиИсполнения);
	
КонецПроцедуры

&НаКлиенте
Процедура ИсполнителиИсполненияПослеУдаления(Элемент)
	
	РаботаСБизнесПроцессамиКлиент.ИсполнителиИсполненияПослеУдаления(
		ЭтаФорма, Элементы.ИсполнителиИсполнения, ИсполнителиИсполнения, ВариантИсполнения);
	
КонецПроцедуры

&НаКлиенте
Процедура ИсполнителиИсполненияОкончаниеПеретаскивания(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

// Поле Исполнитель

&НаКлиенте
Процедура ИсполнительИсполненияПриИзменении(Элемент)
	
	РаботаСБизнесПроцессамиКлиент.ИсполнительПриИзменении(
		ЭтаФорма, Элементы.ИсполнителиИсполнения, ИсполнителиИсполнения);
	
КонецПроцедуры

&НаКлиенте
Процедура ИсполнительИсполненияНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	РаботаСБизнесПроцессамиКлиент.ИсполнительИсполненияНачалоВыбора(
		ЭтаФорма, СтандартнаяОбработка, Элементы.ИсполнителиИсполнения,
		ИсполнителиИсполнения, ВариантИсполнения);
	
КонецПроцедуры

&НаКлиенте
Процедура ИсполнительИсполненияОчистка(Элемент, СтандартнаяОбработка)
	
	РаботаСБизнесПроцессамиКлиент.ИсполнительОчистка(
		СтандартнаяОбработка, Элементы.ИсполнителиИсполнения);
	
КонецПроцедуры

&НаКлиенте
Процедура ИсполнительИсполненияОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	РаботаСБизнесПроцессамиКлиент.ИсполнительОбработкаВыбора(
		ЭтаФорма, ВыбранноеЗначение, Элементы.ИсполнителиИсполнения);
	
КонецПроцедуры

&НаКлиенте
Процедура ИсполнительИсполненияАвтоПодбор(Элемент, Текст, ДанныеВыбора, Ожидание, СтандартнаяОбработка)
	
	РаботаСБизнесПроцессамиКлиент.ИсполнительАвтоПодбор(
		ЭтаФорма, Текст, ДанныеВыбора, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ИсполнительИсполненияОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, СтандартнаяОбработка)
	
	РаботаСБизнесПроцессамиКлиент.ИсполнительОкончаниеВводаТекста(
		ЭтаФорма, Текст, ДанныеВыбора, СтандартнаяОбработка);
	
КонецПроцедуры

// Поле ПорядокИсполнения

&НаКлиенте
Процедура ПорядокИсполненияПриИзменении(Элемент)
	
	РаботаСБизнесПроцессамиКлиент.ПорядокИсполненияПриИзмененииТаблицыИсполнители(
		ЭтаФорма, Элементы.ИсполнителиИсполнения, ИсполнителиИсполнения);
	
КонецПроцедуры

// Поле ИсполнителиСрокИсполненияПредставление

&НаКлиенте
Процедура ИсполнителиСрокИсполненияПредставлениеПриИзменении(Элемент)
	
	СрокиИсполненияПроцессовКлиент.ИзменитьСрокИсполненияПоПредставлениюВТаблицеИсполнители(
		ЭтаФорма, Элементы.ИсполнителиИсполнения, ИсполнителиИсполнения, ВариантИсполнения);
	
КонецПроцедуры

&НаКлиенте
Процедура ИсполнителиСрокИсполненияПредставлениеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СрокиИсполненияПроцессовКлиент.ВыбратьСрокИсполненияДляСтрокиТаблицыИсполнители(
		ЭтаФорма, Элементы.ИсполнителиИсполнения, ИсполнителиИсполнения, ВариантИсполнения);
	
КонецПроцедуры

&НаКлиенте
Процедура ИсполнителиСрокИсполненияПредставлениеРегулирование(Элемент, Направление, СтандартнаяОбработка)
	
	СрокиИсполненияПроцессовКлиент.ИзменитьСрокИсполненияВТаблицеИсполнители(
		ЭтаФорма, Элементы.ИсполнителиИсполнения, ИсполнителиИсполнения, Направление, ВариантИсполнения);
	
КонецПроцедуры

// Шаблоны текста для наименования и описания
&НаКлиенте
Процедура НаименованиеИсполнениеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)

	РаботаСБизнесПроцессамиКлиент.ВыбратьШаблонТекстаРеализация(ЭтаФорма, "НаименованиеИсполнение",
		ПредопределенноеЗначение("Перечисление.ОбластиПримененияШаблоновТекстов.ПроцессИсполнениеНаименование"));
	
КонецПроцедуры

&НаКлиенте
Процедура ОписаниеИсполнениеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	РаботаСБизнесПроцессамиКлиент.ВыбратьШаблонТекстаРеализация(ЭтаФорма, "ОписаниеИсполнение",
		ПредопределенноеЗначение("Перечисление.ОбластиПримененияШаблоновТекстов.ПроцессИсполнениеОписание"));

КонецПроцедуры

&НаКлиенте
Процедура НаименованиеИсполнениеАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Текст) Тогда 
		
		ДанныеВыбора = РаботаСШаблонамиТекстовСервер.СформироватьДанныеВыбораШаблона(
			ПараметрыПолученияДанных, 
			ПредопределенноеЗначение("Перечисление.ОбластиПримененияШаблоновТекстов.ПроцессИсполнениеНаименование"));
			
		Если ДанныеВыбора.Количество() <> 0 Тогда
			СтандартнаяОбработка = Ложь;
		КонецЕсли;
			
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НаименованиеИсполнениеОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если ТипЗнч(ВыбранноеЗначение) = Тип("Структура")  Тогда 
		НаименованиеИсполнения = ВыбранноеЗначение.Шаблон;
		Модифицированность = Истина;	
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ИсполнителиОписаниеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	Область = ПредопределенноеЗначение("Перечисление.ОбластиПримененияШаблоновТекстов.ПроцессИсполнениеОписание");
	ЗаголовокФормыВыбора = НСтр("ru = 'Выбор шаблона описания'; en = 'Select details template'");
	ИмяОбработчика = "ИсполнителиОписаниеНачалоВыбораПродолжение";
	
	ПараметрыФормы = Новый Структура("ОбластьПрименения, Заголовок", 
		Область,
		ЗаголовокФормыВыбора);
		
	ДопПараметры = Новый Структура;
	ДопПараметры.Вставить("Форма", ЭтаФорма);
		
	ОписаниеОповещения = Новый ОписаниеОповещения(
		ИмяОбработчика,
		ЭтотОбъект,
		ДопПараметры);
		
	ОткрытьФорму("Справочник.ШаблоныТекстов.ФормаВыбора", 
		ПараметрыФормы, ЭтаФорма,,,, ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ИсполнителиОписаниеНачалоВыбораПродолжение(Результат, Параметры) Экспорт 
	
	Если ТипЗнч(Результат) = Тип("СправочникСсылка.ШаблоныТекстов")  Тогда 
		Элементы.ИсполнителиИсполнения.ТекущиеДанные.Описание = ОбщегоНазначенияДокументооборотВызовСервера.ЗначениеРеквизитаОбъекта(
			Результат, "Шаблон");
	КонецЕсли;	
		
КонецПроцедуры	

// Шаблоны текста для наименования и описания

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормы_ИсполнителиОзнакомления

&НаКлиенте
Процедура ИсполнителиОзнакомленияПриАктивизацииСтроки(Элемент)
	
	РаботаСБизнесПроцессамиКлиент.ИсполнителиПриАктивизацииСтроки(
		ЭтаФорма,
		Элементы.ИсполнителиОзнакомления,
		Элементы.ИсполнителиОзнакомленияСрокИсполненияПредставление,
		ДоступностьПоШаблонуОзнакомления);
	
КонецПроцедуры

&НаКлиенте
Процедура ИсполнителиОзнакомленияПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	РаботаСБизнесПроцессамиКлиент.ИсполнителиПриНачалеРедактирования(
		ЭтаФорма, НоваяСтрока,
		Элементы.ИсполнителиОзнакомления,
		ИсполнителиОзнакомления);
	
КонецПроцедуры

&НаКлиенте
Процедура ИсполнителиОзнакомленияПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	РаботаСБизнесПроцессамиКлиент.ИсполнителиПриОкончанииРедактирования(
		ЭтаФорма, НоваяСтрока, ОтменаРедактирования, Элементы.ИсполнителиОзнакомления, ИсполнителиОзнакомления);
	
КонецПроцедуры

&НаКлиенте
Процедура ИсполнителиОзнакомленияПослеУдаления(Элемент)
	
	РаботаСБизнесПроцессамиКлиент.ИсполнителиПослеУдаления(ЭтаФорма, ИсполнителиОзнакомления);
	
КонецПроцедуры

&НаКлиенте
Процедура ИсполнителиОзнакомленияОкончаниеПеретаскивания(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

// ИсполнительОзнакомления

&НаКлиенте
Процедура ИсполнительОзнакомленияПриИзменении(Элемент)
	
	РаботаСБизнесПроцессамиКлиент.ИсполнительПриИзменении(
		ЭтаФорма, Элементы.ИсполнителиОзнакомления, ИсполнителиОзнакомления);
	
КонецПроцедуры

&НаКлиенте
Процедура ИсполнительОзнакомленияНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	РаботаСБизнесПроцессамиКлиент.ИсполнительНачалоВыбора(
		ЭтаФорма, СтандартнаяОбработка,
		Элементы.ИсполнителиОзнакомления, ИсполнителиОзнакомления);
	
КонецПроцедуры

&НаКлиенте
Процедура ИсполнительОзнакомленияОчистка(Элемент, СтандартнаяОбработка)
	
	РаботаСБизнесПроцессамиКлиент.ИсполнительОчистка(
		СтандартнаяОбработка, Элементы.ИсполнителиОзнакомления);
	
КонецПроцедуры

&НаКлиенте
Процедура ИсполнительОзнакомленияОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	РаботаСБизнесПроцессамиКлиент.ИсполнительОбработкаВыбора(
		ЭтаФорма, ВыбранноеЗначение, Элементы.ИсполнителиОзнакомления);
	
КонецПроцедуры

&НаКлиенте
Процедура ИсполнительОзнакомленияАвтоПодбор(Элемент, Текст, ДанныеВыбора, Ожидание, СтандартнаяОбработка)
	
	РаботаСБизнесПроцессамиКлиент.ИсполнительАвтоПодбор(
		ЭтаФорма, Текст, ДанныеВыбора, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ИсполнительОзнакомленияОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, СтандартнаяОбработка)
	
	РаботаСБизнесПроцессамиКлиент.ИсполнительОкончаниеВводаТекста(
		ЭтаФорма, Текст, ДанныеВыбора, СтандартнаяОбработка);
	
КонецПроцедуры

// Поле ИсполнителиОзнакомленияСрокИсполненияПредставление

&НаКлиенте
Процедура ИсполнителиОзнакомленияСрокИсполненияПредставлениеПриИзменении(Элемент)
	
	СрокиИсполненияПроцессовКлиент.ИзменитьСрокИсполненияПоПредставлениюВТаблицеИсполнители(
		ЭтаФорма, Элементы.ИсполнителиОзнакомления, ИсполнителиОзнакомления);
	
КонецПроцедуры

&НаКлиенте
Процедура ИсполнителиОзнакомленияСрокИсполненияПредставлениеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СрокиИсполненияПроцессовКлиент.ВыбратьСрокИсполненияДляСтрокиТаблицыИсполнители(
		ЭтаФорма, Элементы.ИсполнителиОзнакомления, ИсполнителиОзнакомления);
	
КонецПроцедуры

&НаКлиенте
Процедура ИсполнителиОзнакомленияСрокИсполненияПредставлениеРегулирование(Элемент, Направление, СтандартнаяОбработка)
	
	СрокиИсполненияПроцессовКлиент.ИзменитьСрокИсполненияВТаблицеИсполнители(
		ЭтаФорма, Элементы.ИсполнителиОзнакомления, ИсполнителиОзнакомления, Направление);
		
	
КонецПроцедуры

// Шаблоны текста для наименования и описания
&НаКлиенте
Процедура НаименованиеОзнакомленияНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)

	РаботаСБизнесПроцессамиКлиент.ВыбратьШаблонТекстаРеализация(ЭтаФорма, "НаименованиеОзнакомления",
		ПредопределенноеЗначение("Перечисление.ОбластиПримененияШаблоновТекстов.ПроцессОзнакомлениеНаименование"));
	
КонецПроцедуры

&НаКлиенте
Процедура ОписаниеОзнакомленияНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	РаботаСБизнесПроцессамиКлиент.ВыбратьШаблонТекстаРеализация(ЭтаФорма, "ОписаниеОзнакомления",
		ПредопределенноеЗначение("Перечисление.ОбластиПримененияШаблоновТекстов.ПроцессОзнакомлениеОписание"));

КонецПроцедуры

&НаКлиенте
Процедура НаименованиеОзнакомленияАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Текст) Тогда 
		
		ДанныеВыбора = РаботаСШаблонамиТекстовСервер.СформироватьДанныеВыбораШаблона(
			ПараметрыПолученияДанных, 
			ПредопределенноеЗначение("Перечисление.ОбластиПримененияШаблоновТекстов.ПроцессОзнакомлениеНаименование"));
			
		Если ДанныеВыбора.Количество() <> 0 Тогда
			СтандартнаяОбработка = Ложь;
		КонецЕсли;
			
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НаименованиеОзнакомленияОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если ТипЗнч(ВыбранноеЗначение) = Тип("Структура")  Тогда 
		НаименованиеОзнакомления = ВыбранноеЗначение.Шаблон;
		Модифицированность = Истина;	
	КонецЕсли;	
	
КонецПроцедуры


#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗаписатьИЗакрытьВыполнить()
	
	ОчиститьСообщения();
	Если Записать() Тогда
		ОповеститьОбИзменении(Объект.Ссылка);
		ПоказатьОповещениеПользователя(
			"Изменение:", 
			ПолучитьНавигационнуюСсылку(Объект.Ссылка),
			Строка(Объект.Ссылка),
			БиблиотекаКартинок.Информация32);
		Закрыть();	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьВыполнить(Команда)
	
	Если Записать() Тогда
		ОповеститьОбИзменении(Объект.Ссылка);
		ПоказатьОповещениеПользователя(
			"Изменение:", 
			ПолучитьНавигационнуюСсылку(Объект.Ссылка),
			Строка(Объект.Ссылка),
			БиблиотекаКартинок.Информация32);
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура Перенаправить(Команда)
	
	КомандыРаботыСБизнесПроцессамиКлиент.Перенаправить(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗадатьВопрос(Команда)
	
	КомандыРаботыСБизнесПроцессамиКлиент.ЗадатьВопрос(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ПринятьКИсполнению(Команда)
	
	КомандыРаботыСБизнесПроцессамиКлиент.ПринятьЗадачуКИсполнению(ЭтаФорма, ТекущийПользователь);	
	
КонецПроцедуры

&НаКлиенте
Процедура ОтменитьПринятиеКИсполнению(Команда)
	
	КомандыРаботыСБизнесПроцессамиКлиент.ОтменитьПринятиеЗадачиКИсполнению(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьБизнесПроцесс(Команда)
	
	ПоказатьЗначение(, Объект.БизнесПроцесс);
	
КонецПроцедуры

&НаКлиенте
Процедура ПереключитьХронометраж(Команда)
	
	КомандыРаботыСБизнесПроцессамиКлиент.ПереключитьХронометраж(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура УказатьТрудозатраты(Команда)
	
	ДатаОтчета = ТекущаяДата();
	Если Объект.Выполнена Тогда
		ДатаОтчета = Объект.ДатаИсполнения;
	КонецЕсли;	
	
	УчетВремениКлиент.ДобавитьВОтчетКлиент(
		ДатаОтчета,
		ВключенХронометраж, 
		ДатаНачалаХронометража, 
		ДатаКонцаХронометража, 
		ВидыРабот, 
		Объект.Ссылка,
		СпособУказанияВремени,
		ЭтаФорма.Элементы.ПереключитьХронометраж,
		Объект.Выполнена,
		ЭтаФорма);
		
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьДатуВыполнения(Команда)
	
	РаботаСБизнесПроцессамиКлиент.ИзменитьДатуВыполнения(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура Подписаться(Команда)
	
	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ПараметрыФормы = Новый Структура("ОбъектПодписки", Объект.Ссылка);
		ОткрытьФорму("ОбщаяФорма.ПодпискаНаУведомленияПоОбъекту", ПараметрыФормы);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказыватьИнструкции(Команда)
	
	ПоказыватьИнструкции = Не ПоказыватьИнструкции;
	ПолучитьИнструкции();
	
КонецПроцедуры

&НаКлиенте
Процедура РасширеннаяФорма(Команда)
	
	РасширеннаяФорма = Не РасширеннаяФорма;
	Элементы.РасширеннаяФорма.Пометка = РасширеннаяФорма;
	
	Если Не РасширеннаяФорма 
		И ВариантРассмотрения <> ВариантРассмотренияНаправитьНаИсполнение 
		И ВариантРассмотрения <> ВариантРассмотренияНаправитьНаОзнакомление Тогда 
		Элементы.ГруппаНаИсполнение.Видимость = Ложь;
		Элементы.ГруппаОзнакомление.Видимость = Ложь;
		Элементы.ГруппаСтраницы.ОтображениеСтраниц = ОтображениеСтраницФормы.Нет;
	Иначе 
		Элементы.ГруппаНаИсполнение.Видимость = Истина;
		Элементы.ГруппаОзнакомление.Видимость = Истина;
		Элементы.ГруппаСтраницы.ОтображениеСтраниц = ОтображениеСтраницФормы.ЗакладкиСверху;
	КонецЕсли;
	
	Если ВариантРассмотрения = ВариантРассмотренияВвестиТекстРезолюции Тогда 
		Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.СтраницаЗадача;
		Элементы.ГруппаКомандыВыполнения.ТекущаяСтраница = Элементы.Рассмотрение;
	ИначеЕсли ВариантРассмотрения = ВариантРассмотренияНаправитьНаИсполнение Тогда 
		Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаНаИсполнение;
		Элементы.ГруппаКомандыВыполнения.ТекущаяСтраница = Элементы.Исполнение;
	ИначеЕсли ВариантРассмотрения = ВариантРассмотренияНаправитьНаОзнакомление Тогда 
		Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаОзнакомление;
		Элементы.ГруппаКомандыВыполнения.ТекущаяСтраница = Элементы.Ознакомление;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПоШаблону(Команда)
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ЗавершениеВставкиШаблонаТекста", ЭтотОбъект);
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ОбластьПрименения", 
		ПредопределенноеЗначение("Перечисление.ОбластиПримененияШаблоновТекстов.Резолюции"));
		
	ОткрытьФорму("Справочник.ШаблоныТекстов.Форма.ФормаВыбораШаблонаТекста",
		ПараметрыФормы,
		ЭтаФорма,,,,
		ОписаниеОповещения,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗавершениеВставкиШаблонаТекста(ШаблонТекста, Параметры) Экспорт
	
	Если ШаблонТекста = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ЭтаФорма.ТекущийЭлемент <> Элементы.Резолюция Тогда
		Элементы.Резолюция.ВыделенныйТекст = Элементы.Резолюция.ВыделенныйТекст + ШаблонТекста;
		ЭтаФорма.ТекущийЭлемент = Элементы.Резолюция;
	Иначе
		Элементы.Резолюция.ВыделенныйТекст = ШаблонТекста;
	КонецЕсли;
	
	Модифицированность = Истина;
	
КонецПроцедуры

// Процесс Исполнения

&НаКлиенте
Процедура ЗаполнитьПоШаблонуИсполнение(Команда)
	
	Предметы = МультипредметностьКлиентСервер.ПолучитьМассивПредметовОбъекта(Объект,, Истина);
	ШаблоныИсполнения.ЗагрузитьЗначения(МультипредметностьВызовСервера.ПолучитьШаблоныПоПредметам(Предметы, "ШаблоныИсполнения"));
	
	ОписаниеОповещения = Новый ОписаниеОповещения(
		"ЗаполнитьПоШаблонуИсполнениеПродолжение",
		ЭтотОбъект);
	ШаблоныБизнесПроцессовКлиент.ВыбратьШаблонБизнесПроцесса("ШаблоныИсполнения", ШаблоныИсполнения, ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПоШаблонуИсполнениеПродолжение(РезультатВыбора, Параметры) Экспорт
	
	Если ЗначениеЗаполнено(РезультатВыбора) Тогда 
		ЗаполнитьИсполнениеПоШаблонуНаСервере(РезультатВыбора);
		Модифицированность = Истина;
	КонецЕсли;
	РаботаСБизнесПроцессамиКлиентСервер.ЗаполнитьШаг(ИсполнителиИсполнения);
	
КонецПроцедуры

// Процесс Исполнения

&НаКлиенте
Процедура ЗаполнитьПоШаблонуОзнакомление(Команда)
	
	Предметы = МультипредметностьКлиентСервер.ПолучитьМассивПредметовОбъекта(Объект,, Истина);
	ШаблоныОзнакомления.ЗагрузитьЗначения(МультипредметностьВызовСервера.ПолучитьШаблоныПоПредметам(Предметы, "ШаблоныОзнакомления"));
	
	ОписаниеОповещения = Новый ОписаниеОповещения(
		"ЗаполнитьПоШаблонуОзнакомлениеПродолжение",
		ЭтотОбъект);
		
	ШаблоныБизнесПроцессовКлиент.ВыбратьШаблонБизнесПроцесса("ШаблоныОзнакомления", ШаблоныОзнакомления, ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПоШаблонуОзнакомлениеПродолжение(РезультатВыбора, Параметры) Экспорт
	
	Если ЗначениеЗаполнено(РезультатВыбора) Тогда 
		ЗаполнитьОзнакомлениеПоШаблонуНаСервере(РезультатВыбора);
		Модифицированность = Истина;
	КонецЕсли;
	
КонецПроцедуры

// Команды выполнения задачи

&НаКлиенте
Процедура Выполнено(Команда)
	
	ВариантРассмотрения = ВариантРассмотренияВвестиТекстРезолюции;
	ЗаполнениеПоШаблонуРезолюции = Ложь;
	ОбработатьВыполнение();
	
КонецПроцедуры

&НаКлиенте
Процедура РассмотретьПоШаблону(Команда)
	
	СписокКоманд = Новый СписокЗначений;
	
	Для Каждого Стр Из ШаблоныКоманд Цикл
		СписокКоманд.Добавить(Стр.Шаблон, Стр.Заголовок);
	КонецЦикла;
	
	ОписаниеОповещения = Новый ОписаниеОповещения(
		"ЗавершитьРассмотрениеПоШаблону", ЭтаФорма);
	
	ПоказатьВыборИзМеню(ОписаниеОповещения, СписокКоманд, Элементы.РассмотретьПоШаблону);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗавершитьРассмотрениеПоШаблону(ШаблонРезолюции, ДопПараметры) Экспорт
	
	Если ШаблонРезолюции = Неопределено Тогда
		Возврат
	КонецЕсли;
	
	ЗаполнениеПоШаблонуРезолюции = Истина;
	РассмотретьПоШаблонуПродолжение(ШаблонРезолюции.Значение);
	
КонецПроцедуры

&НаКлиенте
Процедура РассмотретьПоШаблону0(Команда)
	
	Если ЗначениеЗаполнено(Шаблон0) Тогда 
		ЗаполнениеПоШаблонуРезолюции = Истина;
		РассмотретьПоШаблонуПродолжение(Шаблон0);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РассмотретьПоШаблону1(Команда)
	
	Если ЗначениеЗаполнено(Шаблон1) Тогда 
		ЗаполнениеПоШаблонуРезолюции = Истина;
		РассмотретьПоШаблонуПродолжение(Шаблон1);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Ознакомить(Команда)
	
	ВариантРассмотрения = ВариантРассмотренияНаправитьНаОзнакомление;
	ЗаполнениеПоШаблонуРезолюции = Ложь;
	ОбработатьВыполнение();
	
КонецПроцедуры

&НаКлиенте
Процедура Исполнить(Команда)
	
	ВариантРассмотрения = ВариантРассмотренияНаправитьНаИсполнение;
	ЗаполнениеПоШаблонуРезолюции = Ложь;
	ОбработатьВыполнение();
	
КонецПроцедуры

&НаКлиенте
Процедура ПраваДоступа(Команда)
	
	ДокументооборотПраваДоступаКлиент.ОткрытьФормуПравДоступа(ЭтаФорма);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы_ИсполнителиИсполнения

&НаКлиенте
Процедура ПодобратьИсполнителейИсполнения(Команда)
	
	РаботаСБизнесПроцессамиКлиент.ПодобратьИсполнителейИсполнения(
		ЭтаФорма, Элементы.ИсполнителиИсполнения, ИсполнителиИсполнения, ВариантИсполнения);
	
КонецПроцедуры

&НаКлиенте
Процедура ПереместитьВверх(Команда)
	
	РаботаСБизнесПроцессамиКлиент.ПереместитьИсполнителяПроцессаИсполнения(
		ЭтаФорма, Элементы.ИсполнителиИсполнения, ИсполнителиИсполнения, ВариантИсполнения, -1);
	
КонецПроцедуры

&НаКлиенте
Процедура ПереместитьВниз(Команда)
	
	РаботаСБизнесПроцессамиКлиент.ПереместитьИсполнителяПроцессаИсполнения(
		ЭтаФорма, Элементы.ИсполнителиИсполнения, ИсполнителиИсполнения, ВариантИсполнения, 1);
	
КонецПроцедуры

&НаКлиенте
Процедура НазначитьОтветственным(Команда)
	
	РаботаСБизнесПроцессамиКлиент.НазначитьОтветственным(
		ЭтаФорма,
		Элементы.ИсполнителиИсполнения,
		ИсполнителиИсполнения,
		ВариантИсполнения);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы_ИсполнителиОзнакомления

&НаКлиенте
Процедура ПодобратьИсполнителейОзнакомления(Команда)
	
	РаботаСБизнесПроцессамиКлиент.ПодобратьИсполнителей(
		ЭтаФорма, Элементы.ИсполнителиОзнакомления, ИсполнителиОзнакомления);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы_Подзадачи

&НаКлиенте
Процедура ПроцессСогласование(Команда)
	
	РаботаСБизнесПроцессамиКлиент.ОткрытьПомощникСозданияОсновныхПроцессов(
		"Согласование", Объект.Ссылка, ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ПроцессУтверждение(Команда)
	
	РаботаСБизнесПроцессамиКлиент.ОткрытьПомощникСозданияОсновныхПроцессов(
		"Утверждение", Объект.Ссылка, ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ПроцессРегистрация(Команда)
	
	РаботаСБизнесПроцессамиКлиент.ОткрытьПомощникСозданияОсновныхПроцессов(
		"Регистрация", Объект.Ссылка, ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ПроцессРассмотрение(Команда)
	
	РаботаСБизнесПроцессамиКлиент.ОткрытьПомощникСозданияОсновныхПроцессов(
		"Рассмотрение", Объект.Ссылка, ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ПроцессИсполнение(Команда)
	
	РаботаСБизнесПроцессамиКлиент.ОткрытьПомощникСозданияОсновныхПроцессов(
		"Исполнение", Объект.Ссылка, ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ПроцессОзнакомление(Команда)
	
	РаботаСБизнесПроцессамиКлиент.ОткрытьПомощникСозданияОсновныхПроцессов(
		"Ознакомление", Объект.Ссылка, ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ПроцессПриглашение(Команда)
	
	РаботаСБизнесПроцессамиКлиент.ОткрытьПомощникСозданияОсновныхПроцессов(
		"Приглашение", Объект.Ссылка, ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ПроцессОбработка(Команда)
	
	РаботаСБизнесПроцессамиКлиент.ОткрытьПомощникСозданияОсновныхПроцессов(
		"КомплексныйПроцесс", Объект.Ссылка, ЭтаФорма);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы_РаботаСФлагами

&НаКлиенте
Процедура КрасныйФлаг(Команда)
	
	РаботаСФлагамиОбъектовКлиент.УстановитьФлагВФормеОбъекта(
		ЭтаФорма,
		ПредопределенноеЗначение("Перечисление.ФлагиОбъектов.Красный"),
		БиблиотекаКартинок.КрасныйФлаг);
	
КонецПроцедуры

&НаКлиенте
Процедура СинийФлаг(Команда)
	
	РаботаСФлагамиОбъектовКлиент.УстановитьФлагВФормеОбъекта(
		ЭтаФорма,
		ПредопределенноеЗначение("Перечисление.ФлагиОбъектов.Синий"),
		БиблиотекаКартинок.СинийФлаг);
	
КонецПроцедуры

&НаКлиенте
Процедура ЖелтыйФлаг(Команда)
	
	РаботаСФлагамиОбъектовКлиент.УстановитьФлагВФормеОбъекта(
		ЭтаФорма,
		ПредопределенноеЗначение("Перечисление.ФлагиОбъектов.Желтый"),
		БиблиотекаКартинок.ЖелтыйФлаг);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗеленыйФлаг(Команда)
	
	РаботаСФлагамиОбъектовКлиент.УстановитьФлагВФормеОбъекта(
		ЭтаФорма,
		ПредопределенноеЗначение("Перечисление.ФлагиОбъектов.Зеленый"),
		БиблиотекаКартинок.ЗеленыйФлаг);
	
КонецПроцедуры

&НаКлиенте
Процедура ОранжевыйФлаг(Команда)
	
	РаботаСФлагамиОбъектовКлиент.УстановитьФлагВФормеОбъекта(
		ЭтаФорма,
		ПредопределенноеЗначение("Перечисление.ФлагиОбъектов.Оранжевый"),
		БиблиотекаКартинок.ОранжевыйФлаг);
	
КонецПроцедуры

&НаКлиенте
Процедура ЛиловыйФлаг(Команда)
	
	РаботаСФлагамиОбъектовКлиент.УстановитьФлагВФормеОбъекта(
		ЭтаФорма,
		ПредопределенноеЗначение("Перечисление.ФлагиОбъектов.Лиловый"),
		БиблиотекаКартинок.ЛиловыйФлаг);
	
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьФлаг(Команда)
	
	РаботаСФлагамиОбъектовКлиент.УстановитьФлагВФормеОбъекта(
		ЭтаФорма,
		ПредопределенноеЗначение("Перечисление.ФлагиОбъектов.ПустаяСсылка"),
		БиблиотекаКартинок.ПустойФлаг);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы_ДеревоПриложений

&НаКлиенте
Процедура ДобавитьПредмет(Команда)
	
	ДеревоПриложенийДобавлениеНаКлиенте();
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьФайл(Команда)
	
	ДеревоПриложенийДобавлениеНаКлиенте(ПредопределенноеЗначение("Справочник.Файлы.ПустаяСсылка"));
	
КонецПроцедуры

&НаКлиенте
Процедура УдалитьПредмет(Команда)
	
	ДеревоПриложенийУдалениеНаКлиенте();
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьДляПросмотра(Команда)
	
	РаботаСБизнесПроцессамиКлиент.ОткрытьТекущийФайлДляПросмотра(ЭтаФорма, Элементы.ДеревоПриложений);
	
КонецПроцедуры	

&НаКлиенте
Процедура Редактировать(Команда)
	
	РаботаСБизнесПроцессамиКлиент.РедактироватьТекущийФайл(
		ЭтаФорма, Элементы.ДеревоПриложений);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗакончитьРедактирование(Команда)
	
	РаботаСБизнесПроцессамиКлиент.ЗакончитьРедактированиеТекущегоФайла(
		ЭтаФорма, Элементы.ДеревоПриложений);
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьКак(Команда)
	
	РаботаСБизнесПроцессамиКлиент.СохранитьТекущийФайл(ЭтаФорма, Элементы.ДеревоПриложений);
	
КонецПроцедуры	

&НаКлиенте
Процедура КомандаОбновитьДеревоПриложений(Команда)
	
	ОбновитьДеревоПриложений();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтображатьУдаленныеПриложения(Команда)
	
	ОтображатьУдаленныеПриложения = Не ОтображатьУдаленныеПриложения;
	Элементы.ДеревоПриложенийКонтекстноеМенюОтображатьУдаленные.Пометка = ОтображатьУдаленныеПриложения;
	
	ТекущаяСсылкаВДереве = Неопределено;
	Если Элементы.ДеревоПриложений.ТекущиеДанные <> Неопределено Тогда
		ТекущаяСсылкаВДереве = Элементы.ДеревоПриложений.ТекущиеДанные.Ссылка;
	КонецЕсли;	
	
	ОтображатьУдаленныеПриложенияСервер();
	
	Если ТекущаяСсылкаВДереве <> Неопределено Тогда
		РаботаСБизнесПроцессамиКлиент.УстановитьТекущуюСтрокуВДеревеПриложений(
			ЭтаФорма, 
			ДеревоПриложений.ПолучитьЭлементы(), 
			ТекущаяСсылкаВДереве);
	КонецЕсли;	
		
	РаботаСБизнесПроцессамиКлиент.УстановитьДоступностьКомандРаботыСФайлами(
		ЭтаФорма, 
		Элементы.ДеревоПриложений);
		
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПолучитьДанныеПроекта()
	
	ЕдиницаТрудозатрат = Константы.ОсновнаяЕдиницаТрудозатрат.Получить();
	Если ПолучитьФункциональнуюОпцию("ВестиУчетПоПроектам") Тогда 
		Если ЗначениеЗаполнено(Объект.ПроектнаяЗадача) Тогда 
			ЕдиницаТрудозатрат = ОбщегоНазначенияДокументооборот.ЗначениеРеквизитаОбъектаВПривилегированномРежиме(
				Объект.ПроектнаяЗадача, "ТекущийПланЕдиницаТрудозатрат");
		ИначеЕсли ЗначениеЗаполнено(Объект.Проект) Тогда 
			ЕдиницаТрудозатрат = ОбщегоНазначенияДокументооборот.ЗначениеРеквизитаОбъектаВПривилегированномРежиме(
				Объект.Проект, "ЕдиницаТрудозатратЗадач");
		КонецЕсли;
	КонецЕсли;
	
	ЗаполнитьОписаниеТрудозатрат(ЭтаФорма);
	
КонецПроцедуры	

&НаСервере
Процедура УстановитьДоступность() Экспорт
	
	Если ИсполнителиИсполнения.Количество() > 1 Тогда 
		Элементы.НазначитьОтветственным.Доступность = Истина;
	Иначе
		Элементы.НазначитьОтветственным.Доступность = Ложь;
	КонецЕсли;
	
	Если ВариантИсполнения = Перечисления.ВариантыМаршрутизацииЗадач.Смешанно Тогда 
		Элементы.ПорядокИсполнения.Видимость = Истина;
		Элементы.Шаг.Видимость = Истина;
	ИначеЕсли ВариантИсполнения = Перечисления.ВариантыМаршрутизацииЗадач.Последовательно Тогда
		Элементы.ПорядокИсполнения.Видимость = Ложь;
		Элементы.Шаг.Видимость = Ложь;
	Иначе
		Элементы.ПорядокИсполнения.Видимость = Ложь;
		Элементы.Шаг.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьДоступностьПоШаблонуИсполнения()
	
	СтруктураПредметов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Объект.БизнесПроцесс,"Предметы, ПредметыЗадач");
	
	СтруктураДоступности = Новый Структура("Шаблон, Ссылка, ВедущаяЗадача, Предметы, ПредметыЗадач", 
		ШаблонИсполнения, 
		Объект.БизнесПроцесс,
		ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.БизнесПроцесс, "ВедущаяЗадача"),
		СтруктураПредметов.Предметы.Выгрузить(),
		СтруктураПредметов.ПредметыЗадач.Выгрузить());
	
	ДоступностьПоШаблонуИсполнения = Истина;
	
	Если Не ЗначениеЗаполнено(СтруктураДоступности.Шаблон)
		И Не ЗначениеЗаполнено(СтруктураДоступности.ВедущаяЗадача) Тогда 
		
		Возврат;
	КонецЕсли;
	
	ДоступностьПоШаблонуИсполнения = ШаблоныБизнесПроцессов.ДоступностьПоШаблону(СтруктураДоступности);
	
	Если ЗначениеЗаполнено(КонтролерИсполнения) Тогда 
		Элементы.КонтролерИсполнения.ТолькоПросмотр = Не ДоступностьПоШаблонуИсполнения;
	Иначе
		Элементы.КонтролерИсполнения.ТолькоПросмотр = Ложь;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ПроверяющийИсполнения) Тогда 
		Элементы.ПроверяющийИсполнения.ТолькоПросмотр = Не ДоступностьПоШаблонуИсполнения;
	Иначе
		Элементы.ПроверяющийИсполнения.ТолькоПросмотр = Ложь;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ВажностьИсполнения) Тогда 
		Элементы.ВажностьИсполнения.ТолькоПросмотр = Не ДоступностьПоШаблонуИсполнения;
	Иначе
		Элементы.ВажностьИсполнения.ТолькоПросмотр = Ложь;
	КонецЕсли;
	
	Если ИсполнителиИсполнения.Количество() > 0 Тогда
		Элементы.ИсполнителиИсполнения.ТолькоПросмотр = Не ДоступностьПоШаблонуИсполнения;
		Элементы.ПодобратьИсполнение.Доступность = ДоступностьПоШаблонуИсполнения;
	Иначе
		Элементы.ИсполнителиИсполнения.ТолькоПросмотр = Ложь;
		Элементы.ПодобратьИсполнение.Доступность = Истина;
	КонецЕсли;
	
	УстановитьДоступность();
	
	ПараметрыДоступности = 
		СрокиИсполненияПроцессовКлиентСервер.ПараметрыДоступностиЭлементаУправления();
	ПараметрыДоступности.ДоступностьПоШаблону = ДоступностьПоШаблонуИсполнения;
	
	СрокиИсполненияПроцессовКлиентСервер.НастроитьЭлементУправленияСроком(
		ЭтаФорма,
		Элементы.СрокОбработкиРезультатовИсполненияПредставление,
		СрокОбработкиРезультатовИсполненияПредставление,
		ПараметрыДоступности);
	
	СрокиИсполненияПроцессовКлиентСервер.НастроитьЭлементУправленияСроком(
		ЭтаФорма,
		Элементы.КоличествоИтерацийИсполнения,
		КоличествоИтерацийИсполнения,
		ПараметрыДоступности);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьИсполнениеПоШаблонуНаСервере(Шаблон)
	
	ИсполнениеОбъект = БизнесПроцессы.Исполнение.СоздатьБизнесПроцесс();
	ИсполнениеОбъект.Заполнить(Новый Структура());
	
	Если ТипЗнч(Объект.Исполнитель) = Тип("СправочникСсылка.Пользователи") Тогда 
		ИсполнениеОбъект.Автор = Объект.Исполнитель;
	Иначе
		ИсполнениеОбъект.Автор = ПользователиКлиентСервер.ТекущийПользователь();
	КонецЕсли;
	
	Мультипредметность.ПередатьПредметыПроцессу(ИсполнениеОбъект, Объект.Предметы);
	
	ИсполнениеОбъект.ЗаполнитьПоШаблону(Шаблон);
	Если ЗначениеЗаполнено(ИсполнениеОбъект.Наименование) Тогда 
		НаименованиеИсполнения = ИсполнениеОбъект.Наименование;
	КонецЕсли;	
	
	Если ЗначениеЗаполнено(ИсполнениеОбъект.Описание) Тогда 
		ОписаниеИсполнения = ИсполнениеОбъект.Описание;
	КонецЕсли;	
	
	// Срок обработки результатов
	Если ЗначениеЗаполнено(ИсполнениеОбъект.ВариантУстановкиСрокаОбработкиРезультатов) Тогда
		ВариантУстановкиСрокаОбработкиРезультатовИсполнения = 
			ИсполнениеОбъект.ВариантУстановкиСрокаОбработкиРезультатов;
	КонецЕсли;
	Если ЗначениеЗаполнено(ИсполнениеОбъект.СрокОбработкиРезультатовДни) Тогда
		СрокОбработкиРезультатовДниИсполнения = ИсполнениеОбъект.СрокОбработкиРезультатовДни;
	КонецЕсли;
	Если ЗначениеЗаполнено(ИсполнениеОбъект.СрокОбработкиРезультатовЧасы) Тогда
		СрокОбработкиРезультатовЧасыИсполнения = ИсполнениеОбъект.СрокОбработкиРезультатовЧасы;
	КонецЕсли;
	Если ЗначениеЗаполнено(ИсполнениеОбъект.СрокОбработкиРезультатовМинуты) Тогда
		СрокОбработкиРезультатовМинутыИсполнения = ИсполнениеОбъект.СрокОбработкиРезультатовМинуты;
	КонецЕсли;
	
	// Количество итераций
	Если ЗначениеЗаполнено(ИсполнениеОбъект.КоличествоИтераций) Тогда
		КоличествоИтерацийИсполнения = ИсполнениеОбъект.КоличествоИтераций;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ИсполнениеОбъект.Важность) Тогда 
		ВажностьИсполнения = ИсполнениеОбъект.Важность;
	КонецЕсли;
	
	КонтролерИсполнения = ИсполнениеОбъект.Контролер;
	
	ПроверяющийИсполнения = ИсполнениеОбъект.Проверяющий;
	
	ИсполнителиИсполнения.Загрузить(ИсполнениеОбъект.Исполнители.Выгрузить());
	ШаблонИсполнения = Шаблон;
	
	ВариантИсполнения = ИсполнениеОбъект.ВариантИсполнения;
	
	// Сроки исполнения процессов
	ОбновитьСрокиИсполненияНаСервере();
	
	УстановитьДоступностьПоШаблонуИсполнения();
	
КонецПроцедуры

&НаСервере
Процедура УстановитьДоступностьПоШаблонуОзнакомления()
	
	СтруктураПредметов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Объект.БизнесПроцесс,"Предметы, ПредметыЗадач");
	
	СтруктураДоступности = Новый Структура("Шаблон, Ссылка, ВедущаяЗадача, Предметы, ПредметыЗадач", 
		ШаблонОзнакомления, 
		Объект.БизнесПроцесс, 
		ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.БизнесПроцесс, "ВедущаяЗадача"),
		СтруктураПредметов.Предметы.Выгрузить(),
		СтруктураПредметов.ПредметыЗадач.Выгрузить());
	
	ДоступностьПоШаблонуОзнакомления = Истина;
	
	Если Не ЗначениеЗаполнено(СтруктураДоступности.Шаблон)
		И Не ЗначениеЗаполнено(СтруктураДоступности.ВедущаяЗадача) Тогда 
		
		Возврат;
	КонецЕсли;
	
	ДоступностьПоШаблонуОзнакомления = ШаблоныБизнесПроцессов.ДоступностьПоШаблону(СтруктураДоступности);
	
	Если ЗначениеЗаполнено(ВажностьОзнакомления) Тогда 
		Элементы.ВажностьОзнакомления.ТолькоПросмотр = Не ДоступностьПоШаблонуОзнакомления;
	Иначе
		Элементы.ВажностьОзнакомления.ТолькоПросмотр = Ложь;
	КонецЕсли;
	
	Если ИсполнителиОзнакомления.Количество() > 0 Тогда
		Элементы.ИсполнителиОзнакомления.ТолькоПросмотр = Не ДоступностьПоШаблонуОзнакомления;
		Элементы.ПодобратьОзнакомление.Доступность = ДоступностьПоШаблонуОзнакомления;
	Иначе
		Элементы.ИсполнителиОзнакомления.ТолькоПросмотр = Ложь;
		Элементы.ПодобратьОзнакомление.Доступность = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьОзнакомлениеПоШаблонуНаСервере(Шаблон)
	
	ОзнакомлениеОбъект = БизнесПроцессы.Ознакомление.СоздатьБизнесПроцесс();
	ОзнакомлениеОбъект.Заполнить(Новый Структура());
	
	Если ТипЗнч(Объект.Исполнитель) = Тип("СправочникСсылка.Пользователи") Тогда 
		ОзнакомлениеОбъект.Автор = Объект.Исполнитель;
	Иначе
		ОзнакомлениеОбъект.Автор = ПользователиКлиентСервер.ТекущийПользователь();
	КонецЕсли;
	
	Мультипредметность.ПередатьПредметыПроцессу(ОзнакомлениеОбъект, Объект.Предметы);
	
	ОзнакомлениеОбъект.ЗаполнитьПоШаблону(Шаблон);
	Если ЗначениеЗаполнено(ОзнакомлениеОбъект.Наименование) Тогда 
		НаименованиеОзнакомления = ОзнакомлениеОбъект.Наименование;
	КонецЕсли;	
	
	Если ЗначениеЗаполнено(ОзнакомлениеОбъект.Описание) Тогда 
		ОписаниеОзнакомления = ОзнакомлениеОбъект.Описание;
	КонецЕсли;	
	
	Если ЗначениеЗаполнено(ОзнакомлениеОбъект.Важность) Тогда 
		ВажностьОзнакомления = ОзнакомлениеОбъект.Важность;
	КонецЕсли;
	
	ИсполнителиОзнакомления.Загрузить(ОзнакомлениеОбъект.Исполнители.Выгрузить());
	ШаблонОзнакомления = Шаблон;
	
	// Сроки исполнения процессов
	ОбновитьСрокиИсполненияНаСервере();
	
	УстановитьДоступностьПоШаблонуОзнакомления();
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ЗаполнитьОписаниеТрудозатрат(Форма)
	
	Если НЕ Форма.ВестиУчетПлановыхТрудозатратВБизнесПроцессах Тогда
		Возврат;
	КонецЕсли;
	
	// Трудозатраты для процесса Исполнения
	
	ПараметрыОписания = Новый Структура;
	ПараметрыОписания.Вставить("Контролер", Истина);
	ПараметрыОписания.Вставить("Проверяющий", Истина);
	ПараметрыОписания.Вставить("Исполнители", Истина);
	ПараметрыОписания.Вставить("ПредставлениеИсполнителей", НСтр("ru = 'Исполнители'; en = 'Performers'"));
	
	ПараметрыОписания.Вставить("ИмяРеквизитаОбъекта", "");
	ПараметрыОписания.Вставить("ИмяТаблицыИсполнители", "ИсполнителиИсполнения");
	
	РаботаСБизнесПроцессамиКлиентСервер.ЗаполнитьОписаниеТрудозатрат(
		Форма, ПараметрыОписания, "ОписаниеТрудозатратПроцессаИсполнения");
	
	// Трудозатраты для процесса Ознакомления
	
	ПараметрыОписания = Новый Структура;
	ПараметрыОписания.Вставить("Исполнитель", Истина);
	ПараметрыОписания.Вставить("ПредставлениеИсполнителя", НСтр("ru = 'Исполнители (для каждого)'; en = 'Performers (separately)'"));
	
	ПараметрыОписания.Вставить("ИмяРеквизитаОбъекта", "");
	ПараметрыОписания.Вставить("ИмяРеквизитаТрудозатратыПланИсполнителя",
		"ТрудозатратыПланИсполнителяОзнакомления");
	
	РаботаСБизнесПроцессамиКлиентСервер.ЗаполнитьОписаниеТрудозатрат(
		Форма, ПараметрыОписания, "ОписаниеТрудозатратПроцессаОзнакомления");
	
КонецПроцедуры

&НаСервере
Процедура ПоказатьРасширеннуюФорму()
	
	Если Не РасширеннаяФорма 
		И ВариантРассмотрения <> ВариантРассмотренияНаправитьНаИсполнение 
		И ВариантРассмотрения <> ВариантРассмотренияНаправитьНаОзнакомление Тогда 
		Элементы.ГруппаНаИсполнение.Видимость = Ложь;
		Элементы.ГруппаОзнакомление.Видимость = Ложь;
		Элементы.ГруппаСтраницы.ОтображениеСтраниц = ОтображениеСтраницФормы.Нет;
	Иначе 
		Элементы.ГруппаНаИсполнение.Видимость = Истина;
		Элементы.ГруппаОзнакомление.Видимость = Истина;
		Элементы.ГруппаСтраницы.ОтображениеСтраниц = ОтображениеСтраницФормы.ЗакладкиСверху;
	КонецЕсли;
	
	Если ВариантРассмотрения = ВариантРассмотренияВвестиТекстРезолюции Тогда 
		Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.СтраницаЗадача;
		Элементы.ГруппаКомандыВыполнения.ТекущаяСтраница = Элементы.Рассмотрение;
	ИначеЕсли ВариантРассмотрения = ВариантРассмотренияНаправитьНаИсполнение Тогда 
		Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаНаИсполнение;
		Элементы.ГруппаКомандыВыполнения.ТекущаяСтраница = Элементы.Исполнение;
	ИначеЕсли ВариантРассмотрения = ВариантРассмотренияНаправитьНаОзнакомление Тогда 
		Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаОзнакомление;
		Элементы.ГруппаКомандыВыполнения.ТекущаяСтраница = Элементы.Ознакомление;
	КонецЕсли;
	
	Элементы.РасширеннаяФорма.Пометка = РасширеннаяФорма;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьВыполнение()
	
	Если Модифицированность И Не Записать() Тогда
		Возврат;
	КонецЕсли;
	
	Если РаботаСБизнесПроцессамиКлиент.ЗапретВыполненияИзФормы(ЭтаФорма) Тогда
		Возврат;
	КонецЕсли;
	
	ОписаниеОповещения = Новый ОписаниеОповещения(
		"ПродолжитьОбработкуВыполнения", ЭтаФорма);
	
	РаботаСБизнесПроцессамиКлиент.ВыбратьИсполнителяЗадачи(
		ЭтаФорма,
		Объект.Исполнитель,
		ТекущийПользователь,
		ФактическийИсполнительЗадачи,
		ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ПродолжитьОбработкуВыполнения(
	ВыбранныйФактическийИсполнитель, ДопПараметры) Экспорт
	
	Если ВыбранныйФактическийИсполнитель = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ВыбранныйФактическийИсполнитель <> Объект.Исполнитель Тогда
		ФактическийИсполнительЗадачи = ВыбранныйФактическийИсполнитель;
	КонецЕсли; 
	
	Отказ = Ложь;
	ОчиститьСообщения();
	
	ОбщегоНазначенияДокументооборотКлиент.УдалитьПустыеСтрокиТаблицы(
		ИсполнителиИсполнения, "Исполнитель");
	РаботаСБизнесПроцессамиКлиентСервер.ЗаполнитьШаг(ИсполнителиИсполнения);
	
	ОбщегоНазначенияДокументооборотКлиент.УдалитьПустыеСтрокиТаблицы(
		ИсполнителиОзнакомления, "Исполнитель");
	РаботаСБизнесПроцессамиКлиентСервер.ЗаполнитьНомерСтрокиВТаблицеИсполнителей(
		ИсполнителиОзнакомления);
	
	Если ВариантРассмотрения = ВариантРассмотренияВвестиТекстРезолюции Тогда 
		
		Если ЗаполнениеПоШаблонуРезолюции И Не ЗначениеЗаполнено(ТекстРезолюции) Тогда 
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				НСтр("ru = 'Шаблон резолюции не заполнен'; en = 'Resolution template is not filled in'"),,
				"Объект.РезультатВыполнения",,Отказ);
		ИначеЕсли Не ЗаполнениеПоШаблонуРезолюции И Не ЗначениеЗаполнено(Объект.РезультатВыполнения) Тогда 
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				НСтр("ru = 'Поле ""Резолюция"" не заполнено'; en = '""Resolution"" field is not filled in'"),,
				"Объект.РезультатВыполнения",,Отказ);
		КонецЕсли;
		
	ИначеЕсли ВариантРассмотрения = ВариантРассмотренияНаправитьНаИсполнение Тогда 
		
		Если ПустаяСтрока(НаименованиеИсполнения) Тогда 
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				НСтр("ru = 'Поле ""Заголовок"" не заполнено'; en = '""Title"" field is not filled in'"),, 
				"НаименованиеИсполнения",,Отказ);
		КонецЕсли;	
		
		Если ИсполнителиИсполнения.Количество() = 0 Тогда 
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				НСтр("ru = 'Не введено ни одной строки в список ""Исполнители""'; en = 'There is nothing in the ""Performers"" list'"),, 
				"ИсполнителиИсполнения",,Отказ);
		КонецЕсли;	
		
		Для Каждого Строка Из ИсполнителиИсполнения Цикл
			Если Не ЗначениеЗаполнено(Строка.Исполнитель) Тогда 
				ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Не заполнена колонка ""Исполнитель"" в строке %1 списка ""Исполнители""'; en = 'Column ""Performer"" is not filled in at row %1 of ""Performers"" list'"),
					ИсполнителиИсполнения.Индекс(Строка)+1);
				
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
					ТекстОшибки,,"ИсполнителиИсполнения["+ИсполнителиИсполнения.Индекс(Строка)+"].Исполнитель",,Отказ);
			КонецЕсли;	
		КонецЦикла;
		
	ИначеЕсли ВариантРассмотрения = ВариантРассмотренияНаправитьНаОзнакомление Тогда 
		
		Если ПустаяСтрока(НаименованиеОзнакомления) Тогда 
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				НСтр("ru = 'Поле ""Заголовок"" не заполнено'; en = '""Title"" field is not filled in'"),, 
				"НаименованиеОзнакомления",,Отказ);
		КонецЕсли;
		
		Если ИсполнителиОзнакомления.Количество() = 0 Тогда 
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				НСтр("ru = 'Не введено ни одной строки в список ""Исполнители""'; en = 'There is nothing in the ""Performers"" list'"),, 
				"ИсполнителиОзнакомления",,Отказ);
		КонецЕсли;
		
		Для Каждого Строка Из ИсполнителиОзнакомления Цикл
			Если Не ЗначениеЗаполнено(Строка.Исполнитель) Тогда 
				ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Не заполнена колонка ""Исполнитель"" в строке %1 списка ""Исполнители""'; en = 'Column ""Performer"" is not filled in at row %1 of ""Performers"" list'"),
					ИсполнителиОзнакомления.Индекс(Строка)+1);
				
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
					ТекстОшибки,,"ИсполнителиОзнакомления["+ИсполнителиОзнакомления.Индекс(Строка)+"].Исполнитель",,Отказ);
			КонецЕсли;	
		КонецЦикла;	
		
		// Проверка исполнителей на дубли
		РаботаСБизнесПроцессамиКлиентСервер.ПроверитьНаДублиТаблицуИсполнителей(
			ИсполнителиОзнакомления, "ИсполнителиОзнакомления", Отказ, Ложь);
		
	КонецЕсли;
	
	Если Отказ Тогда 
		Возврат;
	КонецЕсли;
	
	// Проверка корректности заполнения сроков
	ОписаниеОповещения = Новый ОписаниеОповещения(
		"ПродолжитьПослеПроверкиКорректностиЗаполненияСроков",
		ЭтотОбъект);
	
	СтруктураСроков = СтруктураСроковДляПроверкиКорректностиЗаполнения();
	СрокиИсполненияПроцессовКлиент.ПроверитьКорректностьЗаполненияСроковПередСтартом(
		ЭтаФорма, СтруктураСроков, Новый Структура, Отказ, ОписаниеОповещения);
	
	Если Не Отказ Тогда
		ВыполнитьОбработкуОповещения(ОписаниеОповещения, КодВозвратаДиалога.Да);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПродолжитьПослеПроверкиКорректностиЗаполненияСроков(Результат, Параметры) Экспорт
	
	Если Результат <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	ОписаниеОповещения = Новый ОписаниеОповещения(
		"ПродолжениеВыполненияЗадачиПослеПроверкиНаЗанятыеФайлы",
		ЭтотОбъект);
	
	РаботаСБизнесПроцессамиКлиент.ПроверитьНаличиеЗанятыхФайлов(Объект, ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ПродолжениеВыполненияЗадачиПослеПроверкиНаЗанятыеФайлы(Результат, Параметры) Экспорт
	
	Если Результат <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПродолжениеВыполненияЗадачиПослеПроверкиОтсутствия",
		ЭтотОбъект);
	Если Не ОтсутствияКлиент.ПроверитьОтсутствиеПоЗадачеРассмотрения(ЭтаФорма, ОписаниеОповещения) Тогда
		Возврат;
	КонецЕсли;
	
	ВыполнитьОбработкуОповещения(ОписаниеОповещения, КодВозвратаДиалога.Да);
	
КонецПроцедуры

&НаКлиенте
Процедура ПродолжениеВыполненияЗадачиПослеПроверкиОтсутствия(Результат, Параметры) Экспорт
	
	Если Результат <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	ОценкаПроизводительностиКлиентСервер.НачатьЗамерВремени(
		"РассмотрениеВыполнениеКомандыВыполнено");	
	
	СформироватьТекстРезолюции();
	
	ПодписиКРезолюциямПредметов = Новый Структура;
	
	ОписаниеОповещения = Новый ОписаниеОповещения(
		"ПродолжениеВыполненияЗадачиПослеПодписанияРезолюций",
		ЭтотОбъект);
	
	Если ПодписатьРезолюцииЭП Тогда
		
		МассивРезолюций = Новый Массив;
		МассивРезолюцийДляПодписания = Новый Массив;
		МассивАдресов = Новый Массив;
		МассивДанныхДляЗанесенияВРегистр = Новый Массив;
		
		Для Каждого Предмет Из Объект.Предметы Цикл
			Если ТипЗнч(Предмет.Предмет) = Тип("СправочникСсылка.ВнутренниеДокументы")
				ИЛИ ТипЗнч(Предмет.Предмет) = Тип("СправочникСсылка.ВходящиеДокументы") Тогда
				
				СтруктураРезолюцииДляПодписания = РаботаСРезолюциями.ПолучитьСтруктуруКлючевыхРеквизитовРезолюции();
				СтруктураРезолюцииДляПодписания.Документ = Предмет.Предмет;
				СтруктураРезолюцииДляПодписания.ДатаРезолюции = Объект.ДатаИсполнения;
				СтруктураРезолюцииДляПодписания.АвторРезолюции = Объект.Исполнитель;
				СтруктураРезолюцииДляПодписания.ВнесРезолюцию = Объект.Исполнитель;
				
				Если ЗаполнениеПоШаблонуРезолюции Тогда 
					СтруктураРезолюцииДляПодписания.ТекстРезолюции = ТекстРезолюции;
				Иначе
					СтруктураРезолюцииДляПодписания.ТекстРезолюции = Объект.РезультатВыполнения;
				КонецЕсли;
				
				
				МассивРезолюцийДляПодписания.Добавить(СтруктураРезолюцииДляПодписания);
				
				СтруктураРезолюции = РаботаСРезолюциями.ПолучитьСтруктуруКлючевыхРеквизитовРезолюции();
				ЗаполнитьЗначенияСвойств(СтруктураРезолюции, СтруктураРезолюцииДляПодписания);
				СтруктураРезолюции.Вставить("Наименование", РаботаСРезолюциямиКлиентСервер.ПолучитьНаименованиеРезолюции(
					Предмет.Предмет));
					
				МассивРезолюций.Добавить(СтруктураРезолюции);
				
			КонецЕсли;
		КонецЦикла;
		
		Если МассивРезолюцийДляПодписания.Количество() > 0 Тогда
			
			Заголовки = Новый Структура("Операция");
			Заголовки.Операция = НСтр("ru = 'Подписать резолюции предметов'; en = 'Sign subject resolutions'");
			
			РаботаСЭПКлиент.Подписать(МассивРезолюцийДляПодписания, УникальныйИдентификатор, ОписаниеОповещения, Заголовки);
			
		КонецЕсли;
		
	Иначе
		
		РезультатОповещения = Новый Структура;
		РезультатОповещения.Вставить("Успех", Истина);
		РезультатОповещения.Вставить("НаборДанных", Новый Массив);
		ВыполнитьОбработкуОповещения(ОписаниеОповещения, РезультатОповещения);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПродолжениеВыполненияЗадачиПослеПодписанияРезолюций(Результат, Параметры) Экспорт
	
	Если Не Результат.Успех Тогда
		Возврат;
	КонецЕсли;
	
	//Текст шаблона резолюции вставляем только после прохождения всех проверок.
	Если ВариантРассмотрения = ВариантРассмотренияВвестиТекстРезолюции 
		И ЗаполнениеПоШаблонуРезолюции Тогда 
		Объект.РезультатВыполнения = ТекстРезолюции;
	КонецЕсли;
	
	Для Каждого ДанныеДляЗанесенияВРегистр Из Результат.НаборДанных Цикл
		Если Не ДанныеДляЗанесенияВРегистр.Свойство("СвойстваПодписи") Тогда
			Продолжить;
		КонецЕсли;
		Резолюция = ДанныеДляЗанесенияВРегистр.Представление;
		Ключ = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			"Предмет_%1_%2",
			СтрЗаменить(Строка(Резолюция.Документ.УникальныйИдентификатор()), "-", ""),
			Формат(Резолюция.ДатаРезолюции, "ДФ=yyyyMMddHHmmss"));
		ПодписиКРезолюциямПредметов.Вставить(Ключ, ДанныеДляЗанесенияВРегистр.СвойстваПодписи);
	КонецЦикла;
	
	Если Не ВыполнениеЗадачКлиент.ВыполнитьЗадачуИзФормы(ЭтаФорма) Тогда
		Возврат;
	КонецЕсли;
	
	ОписаниеОповещения = Новый ОписаниеОповещения(
		"ПродолжениеВыполненияЗадачиПослеВводаВремени",
		ЭтотОбъект);
	
	УчетВремениКлиент.ДобавитьВОтчетПослеВыполненияЗадачи(ОпцияИспользоватьУчетВремени,
		Объект.ДатаИсполнения, Объект.Ссылка, ВключенХронометраж, 
		ДатаНачалаХронометража, ДатаКонцаХронометража,
		ВидыРабот, СпособУказанияВремени, ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ПродолжениеВыполненияЗадачиПослеВводаВремени(Результат, Параметры) Экспорт
	
	ПоказатьОповещениеПользователя(
		НСтр("ru = 'Выполнение:'; en = 'Execution:'"),
		ПолучитьНавигационнуюСсылку(Объект.Ссылка),
		Строка(Объект.Ссылка),
		БиблиотекаКартинок.Информация32);
	
	Оповестить("ЗадачаВыполнена", Объект.Ссылка);
	Закрыть();
	
КонецПроцедуры

&НаСервере
Процедура СформироватьТекстРезолюции()
	
	Если ВариантРассмотрения = Перечисления.ВариантыРассмотрения.НаправитьНаИсполнение Тогда
		
		РезультатВыполнения = "";
		Если ЗначениеЗаполнено(НаименованиеИсполнения) Тогда 
			РезультатВыполнения = РезультатВыполнения + НаименованиеИсполнения + Символы.ПС;
		КонецЕсли;
		Если ЗначениеЗаполнено(ОписаниеИсполнения) Тогда 
			РезультатВыполнения = РезультатВыполнения + ОписаниеИсполнения + Символы.ПС;
		КонецЕсли;	
		Если ЗначениеЗаполнено(СрокИсполненияПроцессаИсполнения) Тогда
			РезультатВыполнения = РезультатВыполнения + НСтр("ru = 'Срок'; en = 'Due date'") + ": " + Формат(СрокИсполненияПроцессаИсполнения, "ДФ=dd.MM.yyyy") + Символы.ПС;
		КонецЕсли;
		Если ЗначениеЗаполнено(ВажностьИсполнения) И ВажностьИсполнения <> Перечисления.ВариантыВажностиЗадачи.Обычная Тогда
			Важности = Новый Соответствие;
			Важности.Вставить(Перечисления.ВариантыВажностиЗадачи.Высокая, НСтр("ru = 'высокая'; en = 'high'"));
			Важности.Вставить(Перечисления.ВариантыВажностиЗадачи.Обычная, НСтр("ru = 'обычная'; en = 'normal'"));
			Важности.Вставить(Перечисления.ВариантыВажностиЗадачи.Низкая,  НСтр("ru = 'низкая'; en = 'low'"));
			
			РезультатВыполнения = РезультатВыполнения + НСтр("ru = 'Важность'; en = 'Importance'") + ": " + Важности.Получить(ВажностьИсполнения) + Символы.ПС;
		КонецЕсли;
		Если ИсполнителиИсполнения.Количество() > 0 Тогда 
			РезультатВыполнения = РезультатВыполнения + НСтр("ru = 'Исполнители'; en = 'Performers'") + ": " + Символы.ПС;
		КонецЕсли;	
		Для Каждого Строка Из ИсполнителиИсполнения Цикл
			РезультатВыполнения = РезультатВыполнения + Строка(Строка.Исполнитель);
			Если ЗначениеЗаполнено(Строка.СрокИсполнения) Тогда 
				РезультатВыполнения = РезультатВыполнения + " " + Формат(Строка.СрокИсполнения, "ДФ=dd.MM.yyyy");
			КонецЕсли;	
			РезультатВыполнения = РезультатВыполнения + " " + Строка.Описание + Символы.ПС;
		КонецЦикла;	
		Если ЗначениеЗаполнено(КонтролерИсполнения) Тогда 
			РезультатВыполнения = РезультатВыполнения + НСтр("ru = 'Контролер'; en = 'Monitor'") + ": " + Строка(КонтролерИсполнения) + Символы.ПС;
		КонецЕсли;
		Если ЗначениеЗаполнено(ПроверяющийИсполнения) Тогда 
			РезультатВыполнения = РезультатВыполнения + НСтр("ru = 'Проверить'; en = 'Verify'") + ": " + Строка(ПроверяющийИсполнения) + Символы.ПС;
		КонецЕсли;	
		
		Объект.РезультатВыполнения = РезультатВыполнения;
		
	ИначеЕсли ВариантРассмотрения = Перечисления.ВариантыРассмотрения.НаправитьНаОзнакомление Тогда
		
		РезультатВыполнения = "";
		Если ЗначениеЗаполнено(НаименованиеОзнакомления) Тогда 
			РезультатВыполнения = РезультатВыполнения + НаименованиеОзнакомления + Символы.ПС;
		КонецЕсли;
		Если ЗначениеЗаполнено(ОписаниеОзнакомления) Тогда 
			РезультатВыполнения = РезультатВыполнения + ОписаниеОзнакомления + Символы.ПС;
		КонецЕсли;
		Если ЗначениеЗаполнено(СрокИсполненияПроцессаОзнакомления) Тогда
			РезультатВыполнения = РезультатВыполнения + НСтр("ru = 'Срок'; en = 'Due date'") + ": " + Формат(СрокИсполненияПроцессаОзнакомления, "ДФ=dd.MM.yyyy") + Символы.ПС;
		КонецЕсли;
		Если ЗначениеЗаполнено(ВажностьОзнакомления) И ВажностьОзнакомления <> Перечисления.ВариантыВажностиЗадачи.Обычная Тогда
			Важности = Новый Соответствие;
			Важности.Вставить(Перечисления.ВариантыВажностиЗадачи.Высокая, НСтр("ru = 'высокая'; en = 'high'"));
			Важности.Вставить(Перечисления.ВариантыВажностиЗадачи.Обычная, НСтр("ru = 'обычная'; en = 'normal'"));
			Важности.Вставить(Перечисления.ВариантыВажностиЗадачи.Низкая,  НСтр("ru = 'низкая'; en = 'low'"));
			
			РезультатВыполнения = РезультатВыполнения + НСтр("ru = 'Важность'; en = 'Importance'") + ": " + Важности.Получить(ВажностьОзнакомления) + Символы.ПС;
		КонецЕсли;
		Для Каждого Строка Из ИсполнителиОзнакомления Цикл
			РезультатВыполнения = РезультатВыполнения + Строка(Строка.Исполнитель);
		КонецЦикла;	
		
		Объект.РезультатВыполнения = РезультатВыполнения;
		
	КонецЕсли;	
	
КонецПроцедуры

&НаСервере
Процедура ПолучитьИнструкции()
	
	РаботаСИнструкциями.ПолучитьИнструкции(ЭтаФорма, 70, 100);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_РедактироватьСоставСвойств()
	
	УправлениеСвойствамиКлиент.РедактироватьСоставСвойств(ЭтаФорма, Объект.Ссылка);
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьЭлементыДополнительныхРеквизитов()
	
	УправлениеСвойствами.ОбновитьЭлементыДополнительныхРеквизитов(ЭтаФорма,
		РеквизитФормыВЗначение("Объект"));

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции_ДеревоПриложений

&НаКлиенте
Процедура ОбновитьДеревоПриложений(ТекущееИмяПредметаВДереве = Неопределено)
	
	ТекущаяСсылкаВДереве = Неопределено;
	
	Если Элементы.ДеревоПриложений.ТекущиеДанные <> Неопределено И ТекущееИмяПредметаВДереве = Неопределено Тогда
		ТекущаяСсылкаВДереве = Элементы.ДеревоПриложений.ТекущиеДанные.Ссылка;
		ТекущееИмяПредметаВДереве = Элементы.ДеревоПриложений.ТекущиеДанные.ИмяПредмета;
	КонецЕсли;
	
	Если Элементы.Найти("ДеревоПриложений") <> Неопределено  Тогда
		ОбновитьДеревоПриложенийСервер();
	КонецЕсли;
	
	Если ТекущаяСсылкаВДереве <> Неопределено ИЛИ ТекущееИмяПредметаВДереве <> Неопределено Тогда
		РаботаСБизнесПроцессамиКлиент.УстановитьТекущуюСтрокуВДеревеПриложений(
			ЭтаФорма, 
			ДеревоПриложений.ПолучитьЭлементы(), 
			ТекущаяСсылкаВДереве, ТекущееИмяПредметаВДереве);
	КонецЕсли;
		
	РаботаСБизнесПроцессамиКлиент.УстановитьДоступностьКомандРаботыСФайлами(
		ЭтаФорма, 
		Элементы.ДеревоПриложений);
		
КонецПроцедуры

&НаСервере
Процедура ОбновитьДеревоПриложенийСервер()
	
	РаботаСБизнесПроцессамиВызовСервера.ЗаполнитьДеревоПриложений(ЭтаФорма);
	
КонецПроцедуры	

&НаСервере
Процедура УстановитьПредметСервер()
	
	Мультипредметность.УстановитьЗначенияДопРеквизитовИДоступностьЭлементовФормыПроцесса(ЭтаФорма, Объект);
	ПолучитьИнструкции();
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоПриложенийДобавлениеНаКлиенте(Предмет = Неопределено)

	ОчиститьСообщения();
	СообщениеОбОшибке = "";
	НовыйИмяПредмета = Неопределено;
	
	ПараметрыОбработчикаОповещения = Новый Структура();
	ПараметрыОбработчикаОповещения.Вставить("СообщениеОбОшибке", СообщениеОбОшибке);
	
	ОписаниеОповещения = Новый ОписаниеОповещения(
		"ДеревоПриложенийВыборПродолжение",
		ЭтотОбъект,
		ПараметрыОбработчикаОповещения);
	
	Если Не МультипредметностьКлиент.ДобавитьПредметЗадачи(ЭтаФорма, СообщениеОбОшибке, НовыйИмяПредмета, Предмет,,ОписаниеОповещения) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			СообщениеОбОшибке,,
			"ДеревоПриложений");
		Возврат;
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура ДеревоПриложенийДобавлениеНаКлиентеПродолжение(Результат, Параметры) Экспорт
	
	Если Результат = Истина Тогда
		ОбновитьДеревоПриложений();
		УстановитьПредметСервер();	
	Иначе
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			Параметры.СообщениеОбОшибке,, "ДеревоПриложений");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьКарточкуНаКлиенте()
	
	ТекущиеДанные = Элементы.ДеревоПриложений.ТекущиеДанные;
	Если ЗначениеЗаполнено(ТекущиеДанные.Ссылка) Тогда
		ПоказатьЗначение(,ТекущиеДанные.Ссылка);
	Иначе
		ОчиститьСообщения();
		СообщениеОбОшибке = "";
		
		ПараметрыОбработчикаОповещения = Новый Структура;
		ПараметрыОбработчикаОповещения.Вставить("СообщениеОбОшибке", СообщениеОбОшибке);
		ОписаниеОповещения = Новый ОписаниеОповещения(
			"ОткрытьКарточкуНаКлиентеПродолжение",
			ЭтотОбъект,
			ПараметрыОбработчикаОповещения);
			
		Если Не МультипредметностьКлиент.ДобавитьПредметЗадачи(
			ЭтаФорма,
			СообщениеОбОшибке, 
			ТекущиеДанные.ИмяПредмета,
			ТекущиеДанные.Ссылка,,
			ОписаниеОповещения) Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				СообщениеОбОшибке,, "ДеревоПриложений");
			Возврат;
		КонецЕсли;
	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьКарточкуНаКлиентеПродолжение(Результат, Параметры) Экспорт
	
	Если Результат = Истина Тогда
		ОбновитьДеревоПриложений();
		УстановитьПредметСервер();
	Иначе
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			Параметры.СообщениеОбОшибке,, "ДеревоПриложений");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоПриложенийУдалениеНаКлиенте()
	
	ВыделенныеСтрокиПредметов = Новый Массив;
	Для Каждого ВыделеннаяСтр Из Элементы.ДеревоПриложений.ВыделенныеСтроки Цикл
		ДанныеСтроки = Элементы.ДеревоПриложений.ДанныеСтроки(ВыделеннаяСтр);
		ВыделенныеСтрокиПредметов.Добавить(ДанныеСтроки);
	КонецЦикла;
	
	ОписаниеОповещения = Новый ОписаниеОповещения(
		"ДеревоПриложенийУдалениеНаКлиентеПродолжение",
		ЭтотОбъект,
		ВыделенныеСтрокиПредметов);
		
	МультипредметностьКлиент.ПолученоПодтверждениеОбУдаленииПредмета(Объект, ВыделенныеСтрокиПредметов, ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоПриложенийУдалениеНаКлиентеПродолжение(Результат, ВыделенныеСтрокиПредметов) Экспорт
	
	Если Результат = Ложь Тогда
		Возврат;
	КонецЕсли;
	
	ОчиститьСообщения();
	СообщениеОбОшибке = "";
	
	ИменаУдаляемыхПредметов = Новый Массив;
	Для Каждого ВыделеннаяСтр Из ВыделенныеСтрокиПредметов Цикл
		Если ВыделеннаяСтр.ДоступноУдаление Тогда
			ИменаУдаляемыхПредметов.Добавить(ВыделеннаяСтр.ИмяПредмета);
		КонецЕсли;
	КонецЦикла;
	
	Если ИменаУдаляемыхПредметов.Количество() = 0 Тогда
		
		КоличествоВыделенныхСтрок = ВыделенныеСтрокиПредметов.Количество();
		Если КоличествоВыделенныхСтрок = 1 Тогда
			ТекстСообщения = НСтр("ru = 'Удалить текущий предмет можно только в карточке процесса.'; en = 'The current subject can be deleted in the process form only.'");
		Иначе
			ТекстСообщения = НСтр("ru = 'Удалить выделенные предметы можно только в карточке процесса.'; en = 'Selected subjects can be deleted in the process form only.'");
		КонецЕсли;
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			ТекстСообщения,,
			"ДеревоПриложений");
		Возврат;
	КонецЕсли;
	
	Если Не МультипредметностьКлиент.УдалитьПредметыЗадачи(ЭтаФорма, СообщениеОбОшибке, ИменаУдаляемыхПредметов) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			СообщениеОбОшибке,,
			"ДеревоПриложений");
		Возврат;
	КонецЕсли;
	
	ОбновитьДеревоПриложений();
	УстановитьПредметСервер();
	
КонецПроцедуры

&НаСервере
Процедура ОтображатьУдаленныеПриложенияСервер()
	
	РаботаСБизнесПроцессамиВызовСервера.ЗаполнитьДеревоПриложений(ЭтаФорма);
	ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(
		ИмяФормы,
		"ОтображатьУдаленныеПриложения",
		ОтображатьУдаленныеПриложения);
	
	КонецПроцедуры
	
#КонецОбласти

#Область СлужебныеПроцедурыИФункции_Хронометраж

&НаСервере
Процедура ПереключитьХронометражСервер(ПараметрыОповещения) Экспорт
	
	УчетВремени.ПереключитьХронометражСервер(
	ПараметрыОповещения,
	ДатаНачалаХронометража,
	ДатаКонцаХронометража,
	ВключенХронометраж,
	Объект.Ссылка,
	ВидыРабот,
	ЭтаФорма.Команды.ПереключитьХронометраж,
	ЭтаФорма.Элементы.ПереключитьХронометраж);
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьВОтчетИОбновитьФорму(ПараметрыОтчета, ПараметрыОповещения) Экспорт
	
	УчетВремени.ДобавитьВОтчетИОбновитьФорму(
		ПараметрыОтчета, 
		ПараметрыОповещения,
		ДатаНачалаХронометража,
		ДатаКонцаХронометража,
		ВключенХронометраж,
		ЭтаФорма.Команды.ПереключитьХронометраж,
		ЭтаФорма.Элементы.ПереключитьХронометраж);
	
КонецПроцедуры

&НаСервере
Процедура ОтключитьХронометражСервер() Экспорт
	
	УчетВремени.ОтключитьХронометражСервер(
	ДатаНачалаХронометража,
	ДатаКонцаХронометража,
	ВключенХронометраж,
	Объект.Ссылка,
	ЭтаФорма.Команды.ПереключитьХронометраж,
	ЭтаФорма.Элементы.ПереключитьХронометраж);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции_ШаблоныРезолюций

&НаСервере
Процедура ЗаполнитьШаблоныКоманд()
	
	ТаблицаКоманд = БизнесПроцессыИЗадачиСервер.ПолучитьТаблицуШаблоновРезолюций(
		ТекущийПользователь);
	КоличествоКоманд = ТаблицаКоманд.Количество();
	ШаблоныКоманд.Очистить();
	
	Ит = 0;
	Если КоличествоКоманд = 0 Тогда
		Возврат;
	ИначеЕсли КоличествоКоманд < 3 Тогда
		Для Каждого СтрокаТаблицы Из ТаблицаКоманд Цикл
			ЭтаФорма["Шаблон"+Ит] = СтрокаТаблицы.Ссылка;
			Ит = Ит + 1;
		КонецЦикла;
	Иначе
		Для Каждого СтрокаТаблицы Из ТаблицаКоманд Цикл
			ДобавитьШаблонВыполнения(СокрЛП(СтрокаТаблицы.Наименование), СтрокаТаблицы.Ссылка);
			Ит = Ит + 1;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьВидимостьКомандРассмотретьПоШаблону()
	
	Элементы.РассмотретьПоШаблону.Видимость = Ложь;
	Элементы.РассмотретьПоШаблону0.Видимость = Ложь;
	Элементы.РассмотретьПоШаблону1.Видимость = Ложь;
	
	Если КоличествоКоманд = 0 Тогда
		Возврат;
	ИначеЕсли КоличествоКоманд < 3 Тогда
		Ит = 0;
		Пока Ит < КоличествоКоманд Цикл
			Элементы["РассмотретьПоШаблону"+Ит].Заголовок = Лев(ЭтаФорма["Шаблон" + Ит], 30);
			Элементы["РассмотретьПоШаблону"+Ит].Видимость = Истина;
			Ит = Ит + 1;
		КонецЦикла;
	Иначе
		Элементы.РассмотретьПоШаблону.Видимость = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьКомандыРассмотренияПоШаблону()
	
	ЗаполнитьШаблоныКоманд();
	ОбновитьВидимостьКомандРассмотретьПоШаблону();
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьШаблонВыполнения(Заголовок, Шаблон)
	
	КомандаРассмотретьПоШаблону = ШаблоныКоманд.Добавить();
	КомандаРассмотретьПоШаблону.Заголовок = Заголовок;
	КомандаРассмотретьПоШаблону.Шаблон = Шаблон;
	
КонецПроцедуры

&НаСервере
Процедура ЗаменитьЗаголовокКоманды(СтруктураШаблона)
	
	Для Каждого Строка Из ШаблоныКоманд Цикл 
		Если Строка.Шаблон = СтруктураШаблона.Шаблон Тогда
			Строка.Заголовок = СтруктураШаблона.Наименование;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура РассмотретьПоШаблонуПродолжение(Шаблон) 
	
	ТекстШаблона = ПолучитьТекстШаблона(Шаблон);
	
	Если ЗначениеЗаполнено(Объект.РезультатВыполнения) Тогда 
		ТекстРезолюции = Объект.РезультатВыполнения + Символы.ПС + ТекстШаблона;
	Иначе 
		ТекстРезолюции = ТекстШаблона;
	КонецЕсли;
	
	ВариантРассмотрения = ВариантРассмотренияВвестиТекстРезолюции;
	ОбработатьВыполнение();
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьТекстШаблона(Шаблон)
	
	Возврат Справочники.ШаблоныТекстов.ПолучитьТекстШаблона(Шаблон);
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции_СрокиИсполненияПроцессов

// Заполняет представление сроков в карточке процесса
//
&НаСервере
Процедура ОбновитьСрокиИсполненияНаСервере(ОбновитьВсеСроки = Ложь) Экспорт
	
	Если Не Объект.Выполнена Тогда
	
		Если Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаНаИсполнение
			Или ОбновитьВсеСроки Тогда
			
			СтруктураДляРасчета = СрокиИсполненияПроцессов.СтруктураДляРасчетаСрокаПроцессаИсполнения();
			СтруктураДляРасчета.Ссылка = БизнесПроцессы.Исполнение.ПустаяСсылка();
			СтруктураДляРасчета.ВариантИсполнения = ВариантИсполнения;
			СтруктураДляРасчета.КоличествоИтераций = КоличествоИтерацийИсполнения;
			СтруктураДляРасчета.Проверяющий = ПроверяющийИсполнения;
			СтруктураДляРасчета.ВариантУстановкиСрокаОбработкиРезультатов = ВариантУстановкиСрокаОбработкиРезультатовИсполнения;
			СтруктураДляРасчета.СрокОбработкиРезультатов = СрокОбработкиРезультатовИсполнения;
			СтруктураДляРасчета.СрокОбработкиРезультатовДни = СрокОбработкиРезультатовДниИсполнения;
			СтруктураДляРасчета.СрокОбработкиРезультатовЧасы = СрокОбработкиРезультатовЧасыИсполнения;
			СтруктураДляРасчета.СрокОбработкиРезультатовМинуты = СрокОбработкиРезультатовМинутыИсполнения;
			СтруктураДляРасчета.Исполнители = ИсполнителиИсполнения.Выгрузить();
			
			ПараметрыДляРасчетаСроков = СрокиИсполненияПроцессов.ПараметрыДляРасчетаСроков();
			ПараметрыДляРасчетаСроков.ДатаОтсчета = ДатаОтсчетаДляРасчетаСроков;
			ПараметрыДляРасчетаСроков.РеквизитТаблицаСИзмененнымСроком = РеквизитТаблицаСИзмененнымСроком;
			ПараметрыДляРасчетаСроков.ИндексСтроки = ИндексСтрокиСИзмененнымСроком;
			
			СрокиИсполненияПроцессов.РассчитатьСрокиПроцессаИсполнения(
				СтруктураДляРасчета, ПараметрыДляРасчетаСроков);
			
			СрокОбработкиРезультатовИсполнения = СтруктураДляРасчета.СрокОбработкиРезультатов;
			СрокОбработкиРезультатовДниИсполнения = СтруктураДляРасчета.СрокОбработкиРезультатовДни;
			СрокОбработкиРезультатовЧасыИсполнения = СтруктураДляРасчета.СрокОбработкиРезультатовЧасы;
			СрокОбработкиРезультатовМинутыИсполнения = СтруктураДляРасчета.СрокОбработкиРезультатовМинуты;
			
			ИндексТекущейСтроки = ИсполнителиИсполнения.Количество() - 1;
			Пока ИндексТекущейСтроки >= 0 Цикл
				ТекущаяСтрока = ИсполнителиИсполнения[ИндексТекущейСтроки];
				СтрСоСроком = СтруктураДляРасчета.Исполнители[ИндексТекущейСтроки];
				ЗаполнитьЗначенияСвойств(ТекущаяСтрока, СтрСоСроком,
					"СрокИсполнения, СрокИсполненияДни, СрокИсполненияЧасы, СрокИсполненияМинуты");
				ИндексТекущейСтроки = ИндексТекущейСтроки - 1;
			КонецЦикла;
			
			СрокИсполненияПроцессаИсполнения = СтруктураДляРасчета.СрокИсполненияПроцесса;
			
		КонецЕсли;
			
		Если Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаОзнакомление
			Или ОбновитьВсеСроки Тогда
			
			СтруктураДляРасчета = СрокиИсполненияПроцессов.СтруктураДляРасчетаСрокаОзнакомления();
			СтруктураДляРасчета.Ссылка = БизнесПроцессы.Ознакомление.ПустаяСсылка();
			СтруктураДляРасчета.Исполнители = ИсполнителиОзнакомления.Выгрузить();
			
			ПараметрыДляРасчетаСроков = СрокиИсполненияПроцессов.ПараметрыДляРасчетаСроков();
			ПараметрыДляРасчетаСроков.ДатаОтсчета = ДатаОтсчетаДляРасчетаСроков;
			
			СрокиИсполненияПроцессов.РассчитатьСрокиОзнакомления(
				СтруктураДляРасчета, ПараметрыДляРасчетаСроков);
			
			ИндексТекущейСтроки = ИсполнителиОзнакомления.Количество() - 1;
			Пока ИндексТекущейСтроки >= 0 Цикл
				ТекущаяСтрока = ИсполнителиОзнакомления[ИндексТекущейСтроки];
				СтрСоСроком = СтруктураДляРасчета.Исполнители[ИндексТекущейСтроки];
				ЗаполнитьЗначенияСвойств(ТекущаяСтрока, СтрСоСроком,
					"СрокИсполнения, СрокИсполненияДни, СрокИсполненияЧасы, СрокИсполненияМинуты");
				ИндексТекущейСтроки = ИндексТекущейСтроки - 1;
			КонецЦикла;
			
			СрокИсполненияПроцессаОзнакомления = СтруктураДляРасчета.СрокИсполненияПроцесса;
			
		КонецЕсли;
		
		РеквизитТаблицаСИзмененнымСроком = "";
		ИндексСтрокиСИзмененнымСроком = 0;
		
	КонецЕсли;
	
	ОбновитьПризнакиИстекшихСроков();
	ЗаполнитьПредставлениеСроковИсполненияОзнакомления(ЭтаФорма);
	
КонецПроцедуры

// см. ОбновитьСрокиИсполненияНаСервере
&НаКлиенте
Процедура ОбновитьСрокиИсполнения()
	
	ОбновитьСрокиИсполненияНаСервере();
	
КонецПроцедуры

// см. ОбновитьСрокиИсполнения
&НаКлиенте
Процедура ОбновитьСрокиИсполненияОтложенно(РеквизитТаблица = "", ИндексСтроки = 0) Экспорт
	
	РеквизитТаблицаСИзмененнымСроком = РеквизитТаблица;
	ИндексСтрокиСИзмененнымСроком = ИндексСтроки;
	
	ПодключитьОбработчикОжидания("ОбновитьСрокиИсполнения", 0.2, Истина);
	
КонецПроцедуры

// Заполняет представление сроков исполнения в карточке процесса.
//
&НаКлиенте
Процедура ЗаполнитьПредставлениеСроковИсполнения() Экспорт
	
	ЗаполнитьПредставлениеСроковИсполненияОзнакомления(ЭтаФорма);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ЗаполнитьПредставлениеСроковИсполненияОзнакомления(Форма)
	
	Если Форма.Элементы.ГруппаСтраницы.ТекущаяСтраница = Форма.Элементы.ГруппаНаИсполнение Тогда
		
		СрокиИсполненияПроцессовКлиентСервер.ЗаполнитьПредставлениеСроковВТаблицеИсполнителей(
			Форма.ИсполнителиИсполнения, Форма.ИспользоватьДатуИВремяВСрокахЗадач);
			
		СрокиИсполненияПроцессовКлиентСервер.ЗаполнитьПредставлениеСроковУчастника(
			Форма.СрокОбработкиРезультатовИсполненияПредставление,
			Форма.СрокОбработкиРезультатовИсполнения,
			Форма.СрокОбработкиРезультатовДниИсполнения,
			Форма.СрокОбработкиРезультатовЧасыИсполнения,
			Форма.СрокОбработкиРезультатовМинутыИсполнения,
			Форма.ВариантУстановкиСрокаОбработкиРезультатовИсполнения,
			Форма.ИспользоватьДатуИВремяВСрокахЗадач);
		
		СрокиИсполненияПроцессовКлиентСервер.ЗаполнитьПредставлениеСрокаИсполненияПроцесса(
			Форма.СрокИсполненияПроцессаИсполненияПредставление,
			Форма.СрокИсполненияПроцессаИсполнения, 0,0,0,
			Форма.ИспользоватьДатуИВремяВСрокахЗадач);
		
	ИначеЕсли Форма.Элементы.ГруппаСтраницы.ТекущаяСтраница = Форма.Элементы.ГруппаОзнакомление Тогда
		
		СрокиИсполненияПроцессовКлиентСервер.ЗаполнитьПредставлениеСроковВТаблицеИсполнителей(
			Форма.ИсполнителиОзнакомления, Форма.ИспользоватьДатуИВремяВСрокахЗадач);
			
		СрокиИсполненияПроцессовКлиентСервер.ЗаполнитьПредставлениеСрокаИсполненияПроцесса(
			Форма.СрокИсполненияПроцессаОзнакомленияПредставление,
			Форма.СрокИсполненияПроцессаОзнакомления, 0,0,0,
			Форма.ИспользоватьДатуИВремяВСрокахЗадач);
		
	КонецЕсли;
	
КонецПроцедуры

// Возвращает сроки процесса для проверки корректности заполнения.
//
// Возвращаемое значение:
//  Структура:
//   * Исполнители - ДанныеФормыКоллекция - соотвествует реквизитам ИсполнителиИсполнения или ИсполнителиОзнакомления
//   * Проверяющий - СправочникСсылка.Пользователи,
//                   СправочникСсылка.ПолныеРоли - соотвествует реквизиту ПроверяющийИсполнения
//   * СрокОбработкиРезультатов - Дата - соотвествует реквизиту СрокОбработкиРезультатовИсполнения
//   * СрокИсполненияПроцесса - Дата - соотвествует реквизитам СрокИсполненияПроцессаИсполнения или СрокИсполненияПроцессаОзнакомления
//
&НаКлиенте
Функция СтруктураСроковДляПроверкиКорректностиЗаполнения()
	
	СтруктураСроковДляПроверки = Новый Структура;
	
	Если ВариантРассмотрения = 
		ПредопределенноеЗначение("Перечисление.ВариантыРассмотрения.НаправитьНаИсполнение") Тогда
		
		СтруктураСроковДляПроверки.Вставить("Исполнители", ИсполнителиИсполнения);
		СтруктураСроковДляПроверки.Вставить("Проверяющий", ПроверяющийИсполнения);
		СтруктураСроковДляПроверки.Вставить("СрокОбработкиРезультатов", СрокОбработкиРезультатовИсполнения);
		СтруктураСроковДляПроверки.Вставить("СрокИсполненияПроцесса", СрокИсполненияПроцессаИсполнения);
		
	ИначеЕсли ВариантРассмотрения = 
		ПредопределенноеЗначение("Перечисление.ВариантыРассмотрения.НаправитьНаОзнакомление") Тогда
		
		СтруктураСроковДляПроверки.Вставить("Исполнители", ИсполнителиОзнакомления);
		СтруктураСроковДляПроверки.Вставить("СрокИсполненияПроцесса", СрокИсполненияПроцессаОзнакомления);
		
	КонецЕсли;
	
	Возврат СтруктураСроковДляПроверки;
	
КонецФункции

// Устанавливает условное оформление истекших сроков.
//
&НаСервере
Процедура УстановитьУсловноеОформлениеИстекшихСроков()
	
	СрокиИсполненияПроцессов.УстановитьУсловноеОформлениеИстекшегоСрока(
		ЭтаФорма,
		НСтр("ru = 'Срок исполнения истек (Исполнители исполнения)'; en = 'Due date is expired (Performers)'"),
		"ИсполнителиИсполнения.СрокИсполненияИстек",
		"ИсполнителиСрокИсполненияПредставление");
	
	СрокиИсполненияПроцессов.УстановитьУсловноеОформлениеИстекшегоСрока(
		ЭтаФорма,
		НСтр("ru = 'Срок обработки результатов исполнения истек'; en = 'Results processing due date is expired'"),
		"СрокОбработкиРезультатовИсполненияИстек",
		"СрокОбработкиРезультатовИсполненияПредставление");
		
	СрокиИсполненияПроцессов.УстановитьУсловноеОформлениеИстекшегоСрока(
		ЭтаФорма,
		НСтр("ru = 'Срок исполнения процесса исполнения истек'; en = 'Performance process due date is expired'"),
		"СрокИсполненияПроцессаИсполненияИстек",
		"СрокИсполненияПроцессаИсполненияПредставление");
	
	СрокиИсполненияПроцессов.УстановитьУсловноеОформлениеИстекшегоСрока(
		ЭтаФорма,
		НСтр("ru = 'Срок исполнения истек (Исполнители ознакомления)'; en = 'Due date is expired (Examination performers)'"),
		"ИсполнителиОзнакомления.СрокИсполненияИстек",
		"ИсполнителиОзнакомленияСрокИсполненияПредставление");
	
	СрокиИсполненияПроцессов.УстановитьУсловноеОформлениеИстекшегоСрока(
		ЭтаФорма,
		НСтр("ru = 'Срок исполнения процесса ознакомления истек'; en = 'Examination process due date is expired'"),
		"СрокИсполненияПроцессаОзнакомленияИстек",
		"СрокИсполненияПроцессаОзнакомленияПредставление");
	
КонецПроцедуры

// Обновляет признаки истекших сроков в карточке.
//
&НаСервере
Процедура ОбновитьПризнакиИстекшихСроков()
	
	ДатаИсполненияПоУмолчанию = ?(Объект.Выполнена, Объект.ДатаИсполнения, ТекущаяДатаСеанса());
	
	Если ВариантРассмотрения = 
		ПредопределенноеЗначение("Перечисление.ВариантыРассмотрения.НаправитьНаИсполнение") Тогда
		
		СрокиИсполненияПроцессов.ОбновитьПризнакИстекшихСроковВТаблицеИсполнителей(
			ИсполнителиИсполнения, ДатаИсполненияПоУмолчанию);
		
		СрокиИсполненияПроцессов.ОбновитьПризнакИстекшегоСрокаУчастника(
			СрокОбработкиРезультатовИсполнения,
			СрокОбработкиРезультатовИсполненияИстек,
			ДатаИсполненияПоУмолчанию,
			"Проверяющий");
		
		СрокиИсполненияПроцессов.ОбновитьПризнакИстекшегоСрокаПроцесса(
			СрокИсполненияПроцессаИсполнения,
			ДатаИсполненияПоУмолчанию,
			СрокИсполненияПроцессаИсполненияИстек);
		
	ИначеЕсли ВариантРассмотрения = 
		ПредопределенноеЗначение("Перечисление.ВариантыРассмотрения.НаправитьНаОзнакомление") Тогда
		
		СрокиИсполненияПроцессов.ОбновитьПризнакИстекшихСроковВТаблицеИсполнителей(
			ИсполнителиОзнакомления, ДатаИсполненияПоУмолчанию);
			
		СрокиИсполненияПроцессов.ОбновитьПризнакИстекшегоСрокаПроцесса(
			СрокИсполненияПроцессаОзнакомления,
			ДатаИсполненияПоУмолчанию,
			СрокИсполненияПроцессаОзнакомленияИстек);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
