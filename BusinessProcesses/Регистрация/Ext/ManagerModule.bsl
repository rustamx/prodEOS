#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает текущих участников процесса в виде структуры
//
// Параметры:
//   Процесс
//      БизнесПроцессОбъект
//      БизнесПроцессСсылка
//
// Возвращаемое значение:
//   Структура
//
Функция ТекущиеУчастникиПроцесса(Процесс) Экспорт
	
	РеквизитыПроцессаСтрокой = 
		"Автор,
		|СрокОбработкиРезультатов,
		|СрокОбработкиРезультатовДни,
		|СрокОбработкиРезультатовЧасы,
		|СрокОбработкиРезультатовМинуты,
		|Исполнитель,
		|СрокИсполнения,
		|СрокИсполненияДни,
		|СрокИсполненияЧасы,
		|СрокИсполненияМинуты";
	
	Участники = Новый Структура(РеквизитыПроцессаСтрокой);
		
	Если ОбщегоНазначения.ЭтоСсылка(ТипЗнч(Процесс)) Тогда
		РеквизитыПроцессаПоСсылке = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
			Процесс, РеквизитыПроцессаСтрокой);
		ЗаполнитьЗначенияСвойств(Участники, РеквизитыПроцессаПоСсылке);
	Иначе
		ЗаполнитьЗначенияСвойств(Участники, Процесс);
	КонецЕсли;
	
	Возврат Участники;
	
КонецФункции

Функция ПлановыеТрудозатратыИсполнителяЗадачи(БизнесПроцесс, Задача, ТочкаМаршрута) Экспорт
	
	РеквизитыПроцесса = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(БизнесПроцесс,
		"ТрудозатратыПланИсполнителя");
		
	Трудозатраты = 0;
		
	Если ТочкаМаршрута = ТочкиМаршрута.Зарегистрировать Тогда
		Трудозатраты = РеквизитыПроцесса.ТрудозатратыПланИсполнителя;
	КонецЕсли;
	
	Возврат Трудозатраты;
	
КонецФункции

Функция ПредставлениеРезультатаЗадачи(ЗадачаОбъект) Экспорт
	
	Представление = "";
	
	Если ЗадачаОбъект.ТочкаМаршрута = ТочкиМаршрута.Зарегистрировать Тогда
		
		РезультатыРегистрации = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(
			ЗадачаОбъект.БизнесПроцесс, "РезультатыРегистрации").Выгрузить();
			
		НайденнаяСтрока = РезультатыРегистрации.Найти(ЗадачаОбъект.Ссылка, "ЗадачаИсполнителя");
		
		Если НайденнаяСтрока <> Неопределено Тогда 
			Представление = Строка(НайденнаяСтрока.РезультатРегистрации) + ".";
		КонецЕсли;
		
	ИначеЕсли ЗадачаОбъект.ТочкаМаршрута = ТочкиМаршрута.Ознакомиться Тогда
		
		РеквизитыПроцесса = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ЗадачаОбъект.БизнесПроцесс,
			"РезультатРегистрации, РезультатыОзнакомлений");
		
		РезультатыОзнакомлений = РеквизитыПроцесса.РезультатыОзнакомлений.Выгрузить();
		
		НайденнаяСтрока = РезультатыОзнакомлений.Найти(ЗадачаОбъект.Ссылка, "ЗадачаИсполнителя");
		Если НайденнаяСтрока <> Неопределено Тогда 
			
			Если НайденнаяСтрока.ОтправленоНаПовторнуюРегистрацию Тогда
				Представление = НСТР("ru = 'Отправлено на повторную регистрацию.'; en = 'Sent for repeat registration.'");
			Иначе
				Если РеквизитыПроцесса.РезультатРегистрации = Перечисления.РезультатыРегистрации.НеЗарегистрировано Тогда
					Представление = НСТР("ru = 'Регистрация прекращена.'; en = 'Registration terminated.'");
				Иначе
					Представление = НСТР("ru = 'Ознакомился.'; en = 'Examined.'");
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Представление;
	
КонецФункции

// Заполняет html обзор данными процесса.
//
// Параметры:
//   HTMLТекст - Строка
//   Шаблон - БизнесПроцессСсылка.Регистрация - ссылка на процесс
//
Процедура ЗаполнитьОбзорПроцесса(HTMLТекст, Процесс) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	РеквизитыПроцесса = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Процесс,
		"Исполнитель,
		|СрокИсполненияПроцесса,
		|СрокОбработкиРезультатов,
		|СрокОбработкиРезультатовДни,
		|СрокОбработкиРезультатовМинуты,
		|СрокОбработкиРезультатовЧасы,
		|СрокИсполнения,
		|СрокИсполненияДни,
		|СрокИсполненияЧасы,
		|СрокИсполненияМинуты,
		|СрокИсполненияПроцесса,
		|ВариантУстановкиСрокаИсполнения,
		|ВариантУстановкиСрокаОбработкиРезультатов,
		|КоличествоИтераций,
		|СрокИсполненияПроцесса,
		|ДатаЗавершения,
		|РезультатРегистрации,
		|Завершен");
		
	Исполнитель = РеквизитыПроцесса.Исполнитель;
	СрокИсполненияПроцесса = РеквизитыПроцесса.СрокИсполненияПроцесса;
	ДатаЗавершения = РеквизитыПроцесса.ДатаЗавершения;
	
	ДатыИсполненияУчастников = ДатыИсполненияУчастников(Процесс);
	
	ЦветЗакрытыеНеактуальныеЗаписи = ОбзорПроцессовВызовСервера.ЦветЗакрытыеНеактуальныеЗаписи();
	
	ЦветПросроченныеДанные = ОбзорПроцессовВызовСервера.ЦветПросроченныеДанные();
	
	ТекущаяДатаСеанса = ТекущаяДатаСеанса();
	
	ДатаИсполненияПоУмолчанию = ТекущаяДатаСеанса;
	Если ЗначениеЗаполнено(ДатаЗавершения) Тогда
		ДатаИсполненияПоУмолчанию = ДатаЗавершения;
	КонецЕсли;
	
	ИспользоватьДатуИВремяВСрокахЗадач = ПолучитьФункциональнуюОпцию("ИспользоватьДатуИВремяВСрокахЗадач");
	Если ИспользоватьДатуИВремяВСрокахЗадач Тогда
		ФорматСрока = "ДФ='dd.MM.yyyy HH:mm'";
	Иначе
		ФорматСрока = "ДФ='dd.MM.yyyy'";
	КонецЕсли;
	
	// Исполнитель
	Если ЗначениеЗаполнено(Исполнитель) Тогда
		HTMLТекст = HTMLТекст + "<p>";
		ОбзорОбъектовКлиентСервер.ДобавитьПодпись(HTMLТекст, НСтр("ru = 'Исполнитель: '; en = 'Performer: '"));
		
		ЗадачаИсполнителя = ОбзорПроцессовВызовСервера.ПолучитьЗадачуПроцесса(
			Процесс, БизнесПроцессы.Регистрация.ТочкиМаршрута.Зарегистрировать);
		Если ЗначениеЗаполнено(ЗадачаИсполнителя) Тогда
			
			ЗадачаМассив = Новый Массив;
			ЗадачаМассив.Добавить(ЗадачаИсполнителя);
			РезультатСоответствие 
				= ОбзорПроцессовВызовСервера.РезультатыВыполненияПоОбъектам(ЗадачаМассив, "Задача");
			Если РезультатСоответствие.Количество() <> 0 Тогда	
					
				РезультатВыполненияЗадачи = РезультатСоответствие.Получить(ЗадачаИсполнителя);
				Если РезультатВыполненияЗадачи <> Неопределено Тогда
					
					Картинка = ОбзорПроцессовВызовСервера.ПолучитьКартинкуПоСтатусуВыполнения(
						РезультатВыполненияЗадачи);
					
					ОбзорОбъектовКлиентСервер.ДобавитьКартинку(HTMLТекст, Картинка);
					
				КонецЕсли;
					
			КонецЕсли;		
			
		КонецЕсли;	
		
		ОбзорОбъектовКлиентСервер.ДобавитьЗначение(HTMLТекст, Исполнитель, "");
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(РеквизитыПроцесса.СрокИсполнения) 
		Или ЗначениеЗаполнено(РеквизитыПроцесса.СрокИсполненияДни) 
		Или ЗначениеЗаполнено(РеквизитыПроцесса.СрокИсполненияМинуты) 
		Или ЗначениеЗаполнено(РеквизитыПроцесса.СрокИсполненияЧасы) Тогда
		
		HTMLТекст = HTMLТекст + "<br>";
		
		ОбзорОбъектовКлиентСервер.ДобавитьПодпись(HTMLТекст, НСтр("ru = 'Срок регистрации:'; en = 'Registration due date:'"));
		
		ПредставлениеСрока = ОбзорПроцессовВызовСервера.ПредставлениеСрокаИсполнения(
			РеквизитыПроцесса.СрокИсполнения, 
			РеквизитыПроцесса.СрокИсполненияДни, 
			РеквизитыПроцесса.СрокИсполненияЧасы, 
			РеквизитыПроцесса.СрокИсполненияМинуты, 
			ИспользоватьДатуИВремяВСрокахЗадач, 
			РеквизитыПроцесса.ВариантУстановкиСрокаИсполнения);
			
		ДатаИсполнения = ДатыИсполненияУчастников.Получить("Исполнитель");
		Если ЗначениеЗаполнено(ДатаИсполнения) Тогда
			ДатаИсполнения = Мин(ДатаИсполненияПоУмолчанию, ДатаИсполнения);
		Иначе
			ДатаИсполнения = ДатаИсполненияПоУмолчанию;
		КонецЕсли;
		
		ДатаИсполнения = ДатаИсполнения - Секунда(ДатаИсполнения);
		
		ЦветПредставленияСрока = "";
		Если РеквизитыПроцесса.СрокИсполнения < ДатаИсполнения Тогда
			ЦветПредставленияСрока = ЦветПросроченныеДанные;
		КонецЕсли;
			
		ОбзорОбъектовКлиентСервер.ДобавитьЗначение(HTMLТекст, ПредставлениеСрока, ЦветПредставленияСрока);
		
	КонецЕсли;
		
	HTMLТекст = HTMLТекст + "<p>";
	HTMLТекст = HTMLТекст + СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		"<A href=v8doc:%1>%2</A>",
		"Подзадачи_" + Процесс.УникальныйИдентификатор() + "_Регистрация",
		НСтр("ru = 'Все задачи'; en = 'All tasks'"));
		
	Если ЗначениеЗаполнено(РеквизитыПроцесса.СрокОбработкиРезультатов) 
		Или ЗначениеЗаполнено(РеквизитыПроцесса.СрокОбработкиРезультатовДни) 
		Или ЗначениеЗаполнено(РеквизитыПроцесса.СрокОбработкиРезультатовМинуты) 
		Или ЗначениеЗаполнено(РеквизитыПроцесса.СрокОбработкиРезультатовЧасы) Тогда
		
		HTMLТекст = HTMLТекст + "<p>";
		
		ОбзорОбъектовКлиентСервер.ДобавитьПодпись(HTMLТекст, НСтр("ru = 'Срок обработки результата:'; en = 'Result processing due date:'"));
		
		ПредставлениеСрока = ОбзорПроцессовВызовСервера.ПредставлениеСрокаИсполнения(
			РеквизитыПроцесса.СрокОбработкиРезультатов, 
			РеквизитыПроцесса.СрокОбработкиРезультатовДни, 
			РеквизитыПроцесса.СрокОбработкиРезультатовЧасы, 
			РеквизитыПроцесса.СрокОбработкиРезультатовМинуты, 
			ИспользоватьДатуИВремяВСрокахЗадач, 
			РеквизитыПроцесса.ВариантУстановкиСрокаОбработкиРезультатов);
			
		ЦветПредставленияСрока = "";
		Если Не РеквизитыПроцесса.Завершен
			Или РеквизитыПроцесса.РезультатРегистрации =
				Перечисления.РезультатыРегистрации.НеЗарегистрировано Тогда
			
			ДатаИсполнения = ДатыИсполненияУчастников.Получить("Автор");
			Если ЗначениеЗаполнено(ДатаИсполнения) Тогда
				ДатаИсполнения = Мин(ДатаИсполненияПоУмолчанию, ДатаИсполнения);
			Иначе
				ДатаИсполнения = ДатаИсполненияПоУмолчанию;
			КонецЕсли;
			
			ДатаИсполнения = ДатаИсполнения - Секунда(ДатаИсполнения);
			
			Если РеквизитыПроцесса.СрокОбработкиРезультатов < ДатаИсполнения Тогда
				ЦветПредставленияСрока = ЦветПросроченныеДанные;
			КонецЕсли;
		КонецЕсли;
		
		ОбзорОбъектовКлиентСервер.ДобавитьЗначение(HTMLТекст, ПредставлениеСрока, ЦветПредставленияСрока);
		
	КонецЕсли;
		
	Если ЗначениеЗаполнено(СрокИсполненияПроцесса) Тогда
		
		HTMLТекст = HTMLТекст + "<p>";
		
		Если Не ЗначениеЗаполнено(ДатаЗавершения) Тогда
			ДатаЗавершения = ТекущаяДатаСеанса;
		КонецЕсли;
		
		ДатаЗавершения = ДатаЗавершения - Секунда(ДатаЗавершения);
		
		ЦветПредставленияСрока = "";
		Если СрокИсполненияПроцесса < ДатаЗавершения Тогда
			ЦветПредставленияСрока = ЦветПросроченныеДанные;
		КонецЕсли;
		
		ОбзорОбъектовКлиентСервер.ДобавитьПодпись(HTMLТекст, НСтр("ru = 'Срок процесса:'; en = 'Process due date:'"));
		ОбзорОбъектовКлиентСервер.ДобавитьЗначение(
			HTMLТекст, Формат(СрокИсполненияПроцесса, ФорматСрока), ЦветПредставленияСрока);
		
		Если РеквизитыПроцесса.КоличествоИтераций <> 0 Тогда
			
			HTMLТекст = HTMLТекст + " (";
			
			ОбзорОбъектовКлиентСервер.ДобавитьПодпись(HTMLТекст, НСтр("ru = 'Кол. циклов:'; en = 'Cycle count:'"));
			ОбзорОбъектовКлиентСервер.ДобавитьЗначение(HTMLТекст, 
				Формат(РеквизитыПроцесса.КоличествоИтераций, "ЧЦ=2"), "");
			
			HTMLТекст = HTMLТекст + ")";
			
		КонецЕсли;		
		
	КонецЕсли;
		
КонецПроцедуры

// Определяет список команд заполнения.
//
// Параметры:
//   КомандыЗаполнения - ТаблицаЗначений - Таблица с командами заполнения. Для изменения.
//       См. описание 1 параметра процедуры ЗаполнениеОбъектовПереопределяемый.ПередДобавлениемКомандЗаполнения().
//   Параметры - Структура - Вспомогательные параметры. Для чтения.
//       См. описание 2 параметра процедуры ЗаполнениеОбъектовПереопределяемый.ПередДобавлениемКомандЗаполнения().
//
Процедура ДобавитьКомандыЗаполнения(КомандыЗаполнения, Параметры) Экспорт
КонецПроцедуры

// Возвращает признак того что процесс использует условия выполнения задач.
//
// Параметры:
//  ТочкаМаршрута - ТочкаМаршрутаСсылка - Точка маршрута.
//  Параметры - Структура - Параметры.
// 
// Возвращаемое значение:
//  Булево - Использует условия выполнения задач.
//
Функция ИспользуетУсловияЗапретаВыполненияЗадач(ТочкаМаршрута = Неопределено, Параметры = Неопределено) Экспорт
	
	Если Параметры = Неопределено Тогда
		Параметры = Новый Структура;
	КонецЕсли;
	
	ИспользуетУсловияЗапретаВыполненияЗадач = Истина;
	
	Если ТочкаМаршрута = ТочкиМаршрута.Зарегистрировать Тогда
		
		Если Параметры.Свойство("ВариантВыполнения") Тогда
			ИспользуетУсловияЗапретаВыполненияЗадач =
				Параметры.ВариантВыполнения <>
					Перечисления.ВариантыВыполненияПроцессовИЗадач.Отрицательно;
		ИначеЕсли Параметры.Свойство("РезультатРегистрации") Тогда
			ИспользуетУсловияЗапретаВыполненияЗадач =
				Параметры.РезультатРегистрации <>
					Перечисления.РезультатыРегистрации.НеЗарегистрировано;
		КонецЕсли;
		
	ИначеЕсли ТочкаМаршрута = ТочкиМаршрута.Ознакомиться Тогда
		
		Если Параметры.Свойство("ВариантВыполнения") Тогда
			ИспользуетУсловияЗапретаВыполненияЗадач =
				Параметры.ВариантВыполнения <>
					Перечисления.ВариантыВыполненияПроцессовИЗадач.Отрицательно;
		ИначеЕсли Параметры.Свойство("ПовторитьРегистрацию") Тогда
			ИспользуетУсловияЗапретаВыполненияЗадач = Не Параметры.ПовторитьРегистрацию;
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат ИспользуетУсловияЗапретаВыполненияЗадач;
	
КонецФункции

// Формирует комментарий автоматического выполнения задачи.
//
// Параметры:
//  ТочкаМаршрута - ТочкаМаршрута - Точка маршрута.
//  ВариантВыполнения - Булево - Вариант выполнения.
//
// Возвращаемое значение:
//  Строка - Комментарий автоматического выполнения задачи.
//
Функция КомментарийВыполненаАвтоматически(ТочкаМаршрута, ВариантВыполнения) Экспорт
	
	Комментарий = "";
	
	Если ТочкаМаршрута = ТочкиМаршрута.Зарегистрировать Тогда
		Комментарий = НСтр("ru = 'Не зарегистрировано автоматически'; en = 'Not registered automatically'");
	ИначеЕсли ТочкаМаршрута = ТочкиМаршрута.Ознакомиться Тогда
		Комментарий = НСтр("ru = 'Ознакомился автоматически'; en = 'Examined automatically'");
	КонецЕсли;
	
	Возврат Комментарий;
	
КонецФункции

#КонецОбласти

#Область ПрограммныйИнтерфейс_РабочиеГруппы

// Возвращает признак наличия метода ДобавитьУчастниковВТаблицу у менеджера объекта
//
Функция ЕстьМетодДобавитьУчастниковВТаблицу() Экспорт
	Возврат Истина;
КонецФункции

// Добавляет участников бизнес-процесса в переданную таблицу
//
Процедура ДобавитьУчастниковВТаблицу(ТаблицаНабора, БизнесПроцесс) Экспорт
	
	РаботаСБизнесПроцессами.ДобавитьУчастниковРегистрацииВТаблицу(ТаблицаНабора, БизнесПроцесс);
	
КонецПроцедуры

#КонецОбласти

#Область ПрограммныйИнтерфейс_Предметы

// Возвращает участников для проверки прав на предметы.
//
// Параметры:
//  Процесс - БизнесПроцессОбъект, БизнесПроцессСсылка - процесс
//
// Возвращаемое значение:
//  ТаблицаЗначений
//   * Участник
//   * Изменение
//
Функция УчастникиДляПроверкиПрав(Процесс) Экспорт
	
	ТаблицаУчастников = РаботаСРабочимиГруппами.ПолучитьПустуюТаблицуУчастников();
	
	РаботаСБизнесПроцессами.ДобавитьУчастниковРегистрацииВТаблицу(ТаблицаУчастников, Процесс);
	
	Возврат ТаблицаУчастников;
	
КонецФункции

#КонецОбласти

#Область ПрограммныйИнтерфейс_ВыполнениеЗадач

// Обработчик, вызываемый перед выполнением задачи из процедуры
// ВыполнениеЗадачСервер.ВыполнитьЗадачуСПараметрами
//
// Параметры
//   ЗадачаСсылка  – ЗадачаСсылка.ЗадачаИсполнителя
//   БизнесПроцессСсылка - БизнесПроцессСсылка
//   ТочкаМаршрутаСсылка – точка маршрута
//   ПараметрыВыполнения - Структура
//
Процедура ОбработкаПередВыполнениемЗадачи(
	ЗадачаСсылка, БизнесПроцессСсылка, ТочкаМаршрутаБизнесПроцесса, ПараметрыВыполнения) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если Не ПараметрыВыполнения.Свойство("УникальныйИдентификаторФормыИзмененияПараметров") Тогда
		ПараметрыВыполнения.Вставить("УникальныйИдентификаторФормыИзмененияПараметров", Новый УникальныйИдентификатор());
	КонецЕсли;
	
	Если ТочкаМаршрутаБизнесПроцесса = БизнесПроцессы.Регистрация.ТочкиМаршрута.Зарегистрировать Тогда
		
		ЗаписатьПроцесс = ПараметрыВыполнения.Свойство("РезультатРегистрации")
			Или ПараметрыВыполнения.Свойство("ИсполнительЗадачи");
			
		Если ЗаписатьПроцесс Тогда
			ЗаблокироватьДанныеДляРедактирования(
				БизнесПроцессСсылка,,
				ПараметрыВыполнения.УникальныйИдентификаторФормыИзмененияПараметров);
			
			РегистрацияОбъект = БизнесПроцессСсылка.ПолучитьОбъект();
		КонецЕсли;
			
		Если ПараметрыВыполнения.Свойство("РезультатРегистрации") Тогда
			РегистрацияОбъект.ДополнительныеСвойства.Вставить("ТекущаяЗадача", ЗадачаСсылка);
			РегистрацияОбъект.ДополнительныеСвойства.Вставить("РезультатРегистрации", ПараметрыВыполнения.РезультатРегистрации);
		КонецЕсли;
		
		Если ПараметрыВыполнения.Свойство("ИсполнительЗадачи")
			И ТипЗнч(РегистрацияОбъект.Исполнитель) <> Тип("СправочникСсылка.ПолныеРоли")
			И РегистрацияОбъект.Исполнитель <> ПараметрыВыполнения.ИсполнительЗадачи Тогда
			
			РегистрацияОбъект.Исполнитель = ПараметрыВыполнения.ИсполнительЗадачи;
			
		КонецЕсли;
		
		Если ЗаписатьПроцесс Тогда
			
			РаботаСБизнесПроцессами.ЗаписатьПроцесс(РегистрацияОбъект, "ЗаписьСОбработкойВыполненияЗадачи");
			
			РазблокироватьДанныеДляРедактирования(
				БизнесПроцессСсылка,
				ПараметрыВыполнения.УникальныйИдентификаторФормыИзмененияПараметров);
		КонецЕсли;
		
	ИначеЕсли ТочкаМаршрутаБизнесПроцесса = БизнесПроцессы.Регистрация.ТочкиМаршрута.Ознакомиться Тогда
		
		ЗаписатьПроцесс = ПараметрыВыполнения.Свойство("ПовторитьРегистрацию")
			Или ПараметрыВыполнения.Свойство("ИсполнительЗадачи");
			
		Если ЗаписатьПроцесс Тогда
			ЗаблокироватьДанныеДляРедактирования(
				БизнесПроцессСсылка,,
				ПараметрыВыполнения.УникальныйИдентификаторФормыИзмененияПараметров);
			
			РегистрацияОбъект = БизнесПроцессСсылка.ПолучитьОбъект();
		КонецЕсли;
			
		Если ПараметрыВыполнения.Свойство("ПовторитьРегистрацию") Тогда
			РегистрацияОбъект.ДополнительныеСвойства.Вставить("ТекущаяЗадача", ЗадачаСсылка);
			РегистрацияОбъект.ДополнительныеСвойства.Вставить("ПовторитьРегистрацию", ПараметрыВыполнения.ПовторитьРегистрацию);
			Если ПараметрыВыполнения.Свойство("Исполнитель") Тогда
				РегистрацияОбъект.ДополнительныеСвойства.Вставить("Исполнитель", ПараметрыВыполнения.Исполнитель);
			КонецЕсли;
			
			Если ПараметрыВыполнения.Свойство("СрокИсполненияПроцесса") Тогда
				РегистрацияОбъект.ДополнительныеСвойства.Вставить("СрокИсполненияПроцесса", ПараметрыВыполнения.СрокИсполненияПроцесса);
			КонецЕсли;
			
			Если ПараметрыВыполнения.Свойство("СрокИсполнения") Тогда
				РегистрацияОбъект.ДополнительныеСвойства.Вставить("СрокИсполнения", ПараметрыВыполнения.СрокИсполнения);
			КонецЕсли;
			Если ПараметрыВыполнения.Свойство("СрокИсполненияДни") Тогда
				РегистрацияОбъект.ДополнительныеСвойства.Вставить("СрокИсполненияДни", ПараметрыВыполнения.СрокИсполненияДни);
			КонецЕсли;
			Если ПараметрыВыполнения.Свойство("СрокИсполненияЧасы") Тогда
				РегистрацияОбъект.ДополнительныеСвойства.Вставить("СрокИсполненияЧасы", ПараметрыВыполнения.СрокИсполненияЧасы);
			КонецЕсли;
			Если ПараметрыВыполнения.Свойство("СрокИсполненияМинуты") Тогда
				РегистрацияОбъект.ДополнительныеСвойства.Вставить("СрокИсполненияМинуты", ПараметрыВыполнения.СрокИсполненияМинуты);
			КонецЕсли;
			Если ПараметрыВыполнения.Свойство("ВариантУстановкиСрокаИсполнения") Тогда
				РегистрацияОбъект.ДополнительныеСвойства.Вставить("ВариантУстановкиСрокаИсполнения", ПараметрыВыполнения.ВариантУстановкиСрокаИсполнения);
			КонецЕсли;
			
			Если ПараметрыВыполнения.Свойство("СрокОбработкиРезультатов") Тогда
				РегистрацияОбъект.ДополнительныеСвойства.Вставить("СрокОбработкиРезультатов", ПараметрыВыполнения.СрокОбработкиРезультатов);
			КонецЕсли;
			Если ПараметрыВыполнения.Свойство("СрокОбработкиРезультатовДни") Тогда
				РегистрацияОбъект.ДополнительныеСвойства.Вставить("СрокОбработкиРезультатовДни", ПараметрыВыполнения.СрокОбработкиРезультатовДни);
			КонецЕсли;
			Если ПараметрыВыполнения.Свойство("СрокОбработкиРезультатовЧасы") Тогда
				РегистрацияОбъект.ДополнительныеСвойства.Вставить("СрокОбработкиРезультатовЧасы", ПараметрыВыполнения.СрокОбработкиРезультатовЧасы);
			КонецЕсли;
			Если ПараметрыВыполнения.Свойство("СрокОбработкиРезультатовМинуты") Тогда
				РегистрацияОбъект.ДополнительныеСвойства.Вставить("СрокОбработкиРезультатовМинуты", ПараметрыВыполнения.СрокОбработкиРезультатовМинуты);
			КонецЕсли;
			Если ПараметрыВыполнения.Свойство("ВариантУстановкиСрокаОбработкиРезультатов") Тогда
				РегистрацияОбъект.ДополнительныеСвойства.Вставить("ВариантУстановкиСрокаОбработкиРезультатов", ПараметрыВыполнения.ВариантУстановкиСрокаОбработкиРезультатов);
			КонецЕсли;
			
			Если ПараметрыВыполнения.Свойство("КоличествоИтераций") Тогда
				РегистрацияОбъект.ДополнительныеСвойства.Вставить("КоличествоИтераций", ПараметрыВыполнения.КоличествоИтераций);
			КонецЕсли;
			
			Если ПараметрыВыполнения.Свойство("ПричинаПереносаСрока") Тогда
				РегистрацияОбъект.ДополнительныеСвойства.Вставить("ПричинаПереносаСрока", ПараметрыВыполнения.ПричинаПереносаСрока);
			КонецЕсли;
			
		КонецЕсли;
		
		Если ПараметрыВыполнения.Свойство("ИсполнительЗадачи")
			И ТипЗнч(РегистрацияОбъект.Автор) <> Тип("СправочникСсылка.ПолныеРоли")
			И РегистрацияОбъект.Автор <> ПараметрыВыполнения.ИсполнительЗадачи Тогда
			
			РегистрацияОбъект.Автор = ПараметрыВыполнения.ИсполнительЗадачи;
		КонецЕсли;
		
		Если ЗаписатьПроцесс Тогда
			
			РаботаСБизнесПроцессами.ЗаписатьПроцесс(РегистрацияОбъект, "ЗаписьСОбработкойВыполненияЗадачи");
			
			РазблокироватьДанныеДляРедактирования(
				БизнесПроцессСсылка,
				ПараметрыВыполнения.УникальныйИдентификаторФормыИзмененияПараметров);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

// Обработчик, вызываемый после выполнением задачи из процедуры
// ВыполнениеЗадачСервер.ВыполнитьЗадачуСПараметрами
//
// Параметры
//   ЗадачаСсылка  – ЗадачаСсылка.ЗадачаИсполнителя
//   БизнесПроцессСсылка - БизнесПроцессСсылка
//   ТочкаМаршрутаСсылка – точка маршрута
//   ПараметрыВыполнения - Структура
//
Процедура ОбработкаПослеВыполненияЗадачи(
	ЗадачаСсылка, БизнесПроцессСсылка, ТочкаМаршрутаБизнесПроцесса, ПараметрыВыполнения) Экспорт

КонецПроцедуры

#КонецОбласти

#Область ПрограммныйИнтерфейс_ПоддержкаКомплексныхПроцессов

// Показывает, может ли процесс использоваться в качестве части комплексного процесса
Функция МожетИспользоватьсяВКомплексномПроцессе() Экспорт
	
	Возврат Истина;
	
КонецФункции

// Возвращает возможные результаты выполнения действия комплексного процесса.
//
// Возвращаемое значение:
//  СписокЗначений
//   * Перечисление.РезультатыВыполненияДействийКомплексныхПроцессов
//
Функция РезультатыВыполненияДействияКомплексногоПроцесса() Экспорт
	
	РезультатыВыполнения = Новый СписокЗначений;
	
	ПеречислениеРезультатов = Перечисления.РезультатыВыполненияДействийКомплексныхПроцессов;
	
	РезультатыВыполнения.Добавить(
		ПеречислениеРезультатов.ЗавершеноСЛюбымРезультатом,
		НСтр("ru = 'Любой результат'; en = 'Any result'"));
	РезультатыВыполнения.Добавить(
		ПеречислениеРезультатов.Отрицательно,
		НСтр("ru = 'Не зарегистрировано'; en = 'Not registered'"));
	РезультатыВыполнения.Добавить(
		ПеречислениеРезультатов.Положительно,
		НСтр("ru = 'Зарегистрировано'; en = 'Registered'"));
	
	Возврат РезультатыВыполнения;
	
КонецФункции

#Область КэшДанныхДействий

// Возвращает выбору данных действий.
//
// Параметры:
//  ПараметрыДействий - ТаблицаЗначений - параметры действий, соответствует табличной части
//                                        ПараметрыДействий схемы процесса.
//
// Возвращаемое значение:
//  ВыборкаДанных
//
Функция ВыборкаДанныхДействий(ПараметрыДействий) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ПараметрыДействий.Процесс
		|ПОМЕСТИТЬ ПараметрыДействий
		|ИЗ
		|	&ПараметрыДействий КАК ПараметрыДействий
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Регистрация.Ссылка,
		|	Регистрация.Наименование,
		|	Регистрация.Исполнитель,
		|	Регистрация.СрокИсполненияПроцесса,
		|	Регистрация.Состояние,
		|	Регистрация.Завершен,
		|	Регистрация.ДатаЗавершения
		|ИЗ
		|	ПараметрыДействий КАК ПараметрыДействий
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ БизнесПроцесс.Регистрация КАК Регистрация
		|		ПО ПараметрыДействий.Процесс = Регистрация.Ссылка";
		
	Запрос.УстановитьПараметр("ПараметрыДействий", ПараметрыДействий);
	
	Возврат Запрос.Выполнить().Выбрать();
	
КонецФункции

// Возвращает данные процесса, являющегося действием комплексного процесса.
//
// Параметры:
//  Объект - БизнесПроцессСсылка.Регистрация, ВыборкаДанных
//
// Возвращаемое значение:
//  Структура - см. функцию РаботаСКомплекснымиБизнесПроцессамиКлиентСервер.СтруктураДанныхДействия
//
Функция ДанныеДействия(Объект) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ДанныеПроцесса = РаботаСКомплекснымиБизнесПроцессамиКлиентСервер.СтруктураДанныхДействия();
	
	Если ОбщегоНазначения.ЭтоСсылка(ТипЗнч(Объект)) Тогда
		РеквизитыОбъекта = 
			ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
				Объект, "Наименование, Исполнитель, СрокИсполненияПроцесса,
					|Состояние, Завершен, ДатаЗавершения");
	Иначе
		РеквизитыОбъекта = Объект;
	КонецЕсли;
	
	ДанныеПроцесса.Описание = НСтр("ru = 'Регистрация: '; en = 'Registration: '") + РеквизитыОбъекта.Наименование;
	
	ДанныеПроцесса.СрокИсполненияПроцесса = РеквизитыОбъекта.СрокИсполненияПроцесса;
	
	МассивИсполнителей = Новый Массив;
	РаботаСКомплекснымиБизнесПроцессамиСервер.ДобавитьИсполнителяПроцессаВМассив(
		МассивИсполнителей, РеквизитыОбъекта.Исполнитель);
		
	ДанныеПроцесса.Исполнители = 
		РаботаСКомплекснымиБизнесПроцессамиСервер.ИсполнителиСтрокой(МассивИсполнителей);
	
	ДанныеПроцесса.СостояниеПроцесса = РеквизитыОбъекта.Состояние;
	
	ДанныеПроцесса.ПроцессЗавершен = РеквизитыОбъекта.Завершен;
	
	ДанныеПроцесса.ДатаЗавершения = РеквизитыОбъекта.ДатаЗавершения;
	
	Возврат ДанныеПроцесса;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#Область ПрограммныйИнтерфейс_РИБ

Процедура ОбработатьПолучениеПроцессаИзУзлаРИБ(Узел, ЭлементДанных) Экспорт
	
	ЭтотУзел = РаботаСБизнесПроцессами.ЭтотУзелОбменаДляОбработкиПроцессов();
	
	НеПередающиесяПоля = Новый Массив;
	НеПередающиесяПоля.Добавить("Исполнитель");
	НеПередающиесяПоля.Добавить("УдалитьИсполнитель");
	НеПередающиесяПоля.Добавить("УдалитьОсновнойОбъектАдресации");
	НеПередающиесяПоля.Добавить("УдалитьДополнительныйОбъектАдресации");
	НеПередающиесяПоля.Добавить("Автор");
	
	НомерИтерацииПоСсылке = ОбщегоНазначенияДокументооборот.ЗначениеРеквизитаОбъектаВПривилегированномРежиме(
		ЭлементДанных.Ссылка, "НомерИтерации");
	ИтерацияИзменена = НомерИтерацииПоСсылке < ЭлементДанных.НомерИтерации;
	
	Если ЭтотУзел = ЭлементДанных.УзелОбмена Тогда // Получение процесса в мастер-узле.
		
		ЗаписатьПроцесс = Ложь;
		
		ПроцессПоСсылке = ЭлементДанных.Ссылка.ПолучитьОбъект();
		
		// Добавляем новые предметы в текущий процесс
		КоличествоПредметовДоДобавления = ПроцессПоСсылке.Предметы.Количество();
		
		РаботаСБизнесПроцессами.ДобавитьНовыеПредметыВПроцесс(
			ПроцессПоСсылке, ЭлементДанных.Предметы, ЭлементДанных.ПредметыЗадач);
		
		КоличествоПредметовПослеДобавления = ПроцессПоСсылке.Предметы.Количество();
		
		Если КоличествоПредметовДоДобавления <> КоличествоПредметовПослеДобавления Тогда
			ЗаписатьПроцесс = Истина;
		КонецЕсли;
		
		Если ЗаписатьПроцесс Тогда
			ПроцессПоСсылке.ОбменДанными.Загрузка = Истина;
			ПроцессПоСсылке.ДополнительныеСвойства.Вставить("УзелОтправитель", Узел);
			ПроцессПоСсылке.Записать();
		Иначе
			
			ОтправкаНазад = Ложь;
			
			// Сравниваем полученный процесс с процессом в мастер узле.
			// В случае отличий отправляем процесс обратно.
			МетаданныеЗадачи = ЭлементДанных.Ссылка.Метаданные();
			ИменаПолейДляСравнения = "";
			Разделитель = "";
			Для Каждого РеквизитЗадачи Из МетаданныеЗадачи.Реквизиты Цикл
				Если НеПередающиесяПоля.Найти(РеквизитЗадачи.Имя) <> Неопределено Тогда
					Продолжить;
				КонецЕсли;
				ИменаПолейДляСравнения = ИменаПолейДляСравнения
					+ Разделитель
					+ РеквизитЗадачи.Имя;
				Разделитель = ",";
			КонецЦикла;
			Для Каждого РеквизитЗадачи Из МетаданныеЗадачи.СтандартныеРеквизиты Цикл
				Если НеПередающиесяПоля.Найти(РеквизитЗадачи.Имя) <> Неопределено Тогда
					Продолжить;
				КонецЕсли;
				ИменаПолейДляСравнения = ИменаПолейДляСравнения
					+ Разделитель
					+ РеквизитЗадачи.Имя;
				Разделитель = ",";
			КонецЦикла;
			
			СтруктураПроцессаПоСсылке = Новый Структура(ИменаПолейДляСравнения);
			ЗаполнитьЗначенияСвойств(СтруктураПроцессаПоСсылке, ПроцессПоСсылке);
			
			СтруктураПроцесса = Новый Структура(ИменаПолейДляСравнения);
			ЗаполнитьЗначенияСвойств(СтруктураПроцесса, ЭлементДанных);
			
			Если Не ОбщегоНазначения.ДанныеСовпадают(СтруктураПроцессаПоСсылке, СтруктураПроцесса) Тогда
				ОтправкаНазад = Истина;
			КонецЕсли;
			
			Если Не ОбщегоНазначения.ДанныеСовпадают(
					ПроцессПоСсылке.ДополнительныеРеквизиты.Выгрузить(),
					ЭлементДанных.ДополнительныеРеквизиты.Выгрузить()) Тогда
					
				ОтправкаНазад = Истина;
			КонецЕсли;
			
			Если Не ОбщегоНазначения.ДанныеСовпадают(
					ПроцессПоСсылке.РезультатыРегистрации.Выгрузить(),
					ЭлементДанных.РезультатыРегистрации.Выгрузить()) Тогда
					
				ОтправкаНазад = Истина;
			КонецЕсли;
			
			Если ОтправкаНазад Тогда
				РаботаСБизнесПроцессами.ЗарегистрироватьИзмененияПроцессаЗадачи(ПроцессПоСсылке);
			КонецЕсли;
		КонецЕсли;
		
	ИначеЕсли ЭлементДанных.Завершен Или ИтерацияИзменена Тогда // получение выполненного процесса в обычном узле
		ЭлементДанных.ОбменДанными.Загрузка = Истина;
		ЭлементДанных.ДополнительныеСвойства.Вставить("УзелОтправитель", Узел);
		ЭлементДанных.Записать();
	Иначе
		// Принимает все пришедшие изменения, за исключением полей, которые
		// сохраняются для работы с процессом в текущем узле.
		ПоляДляПолучения = НеПередающиесяПоля;
		ПоляДляПолучения.Добавить("Предметы");
		ПоляДляПолучения.Добавить("ПредметыЗадач");
		ПоляДляПолучения.Добавить("РезультатыОзнакомлений");
		ПоляДляПолучения.Добавить("РезультатыРегистрации");
		
		ПоляПроцесса = СтрСоединить(ПоляДляПолучения, ",");
		
		РеквизитыПроцесса = ОбщегоНазначенияДокументооборот.ЗначенияРеквизитовОбъектаВПривилегированномРежиме(
		ЭлементДанных.Ссылка, ПоляПроцесса);
		
		Предметы = РеквизитыПроцесса.Предметы.Выгрузить();
		РеквизитыПроцесса.Удалить("Предметы");
		
		ПредметыЗадач = РеквизитыПроцесса.ПредметыЗадач.Выгрузить();
		РеквизитыПроцесса.Удалить("ПредметыЗадач");
		
		РезультатыОзнакомлений = РеквизитыПроцесса.РезультатыОзнакомлений.Выгрузить();
		РеквизитыПроцесса.Удалить("РезультатыОзнакомлений");
		
		РезультатыРегистрации = РеквизитыПроцесса.РезультатыРегистрации.Выгрузить();
		РеквизитыПроцесса.Удалить("РезультатыРегистрации");
		
		// Дополняем переданные предметы, которые были ранее.
		РаботаСБизнесПроцессами.ДобавитьНовыеПредметыВПроцесс(ЭлементДанных, Предметы, ПредметыЗадач);
		
		Отбор = Новый Структура;
		Отбор.Вставить("НомерИтерации", ЭлементДанных.НомерИтерации);
		
		ЗагруженныйПроцессВТочкеМаршрутаОзнакомиться =
			ЭлементДанных.РезультатыОзнакомлений.НайтиСтроки(Отбор).Количество() > 0;
		
		Если ЗагруженныйПроцессВТочкеМаршрутаОзнакомиться Тогда
			// Восстанавливаем Автора процесса по ссылке процесса
			ЗаполнитьЗначенияСвойств(ЭлементДанных, РеквизитыПроцесса, "Автор");
		Иначе
			ПроцессПоСсылкеВТочкеМаршрутаОзнакомиться =
				РезультатыОзнакомлений.НайтиСтроки(Отбор).Количество() > 0;
			Если ПроцессПоСсылкеВТочкеМаршрутаОзнакомиться Тогда
				// Восстанавливаем результаты ознакомления, регистрации и исполнителей
				ЭлементДанных.РезультатыОзнакомлений.Загрузить(РезультатыОзнакомлений);
				ЭлементДанных.РезультатыРегистрации.Загрузить(РезультатыРегистрации);
				
				// Восстанавливаем НеПередающиесяПоля по ссылке процесса
				ЗаполнитьЗначенияСвойств(ЭлементДанных, РеквизитыПроцесса);
			Иначе
				// Восстанавливаем исполнителя по ссылке процесса
				ЗаполнитьЗначенияСвойств(ЭлементДанных, РеквизитыПроцесса,
					"Исполнитель");
			КонецЕсли;
		КонецЕсли;
		
		ЭлементДанных.ОбменДанными.Загрузка = Истина;
		ЭлементДанных.ДополнительныеСвойства.Вставить("УзелОтправитель", Узел);
		ЭлементДанных.Записать();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ПрограммныйИнтерфейс_Печать

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт

КонецПроцедуры

Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт

КонецПроцедуры

#КонецОбласти

#Область ПрограммныйИнтерфейс_СрокиИсполненияПроцессов

// Возвращает даты исполнения участников процесса.
//
// Параметры:
//  Процесс - БизнесПроцессСсылка.Регистрация - ссылка на процесс
//
// Возвращаемое значение:
//  Соотвествие
//   * Ключ - Строка, ЗадачаСсылка.ЗадачаИсполнителя - имя реквизита с участником процесса или ссылка на его задачу.
//
Функция ДатыИсполненияУчастников(Процесс) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ДатыИсполнения = Новый Соответствие;
	
	НомерИтерации = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(
		Процесс, "НомерИтерации");
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Процесс", Процесс);
	Запрос.УстановитьПараметр("НомерИтерации", НомерИтерации);
	
	// Получение дата исполнения исполнителями.
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	Задачи.ДатаИсполнения КАК ДатаИсполнения
		|ИЗ
		|	БизнесПроцесс.Регистрация.РезультатыРегистрации КАК РезультатыРегистрации
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Задача.ЗадачаИсполнителя КАК Задачи
		|		ПО РезультатыРегистрации.ЗадачаИсполнителя = Задачи.Ссылка
		|ГДЕ
		|	РезультатыРегистрации.Ссылка = &Процесс
		|	И РезультатыРегистрации.НомерИтерации = &НомерИтерации";
		
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		ДатыИсполнения.Вставить("Исполнитель", Выборка.ДатаИсполнения);
	КонецЕсли;
	
	// Получение даты исполнения обработки результов.
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	Задачи.ДатаИсполнения КАК ДатаИсполнения
		|ИЗ
		|	БизнесПроцесс.Регистрация.РезультатыОзнакомлений КАК РезультатыОзнакомлений
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Задача.ЗадачаИсполнителя КАК Задачи
		|		ПО РезультатыОзнакомлений.ЗадачаИсполнителя = Задачи.Ссылка
		|ГДЕ
		|	РезультатыОзнакомлений.Ссылка = &Процесс
		|	И РезультатыОзнакомлений.НомерИтерации = &НомерИтерации";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		ДатыИсполнения.Вставить("Автор", Выборка.ДатаИсполнения);
	КонецЕсли;
	
	Возврат ДатыИсполнения;
	
КонецФункции

#КонецОбласти

#Область ПрограммныйИнтерфейс_УчетПереносовСроковИсполнения

// Устанавливает срок исполнителю задачи в процессе.
//
// Параметры:
//  Срок - Дата - новый срок исполнителя.
//  Задача - ЗадачаСсылка.ЗадачаИсполнителя - задача исполнителя.
//  Процесс - БизнесПроцессОбъект - изменяемый процесс.
//  ПараметрыИзмененногоРеквизита - Структура - параметры измененного реквизита процесса.
//   * РеквизитТаблицаСИзмененнымСроком - Строка - имя реквизита или табличной части процесса.
//   * ИндексСтроки - Число - индекс строки табличной части. Присутствует если изменяется табличная часть.
//
Процедура УстановитьСрокИсполнителяЗадачиВПроцессе(
	Срок, Задача, Процесс, ПараметрыИзмененногоРеквизита) Экспорт
	
	ПараметрыИзмененногоРеквизита = Новый Структура;
	ПараметрыИзмененногоРеквизита.Вставить("РеквизитТаблицаСИзмененнымСроком", "");
	ПараметрыИзмененногоРеквизита.Вставить("ИндексСтроки", 0);
	
	ТочкаМаршрутаЗадачи = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Задача, "ТочкаМаршрута");
	
	Если ТочкаМаршрутаЗадачи = ТочкиМаршрута.Зарегистрировать Тогда
		Процесс.СрокИсполнения = Срок;
		
		Процесс.ВариантУстановкиСрокаИсполнения = 
			Перечисления.ВариантыУстановкиСрокаИсполнения.ТочныйСрок;
		
		ПараметрыИзмененногоРеквизита.РеквизитТаблицаСИзмененнымСроком = "СрокИсполнения";
		
	ИначеЕсли ТочкаМаршрутаЗадачи = ТочкиМаршрута.Ознакомиться Тогда
		Процесс.СрокОбработкиРезультатов = Срок;
		
		Процесс.ВариантУстановкиСрокаОбработкиРезультатов = 
			Перечисления.ВариантыУстановкиСрокаИсполнения.ТочныйСрок;
			
		ПараметрыИзмененногоРеквизита.РеквизитТаблицаСИзмененнымСроком = "СрокОбработкиРезультатов";
	КонецЕсли;
	
КонецПроцедуры

// Переносит срок исполнения задачи процесса Исполенипя по заявке.
// При переносе обновляются сроки в текущем и зависимых процессах.
//
// Параметры:
//  НовыйСрок - Дата - новый срок задачи.
//  Задача - ЗадачаСсылка.ЗадачаИсполнителя - ссылка на задачу.
//  Процесс - БизнесПроцессСсылка - ссылка на процесс задачи.
//  ПричинаПереносаСрока - Строка - причина, по которой переносится срок задачи.
//  ЗаявкаНаПереносСрока - БизнесПроцессСсылка.РешениеВопросовВыполненияЗадач - заявка на перенос срока.
//
Процедура ИзменитьСрокИсполненияЗадачи(
	НовыйСрок, Задача, Процесс, ПричинаПереносаСрока, ЗаявкаНаПеренос) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	НачатьТранзакцию();
	
	ЗаблокироватьДанныеДляРедактирования(Процесс);
	
	ПроцессОбъект = Процесс.ПолучитьОбъект();
	
	СтарыеУчастникиПроцесса = ТекущиеУчастникиПроцесса(ПроцессОбъект);
	
	ПараметрыИзмененногоРеквизита = Новый Структура;
	
	УстановитьСрокИсполнителяЗадачиВПроцессе(
		НовыйСрок, Задача, ПроцессОбъект, ПараметрыИзмененногоРеквизита);
	
	Если ПараметрыИзмененногоРеквизита.Количество() > 0 Тогда
		
		ПараметрыЗаписи = Новый Структура;
		ПараметрыЗаписи.Вставить("ПричинаПереносаСрока", ПричинаПереносаСрока);
		ПараметрыЗаписи.Вставить("БизнесПроцессПереноса", ЗаявкаНаПеренос);
		
		ПереносСроковВыполненияЗадач.ПередатьПричинуИЗаявкуНаПереносаСрока(
			ПроцессОбъект, ПараметрыЗаписи);
			
		ПараметрыДляРасчетаСроков = СрокиИсполненияПроцессов.ПараметрыДляРасчетаСроков();
		
		ПараметрыДляРасчетаСроков.ДатаОтсчета = 
			СрокиИсполненияПроцессов.ДатаОтсчетаДляРасчетаСроковПроцесса(ПроцессОбъект);
		
		ПараметрыДляРасчетаСроков.РеквизитТаблицаСИзмененнымСроком = 
			ПараметрыИзмененногоРеквизита.РеквизитТаблицаСИзмененнымСроком;
			
		ПараметрыДляРасчетаСроков.ТекущаяИтерация = ПроцессОбъект.НомерИтерации;
		
		СрокиИсполненияПроцессов.РассчитатьСрокиРегистрации(ПроцессОбъект, ПараметрыДляРасчетаСроков);
		
		РаботаСБизнесПроцессами.ЗаписатьПроцесс(
			ПроцессОбъект, "ЗаписьСОбновлениемОбщегоСпискаИПереносомСроковВИерархииПроцессов");
		
		ПроцессОбъект.ИзменитьРеквизитыНевыполненныхЗадач(СтарыеУчастникиПроцесса, ПараметрыЗаписи);
	КонецЕсли;
	
	РазблокироватьДанныеДляРедактирования(Процесс);
	
	ЗафиксироватьТранзакцию();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ОбработкаПолученияФормы(ВидФормы, Параметры, ВыбраннаяФорма, ДополнительнаяИнформация, СтандартнаяОбработка)
	
	Если ВидФормы = "ФормаСписка" Тогда
		
		СтандартнаяОбработка = Ложь;
		Параметры.Вставить("ТипПроцесса", "Регистрация");
		ВыбраннаяФорма = Метаданные.ОбщиеФормы.СписокПроцессов;
		
	ИначеЕсли ВидФормы = "ФормаВыбора" Тогда
		
		СтандартнаяОбработка = Ложь;
		Параметры.Вставить("Заголовок", НСтр("ru = 'Регистрации'; en = 'Registrations'"));
		Параметры.Вставить("ТипПроцесса", Тип("БизнесПроцессСсылка.Регистрация"));
		ВыбраннаяФорма = Метаданные.ОбщиеФормы.ВыборБизнесПроцесса;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

// УправлениеДоступом

Функция ПолучитьПоляДоступа() Экспорт
	
	Возврат "Ссылка";
	
КонецФункции

// Заполняет переданный дескриптор доступа 
Процедура ЗаполнитьОсновнойДескриптор(ОбъектДоступа, ДескрипторДоступа) Экспорт
	
	ДескрипторДоступа.ОбъектДоступа = ОбъектДоступа.Ссылка;
	
КонецПроцедуры

Функция ЕстьМетодЗаполнитьДескрипторыОбъекта() Экспорт
	
	Возврат Истина;
	
КонецФункции

Процедура ЗаполнитьДескрипторыОбъекта(ОбъектДоступа, ТаблицаДескрипторов, ПротоколРасчетаПрав = Неопределено) Экспорт
	
	// Только основной дескриптор, без рабочей группы.
	ДокументооборотПраваДоступа.ЗаполнитьДескрипторОбъектаОсновной(ОбъектДоступа, ТаблицаДескрипторов);
	
КонецПроцедуры

// Возвращает признак того, что менеджер содержит метод ЗапросДляРасчетаПрав()
// 
Функция ЕстьМетодЗапросДляРасчетаПрав() Экспорт
	
	Возврат Истина;
	
КонецФункции

// Возвращает запрос для расчета прав доступа по дескрипторам объекта
// 
// Параметры:
//  
//  Дескрипторы - Массив - массив дескрипторов, чьи права нужно рассчитать
//  ИдОбъекта - Ссылка - идентификатор объекта метаданных, назначенный переданным дескрипторам
//  МенеджерОбъектаДоступа - СправочникМенеджер, ДокументМенеджер - менеджер объекта доступа
// 
// Возвращаемое значение - Запрос - запрос, который выберет права доступа для переданного массива дескрипторов
// 
Функция ЗапросДляРасчетаПрав(Дескрипторы, ИдОбъекта, МенеджерОбъектаДоступа) Экспорт
	
	Возврат БизнесПроцессыИЗадачиСервер.ЗапросДляРасчетаПрав(Дескрипторы, ИдОбъекта, МенеджерОбъектаДоступа);
	
КонецФункции

// Заполняет протокол расчета прав дескрипторов
// 
// Параметры:
//  
//  ПротоколРасчетаПрав - Массив - протокол для заполнения
//  ЗапросПоПравам - Запрос - запрос, который использовался для расчета прав дескрипторов
//  Дескрипторы - Массив - массив дескрипторов, чьи права были рассчитаны
//  
Процедура ЗаполнитьПротоколРасчетаПрав(ПротоколРасчетаПрав, ЗапросПоПравам) Экспорт
	
	БизнесПроцессыИЗадачиСервер.ЗаполнитьПротоколРасчетаПрав(ПротоколРасчетаПрав, ЗапросПоПравам);
	
КонецПроцедуры

// Проверяет наличие метода.
// 
Функция ЕстьМетодПолучитьПраваПоФайлам() Экспорт
	
	Возврат Истина;
	
КонецФункции

// Возвращает права доступа пользователей к переданным файлам.
// 
Функция ПолучитьПраваПоФайлам(Файлы, Пользователи = Неопределено) Экспорт
	
	Возврат БизнесПроцессыИЗадачиСервер.ПолучитьПраваПоФайлам(Файлы, Пользователи);
	
КонецФункции

// Конец УправлениеДоступом

// Получить структуру с описанием формы выполнения задачи.
// Вызывается при открытии формы выполнения задачи.
//
// Параметры
//   ЗадачаСсылка  – ЗадачаСсылка.ЗадачаИсполнителя – задача 
//   ТочкаМаршрутаСсылка – точка маршрута 
//
// Возвращаемое значение:
//   Структура   – структуру с описанием формы выполнения задачи.
//                 Ключ "ИмяФормы" содержит имя формы, передаваемое в метод контекста ОткрытьФорму(). 
//                 Ключ "ПараметрыФормы" содержит параметры формы. 
//
Функция ФормаВыполненияЗадачи(ЗадачаСсылка, ТочкаМаршрутаСсылка) Экспорт
	
	Если ТочкаМаршрутаСсылка = БизнесПроцессы.Регистрация.ТочкиМаршрута.Зарегистрировать Тогда 
		ИмяФормы = "БизнесПроцесс.Регистрация.Форма.ФормаЗадачиИсполнителя";
		
	ИначеЕсли ТочкаМаршрутаСсылка = БизнесПроцессы.Регистрация.ТочкиМаршрута.Ознакомиться Тогда 
		ИмяФормы = "БизнесПроцесс.Регистрация.Форма.ФормаЗадачиОзнакомиться";
		
	КонецЕсли;	
		
	Результат = Новый Структура;
	Результат.Вставить("ПараметрыФормы", Новый Структура("Ключ", ЗадачаСсылка));
	Результат.Вставить("ИмяФормы", ИмяФормы);
	Возврат Результат;	
	
КонецФункции

// Вызывается при выполнении задачи из формы списка.
//
// Параметры
//   ЗадачаСсылка  – ЗадачаСсылка.ЗадачаИсполнителя – задача 
//   ТочкаМаршрутаСсылка – точка маршрута 
//
Процедура ОбработкаВыполненияПоУмолчанию(ЗадачаСсылка, БизнесПроцессСсылка, ТочкаМаршрутаБизнесПроцесса) Экспорт
	
КонецПроцедуры	

// Вызывается при перенаправлении задачи.
//
// Параметры
//   ЗадачаСсылка  – ЗадачаСсылка.ЗадачаИсполнителя – перенаправляемая задача.
//   НоваяЗадачаСсылка  – ЗадачаСсылка.ЗадачаИсполнителя – задача для нового исполнителя.
//
Процедура ПриПеренаправленииЗадачи(ЗадачаСсылка) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Реквизиты = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ЗадачаСсылка, 
		"ТочкаМаршрута, БизнесПроцесс, Исполнитель, РольИсполнителя");
		
	ТочкаМаршрута = Реквизиты.ТочкаМаршрута;
	БизнесПроцесс = Реквизиты.БизнесПроцесс;
	
	Если ТочкаМаршрута = ТочкиМаршрута.Зарегистрировать Тогда 
		
		БизнесПроцессОбъект = БизнесПроцесс.ПолучитьОбъект();
		БизнесПроцессОбъект.Заблокировать();
		
		Если ЗначениеЗаполнено(Реквизиты.Исполнитель) Тогда 
			БизнесПроцессОбъект.Исполнитель = Реквизиты.Исполнитель;
		Иначе
			БизнесПроцессОбъект.Исполнитель = Реквизиты.РольИсполнителя;
		КонецЕсли;
		
		БизнесПроцессОбъект.Записать();
		
	ИначеЕсли ТочкаМаршрута = ТочкиМаршрута.Ознакомиться Тогда 
		
		Если Не ЗначениеЗаполнено(Реквизиты.Исполнитель) Тогда 
			ВызватьИсключение НСтр("ru = 'Задачу автора процесса нельзя перенаправить на роль исполнителя.'; en = 'Author''s process task cannot be forwarded to performer roles.'");
		КонецЕсли;	
		
		БизнесПроцессОбъект = БизнесПроцесс.ПолучитьОбъект();
		БизнесПроцессОбъект.Заблокировать();
		
		БизнесПроцессОбъект.Автор = Реквизиты.Исполнитель;
		БизнесПроцессОбъект.Записать();
		
	КонецЕсли;
	
КонецПроцедуры

// Возвращает признак наличия метода объекта ПриПеренаправленииЗадачи
// 
Функция ЕстьМетодПриПеренаправленииЗадачи() Экспорт
	Возврат Истина;
КонецФункции

// Возвращает массив пользователей переданного бизнес-процесса,
// которые должны иметь иметь права на другие бизнес-процессы, 
// для которых данный бизнес-процесс является ведущим
Функция ПользователиВедущегоБизнесПроцесса(ВедущийБизнесПроцесс) Экспорт
	
	МассивПользователей = Новый Массив;
	Реквизиты = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ВедущийБизнесПроцесс, "Автор");
	Если ЗначениеЗаполнено(Реквизиты.Автор) Тогда
		МассивПользователей.Добавить(Реквизиты.Автор);
	КонецЕсли;
	
	Возврат МассивПользователей;
	
КонецФункции

// Возвращает тип шаблона бизнес-процесса, соответствующего данному процессу
Функция ТипШаблона() Экспорт
	
	Возврат "Справочник.ШаблоныРегистрации";
	
КонецФункции

// Показывает, может ли процесс запускаться через привычные интерфейсы
Функция МожетЗапускатьсяИнтерактивно() Экспорт
	
	Возврат Истина;
	
КонецФункции

// Возвращает текстовое описание назначения процесса
Функция ПолучитьОписаниеПроцесса() Экспорт
	
	Возврат НСтр("ru = 'Используется для передачи на регистрацию входящего, исходящего или внутреннего документа.'; en = 'Is used for registration of incoming, outgoing, or internal documents.'");
	
КонецФункции

// Проверяет, что процесс завершился удачно
Функция ПроцессЗавершилсяУдачно(Ссылка) Экспорт
	
	РеквизитыПроцесса = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
		Ссылка,
		"Завершен, РезультатРегистрации");
	Возврат РеквизитыПроцесса.Завершен 
		И РеквизитыПроцесса.РезультатРегистрации = Перечисления.РезультатыРегистрации.Зарегистрировано;
	
КонецФункции

// Возвращает массив пользователей переданного бизнес-процесса,
// которые должны иметь иметь права на другие бизнес-процессы, 
// для которых данный бизнес-процесс является ведущим
Функция УчастникиПроцессаВлияющиеНаДоступКПодчиненнымОбъектам(Процесс) Экспорт
	
	МассивПользователей = Новый Массив;
	Реквизиты = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Процесс, "Автор, Проект");
	Если ЗначениеЗаполнено(Реквизиты.Автор) Тогда
		ДанныеУчастника = Новый Структура(
			"Участник");
		ДанныеУчастника.Участник = Реквизиты.Автор;
		МассивПользователей.Добавить(ДанныеУчастника);
	КонецЕсли;
	
	// Добавление руководителя проекта
	Если ЗначениеЗаполнено(Реквизиты.Проект) Тогда
		
		РуководительПроекта = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Реквизиты.Проект, "Руководитель");
		Если ЗначениеЗаполнено(РуководительПроекта) Тогда
			ДанныеУчастника = Новый Структура(
				"Участник");
			ДанныеУчастника.Участник = РуководительПроекта;
			МассивПользователей.Добавить(ДанныеУчастника);
		КонецЕсли;
			
	КонецЕсли;
	
	Возврат МассивПользователей;
	
КонецФункции

// Возвращает массив всех участников процесса 
Функция ВсеУчастникиПроцесса(ПроцессСсылка) Экспорт
	
	ВсеУчастники = Новый Массив;
	
	КлючевыеУчастники = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ПроцессСсылка, 
		"Автор, 
		| Исполнитель");
	
	// Автор
	ДанныеУчастника = Новый Структура;
	ДанныеУчастника.Вставить("Участник", КлючевыеУчастники.Автор);
	ВсеУчастники.Добавить(ДанныеУчастника);

	// Исполнитель
	ДанныеУчастника = Новый Структура;
	ДанныеУчастника.Вставить("Участник", КлючевыеУчастники.Исполнитель);
	ВсеУчастники.Добавить(ДанныеУчастника);
	
	Возврат ВсеУчастники;
	
КонецФункции

// Проверяет, подходит ли объект к шаблону бизнес-процесса
Функция ШаблонПодходитДляАвтозапускаБизнесПроцессаПоОбъекту(ШаблонСсылка, ПредметСсылка, Подписчик, ВидСобытия, Условие) Экспорт
	
	Возврат Истина;
	
КонецФункции

// Возвращает признак наличия метода ДополнительныеДанныеПоЗадаче
Функция ЕстьМетодДополнительныеДанныеПоЗадаче() Экспорт
	Возврат Истина;
КонецФункции

// Возвращает структуру дополнительных данных переданной задачи:
//	РезультатВыполнения - результат выполнения задачи
//  ОписаниеСобытия - описание события выполненной задачи для истории
Функция ДополнительныеДанныеПоЗадаче(Задача) Экспорт
	
	СтруктураВозврата = Новый Структура("РезультатВыполнения, ОписаниеСобытияДляИстории");
	
	Если Не Задача.Выполнена Тогда
		Возврат СтруктураВозврата;
	КонецЕсли;
	
	РезультатВыполнения = Неопределено;
	
	Если Задача.ДополнительныеСвойства.Свойство("ПредыдущийИсполнитель")
		И Задача.ДополнительныеСвойства.ПредыдущийИсполнитель <> Задача.Исполнитель Тогда
		
		ПредставлениеИсполнителя = НСтр("ru = '%1 (вместо %2)'; en = '%1 (instead %2)'");
		ПредставлениеИсполнителя = СтрШаблон(
			ПредставлениеИсполнителя,
			Строка(Задача.Исполнитель),
			Строка(Задача.ДополнительныеСвойства.ПредыдущийИсполнитель));
			
		ра_РаботаСПроцессами.ДобавитьЗаписьПоФактическомуИсполнителю(Задача, ПредставлениеИсполнителя);
		
	Иначе
		ПредставлениеИсполнителя = Строка(Задача.Исполнитель);
	КонецЕсли;
	
	ОписаниеСобытияФормат = "%1, %2. ";
	ОписаниеСобытия = СтрШаблон(ОписаниеСобытияФормат,
		Формат(Задача.ДатаИсполнения, "ДФ='dd.MM.yyyy HH:mm'"),
		ПредставлениеИсполнителя);
	
	ТочкаМаршрута = Задача.ТочкаМаршрута;
	ТочкиМаршрутаПроцесса = БизнесПроцессы.Регистрация.ТочкиМаршрута;
	
	Действие = "";
	ТекстРезультатаВыполнения = Задача.РезультатВыполнения;
	
	Если ТочкаМаршрута = ТочкиМаршрутаПроцесса.Зарегистрировать Тогда
		Запрос = Новый Запрос;
		Запрос.Текст = 
			"ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	РегистрацияРезультатыРегистрации.РезультатРегистрации
			|ИЗ
			|	БизнесПроцесс.Регистрация.РезультатыРегистрации КАК РегистрацияРезультатыРегистрации
			|ГДЕ
			|	РегистрацияРезультатыРегистрации.ЗадачаИсполнителя = &ЗадачаИсполнителя";
		Запрос.УстановитьПараметр("ЗадачаИсполнителя", Задача.Ссылка);
		ВыборкаРезультатаЗадачи = Запрос.Выполнить().Выбрать();
		
		Если ВыборкаРезультатаЗадачи.Следующий() Тогда
			
			Если ВыборкаРезультатаЗадачи.РезультатРегистрации = Перечисления.РезультатыРегистрации.Зарегистрировано Тогда
				Действие = НСтр("ru = 'Зарегистрировано'; en = 'Registered'");
				РезультатВыполнения = Перечисления.ВариантыВыполненияПроцессовИЗадач.Положительно;
			Иначе
				Действие = НСтр("ru = 'Не зарегистрировано'; en = 'Not registered'");
				РезультатВыполнения = Перечисления.ВариантыВыполненияПроцессовИЗадач.Отрицательно;
			КонецЕсли;
			
		КонецЕсли;
	ИначеЕсли ТочкаМаршрута = ТочкиМаршрутаПроцесса.Ознакомиться Тогда
		Запрос = Новый Запрос;
		Запрос.Текст = 
			"ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	УтверждениеРезультатыОзнакомлений.ОтправленоНаПовторнуюРегистрацию
			|ИЗ
			|	БизнесПроцесс.Регистрация.РезультатыОзнакомлений КАК УтверждениеРезультатыОзнакомлений
			|ГДЕ
			|	УтверждениеРезультатыОзнакомлений.ЗадачаИсполнителя = &ЗадачаИсполнителя";
			
		Запрос.УстановитьПараметр("ЗадачаИсполнителя", Задача.Ссылка);
		
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			
			Если Выборка.ОтправленоНаПовторнуюРегистрацию Тогда
				Действие = НСтр("ru = 'Отправлено на повторную регистрацию'; en = 'Sent for repeat registration.'");
			Иначе
				Если Задача.БизнесПроцесс.РезультатРегистрации = Перечисления.РезультатыРегистрации.НеЗарегистрировано Тогда
					Действие = НСтр("ru = 'Регистрация прекращена'; en = 'Registration terminated'");
				Иначе
					Действие = НСтр("ru = 'Ознакомлен(а)'; en = 'Examined'");
				КонецЕсли;
			КонецЕсли;
			
		КонецЕсли;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Действие) Тогда
		ОписаниеСобытия = ОписаниеСобытия + Действие
						  + ?(ЗначениеЗаполнено(ТекстРезультатаВыполнения), ": " + Символы.ПС, ".")
						  + ТекстРезультатаВыполнения;
	Иначе
		ОписаниеСобытия = "";
	КонецЕсли;
	
	СтруктураВозврата.РезультатВыполнения = РезультатВыполнения;
	СтруктураВозврата.ОписаниеСобытияДляИстории = ОписаниеСобытия;
	
	Возврат СтруктураВозврата;
	
КонецФункции

// Возвращает массив структур, содержащих описания участников.
// Состав структуры:
//   ТабличнаяЧасть - имя ТЧ, в которой хранятся данные участников. Если данные хранятся в шапке, этот ключ отсутствует.
//   ИмяУчастника - имя реквизита шапки или ТЧ, в котором хранится ссылка на участника.
//   ВлияетНаДоступКПодчиненнымОбъектам - признак, указывающий на необходимость пересчета прав 
//   задач и дочерних процессов при изменении данного участника.
//
Функция ЗаполнитьОписанияУчастников() Экспорт
	
	МассивОписанийУчастников = Новый Массив;
	
	// Автор
	МассивОписанийУчастников.Добавить(Новый Структура(
		"ИмяУчастника,
		|ВлияетНаДоступКПодчиненнымОбъектам", 
		"Автор",
		Истина));
	
	// Исполнитель
	МассивОписанийУчастников.Добавить(Новый Структура(
		"ИмяУчастника,
		|ВлияетНаДоступКПодчиненнымОбъектам", 
		"Исполнитель",
		Ложь));
		
	// Проект
	МассивОписанийУчастников.Добавить(Новый Структура(
		"ИмяУчастника,
		|ВлияетНаДоступКПодчиненнымОбъектам",
		"Проект",
		Ложь));
		
	Возврат МассивОписанийУчастников;
		
КонецФункции

// Возвращает текст компенсации предмета, показываемый пользователю при прерывании
// бизнес-процесса.
//
Функция ТекстКомпенсацииПредмета(ПроцессСсылка) Экспорт
	
	Результат = "";
	
	Предметы = МультипредметностьКлиентСервер.ПолучитьМассивПредметовОбъекта(ПроцессСсылка, , Истина);

	Если Не ЗначениеЗаполнено(Предметы) Тогда
		Возврат "";
	КонецЕсли;

	ПредметыСИзмененнымСостоянием = Новый Массив();
	
	Для каждого Предмет Из Предметы Цикл
		Если ДелопроизводствоКлиентСервер.ЭтоСсылкаНаДокумент(Предмет) Тогда
			СостоянияДокумента = Делопроизводство.ПолучитьСостоянияДокумента(Предмет, ПроцессСсылка);
			Если ЗначениеЗаполнено(СостоянияДокумента) Тогда
				ПредметыСИзмененнымСостоянием.Добавить(Предмет);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	КоличествоИзмененныхПредметов = ПредметыСИзмененнымСостоянием.Количество();
	Если КоличествоИзмененныхПредметов = 0 Тогда
		Возврат "";
	КонецЕсли;

	Если КоличествоИзмененныхПредметов = 1 Тогда
		НаименованиеПредмета = ОбщегоНазначенияДокументооборот.ЗначениеРеквизитаОбъектаВПривилегированномРежиме(
			ПредметыСИзмененнымСостоянием[0], 
			"Наименование");
			
		Возврат СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'У документа ""%1"" будет очищено состояние регистрации.'; en = 'Registration state will be cleared for document ""%1"".'"),
			НаименованиеПредмета);
	Иначе
		Возврат СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'У документов (%1) будет очищено состояние регистрации.'; en = 'Registration state will be cleared for the documents (%1).'"),
			КоличествоИзмененныхПредметов);
	КонецЕсли;

КонецФункции

// Возвращает доступные для процесса роли предметов
Функция ПолучитьДоступныеРолиПредметов() Экспорт
	
	РолиПредметов = Новый Массив;
	
	РолиПредметов.Добавить(Перечисления.РолиПредметов.Основной);
	РолиПредметов.Добавить(Перечисления.РолиПредметов.Вспомогательный);
	
	Возврат РолиПредметов;
	
КонецФункции

// Возвращает массив доступных типов основных предметов
Функция ПолучитьТипыОсновныхПредметов() Экспорт
	
	ТипыПредметов = Новый Массив;
	
	ТипыПредметов.Добавить(Тип("ДокументСсылка.ВходящееПисьмо")); 
	ТипыПредметов.Добавить(Тип("ДокументСсылка.ИсходящееПисьмо")); 
	
	ТипыПредметов.Добавить(Тип("СправочникСсылка.ВнутренниеДокументы")); 
	ТипыПредметов.Добавить(Тип("СправочникСсылка.ВходящиеДокументы")); 
	ТипыПредметов.Добавить(Тип("СправочникСсылка.ИсходящиеДокументы")); 
	
	Возврат ТипыПредметов;
	
КонецФункции

// Возвращает структуру с вариантами ответов для формирования уведомлений
// с возможностью исполнения задач по почте. Варианты ответов определяются в
// зависимости от точки маршрута.
//
// Параметры:
//	 - ЗадачаСсылка - ЗадачаСсылка.ЗадачаИсполнителя - задача для которой определяются
//					  варианты ответов.
//	 - БизнесПроцессСсылка - БизнесПроцессСсылка.Регистрация - бизнес процесс по которому
//							 назначена задача.
//	 - ТочкаМаршрута - ТочкаМаршрутаБизнесПроцессаСсылка - точка маршрута в которой находится
//					   БизнесПроцесс.
//
// Возвращаемые параметры:
//	 - Структура
//		 - СписокВариантовОтветов - СписокЗначений - список значений типа
//									ПеречисленияСсылка.ВариантыВыполненияПроцессовИЗадач,
//									с заполненным представлением; в нем содержатся варианты
//									ответов.
//		 - ИспользоватьКомментарий - Булево - Принимает значение Истина, если для текущей задачи
//									 ввод комментария обязателен.
//
Функция ВариантыОтветовДляВыполненияЗадачиПоПочте(
	ЗадачаСсылка,
	БизнесПроцессСсылка,
	ТочкаМаршрута) Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("СписокВариантовОтветов", Новый СписокЗначений);
	Результат.Вставить("ИспользоватьКомментарий", Ложь);
	
	Если ТочкаМаршрута = ТочкиМаршрута.Зарегистрировать Тогда
		
		Результат.СписокВариантовОтветов.Добавить(
			Перечисления.ВариантыВыполненияПроцессовИЗадач.Положительно,
			НСтр("ru = 'Зарегистрировано'; en = 'Registered'"));
		Результат.СписокВариантовОтветов.Добавить(
			Перечисления.ВариантыВыполненияПроцессовИЗадач.Отрицательно,
			НСтр("ru = 'Не зарегистрировано'; en = 'Not registered'"));
			
		Результат.ИспользоватьКомментарий = Истина;
		
	ИначеЕсли ТочкаМаршрута = ТочкиМаршрута.Ознакомиться Тогда
		
		НаименованиеПоложительногоРезультата = НСтр("ru = 'Ознакомился'; en = 'Examined'");
		НаименованиеОтрицательногоРезультата = "";
		
		РезультатРегистрации = 
			ОбщегоНазначения.ЗначениеРеквизитаОбъекта(БизнесПроцессСсылка, "РезультатРегистрации");
		
		Если РезультатРегистрации =
			Перечисления.РезультатыРегистрации.НеЗарегистрировано Тогда
			
			НаименованиеПоложительногоРезультата = НСтр("ru = 'Завершить регистрацию'; en = 'Complete registration'");
			НаименованиеОтрицательногоРезультата = НСтр("ru = 'Повторить регистрацию'; en = 'Repeat registration'");
			
		КонецЕсли;
		
		Результат.СписокВариантовОтветов.Добавить(
			Перечисления.ВариантыВыполненияПроцессовИЗадач.Положительно,
			НаименованиеПоложительногоРезультата);
		
		Если ЗначениеЗаполнено(НаименованиеОтрицательногоРезультата) Тогда
			Результат.СписокВариантовОтветов.Добавить(
				Перечисления.ВариантыВыполненияПроцессовИЗадач.Отрицательно,
				НаименованиеОтрицательногоРезультата);
			Результат.ИспользоватьКомментарий = Истина;
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Возвращает признак наличия метода РезультатВыполненияПроцесса
Функция ЕстьМетодРезультатВыполненияПроцесса() Экспорт
	Возврат Истина;
КонецФункции

// Возвращает результат выполнения - значение перечисления ВариантыВыполненияПроцессовИЗадач
Функция РезультатВыполненияПроцесса(Ссылка) Экспорт
	
	ЗавершилсяУдачно = ПроцессЗавершилсяУдачно(Ссылка);
	
	Если ЗавершилсяУдачно Тогда
		Возврат Перечисления.ВариантыВыполненияПроцессовИЗадач.Положительно;
	Иначе
		Возврат Перечисления.ВариантыВыполненияПроцессовИЗадач.Отрицательно;
	КонецЕсли;
	
КонецФункции

// ВерсионированиеОбъектов
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт

КонецПроцедуры


#КонецЕсли
