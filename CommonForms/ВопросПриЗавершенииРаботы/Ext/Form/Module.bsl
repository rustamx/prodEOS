

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	ОбновитьОтображениеУведомлений();
	
	Если Элементы.ДекорацияЗанятыеФайлы.Видимость Тогда
		
		Элементы.ОсновнаяГруппа.ТекущаяСтраница = 
			Элементы.ВопросСУведомлениями;
	Иначе
		Элементы.ОсновнаяГруппа.ТекущаяСтраница = 
			Элементы.ВопросБезУведомлений;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ДекорацияЗанятыеФайлыНажатие(Элемент)
	
	ОписаниеОповещения = Новый ОписаниеОповещения(
		"ОбновитьОтображениеУведомленийПослеЗакрытияПодчиненнойФормы", ЭтотОбъект);
	
	ОткрытьФорму("Справочник.Файлы.Форма.СписокЗанятыхФайлов",,,,,,
		ОписаниеОповещения, РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Завершить(Команда)
	
	СтруктураВозврата = Новый Структура("ЗакрытьПрограмму, БольшеНеЗадаватьЭтотВопрос", 
		Истина, БольшеНеЗадаватьЭтотВопрос); 
	Закрыть(СтруктураВозврата);
	
КонецПроцедуры

&НаКлиенте
Процедура НеЗавершать(Команда)
	
	СтруктураВозврата = Новый Структура("ЗакрытьПрограмму, БольшеНеЗадаватьЭтотВопрос", 
		Ложь, БольшеНеЗадаватьЭтотВопрос);
	Закрыть(СтруктураВозврата);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Обновляет отображение уведомлений на форме.
// 
&НаСервере
Процедура ОбновитьОтображениеУведомлений()
	
	// Определение количества занятых файлов
	ПоказыватьЗанятыеФайлыПриЗавершенииРаботы = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
		"НастройкиПрограммы", "ПоказыватьЗанятыеФайлыПриЗавершенииРаботы", Истина);
	КоличествоЗанятыхФайлов = 0;
	Если ПоказыватьЗанятыеФайлыПриЗавершенииРаботы = Истина Тогда
		ТекущийПользователь = Пользователи.ТекущийПользователь();
		КоличествоЗанятыхФайлов =
			РаботаСФайламиВызовСервера.ПолучитьКоличествоЗанятыхФайлов(Неопределено, ТекущийПользователь, Истина);
	КонецЕсли;
	
	Элементы.ДекорацияЗанятыеФайлы.Видимость = Ложь;
	Если КоличествоЗанятыхФайлов > 0 Тогда
		
		ТекстЗаголовка = НСтр("ru = 'Есть занятые файлы (%1)'; en = 'There are locked files (%1)'");
		
		ТекстЗаголовка = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			ТекстЗаголовка,
			КоличествоЗанятыхФайлов);
			
		Элементы.ДекорацияЗанятыеФайлы.Заголовок = ТекстЗаголовка;
			
		Элементы.ДекорацияЗанятыеФайлы.Видимость = Истина;
		
	КонецЕсли;
	
	Если НЕ Элементы.ДекорацияЗанятыеФайлы.Видимость Тогда
		
		Элементы.ВопросСУведомлениямиГруппаЭлементов.Видимость = Ложь;
		Элементы.ПустаяДекорация.Видимость = Истина;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьОтображениеУведомленийПослеЗакрытияПодчиненнойФормы(
	Результат, Параметры) Экспорт
	
	ОбновитьОтображениеУведомлений();
	
КонецПроцедуры

#КонецОбласти
