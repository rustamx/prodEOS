
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если СтандартныеПодсистемыВызовСервера.ПараметрыРаботыКлиента().ИнформационнаяБазаФайловая Тогда
		Элементы.ПутьКАрхивуWindows.Заголовок = НСтр("ru = 'Для сервера 1С:Предприятия под управлением Microsoft Windows'; en = 'For 1C:Enterprise server under Microsoft Windows'"); 
	Иначе
		Элементы.ПутьКАрхивуWindows.КнопкаВыбора = Ложь; 
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Разместить(Команда)
	
	ОчиститьСообщения();
	
	Если ПустаяСтрока(ПутьКАрхивуWindows) И ПустаяСтрока(ПутьКАрхивуLinux) Тогда
		Текст = НСтр("ru = 'Укажите полное имя архива с файлами начального образа (файл *.zip)'; en = 'Specify the full name of the archive with the files the primary image file (* .zip)'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(Текст, , "ПутьКАрхивуWindows");
		Возврат;
	КонецЕсли;
	
	Если НЕ СтандартныеПодсистемыКлиентПовтИсп.ПараметрыРаботыКлиента().ИнформационнаяБазаФайловая Тогда
	
		Если Не ПустаяСтрока(ПутьКАрхивуWindows) И (Лев(ПутьКАрхивуWindows, 2) <> "\\" ИЛИ Найти(ПутьКАрхивуWindows, ":") <> 0) Тогда
			ТекстОшибки = НСтр("ru = 'Путь к архиву с файлами начального образа должен быть в формате UNC (\\servername\resource) '; en = 'The path to the file with the primary image files must be in UNC format (\\servername\resource) '");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки, , "ПутьКАрхивуWindows");
			Возврат;
		КонецЕсли;
	
	КонецЕсли;
	
	Состояние(НСтр("ru = 'Обмен данными'; en = 'Data exchange'"), ,
				НСтр("ru = 'Осуществляется размещение файлов из архива с файлами начального образа...'; en = 'Placing the files from the archive with the files of primary image...'"),
				БиблиотекаКартинок.СоздатьНачальныйОбраз);
	
	ОбменФайламиВызовСервера.ДобавитьФайлыВТома(ПутьКАрхивуWindows, ПутьКАрхивуLinux);
	
	ПоказатьПредупреждение(, НСтр("ru = 'Размещение файлов из архива с файлами начального образа успешно завершено'; en = 'The placement of files from an archive with the files the initial image completed successfully'"));
	
КонецПроцедуры


&НаКлиенте
Процедура ПутьКАрхивуWindowsНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если Не ФайловыеФункцииСлужебныйКлиент.РасширениеРаботыСФайламиПодключено() Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Для данной операции необходимо установить расширение работы с файлами.'; en = 'For this procedure, you must install the extension for work with files.'"));
		Возврат;
	КонецЕсли;
	
	Диалог = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	
	Диалог.Заголовок = НСтр("ru = 'Выберите файл'; en = 'Select file'");
	Диалог.ПолноеИмяФайла				= ?(ЭтаФорма.ПутьКАрхивуWindows = "", "files.zip", ЭтаФорма.ПутьКАрхивуWindows);
	Диалог.МножественныйВыбор			= Ложь;
	Диалог.ПредварительныйПросмотр		= Ложь;
	Диалог.ПроверятьСуществованиеФайла	= Истина;
	Диалог.Фильтр						= "Архивы zip(*.zip)|*.zip";
	
	Если Диалог.Выбрать() Тогда
		
		ЭтаФорма.ПутьКАрхивуWindows = Диалог.ПолноеИмяФайла;
		
	КонецЕсли;
	
КонецПроцедуры

