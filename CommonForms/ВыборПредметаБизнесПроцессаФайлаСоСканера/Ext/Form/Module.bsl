
////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	Параметры.Свойство("БизнесПроцесс", БизнесПроцесс);
	Параметры.Свойство("Предмет", Предмет);
	Параметры.Свойство("ИменаПредметов", ИменаПредметов);
	Параметры.Свойство("ИмяПредмета", ИмяПредмета);
	Параметры.Свойство("РольПредмета", РольПредмета);
	
	ТипПредмета = Предмет.Метаданные().ПолноеИмя();
	ТипПредметаПредставление = ТипЗнч(Предмет);
	
	УстановитьРеквизитыЗаполнения();
	УстановитьЗаголовкиПодсказок();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "Запись_Файл" И Параметр.Событие = "СозданФайл" 
	   И Параметр.ИдентификаторРодительскойФормы = УникальныйИдентификатор 
	   И ЗначениеЗаполнено(Параметр.Файл) Тогда
		УстановитьПредмет(Параметр.Файл);
	ИначеЕсли ИмяСобытия = "ИмпортФайловЗавершен" Тогда 
		Если ТипЗнч(Источник) = Тип("Структура") Тогда
			Если Источник.СвойСтво("ВладелецФайлов") И Источник.ВладелецФайлов = БизнесПроцесс Тогда
				Если Источник.Свойство("МассивСсылокФайлов") Тогда
					УстановитьНесколькоПредметов(Источник.МассивСсылокФайлов);
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	
	УстановитьПредмет(ВыбранноеЗначение);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

&НаКлиенте
Процедура СоздатьПоШаблону(Команда)
	
	Если ЗначениеЗаполнено(ШаблонДокумента) Тогда
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("ФайлОснование", ШаблонДокумента);
		ПараметрыФормы.Вставить("ВладелецФайла", БизнесПроцесс);
		ПараметрыФормы.Вставить("РежимСоздания", "ИзШаблона");
		ПараметрыФормы.Вставить("СписокКатегорий", Неопределено);
		ОткрытьФорму("Справочник.Файлы.ФормаОбъекта", ПараметрыФормы, ЭтаФорма);
	Иначе
		НеОткрыватьФормуВыбораРежимаСозданияФайла = Истина;
		РаботаСФайламиКлиент.ДобавитьФайл(Неопределено, БизнесПроцесс, 
			ЭтаФорма, 1, Истина,,,,УникальныйИдентификатор,НеОткрыватьФормуВыбораРежимаСозданияФайла);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьСоСканера(Команда)
	
	НеОткрыватьФормуВыбораРежимаСозданияФайла = Истина;
	РаботаСФайламиКлиент.ДобавитьФайл(Неопределено, БизнесПроцесс, 
		ЭтаФорма, 3, Истина, , , , УникальныйИдентификатор,НеОткрыватьФормуВыбораРежимаСозданияФайла);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьИзФайлаНаДиске(Команда)
	
	НеОткрыватьФормуВыбораРежимаСозданияФайла = Истина;
	// 2 - создать из файла
	РаботаСФайламиКлиент.ДобавитьФайл(Неопределено, БизнесПроцесс, 
		ЭтаФорма, 2, Истина,,,,УникальныйИдентификатор,НеОткрыватьФормуВыбораРежимаСозданияФайла);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьФайлИзСписка(Команда)
	
	ИмяФормыВыбора = "Справочник.Файлы.Форма.ФормаВыбораФайлаВПапках";

	ПараметрыФормы = Новый Структура;
	ОткрытьФорму(ИмяФормыВыбора, ПараметрыФормы, ЭтаФорма);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаСервере
Процедура УстановитьРеквизитыЗаполнения()
	
	ПредметыПроцесса = Мультипредметность.ПолучитьПредметыПроцесса(БизнесПроцесс);
	СтрокиТекущегоИмени = ПредметыПроцесса.НайтиСтроки(Новый Структура("ИмяПредмета",ИмяПредмета));
	Если СтрокиТекущегоИмени.Количество() > 0 Тогда
		Если ЗначениеЗаполнено(СтрокиТекущегоИмени[0].ШаблонОснование) Тогда
			ШаблонДокумента = СтрокиТекущегоИмени[0].ШаблонОснование;
		КонецЕсли;
		СтрокиОснования = ПредметыПроцесса.НайтиСтроки(Новый Структура("ИмяПредмета",СтрокиТекущегоИмени[0].ИмяПредметаОснование));
		ИмяПредметаОснование = СтрокиТекущегоИмени[0].ИмяПредметаОснование;
		Если СтрокиОснования.Количество() > 0 Тогда
			ПредметОснование = СтрокиОснования[0].Предмет;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьЗаголовкиПодсказок()
	
	ЗаголовокСоздать = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'Создать новый %1'; en = 'Create new %1'"),
		НРег(ТипПредметаПредставление));
	Если ЗначениеЗаполнено(ШаблонДокумента) Тогда
		ЗаголовокСоздать = ЗаголовокСоздать + " " 
			+ СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'по шаблону ""%1""'; en = 'using template ""%1""'"), Строка(ШаблонДокумента));
	Иначе
		ЗаголовокСоздать = ЗаголовокСоздать + " " + НСтр("ru = 'по шаблону'; en = 'using template'");
	КонецЕсли;
	Если ЗначениеЗаполнено(ПредметОснование) Тогда
		ЗаголовокСоздать = ЗаголовокСоздать + " " 
			+ СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'на основании предмета ""%1""'; en = 'based on subject ""%1""'"), Строка(ИмяПредметаОснование));
	КонецЕсли;
	ЗаголовокСоздать = ЗаголовокСоздать + ".";
	
	Элементы.СоздатьПодсказка.Заголовок = ЗаголовокСоздать;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьПредмет(НовыйПредмет)
	
	Предмет = НовыйПредмет;
	
	ИмяНовогоПредмета = ИмяПредмета;
	
	Если ЗначениеЗаполнено(Предмет)
		И Не ЗначениеЗаполнено(ИмяПредмета) Тогда
		
		ИмяНовогоПредмета = МультипредметностьВызовСервера.
			ПолучитьСсылкуНаИмяПредметаПоСсылкеНаПредмет(Предмет, ИменаПредметов);
	КонецЕсли;
	
	Описание = ОбщегоНазначенияДокументооборотВызовСервера.ПредметСтрокой(
		Предмет, ИмяНовогоПредмета);
	
	Результат = Новый Структура;
	Результат.Вставить("РольПредмета", РольПредмета);
	Результат.Вставить("Предмет", Предмет);
	Результат.Вставить("ТипПредмета", ТипПредмета);
	Результат.Вставить("ИмяПредмета", ИмяНовогоПредмета);
	Результат.Вставить("Описание", Описание);
	
	Закрыть(Результат);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьНесколькоПредметов(МассивПредметов)
	
	Результат = НовыйМассивСтруктурПредметов(МассивПредметов);
	
	Закрыть(Результат);
	
КонецПроцедуры

&НаСервере
Функция НовыйМассивСтруктурПредметов(МассивПредметов)
	
	МассивСтруктур = Новый Массив;
	
	ПервоеИмяИспользовано = Ложь;
	
	Для Каждого НовыйПредмет Из МассивПредметов Цикл
		
		Если Не ПервоеИмяИспользовано И ЗначениеЗаполнено(ИмяПредмета) Тогда
			НовоеИмяПредмета = ИмяПредмета;
			ПервоеИмяИспользовано = Истина;
		Иначе
			НовоеИмяПредмета =  МультипредметностьВызовСервера.ПолучитьСсылкуНаИмяПредметаПоСсылкеНаПредмет(НовыйПредмет, ИменаПредметов);
        КонецЕсли;
		ИменаПредметов.Добавить(НовоеИмяПредмета);
		
		СтруктураПредмета = Новый Структура;
		СтруктураПредмета.Вставить("РольПредмета", РольПредмета);
		СтруктураПредмета.Вставить("ИмяПредмета", НовоеИмяПредмета);
		СтруктураПредмета.Вставить("Предмет", НовыйПредмет);
		СтруктураПредмета.Вставить("Описание", ОбщегоНазначенияДокументооборотВызовСервера.ПредметСтрокой(НовыйПредмет, НовоеИмяПредмета));
		
		МассивСтруктур.Добавить(СтруктураПредмета);
		
	КонецЦикла;
	
	Возврат МассивСтруктур;
	
КонецФункции

