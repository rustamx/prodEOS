
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗаполнитьДерево();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если ЗначениеЗаполнено(Параметры.ТекущаяСтрока) Тогда 
		Строки1 = Дерево.ПолучитьЭлементы();
		Для Каждого Строка1 Из Строки1 цикл
			Строки2 = Строка1.ПолучитьЭлементы();
			Для Каждого Строка2 Из Строки2 Цикл
				Если Строка2.ТочкаМаршрута = Параметры.ТекущаяСтрока Тогда 
					Элементы.Дерево.ТекущаяСтрока = Строка2.ПолучитьИдентификатор();
					Прервать;
				КонецЕсли;
			КонецЦикла;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыДерево

&НаКлиенте
Процедура ДеревоВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ВыборТочкиМаршрута();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Выбрать(Команда)
	
	ВыборТочкиМаршрута();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьДерево()
	
	ДеревоЗначений = РеквизитФормыВЗначение("Дерево");
	
	ВсеБизнесПроцессы = Метаданные.БизнесПроцессы;
	Для Каждого БизнесПроцесс Из ВсеБизнесПроцессы Цикл
		
		СтрокаРодитель = ДеревоЗначений.Строки.Добавить();
		СтрокаРодитель.ТочкаМаршрута = Неопределено;
		СтрокаРодитель.Представление = БизнесПроцесс.Представление();
		
		ТочкиМаршрута = БизнесПроцессы[БизнесПроцесс.Имя].ТочкиМаршрута;
		Для Каждого ТочкаМаршрута Из ТочкиМаршрута Цикл
			Если ТочкаМаршрута.Вид = ВидТочкиМаршрутаБизнесПроцесса.Действие Тогда 
				НоваяСтрока = СтрокаРодитель.Строки.Добавить();
				НоваяСтрока.ТочкаМаршрута = ТочкаМаршрута;
				НоваяСтрока.Представление = Строка(ТочкаМаршрута);
			КонецЕсли;	
		КонецЦикла;	
		
		Если СтрокаРодитель.Строки.Количество() = 0 Тогда 
			ДеревоЗначений.Строки.Удалить(СтрокаРодитель);
		КонецЕсли;	
		
	КонецЦикла;	
	
	ЗначениеВРеквизитФормы(ДеревоЗначений,"Дерево");
	
КонецПроцедуры	

&НаКлиенте
Процедура ВыборТочкиМаршрута()
	
	ТекущиеДанные = Элементы.Дерево.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ТекущиеДанные.ТочкаМаршрута) Тогда 
		Возврат;
	КонецЕсли;
	
	ОповеститьОВыборе(ТекущиеДанные.ТочкаМаршрута);
	
КонецПроцедуры

#КонецОбласти
