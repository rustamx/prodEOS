#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Параметры.Заголовок) Тогда
		Заголовок = Параметры.Заголовок;
		АвтоЗаголовок = Ложь;
	КонецЕсли;
	
	Если ТолькоПросмотр Тогда
		Элементы.OK.Видимость = Ложь;
		Элементы.Отмена.Видимость = Ложь;
		Элементы.Закрыть.Видимость = Истина;
		Элементы.Закрыть.КнопкаПоУмолчанию = Истина;
	КонецЕсли;
	
	Роль = Параметры.РольИсполнителя;
	ОсновнойОбъектАдресации = Параметры.ОсновнойОбъектАдресации;
	ДополнительныйОбъектАдресации = Параметры.ДополнительныйОбъектАдресации;
	
	УстановитьТипыОбъектовАдресации();
	УстановитьСостояниеЭлементов();
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	Если ИспользуетсяБезОбъектовАдресации Тогда
		Возврат;
	КонецЕсли;
	
	ЗаданыТипыОсновногоОбъектаАдресации = ИспользуетсяСОбъектамиАдресации И ЗначениеЗаполнено(ТипыОсновногоОбъектаАдресации);
	ЗаданыТипыДополнительногоОбъектаАдресации = ИспользуетсяСОбъектамиАдресации И ЗначениеЗаполнено(ТипыДополнительногоОбъектаАдресации);
	
	Если ЗаданыТипыОсновногоОбъектаАдресации И ОсновнойОбъектАдресации = Неопределено Тогда
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю( 
		    СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Поле ""%1"" не заполнено.'; en = 'Field""%1"" is not filled in.'"), 
				Роль.ТипыОсновногоОбъектаАдресации.Наименование ),,,
				"ОсновнойОбъектАдресации", 
				Отказ);
				
	ИначеЕсли ЗаданыТипыДополнительногоОбъектаАдресации И ДополнительныйОбъектАдресации = Неопределено Тогда
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Поле ""%1"" не заполнено.'; en = 'Field""%1"" is not filled in.'"), 
			  Роль.ТипыДополнительногоОбъектаАдресации.Наименование ),,, 
			"ДополнительныйОбъектАдресации", 
			Отказ);
			
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ИсполнительПриИзменении(Элемент)
	
	ОсновнойОбъектАдресации = Неопределено;
	ДополнительныйОбъектАдресации = Неопределено;
	УстановитьТипыОбъектовАдресации();
	УстановитьСостояниеЭлементов();
	
КонецПроцедуры

&НаКлиенте
Процедура OKВыполнить()

	ОчиститьСообщения();
	Если НЕ ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	
	РезультатВыбора = ПараметрыЗакрытия();
	
	ОповеститьОВыборе(РезультатВыбора);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьТипыОбъектовАдресации()
	
	ТипыОсновногоОбъектаАдресации = Роль.ТипыОсновногоОбъектаАдресации.ТипЗначения;
	ТипыДополнительногоОбъектаАдресации = Роль.ТипыДополнительногоОбъектаАдресации.ТипЗначения;
	ИспользуетсяСОбъектамиАдресации = Роль.ИспользуетсяСОбъектамиАдресации;
	ИспользуетсяБезОбъектовАдресации = Роль.ИспользуетсяБезОбъектовАдресации;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьСостояниеЭлементов()

	ЗаданыТипыОсновногоОбъектаАдресации = ИспользуетсяСОбъектамиАдресации
		И ЗначениеЗаполнено(ТипыОсновногоОбъектаАдресации);
	ЗаданыТипыДополнительногоОбъектаАдресации = ИспользуетсяСОбъектамиАдресации 
		И ЗначениеЗаполнено(ТипыДополнительногоОбъектаАдресации);
		
	КлючСохраненияПоложенияОкна = 
		?(ЗаданыТипыОсновногоОбъектаАдресации Или ЗначениеЗаполнено(ОсновнойОбъектАдресации), "Основной", "")
		+ ?(ЗаданыТипыДополнительногоОбъектаАдресации Или ЗначениеЗаполнено(ДополнительныйОбъектАдресации), "Дополнительный", "");
		
	Элементы.ОсновнойОбъектАдресации.Видимость = ЗаданыТипыОсновногоОбъектаАдресации
		Или ЗначениеЗаполнено(ОсновнойОбъектАдресации);
	Элементы.ОсновнойОбъектАдресации.Заголовок = Роль.ТипыОсновногоОбъектаАдресации.Наименование;
	
	Элементы.ДополнительныйОбъектАдресации.Видимость = ЗаданыТипыДополнительногоОбъектаАдресации
		Или ЗначениеЗаполнено(ДополнительныйОбъектАдресации);
	Элементы.ДополнительныйОбъектАдресации.Заголовок = Роль.ТипыДополнительногоОбъектаАдресации.Наименование;
	
	Элементы.ОсновнойОбъектАдресации.АвтоОтметкаНезаполненного = ЗаданыТипыОсновногоОбъектаАдресации
		И НЕ ИспользуетсяБезОбъектовАдресации;
	Элементы.ДополнительныйОбъектАдресации.АвтоОтметкаНезаполненного = ЗаданыТипыДополнительногоОбъектаАдресации
		И НЕ ИспользуетсяБезОбъектовАдресации;
	Элементы.ОсновнойОбъектАдресации.ОграничениеТипа = ТипыОсновногоОбъектаАдресации;
	Элементы.ДополнительныйОбъектАдресации.ОграничениеТипа = ТипыДополнительногоОбъектаАдресации;
	
	Элементы.Роль.ТолькоПросмотр = ТолькоПросмотр;
	Элементы.ОсновнойОбъектАдресации.ТолькоПросмотр = ТолькоПросмотр;
	Элементы.ДополнительныйОбъектАдресации.ТолькоПросмотр = ТолькоПросмотр;
	
КонецПроцедуры

&НаСервере
Функция ПараметрыЗакрытия()
	
	Результат = Новый Структура;
	
	Результат.Вставить("РольИсполнителя", Роль);
	
	Результат.Вставить("ОсновнойОбъектАдресации", 
		?(ОсновнойОбъектАдресации <> Неопределено
		И НЕ ОсновнойОбъектАдресации.Пустая(), 
			ОсновнойОбъектАдресации,
			Неопределено));
			
	Результат.Вставить("ДополнительныйОбъектАдресации", 
		?(ДополнительныйОбъектАдресации <> Неопределено
		И НЕ ДополнительныйОбъектАдресации.Пустая(),
			ДополнительныйОбъектАдресации,
			Неопределено));
			
	Результат.Вставить("РольИсполнителяПолная", 
		ОбщегоНазначенияДокументооборотПовтИсп.НайтиСоздатьПолнуюРоль(
			Роль,
			Результат.ОсновнойОбъектАдресации,
			Результат.ДополнительныйОбъектАдресации));
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти
