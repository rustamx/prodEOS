#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Параметры.Свойство("ДатаПодписания", ДатаПодписания);
	Параметры.Свойство("Подписал", Подписал);
	Параметры.Свойство("КомментарийПодписи", КомментарийПодписи);
	Параметры.Свойство("РезультатПодписания", РезультатПодписания);
	Параметры.Свойство("УстановилПодпись", УстановилПодпись);
	Параметры.Свойство("ДатаУстановкиПодписи", ДатаУстановкиПодписи);
	Параметры.Свойство("Шаблон", Шаблон);
	
	ТекущийПользователь = ПользователиКлиентСервер.ТекущийПользователь();
	
	Если (Параметры.Свойство("ТолькоПросмотр") И Параметры.ТолькоПросмотр = Истина) Тогда
		ЭтаФорма.ТолькоПросмотр = Истина;
		Элементы.ФормаЗаписать.Видимость = Ложь;
		Элементы.ФормаОтмена.Заголовок = НСтр("ru = 'Закрыть'; en = 'Close'");
	КонецЕсли;
	
	Если Параметры.Свойство("ЗаголовокКомандыЗаписать") Тогда
		Элементы.ФормаЗаписать.Заголовок = Параметры.ЗаголовокКомандыЗаписать;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Подписал)
		И Не ЭтаФорма.ТолькоПросмотр Тогда
			Подписал = ТекущийПользователь;
	КонецЕсли;
	
	ТекущийПользовательПодписант = ТекущийПользователь = Подписал;
	
	Если Не ЗначениеЗаполнено(ДатаУстановкиПодписи)
		И Не ЭтаФорма.ТолькоПросмотр Тогда
			ДатаУстановкиПодписи = ТекущаяДатаСеанса();
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(УстановилПодпись)
		И Не ЭтаФорма.ТолькоПросмотр Тогда
			УстановилПодпись = ПользователиКлиентСервер.ТекущийПользователь();
	КонецЕсли;
	
	УстановитьДоступность();
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	ПроверяемыеРеквизиты.Добавить("Подписал");
	
	Если РезультатПодписания = Перечисления.РезультатыПодписания.Подписан
		Или РезультатПодписания = Перечисления.РезультатыПодписания.Отклонен Тогда
			ПроверяемыеРеквизиты.Добавить("ДатаПодписания");
	КонецЕсли;
	
	Если РезультатПодписания = Перечисления.РезультатыПодписания.Отклонен
		И Не ЗначениеЗаполнено(КомментарийПодписи) Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				НСтр("ru = 'Поле ""Комментарий"" не заполнено'; en = '""Comment"" field is not filled in'"),,
				"КомментарийПодписи",,Отказ);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если Модифицированность = Истина Тогда
		Отказ = Истина;
		ЗакрытьФорму();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПодписалНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	РаботаСПользователямиКлиент.ВыбратьПользователя(Элемент, Подписал);
	
КонецПроцедуры

&НаКлиенте
Процедура РезультатПодписанияПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(РезультатПодписания)
		И (РезультатПодписания = ПредопределенноеЗначение("Перечисление.РезультатыПодписания.Подписан")
		Или РезультатПодписания = ПредопределенноеЗначение("Перечисление.РезультатыПодписания.Отклонен")) Тогда
			ДатаПодписания = ТекущаяДата();
	Иначе
		ДатаПодписания = '00010101';
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Записать(Команда)
	
	СохранитьИЗакрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	ЗакрытьФорму();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ЗакрытьФорму()

	Если Не Модифицированность Тогда
		Закрыть();
		Возврат;
	КонецЕсли;
	
	ОписаниеОповещения = Новый ОписаниеОповещения(
		"ЗакрытьФормуПродолжение",
		ЭтотОбъект);
	
	ПоказатьВопрос(ОписаниеОповещения,
		НСтр("ru = 'Данные были изменены. Сохранить изменения?'; en = 'Data has been changed. Save changes?'"),
		РежимДиалогаВопрос.ДаНетОтмена,,
		КодВозвратаДиалога.Да);

КонецПроцедуры

&НаКлиенте
Процедура ЗакрытьФормуПродолжение(Ответ, Параметры) Экспорт 
	
	Если Ответ = КодВозвратаДиалога.Да Тогда
		Модифицированность = Ложь;
		СохранитьИЗакрыть();
	ИначеЕсли Ответ = КодВозвратаДиалога.Отмена Тогда
		Возврат;
	Иначе
		Модифицированность = Ложь;
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьДоступность()

	Если УстановилПодпись = Подписал
		И ДатаУстановкиПодписи = ДатаПодписания Тогда
			Элементы.ГруппаПодвала.Видимость = Ложь;
			Элементы.ГруппаПустая.Видимость = Истина;
	КонецЕсли;
	
	Если ПользователиСерверПовтИсп.ЭтоПолноправныйПользовательИБ() Тогда
		Возврат;
	КонецЕсли;
	
	ТекущийПользовательПодписант = ТекущийПользователь = Подписал;
	ТекущийПользовательУстановилПодпись = ТекущийПользователь = УстановилПодпись;
	
	Если Не ТекущийПользовательПодписант
		И Не ТекущийПользовательУстановилПодпись Тогда
			ЭтаФорма.ТолькоПросмотр = Истина;
			Элементы.ФормаЗаписать.Видимость = Ложь;
			Элементы.ФормаОтмена.Заголовок = НСтр("ru = 'Закрыть'; en = 'Close'");
	КонецЕсли;
		
	РеквизитыШаблон = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Шаблон,
		"ЗапретитьИзменятьРеквизитыИзШаблона, Подписант");

	Если РеквизитыШаблон.ЗапретитьИзменятьРеквизитыИзШаблона = Истина
		И ЗначениеЗаполнено(РеквизитыШаблон.Подписант) Тогда
			Элементы.Подписал.ТолькоПросмотр = Истина;
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура СохранитьИЗакрыть()

	ОчиститьСообщения();
	Если Не ПроверитьЗаполнение() Тогда 
		Возврат;
	КонецЕсли;
	
	Структура = Новый Структура();
	Структура.Вставить("ДатаПодписания", ДатаПодписания);
	Структура.Вставить("Подписал", Подписал);
	Структура.Вставить("РезультатПодписания", РезультатПодписания);
	Структура.Вставить("КомментарийПодписи", КомментарийПодписи);
	Структура.Вставить("УстановилПодпись", УстановилПодпись);
	Структура.Вставить("ДатаУстановкиПодписи", ТекущаяДата());
	
	Модифицированность = Ложь;
	Закрыть(Структура);

КонецПроцедуры

#КонецОбласти
