&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьПризнакПроверки = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(ИмяФормы, "УстановитьПризнакПроверки");
	Если УстановитьПризнакПроверки = Неопределено Тогда
		УстановитьПризнакПроверки = Ложь;
	КонецЕсли;

	СписокДокументов.ЗагрузитьЗначения(Параметры.Документы);
	ВидСправочникаДокументов = Параметры.Документы[0].Метаданные().Имя;
	УстановитьВидимостьРеквизитов();
	
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьРеквизитов()
	
	Если ВидСправочникаДокументов = "ВходящиеДокументы" Тогда
		Элементы.СтраницыВидДокумента.ТекущаяСтраница = Элементы.СтраницаВходящиеДокументы;
	ИначеЕсли ВидСправочникаДокументов = "ВнутренниеДокументы" Тогда
		Элементы.СтраницыВидДокумента.ТекущаяСтраница = Элементы.СтраницаВнутренниеДокументы;
	ИначеЕсли ВидСправочникаДокументов = "ИсходящиеДокументы" Тогда
		Элементы.СтраницыВидДокумента.ТекущаяСтраница = Элементы.СтраницаИсходящиеДокументы;
	Иначе
		ВызватьИсключение НСтр("ru = 'Некорректный вид справочника документов.'; en = 'Incorrect document catalog type'");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьИЗакрыть(Команда)
	
	СохранитьИЗакрытьСервер();
	Оповестить("УстановкаРеквизитовДокументов", ВидСправочникаДокументов, ЭтаФорма);
	Закрыть();
	
КонецПроцедуры

&НаСервере
Процедура СохранитьИЗакрытьСервер()
	
	НачатьТранзакцию();
	
	Попытка
		МенеджерЗаписиРегистра = РегистрыСведений.ПроверкаЗагруженныхДанных.СоздатьМенеджерЗаписи();
		Для каждого Элемент Из СписокДокументов Цикл
			ДокументОбъект = Элемент.Значение.ПолучитьОбъект();
			ЗаблокироватьДанныеДляРедактирования(Элемент.Значение);
			ЗаполнитьРеквизитыДокумента(ДокументОбъект);
			ДокументОбъект.Записать();
			Если УстановитьПризнакПроверки Тогда
			    МенеджерЗаписиРегистра.Объект = Элемент.Значение;
				МенеджерЗаписиРегистра.Прочитать();
				Если МенеджерЗаписиРегистра.Выбран() Тогда
					МенеджерЗаписиРегистра.Проверен = Истина;
					МенеджерЗаписиРегистра.Проверил = ПользователиКлиентСервер.ТекущийПользователь();
					МенеджерЗаписиРегистра.ДатаПроверки = ТекущаяДатаСеанса();
					МенеджерЗаписиРегистра.Записать();
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
		ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(ИмяФормы, "УстановитьПризнакПроверки", УстановитьПризнакПроверки);
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		ВызватьИсключение ОписаниеОшибки();
	КонецПопытки;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьРеквизитыДокумента(ДокументОбъект)
	
	Если ВидСправочникаДокументов = "ВходящиеДокументы" Тогда
		ЗаполнитьРеквизитДокумента(ДокументОбъект, "ВидДокумента", ВходящиеДокументыВидДокумента);
		ЗаполнитьРеквизитДокумента(ДокументОбъект, "ГрифДоступа", ВходящиеДокументыГрифДоступа);
		ЗаполнитьРеквизитДокумента(ДокументОбъект, "СпособПолучения", ВходящиеДокументыСпособПолучения);
		ЗаполнитьРеквизитДокумента(ДокументОбъект, "НоменклатураДел", ВходящиеДокументыНоменклатураДел);
		ЗаполнитьРеквизитДокумента(ДокументОбъект, "Организация", ВходящиеДокументыОрганизация);
		ЗаполнитьРеквизитДокумента(ДокументОбъект, "Подразделение", ВходящиеДокументыПодразделение);
		ЗаполнитьРеквизитДокумента(ДокументОбъект, "ВопросДеятельности", ВходящиеДокументыВопросДеятельности);
				
	ИначеЕсли ВидСправочникаДокументов = "ВнутренниеДокументы" Тогда
		ЗаполнитьРеквизитДокумента(ДокументОбъект, "ВидДокумента", ВнутренниеДокументыВидДокумента);
		ЗаполнитьРеквизитДокумента(ДокументОбъект, "ГрифДоступа", ВнутренниеДокументыГрифДоступа);
		ЗаполнитьРеквизитДокумента(ДокументОбъект, "Папка", ВнутренниеДокументыПапка);
		ЗаполнитьРеквизитДокумента(ДокументОбъект, "НоменклатураДел", ВнутренниеДокументыНоменклатураДел);
		ЗаполнитьРеквизитДокумента(ДокументОбъект, "Организация", ВнутренниеДокументыОрганизация);
		ЗаполнитьРеквизитДокумента(ДокументОбъект, "Подразделение", ВнутренниеДокументыПодразделение);
		ЗаполнитьРеквизитДокумента(ДокументОбъект, "ВопросДеятельности", ВнутренниеДокументыВопросДеятельности);
			
	ИначеЕсли ВидСправочникаДокументов = "ИсходящиеДокументы" Тогда
		ЗаполнитьРеквизитДокумента(ДокументОбъект, "ВидДокумента", ИсходящиеДокументыВидДокумента);
		ЗаполнитьРеквизитДокумента(ДокументОбъект, "ГрифДоступа", ИсходящиеДокументыГрифДоступа);
		ЗаполнитьРеквизитДокумента(ДокументОбъект, "НоменклатураДел", ИсходящиеДокументыНоменклатураДел);
		ЗаполнитьРеквизитДокумента(ДокументОбъект, "Организация", ИсходящиеДокументыОрганизация);
		ЗаполнитьРеквизитДокумента(ДокументОбъект, "Подразделение", ИсходящиеДокументыПодразделение);
		ЗаполнитьРеквизитДокумента(ДокументОбъект, "ВопросДеятельности", ИсходящиеДокументыВопросДеятельности);
				
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьРеквизитДокумента(ДокументОбъект, ИмяРеквизита, ЗначениеРеквизита)
	Если ЗначениеЗаполнено(ЗначениеРеквизита) Тогда
		ДокументОбъект[ИмяРеквизита] = ЗначениеРеквизита;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ВидДокументаПриИзменении(Элемент = Неопределено)
	Если Элемент = Элементы.ВходящиеДокументыВидДокумента Тогда
		УстановитьПараметрыФункциональныхОпцийФормы(Новый Структура("ВидВходящегоДокумента", ВходящиеДокументыВидДокумента));
	ИначеЕсли Элемент = Элементы.ВнутренниеДокументыВидДокумента Тогда
		УстановитьПараметрыФункциональныхОпцийФормы(Новый Структура("ВидВнутреннегоДокумента", ВнутренниеДокументыВидДокумента));
	ИначеЕсли Элемент = Элементы.ИсходящиеДокументыВидДокумента Тогда
		УстановитьПараметрыФункциональныхОпцийФормы(Новый Структура("ВидИсходящегоДокумента", ИсходящиеДокументыВидДокумента));
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ВидДокументаПриИзменении();
КонецПроцедуры

