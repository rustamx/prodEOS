
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	КлючНастройки = Параметры.КлючПерсональнойНастройки;
	ИмяНастройки = Параметры.ИмяПерсональнойНастройки;
	
	ТекстВопроса = Параметры.ТекстВопроса;
	ОбработатьЗакрытиеФормы = Истина;
	НажатаКнопка = Ложь;
	
	Заголовок = Параметры.Заголовок;
	
	Если Элементы.Найти("ФормаКоманда" + Параметры.ВариантОтветаПоУмолчанию) <> Неопределено Тогда
		Элементы["ФормаКоманда" + Параметры.ВариантОтветаПоУмолчанию].КнопкаПоУмолчанию = Истина;
	КонецЕсли;
	
	Для Каждого ВариантОтвета Из Параметры.СписокДоступныхВариантов Цикл
		Для Каждого ЭлементФормы Из Элементы Цикл
			Если ТипЗнч(ЭлементФормы) = Тип("КнопкаФормы")
				И Команды[ЭлементФормы.ИмяКоманды].Заголовок = ВариантОтвета.Значение Тогда
				ЭлементФормы.Видимость = Истина;
				Если ЗначениеЗаполнено(ВариантОтвета.Представление) Тогда
					ЭлементФормы.Заголовок = ВариантОтвета.Представление;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если ОбщегоНазначенияДокументооборотКлиент.ПередЗакрытием(
		Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка, Модифицированность) Тогда
		Возврат;
	КонецЕсли;
	
	Если ОбработатьЗакрытиеФормы И НЕ НажатаКнопка Тогда
		ОбработатьЗакрытиеФормы = Ложь;
		СтандартнаяОбработка = Ложь;
		Закрыть(Ложь);
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	Если ОбщегоНазначенияДокументооборотКлиент.ПриЗакрытии(ЗавершениеРаботы) Тогда
		Возврат;
	КонецЕсли;
	
	ПриЗакрытииСервер();
	
КонецПроцедуры

&НаСервере
Процедура ПриЗакрытииСервер()
	
	ХранилищеОбщихНастроек.Сохранить(КлючНастройки, ИмяНастройки, НЕ БольшеНеПоказывать);
	ОбновитьПовторноИспользуемыеЗначения();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаНажатияКнопки(Команда)
	
	НажатаКнопка = Истина;
	Закрыть(Строка(КодВозвратаДиалога[СтрЗаменить(Команда.Имя, "Команда", "")]));
	
КонецПроцедуры


