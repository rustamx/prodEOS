////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Параметры.Свойство("БизнесПроцесс", БизнесПроцесс);
	
	Если Не ЗначениеЗаполнено(БизнесПроцесс) Тогда
		Возврат;
	КонецЕсли;
	
	БизнесПроцессМенеджер = ОбщегоНазначения.МенеджерОбъектаПоСсылке(БизнесПроцесс);
	ТекстКомпенсации = БизнесПроцессМенеджер.ТекстКомпенсацииПредмета(БизнесПроцесс);
	Если ЗначениеЗаполнено(ТекстКомпенсации) Тогда
		ТекстКомпенсации = ТекстКомпенсации + Символы.ПС;
	КонецЕсли;

	УстановитьПривилегированныйРежим(Истина);
	
	ЧислоЗадач = БизнесПроцессыИЗадачиВызовСервера.ПолучитьЧислоНевыполненныхЗадачБизнесПроцесса(
		БизнесПроцесс);
		
	ЧислоПодчиненныхПроцессов = 
		БизнесПроцессыИЗадачиВызовСервера.ПолучитьЧислоНевыполненныхПодчиненныхПроцессовБизнесПроцесса(
			БизнесПроцесс);

	УстановитьПривилегированныйРежим(Ложь);
	
	Если ЧислоПодчиненныхПроцессов > 0 Тогда
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Будет выполнено прерывание процесса, его невыполненных задач (%1) и подчиненных процессов (%2).
				 |%3
				 |Внимание! Все внесенные изменения будут необратимыми.';
				 |en = 'This will terminate the process, its unexecuted tasks (%1), and its subprocesses (%2).
				 |%3
				 |Attention! All the changes are irreversible.'"),
			ЧислоЗадач,
			ЧислоПодчиненныхПроцессов,
			ТекстКомпенсации);
	Иначе
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Будет выполнено прерывание процесса, его невыполненных задач (%1).
				 |%2
				 |Внимание! Все внесенные изменения будут необратимыми.';
				 |en = 'This will terminate the process and its unexecuted tasks (%1).
				 |%2
				 |Attention! All the changes are irreversible.'"),
			ЧислоЗадач,
			ТекстКомпенсации);
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

&НаКлиенте
Процедура ПрерватьПроцесс(Команда)
	
	Результат = Новый Структура;
	
	Результат.Вставить(
		"ПричинаПрерывания", 
		СтрЗаменить(ПричинаПрерывания, Символы.ПС, " "));
		
	Результат.Вставить(
		"ВладелецФормы",
		ВладелецФормы);
	
	Закрыть(Результат);
	
	Оповестить("Процесс_ВводПричиныПрерывания", Результат, ЭтаФорма);
	
КонецПроцедуры


