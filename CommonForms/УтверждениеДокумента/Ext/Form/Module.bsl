#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Параметры.Свойство("АвторУтверждения", АвторУтверждения);
	Параметры.Свойство("ДатаУтверждения", ДатаУтверждения);
	Параметры.Свойство("Результат", Результат); 
	Параметры.Свойство("Комментарий", Комментарий); 
	Параметры.Свойство("ИдентификаторСтроки", ИдентификаторСтроки);
	Параметры.Свойство("УстановилГриф", УстановилГриф);
	Параметры.Свойство("ДатаУстановкиГрифа", ДатаУстановкиГрифа);
	Параметры.Свойство("Шаблон", Шаблон);
	
	ТекущийПользователь = ПользователиКлиентСервер.ТекущийПользователь();
	
	Если Параметры.Свойство("ТолькоПросмотр") И Параметры.ТолькоПросмотр = Истина Тогда 
		ЭтаФорма.ТолькоПросмотр = Истина;
		Элементы.ФормаЗаписать.Видимость = Ложь;
		Элементы.ФормаОтмена.Заголовок = НСтр("ru = 'Закрыть'; en = 'Close'");
	КонецЕсли;
	
	Если Параметры.Свойство("ЗаголовокКомандыЗаписать") Тогда
		Элементы.ФормаЗаписать.Заголовок = Параметры.ЗаголовокКомандыЗаписать;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(АвторУтверждения)
		И Не ЭтаФорма.ТолькоПросмотр Тогда
		АвторУтверждения = ТекущийПользователь;
	КонецЕсли;
	
	ТекущийПользовательАвтор = ТекущийПользователь = АвторУтверждения;

	Если Не ЗначениеЗаполнено(ДатаУстановкиГрифа)
		И Не ЭтаФорма.ТолькоПросмотр Тогда
			ДатаУстановкиГрифа = ТекущаяДатаСеанса();
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(УстановилГриф)
		И Не ЭтаФорма.ТолькоПросмотр Тогда
			УстановилГриф = ТекущийПользователь;
	КонецЕсли;
		
	УстановитьДоступность();
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	ПроверяемыеРеквизиты.Добавить("АвторУтверждения");
	
	Если Результат = Перечисления.РезультатыУтверждения.Утверждено Тогда
		ПроверяемыеРеквизиты.Добавить("ДатаУтверждения");
	КонецЕсли;
	
	Если Результат = Перечисления.РезультатыУтверждения.НеУтверждено
		И Не ЗначениеЗаполнено(Комментарий) Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				НСтр("ru = 'Поле ""Комментарий"" не заполнено'; en = '""Comment"" field is not filled in'"),,
				"Комментарий",,Отказ);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если Модифицированность = Истина Тогда
		Отказ = Истина;
		ЗакрытьФорму();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура АвторУтвержденияНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)

	СтандартнаяОбработка = Ложь;
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("РежимРаботыФормы", 1);
	ПараметрыФормы.Вставить("УпрощенныйИнтерфейс", Истина);
	ПараметрыФормы.Вставить("ОтображатьСотрудников", Истина);
	ПараметрыФормы.Вставить("ОтображатьРоли", Истина);
	ПараметрыФормы.Вставить("ВыбиратьКонтейнерыПользователей", Истина);
	
	ПараметрыФормы.Вставить("ЗаголовокФормы", НСтр("ru = 'Подбор утверждающего'; en = 'Filling in the confirming person'"));
	ПараметрыФормы.Вставить("ЗаголовокСпискаВыбранных", НСтр("ru = 'Выбранные пользователи:'; en = 'Selected users:'"));
	ПараметрыФормы.Вставить("ЗаголовокСпискаАдреснойКниги", НСтр("ru = 'Все пользователи:'; en = 'All users:'"));

	ПараметрыФормы.Вставить("КонтролироватьДублиАдресатов", Истина);
	
	ОткрытьФорму("Справочник.АдреснаяКнига.ФормаСписка",
		ПараметрыФормы,
		Элементы.АвторУтверждения,,,,,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура РезультатПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(Результат) Тогда
		ДатаУтверждения = ТекущаяДата();
	Иначе
		ДатаУтверждения = '00010101';
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РезультатОчистка(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Записать(Команда)
	
	СохранитьИЗакрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	ЗакрытьФорму();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ЗакрытьФорму()

	Если Не Модифицированность Тогда
		Закрыть();
		Возврат;
	КонецЕсли;
	
	ОписаниеОповещения = Новый ОписаниеОповещения(
		"ЗакрытьФормуПродолжение",
		ЭтотОбъект);
	
	ПоказатьВопрос(ОписаниеОповещения,
		НСтр("ru = 'Данные были изменены. Сохранить изменения?'; en = 'Data has been changed. Save changes?'"),
		РежимДиалогаВопрос.ДаНетОтмена,,
		КодВозвратаДиалога.Да);

КонецПроцедуры

&НаКлиенте
Процедура ЗакрытьФормуПродолжение(Ответ, Параметры) Экспорт 
	
	Если Ответ = КодВозвратаДиалога.Да Тогда
		Модифицированность = Ложь;
		СохранитьИЗакрыть();
	ИначеЕсли Ответ = КодВозвратаДиалога.Отмена Тогда
		Возврат;
	Иначе
		Модифицированность = Ложь;
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьИЗакрыть()

	ОчиститьСообщения();
	Если Не ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	
	Структура = Новый Структура();
	Структура.Вставить("АвторУтверждения", АвторУтверждения);
	Структура.Вставить("ДатаУтверждения", ДатаУтверждения);
	Структура.Вставить("Результат", Результат);
	Структура.Вставить("Комментарий", Комментарий);
	Структура.Вставить("ИдентификаторСтроки", ИдентификаторСтроки);
	Структура.Вставить("ДатаУстановкиГрифа", ТекущаяДата());
	Структура.Вставить("УстановилГриф", УстановилГриф);
	
	Модифицированность = Ложь;
	Закрыть(Структура);

КонецПроцедуры

&НаСервере
Процедура УстановитьДоступность()

	Если УстановилГриф = АвторУтверждения
		И ДатаУстановкиГрифа = ДатаУтверждения Тогда
			Элементы.ГруппаПодвал.Видимость = Ложь;
			Элементы.ГруппаПустая.Видимость = Истина;
	КонецЕсли;
	
	Если ПользователиСерверПовтИсп.ЭтоПолноправныйПользовательИБ() Тогда
		Возврат;
	КонецЕсли;
	
	ТекущийПользовательАвтор = ТекущийПользователь = АвторУтверждения;
	ТекущийПользовательУстановилГриф = ТекущийПользователь = УстановилГриф;
	
	Если Не ТекущийПользовательАвтор
		И Не ТекущийПользовательУстановилГриф Тогда
			ЭтаФорма.ТолькоПросмотр = Истина;
			Элементы.ФормаЗаписать.Видимость = Ложь;
			Элементы.ФормаОтмена.Заголовок = НСтр("ru = 'Закрыть'; en = 'Close'");
	КонецЕсли;

	РеквизитыШаблона = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Шаблон,
		"ЗапретитьИзменятьРеквизитыИзШаблона, ГрифыУтверждения");

	Если РеквизитыШаблона.ЗапретитьИзменятьРеквизитыИзШаблона = Истина Тогда
		АвторыУтвержденияШаблона = РеквизитыШаблона.ГрифыУтверждения.Выгрузить().ВыгрузитьКолонку("АвторУтверждения");
		Если АвторыУтвержденияШаблона.Найти(АвторУтверждения) <> Неопределено Тогда
			Элементы.АвторУтверждения.ТолькоПросмотр = Истина;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
