
////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Процент = "0%";
	Заголовок = Параметры.ЗаголовокФормы;
	
	ИдентификаторФЗ 	= ЗначениеПараметра("ИдентификаторФЗ", Новый УникальныйИдентификатор);
	ВыполнитьФЗ 		= ЗначениеПараметра("ВыполнитьФЗ", Истина);
	ИмяФункции 			= ЗначениеПараметра("ИмяФункции", "");
	МаксимальныйПроцент = ЗначениеПараметра("МаксимальныйПроцент", 100);
	
	Если ВыполнитьФЗ И ЗначениеЗаполнено(ИмяФункции) Тогда
		ПараметрыФункции = ЗначениеПараметра("ПараметрыФункции", Неопределено);
		ФоновыеЗадания.Выполнить(ИмяФункции, ПараметрыФункции, ИдентификаторФЗ);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ПодключитьОбработчикОжидания("ОбновитьПроцентВыполнения", 1);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

&НаКлиенте
Процедура Отмена(Команда)
	
	ОстановитьЗадание(ИдентификаторФЗ);
	Закрыть();
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаСервере
Функция ЗначениеПараметра(ИмяПараметра, ЗначениеПоУмолчанию)
	
	Если Параметры.Свойство(ИмяПараметра) Тогда
		Результат = Параметры[ИмяПараметра];
	Иначе
		Результат = ЗначениеПоУмолчанию;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура ОбновитьПроцентВыполнения()
	
	ОписаниеОшибки = "";
	Индикатор = ПолучитьПроцентВыполненияФоновогоЗадания(
		ИдентификаторФЗ, Процент, МаксимальныйПроцент, ОписаниеОшибки);
	
	Если ЗначениеЗаполнено(ОписаниеОшибки) Тогда
		ОтключитьОбработчикОжидания("ОбновитьПроцентВыполнения");
		ЭтаФорма.Закрыть();
		ВызватьИсключение(ОписаниеОшибки);
	КонецЕсли;
 
	Если Индикатор > 0 Тогда
		Если Индикатор >= МаксимальныйПроцент И Не Завершено Тогда
			Индикатор = МаксимальныйПроцент;
		КонецЕсли;
		Процент = Строка(Индикатор) + "%"; 
		Если Индикатор >= МаксимальныйПроцент И Завершено Тогда
			ОтключитьОбработчикОжидания("ОбновитьПроцентВыполнения");
			Оповестить(
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'ФоновоеВыполнениеЗавершено_%1'; en = 'ФоновоеВыполнениеЗавершено_%1'"),
					ИмяФункции),,
				ЭтаФорма);
			Если ЭтаФорма.Открыта() Тогда
				ЭтаФорма.Закрыть();
			КонецЕсли;
		КонецЕсли;
		Если Индикатор >= МаксимальныйПроцент И Не Завершено Тогда
			Индикатор = МаксимальныйПроцент;
			Завершено = Истина;
		КонецЕсли;
	КонецЕсли;
		
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьПроцентВыполненияФоновогоЗадания(ИдентификаторФЗ, Процент, МаксимальныйПроцент, ОписаниеОшибки)
	
	Задание = ПолучитьПоследнееЗадание(ИдентификаторФЗ);
	
	Если Задание = Неопределено Тогда
		Возврат МаксимальныйПроцент;
	КонецЕсли;
	
	Если Задание.Состояние <> СостояниеФоновогоЗадания.Активно Тогда
		Если Задание.Состояние = СостояниеФоновогоЗадания.ЗавершеноАварийно Тогда
			ОписаниеОшибки = ПодробноеПредставлениеОшибки(Задание.ИнформацияОбОшибке);
		КонецЕсли;
		Возврат МаксимальныйПроцент;
	КонецЕсли;
	
	ПроцентЧисло = Число(СтрЗаменить(Процент, "%", ""));
	
	ВсеСообщения = Задание.ПолучитьСообщенияПользователю(Истина);
	Если ВсеСообщения <> Неопределено Тогда
		Для Каждого Сообщение Из ВсеСообщения Цикл
			Текст = Сообщение.Текст;
			Если Лев(Текст, 3) = "###" Тогда
				КоличествоВыполнено = Число(СтрЗаменить(Текст, "###", ""));
				ПроцентЧисло = Макс(ПроцентЧисло, КоличествоВыполнено);
			Иначе
				Сообщение.Сообщить();
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;    
	
	Возврат ПроцентЧисло;
	
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьПоследнееЗадание(ИдентификаторФЗ)
	
	мсвЗаданий = ФоновыеЗадания.ПолучитьФоновыеЗадания(Новый Структура("Ключ", ИдентификаторФЗ));
	
	ПоследнееЗадание = Неопределено;
	Для Каждого текЗадание Из мсвЗаданий Цикл
		Если ПоследнееЗадание = Неопределено Тогда
			ПоследнееЗадание = текЗадание;
		ИначеЕсли текЗадание.Начало > ПоследнееЗадание.Начало Тогда
			ПоследнееЗадание = текЗадание;
		КонецЕсли;
	КонецЦикла;
	
	Возврат ПоследнееЗадание;
	
КонецФункции  

&НаСервереБезКонтекста
Процедура ОстановитьЗадание(ИдентификаторФЗ)
	
	мсвЗаданий = ФоновыеЗадания.ПолучитьФоновыеЗадания(Новый Структура("Ключ", ИдентификаторФЗ));
	
	ПоследнееЗадание = Неопределено;
	Для Каждого текЗадание Из мсвЗаданий Цикл
		Если ПоследнееЗадание = Неопределено Тогда
			ПоследнееЗадание = текЗадание;
		ИначеЕсли текЗадание.Начало > ПоследнееЗадание.Начало Тогда
			ПоследнееЗадание = текЗадание;
		КонецЕсли;
	КонецЦикла;
	
	ПоследнееЗадание.Отменить();
	
КонецПроцедуры


