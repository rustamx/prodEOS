#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	НомерЭлемента = 0;
	ТекстПредупреждения = Параметры.ТекстПредупреждения;
	Для Каждого ИмяПредмета Из Параметры.ИменаПредметов Цикл
		
		ИмяЭлемента = "КомандаВставкиПредмета" + Формат(НомерЭлемента, "ЧГ=");
		ИменаПредметов.Добавить(ИмяЭлемента, ИмяПредмета);
		
		Команда = Команды.Добавить(ИмяЭлемента);
		Команда.Действие = "Подключаемый_ВыполнитьКомандуВставкиПредмета";
		Команда.Заголовок = ИмяПредмета;
		
		Элемент = Элементы.Добавить(ИмяЭлемента, Тип("КнопкаФормы"), Элементы.ПодменюВставить);
		Элемент.ИмяКоманды = ИмяЭлемента;
		Элемент.ТолькоВоВсехДействиях = Ложь;
		
		НомерЭлемента = НомерЭлемента + 1;
		
	КонецЦикла;
	
	Если Параметры.ТолькоПросмотр Тогда
		
		Элементы.ФормаЗакрыть.Видимость = Истина;
		Элементы.ФормаЗакрыть.КнопкаПоУмолчанию = Истина;
		
		Элементы.ФормаГотово.Видимость = Ложь;
		Элементы.ФормаОтмена.Видимость = Ложь;
		Элементы.ПодменюВставить.Видимость = Ложь;
		Элементы.ТекстПредупреждения.ТолькоПросмотр = Истина;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Готово(Команда)
	
	Закрыть(ТекстПредупреждения);
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ВыполнитьКомандуВставкиПредмета(Команда)
	
	ИмяПредмета = ИменаПредметов.НайтиПоЗначению(Команда.Имя).Представление;
	ТекстПредупреждения = ТекстПредупреждения + "[Предметы.""" + ИмяПредмета + """]";
	
КонецПроцедуры

#КонецОбласти
