
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ТекущийПользователь = ПользователиКлиентСервер.ТекущийПользователь();
	
	Элементы.КомандаИсполнительЗадачи.Заголовок = 
		Строка(Параметры.ИсполнительЗадачи);
		
	Элементы.КомандаТекущийПользователь.Заголовок = СтрШаблон(
		НСтр("ru = 'Я, %1'; en = 'I, %1'"),
		Строка(ТекущийПользователь));
		
	СохраненныеРеквизиты = ХранилищеСистемныхНастроек.Загрузить(ИмяФормы + "/ТекущиеДанные");
	Если ТипЗнч(СохраненныеРеквизиты) = Тип("Соответствие") Тогда
		ЗначениеВРеквизитФормы(
			СохраненныеРеквизиты.Получить("ИсторияВыбораИсполнителя"),
			"ИсторияВыбораИсполнителя");
	КонецЕсли;
	
	ОтборДляПоиска = Новый Структура;
	ОтборДляПоиска.Вставить("ИсполнительЗадачи", Параметры.ИсполнительЗадачи);
	
	НайденныеСтроки = ИсторияВыбораИсполнителя.НайтиСтроки(ОтборДляПоиска);
	Если НайденныеСтроки.Количество() > 0
		И НайденныеСтроки[0].ИсполнительЗадачи = 
			НайденныеСтроки[0].ВыбранныйИсполнитель Тогда
		
		Элементы.КомандаИсполнительЗадачи.КнопкаПоУмолчанию = Истина;
	Иначе
		Элементы.КомандаТекущийПользователь.КнопкаПоУмолчанию = Истина;
	КонецЕсли;
		
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаИсполнительЗадачи(Команда)
	
	ДобавитьРезультатВыбораВИсторию(Параметры.ИсполнительЗадачи);
	Закрыть(Параметры.ИсполнительЗадачи);
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаТекущийПользователь(Команда)
	
	ДобавитьРезультатВыбораВИсторию(ТекущийПользователь);
	Закрыть(ТекущийПользователь);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Сохраняет результат выбора в историю (реквизит ИсторияВыбораИсполнителя).
//
// Параметры:
//   ВыбранныйПользователь - СправочникСсылка.Пользователи - выбранный исполнитель задачи.
//
&НаКлиенте
Процедура ДобавитьРезультатВыбораВИсторию(ВыбранныйПользователь)
	
	ОтборДляПоиска = Новый Структура;
	ОтборДляПоиска.Вставить("ИсполнительЗадачи", Параметры.ИсполнительЗадачи);
	
	НайденныеСтроки = ИсторияВыбораИсполнителя.НайтиСтроки(ОтборДляПоиска);
	Если НайденныеСтроки.Количество() > 0 Тогда
		СтрокаИстории = НайденныеСтроки[0];
	Иначе
		СтрокаИстории = ИсторияВыбораИсполнителя.Добавить();
		СтрокаИстории.ИсполнительЗадачи = Параметры.ИсполнительЗадачи;
	КонецЕсли;
	
	СтрокаИстории.ВыбранныйИсполнитель = ВыбранныйПользователь;
	
КонецПроцедуры

#КонецОбласти
