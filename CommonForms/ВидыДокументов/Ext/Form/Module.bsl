
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Не Делопроизводство.ИспользоватьВидыВнутреннихДокументов() Тогда 
		Элементы.ГруппаСписокВнутренние.Видимость = Ложь;
	КонецЕсли;
	
	Если Не Делопроизводство.ИспользоватьВидыВходящихДокументов() Тогда 
		Элементы.ГруппаСписокВходящие.Видимость = Ложь;
	КонецЕсли;
	
	Если Не Делопроизводство.ИспользоватьВидыИсходящихДокументов() Тогда 
		Элементы.ГруппаСписокИсходящие.Видимость = Ложь;
	КонецЕсли;
	
	Если Параметры.Свойство("РежимВыбора") И Параметры.РежимВыбора Тогда 
		РежимОткрытияОкна = РежимОткрытияОкнаФормы.БлокироватьОкноВладельца;
		Элементы.ГруппаРежимВыбора.Видимость = Истина;
		
		Элементы.СписокВходящие.РежимВыбора = Истина;
		Элементы.СписокИсходящие.РежимВыбора = Истина;
		Элементы.СписокВнутренние.РежимВыбора = Истина;
		
		Если Параметры.Свойство("ТекущаяСтрока") И ЗначениеЗаполнено(Параметры.ТекущаяСтрока) Тогда 
			Если ТипЗнч(Параметры.ТекущаяСтрока) = Тип("СправочникСсылка.ВидыВходящихДокументов") Тогда 
				Элементы.Списки.ТекущаяСтраница = Элементы.ГруппаСписокВходящие;
				Элементы.СписокВходящие.ТекущаяСтрока = Параметры.ТекущаяСтрока;
			ИначеЕсли ТипЗнч(Параметры.ТекущаяСтрока) = Тип("СправочникСсылка.ВидыИсходящихДокументов") Тогда 
				Элементы.Списки.ТекущаяСтраница = Элементы.ГруппаСписокИсходящие;
				Элементы.СписокИсходящие.ТекущаяСтрока = Параметры.ТекущаяСтрока;
			ИначеЕсли ТипЗнч(Параметры.ТекущаяСтрока) = Тип("СправочникСсылка.ВидыВнутреннихДокументов") Тогда 	
				Элементы.Списки.ТекущаяСтраница = Элементы.ГруппаСписокВнутренние;
				Элементы.СписокВнутренние.ТекущаяСтрока = Параметры.ТекущаяСтрока;
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
	НастройкиФормы = ОбщегоНазначения.ХранилищеСистемныхНастроекЗагрузить(ИмяФормы + "/ТекущиеДанные", "");
	Если НастройкиФормы = Неопределено Или НастройкиФормы.Получить("ПоказыватьУдаленные") = Неопределено Тогда
		ПоказатьУдаленные();
	КонецЕсли;
	
	Делопроизводство.СписокДокументовУсловноеОформлениеПомеченныхНаУдаление(СписокВнутренние);
	Делопроизводство.СписокДокументовУсловноеОформлениеПомеченныхНаУдаление(СписокВходящие);
	Делопроизводство.СписокДокументовУсловноеОформлениеПомеченныхНаУдаление(СписокИсходящие);
	
КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеДанныхИзНастроекНаСервере(Настройки)
	
	Если Настройки["ПоказыватьУдаленные"] <> Неопределено Тогда
		ПоказатьУдаленные();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Выбрать(Команда)
	
	Если Элементы.Списки.ТекущаяСтраница = Элементы.ГруппаСписокВходящие Тогда 
		ОповеститьОВыборе(Элементы.СписокВходящие.ТекущаяСтрока);
		
	ИначеЕсли Элементы.Списки.ТекущаяСтраница = Элементы.ГруппаСписокИсходящие Тогда 
		ОповеститьОВыборе(Элементы.СписокИсходящие.ТекущаяСтрока);
		
	ИначеЕсли Элементы.Списки.ТекущаяСтраница = Элементы.ГруппаСписокВнутренние Тогда 
		ОповеститьОВыборе(Элементы.СписокВнутренние.ТекущаяСтрока);
		
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура СписокВходящиеВыборЗначения(Элемент, Значение, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОповеститьОВыборе(Значение);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокИсходящиеВыборЗначения(Элемент, Значение, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОповеститьОВыборе(Значение);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокВнутренниеВыборЗначения(Элемент, Значение, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОповеститьОВыборе(Значение);
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказыватьУдаленные(Команда)
	
	ПоказыватьУдаленные = Не ПоказыватьУдаленные;
	Элементы.СписокВнутренниеПоказыватьУдаленные.Пометка = ПоказыватьУдаленные;
	Элементы.СписокВходящиеПоказыватьУдаленные.Пометка = ПоказыватьУдаленные;
	Элементы.СписокИсходящиеПоказыватьУдаленные.Пометка = ПоказыватьУдаленные;
	
	Если Не ПоказыватьУдаленные Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
			СписокВнутренние, "ПометкаУдаления", Ложь,
			ВидСравненияКомпоновкиДанных.Равно, , Истина);
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
			СписокВходящие, "ПометкаУдаления", Ложь,
			ВидСравненияКомпоновкиДанных.Равно, , Истина);
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
			СписокИсходящие, "ПометкаУдаления", Ложь,
			ВидСравненияКомпоновкиДанных.Равно, , Истина);
	Иначе		
		ОбщегоНазначенияКлиентСервер.УдалитьЭлементыГруппыОтбораДинамическогоСписка(СписокВнутренние, "ПометкаУдаления");
		ОбщегоНазначенияКлиентСервер.УдалитьЭлементыГруппыОтбораДинамическогоСписка(СписокВходящие, "ПометкаУдаления");
		ОбщегоНазначенияКлиентСервер.УдалитьЭлементыГруппыОтбораДинамическогоСписка(СписокИсходящие, "ПометкаУдаления");
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПоказатьУдаленные()
	
	Элементы.СписокВнутренниеПоказыватьУдаленные.Пометка = ПоказыватьУдаленные;
	Элементы.СписокВходящиеПоказыватьУдаленные.Пометка = ПоказыватьУдаленные;
	Элементы.СписокИсходящиеПоказыватьУдаленные.Пометка = ПоказыватьУдаленные;
	
	Если Не ПоказыватьУдаленные Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
			СписокВнутренние, "ПометкаУдаления", Ложь,
			ВидСравненияКомпоновкиДанных.Равно, , Истина);
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
			СписокВходящие, "ПометкаУдаления", Ложь,
			ВидСравненияКомпоновкиДанных.Равно, , Истина);
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
			СписокИсходящие, "ПометкаУдаления", Ложь,
			ВидСравненияКомпоновкиДанных.Равно, , Истина);
	Иначе		
		ОбщегоНазначенияКлиентСервер.УдалитьЭлементыГруппыОтбораДинамическогоСписка(СписокВнутренние, "ПометкаУдаления");
		ОбщегоНазначенияКлиентСервер.УдалитьЭлементыГруппыОтбораДинамическогоСписка(СписокВходящие, "ПометкаУдаления");
		ОбщегоНазначенияКлиентСервер.УдалитьЭлементыГруппыОтбораДинамическогоСписка(СписокИсходящие, "ПометкаУдаления");
	КонецЕсли;
		
КонецПроцедуры


