
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Документ = Параметры.Документ;
	Если ЗначениеЗаполнено(Документ) Тогда 
		
		ПрочитатьСостоянияДокумента();
		
		Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Состояние документа ""%1""'; en = 'Document state ""%1""'"),
			Строка(Документ));
		
	Иначе
		
		Если ТипЗнч(Документ) = Тип("СправочникСсылка.ВходящиеДокументы") Тогда 
			СостояниеРегистрация = Перечисления.СостоянияДокументов.НаРегистрации;
			НачальноеСостояниеРегистрация = Неопределено;
		Иначе	
			СостояниеРегистрация = Перечисления.СостоянияДокументов.Проект;
			НачальноеСостояниеРегистрация = Неопределено;
		КонецЕсли;
		
	КонецЕсли;	
		
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(ИсторияСостояний.Отбор,
		"Документ",
		Документ);
		
	ПриложениеЯвляетсяВебКлиентом = ОбщегоНазначенияДокументооборот.ПриложениеЯвляетсяВебКлиентом();
	
	УстановитьВидимостьГрупп();
	
	Если Параметры.Свойство("ДляПросмотра") И Параметры.ДляПросмотра Тогда 
		Элементы.ФормаЗаписать.Видимость = Ложь;
		Элементы.ФормаОтмена.Заголовок = НСтр("ru = 'Закрыть'; en = 'Close'");
		Команды.Отмена.Подсказка = НСтр("ru = 'Закрыть'; en = 'Close'");
		
		Элементы.СостояниеСогласование.ТолькоПросмотр = Истина;
		Элементы.СостояниеУтверждение.ТолькоПросмотр = Истина;
		Элементы.СостояниеРегистрация.ТолькоПросмотр = Истина;
		Элементы.СостояниеРассмотрение.ТолькоПросмотр = Истина;
		Элементы.СостояниеИсполнение.ТолькоПросмотр = Истина;
		Элементы.СостояниеПодписание.ТолькоПросмотр = Истина;
		
	ИначеЕсли ПриложениеЯвляетсяВебКлиентом Тогда 
	
		Элементы.СостояниеСогласование.РедактированиеТекста = Ложь;
		Элементы.СостояниеУтверждение.РедактированиеТекста = Ложь;
		Элементы.СостояниеРегистрация.РедактированиеТекста = Ложь;
		Элементы.СостояниеРассмотрение.РедактированиеТекста = Ложь;
		Элементы.СостояниеИсполнение.РедактированиеТекста = Ложь;
		Элементы.СостояниеПодписание.РедактированиеТекста = Ложь;
		
		Элементы.СостояниеСогласование.КнопкаОчистки = Истина;
		Элементы.СостояниеУтверждение.КнопкаОчистки = Истина;
		Элементы.СостояниеРегистрация.КнопкаОчистки = Истина;
		Элементы.СостояниеРассмотрение.КнопкаОчистки = Истина;
		Элементы.СостояниеИсполнение.КнопкаОчистки = Истина;
		
		Элементы.СостояниеПодписание.КнопкаОчистки = Истина;
		
	КонецЕсли;	
	
	ТекущийПользователь = ПользователиКлиентСервер.ТекущийПользователь();
	
КонецПроцедуры

&НаКлиенте
Процедура Записать(Команда)
	
	ОчиститьСообщения();
	Если Не ПроверитьЗаполнение() Тогда 
		Возврат;
	КонецЕсли;
	
	МассивСостояний = Новый Массив;
	
	ИменаСостояний = Новый Структура("Согласование, Утверждение, Регистрация, Рассмотрение, Исполнение, Подписание"); 
	Для Каждого Строка Из ИменаСостояний Цикл
		ИмяСостояния = Строка.Ключ;
		
		Если ЭтаФорма["НачальноеСостояние" + ИмяСостояния] <> ЭтаФорма["Состояние" + ИмяСостояния] Тогда 
			СтруктураСостояния = Новый Структура("Состояние, Установил, Период, НачальноеСостояние", 
				ЭтаФорма["Состояние" + ИмяСостояния], 
				ЭтаФорма["Установил" + ИмяСостояния], 
				ЭтаФорма["Дата"		 + ИмяСостояния],
			    ЭтаФорма["НачальноеСостояние" + ИмяСостояния]);
				
			МассивСостояний.Добавить(СтруктураСостояния);
		КонецЕсли;
		
	КонецЦикла;
	
	Закрыть(МассивСостояний);
		
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииСостояния(ИмяСостояния)
	
	Если Не ЗначениеЗаполнено(ЭтаФорма["Состояние" + ИмяСостояния]) Тогда 
		ЭтаФорма["Установил" + ИмяСостояния] = Неопределено;
		ЭтаФорма["Дата" + ИмяСостояния] = '00010101';
		Возврат;
	КонецЕсли;	
	
	Если ЭтаФорма["Состояние" + ИмяСостояния] = ЭтаФорма["НачальноеСостояние" + ИмяСостояния] Тогда 
		Возврат;
	КонецЕсли;	
	
	ЭтаФорма["Установил" + ИмяСостояния] = ТекущийПользователь;
	ЭтаФорма["Дата" + ИмяСостояния] = ТекущаяДата();
	
КонецПроцедуры	

&НаКлиенте
Процедура СогласованиеПриИзменении(Элемент)
	
	ПриИзмененииСостояния("Согласование");
	
КонецПроцедуры

&НаКлиенте
Процедура УтверждениеПриИзменении(Элемент)
	
	ПриИзмененииСостояния("Утверждение");
	
КонецПроцедуры

&НаКлиенте
Процедура РегистрацияПриИзменении(Элемент)
	
	ПриИзмененииСостояния("Регистрация");
	
КонецПроцедуры

&НаКлиенте
Процедура РегистрацияОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	РегистрационныйНомер = ОбщегоНазначенияДокументооборотВызовСервера.ЗначениеРеквизитаОбъекта(Документ,
		"РегистрационныйНомер");
	
	Если Не ДокументЗарегистрирован(РегистрационныйНомер) И ВыбранноеЗначение = ПредопределенноеЗначение("Перечисление.СостоянияДокументов.Зарегистрирован") Тогда 
		СтандартнаяОбработка = Ложь;
		ТекстСообщения = НСтр("ru = 'Состояние ""Зарегистрирован"" не может быть установлено вручную. Воспользуйтесь кнопкой ""Зарегистрировать"".'; en = 'State of ""Registered"" cannot be set manually. Use the ""Register"" button.'");
		ПоказатьПредупреждение(, ТекстСообщения);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Функция ДокументЗарегистрирован(РегистрационныйНомер)
	
	Возврат ЗначениеЗаполнено(РегистрационныйНомер);
	
КонецФункции

&НаКлиенте
Процедура РассмотрениеПриИзменении(Элемент)
	
	ПриИзмененииСостояния("Рассмотрение");
	
КонецПроцедуры

&НаКлиенте
Процедура ИсполнениеПриИзменении(Элемент)
	
	ПриИзмененииСостояния("Исполнение");
	
КонецПроцедуры

&НаКлиенте
Процедура СостояниеПодписаниеПриИзменении(Элемент)
	
	ПриИзмененииСостояния("Подписание");
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ИзмененаИсторияСостояний" И Параметр = Документ Тогда 
		ПрочитатьСостоянияДокумента();
	КонецЕсли;	
	
КонецПроцедуры

&наСервере
Процедура ПрочитатьСостоянияДокумента()
	
	ИменаСостояний = Новый Структура("Согласование, Утверждение, Регистрация, Рассмотрение, Исполнение, Подписание"); 
	Для Каждого Строка Из ИменаСостояний Цикл
		ИмяСостояния = Строка.Ключ;
		
		СтруктураСостояния = Делопроизводство.ПолучитьСтруктуруСостоянияДокумента(Документ, "Состояние" + ИмяСостояния);
		
		ЭтаФорма["Состояние" + ИмяСостояния] = СтруктураСостояния.Состояние;
		ЭтаФорма["Установил" + ИмяСостояния] = СтруктураСостояния.Установил;
		ЭтаФорма["Дата"		 + ИмяСостояния] = СтруктураСостояния.ДатаУстановки;
		
		ЭтаФорма["НачальноеСостояние" + ИмяСостояния] = СтруктураСостояния.Состояние;
		
		Установил = СтруктураСостояния.Установил;
		Если ЗначениеЗаполнено(Установил) И ОбщегоНазначения.ЭтоБизнесПроцесс(Установил.Метаданные()) Тогда 
			Элементы["Установил" + ИмяСостояния].Гиперссылка = Истина;
		КонецЕсли;	
	КонецЦикла;	
	
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьГрупп()
	
	Если Параметры.Свойство("ИспользоватьПодписание")
		И Параметры.ИспользоватьПодписание Тогда 
			Элементы.ГруппаУстановилПодписание.Видимость = Истина;
			Элементы.ГруппаУстановилУтверждение.Видимость = Ложь;
	Иначе
		Элементы.ГруппаУстановилПодписание.Видимость = Ложь;
		Элементы.ГруппаУстановилУтверждение.Видимость = Истина;
	КонецЕсли;

КонецПроцедуры


