
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Если Параметры.Свойство("Заголовок") Тогда 
		Заголовок = Параметры.Заголовок;
	КонецЕсли;	
	
	Если Параметры.Свойство("Выбранные") Тогда 
		Для Каждого Строка Из Параметры.Выбранные Цикл
			НоваяСтрока = Выбранные.Добавить();
			НоваяСтрока.Ссылка = Строка;
		КонецЦикла;	
	КонецЕсли;	
	
	ПоказыватьУдаленные = Ложь;
	УстановитьОтбор(Список, ПоказыватьУдаленные);
	
КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеДанныхИзНастроекНаСервере(Настройки)
	
	ПоказыватьУдаленные = Настройки["ПоказыватьУдаленные"];
	УстановитьОтбор(Список, ПоказыватьУдаленные);
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказыватьУдаленные(Команда)
	
	ПоказыватьУдаленные = Не ПоказыватьУдаленные;
	УстановитьОтбор(Список, ПоказыватьУдаленные);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьОтбор(Список, ПоказыватьУдаленные)
	
	Если ПоказыватьУдаленные Тогда 
		ОбщегоНазначенияКлиентСервер.УдалитьЭлементыГруппыОтбораДинамическогоСписка(Список, "ПометкаУдаления");
	Иначе
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список, "ПометкаУдаления", Ложь);
	КонецЕсли;	
	
КонецПроцедуры	

&НаКлиенте
Процедура Включить(Команда)
	
	ВключитьНаКлиенте();
	
КонецПроцедуры

&НаКлиенте
Процедура Исключить(Команда)
	
	Если Элементы.Выбранные.ВыделенныеСтроки.Количество() > 0 Тогда
		Для каждого Строка Из Элементы.Выбранные.ВыделенныеСтроки Цикл								
			СтрокаПоИдентификатору = Выбранные.НайтиПоИдентификатору(Строка);
			Выбранные.Удалить(СтрокаПоИдентификатору);
		КонецЦикла; 			
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ВключитьНаКлиенте()
	
	Для каждого ВыбранноеЗначение Из Элементы.Список.ВыделенныеСтроки Цикл					
				
		ПараметрыОтбора = Новый Структура("Ссылка", ВыбранноеЗначение);
		Если Выбранные.НайтиСтроки(ПараметрыОтбора).Количество() = 0 Тогда
			НоваяСтрока = Выбранные.Добавить();
			НоваяСтрока.Ссылка = ВыбранноеЗначение;
			Элементы.Выбранные.ТекущаяСтрока = НоваяСтрока.ПолучитьИдентификатор();
		КонецЕсли;
				
	КонецЦикла;
	
КонецПроцедуры	

&НаКлиенте
Процедура Выбрать(Команда)
	
	Если Выбранные.Количество() = 0 Тогда
		ВключитьНаКлиенте();
	КонецЕсли;
	
	Результат = Новый Массив;
	Для каждого ВыбранныеСтрока Из Выбранные Цикл
		Результат.Добавить(ВыбранныеСтрока.Ссылка);
	КонецЦикла;
	ОповеститьОВыборе(Результат);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокВыборЗначения(Элемент, Значение, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ВключитьНаКлиенте();
	
КонецПроцедуры

