#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	ПользователиПустаяСсылка = Справочники.Пользователи.ПустаяСсылка();
	ИспользоватьГрафикиРаботы = ПолучитьФункциональнуюОпцию("ИспользоватьГрафикиРаботы");
	
	РежимПросмотра = "Планирование";
	ВариантРедактирования = "ВСписке";
	
	Проект = Параметры.Проект;
	ЭтаФорма.Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'План проекта ""%1""'; en = 'Project plan ""%1""'"),
		Строка(Проект));
		
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьБизнесПроцессыИЗадачи") Тогда 
		Элементы.ДеревоЗадачЗадачи.Видимость = Ложь;
	КонецЕсли;	
	
	ПоказатьПомеченныеНаУдаление = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
		ЭтаФорма.ИмяФормы,
		"ПоказатьПомеченныеНаУдаление",
		Ложь);
	Элементы.ДеревоЗадачПоказатьПомеченныеНаУдаление.Пометка = ПоказатьПомеченныеНаУдаление;
	
	РеквизитыПроекта = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Проект,
		"Ссылка, СпособПланирования, ГрафикРаботы,
		|АвтоматическиРассчитыватьПланПроекта, 
		|ТекущийПланНачало, ТекущийПланОкончание, 
		|ЗагруженИзMSProject, РедактируетсяВСтороннейСистеме,
		|ЕдиницаТрудозатратЗадач, ЕдиницаДлительностиЗадач,
		|ПроектнаяКоманда, Состояние");
	РеквизитыПроекта.Вставить("ЗаполненаПроектнаяКоманда", 
		Не РеквизитыПроекта.ПроектнаяКоманда.Пустой());
	РеквизитыПроекта.Вставить("ДанныеДоступныДляИзменения", 
		ДокументооборотПраваДоступа.ПолучитьПраваПоОбъекту(Проект).Изменение);
	РеквизитыПроекта.Удалить("ПроектнаяКоманда");
	РеквизитыПроекта.Вставить("ПроектЗавершен", 
		РеквизитыПроекта.Состояние = Перечисления.СостоянияПроектов.Завершен);
	РеквизитыПроекта.Удалить("Состояние");
		
	Если Параметры.Свойство("СостояниеПроектныхЗадач") Тогда 
		ПереданноеСостояниеПроектныхЗадач = Параметры.СостояниеПроектныхЗадач;
	КонецЕсли;
	
	СостояниеПроектныхЗадач = "Все";
	Если ЗначениеЗаполнено(ПереданноеСостояниеПроектныхЗадач) Тогда 
		СостояниеПроектныхЗадач = ПереданноеСостояниеПроектныхЗадач;
	КонецЕсли;	
	ЗаполнитьДеревоЗадач();
	РольПолныеПрава = РольДоступна("ПолныеПрава");
	
	Если Не РольПолныеПрава И (Не РеквизитыПроекта.ДанныеДоступныДляИзменения 
		Или РеквизитыПроекта.РедактируетсяВСтороннейСистеме 
		Или РеквизитыПроекта.ПроектЗавершен) Тогда
		Элементы.ДеревоЗадач.ИзменятьСоставСтрок = Ложь;
		Элементы.ДеревоЗадач.ИзменятьПорядокСтрок = Ложь;
		Элементы.ДеревоЗадачГруппаИзменениеПорядкаЗадач.Доступность = Ложь;
		Элементы.ДеревоЗадачГруппаДобавить.Доступность = Ложь;
		Элементы.ДеревоЗадачСвязатьЗадачи.Доступность = Ложь;
		Элементы.ДеревоЗадачКонтекстноеМенюСвязатьЗадачи.Доступность = Ложь;
		Элементы.ДеревоЗадачКонтекстноеМенюГруппаДобавить.Доступность = Ложь;
		Элементы.ДеревоЗадачКонтекстноеМенюГруппаИзменениеПорядкаЗадач.Доступность = Ложь;
	Иначе	
		Элементы.ДеревоЗадач.ИзменятьСоставСтрок = Истина;
		Элементы.ДеревоЗадач.ИзменятьПорядокСтрок = Истина;
		Элементы.ДеревоЗадачГруппаИзменениеПорядкаЗадач.Доступность = Истина;
		Элементы.ДеревоЗадачГруппаДобавить.Доступность = Истина;
		Элементы.ДеревоЗадачСвязатьЗадачи.Доступность = Истина;
		Элементы.ДеревоЗадачКонтекстноеМенюСвязатьЗадачи.Доступность = Истина;
		Элементы.ДеревоЗадачКонтекстноеМенюГруппаДобавить.Доступность = Истина;
		Элементы.ДеревоЗадачКонтекстноеМенюГруппаИзменениеПорядкаЗадач.Доступность = Истина;
	КонецЕсли;	
	
	УстановитьВидимость(РежимПросмотра);
	
	УстановитьДоступностьПолейДереваЗадач(ВариантРедактирования);
	
	ПоказыватьЗадачиПроцессов = Ложь;
	Элементы.ГруппаЗадачиПроцессов.Видимость = ПоказыватьЗадачиПроцессов;
	Элементы.ДеревоЗадачПоказыватьЗадачиПроцессов.Пометка = ПоказыватьЗадачиПроцессов;
	
	ИспользоватьДатуИВремяВСрокахЗадач = ПолучитьФункциональнуюОпцию(
		"ИспользоватьДатуИВремяВСрокахЗадач");
	
	ФорматДатыДляКолонок = ?(ИспользоватьДатуИВремяВСрокахЗадач, 
		"ДФ='dd.MM.yy H:mm'",
		"ДФ='dd.MM.yy'");
		
	Элементы.ОсновныеЗадачиСрокИсполнения.Формат = ФорматДатыДляКолонок;
	Элементы.ВспомогательныеЗадачиСрокИсполнения.Формат = ФорматДатыДляКолонок;
	
	ДиаграммаГанта.Анимация = АнимацияДиаграммы.НеИспользовать;
	
	Если Не РольПолныеПрава Тогда 
		Если РеквизитыПроекта.РедактируетсяВСтороннейСистеме Тогда
			Элементы.РедактированиеЗапрещеноПодпись.Видимость = Истина;
			Элементы.РедактированиеЗапрещеноПодпись.Заголовок = 
				НСтр("ru = 'Проект редактируется в сторонней системе. Редактирование плана запрещено.'; en = 'Project is being edited in a thirt-party system. Editing of the plan is forbidden.'");
			
		ИначеЕсли РеквизитыПроекта.ПроектЗавершен Тогда
			Элементы.РедактированиеЗапрещеноПодпись.Видимость = Истина;
			Элементы.РедактированиеЗапрещеноПодпись.Заголовок = 
				НСтр("ru = 'Проект завершен. Редактирование плана запрещено.'; en = 'Project finished. Editing the plan is forbidden.'");
			
		ИначеЕсли Не РеквизитыПроекта.ДанныеДоступныДляИзменения Тогда 
			Элементы.РедактированиеЗапрещеноПодпись.Видимость = Истина;
			Элементы.РедактированиеЗапрещеноПодпись.Заголовок = 
				НСтр("ru = 'Нет прав на редактирование плана проекта.'; en = 'Not enough permissions to edit the project plan.'");
			
		КонецЕсли;
	КонецЕсли;
	
	НастройкиФормы = ОбщегоНазначения.ХранилищеСистемныхНастроекЗагрузить(ИмяФормы + "/ТекущиеДанные", "");
	Если НастройкиФормы = Неопределено Или НастройкиФормы.Получить("ПоИсполнителю") = Неопределено Тогда
		ПоИсполнителю = ПредопределенноеЗначение("Справочник.Пользователи.ПустаяСсылка");
	КонецЕсли;
	
	// СтандартныеПодсистемы.Печать
	УправлениеПечатью.ПриСозданииНаСервере(ЭтаФорма, Элементы.ГруппаПечать);
	// Конец СтандартныеПодсистемы.Печать
	
	// Учет трудозатрат
	УчетВремени.ПроинициализироватьПараметрыУчетаВремени(
		ДатаНачалаХронометража,
		ДатаКонцаХронометража,
		ВключенХронометраж,
		ОпцияИспользоватьУчетВремени,
		Неопределено,
		ВидыРабот,
		СпособУказанияВремени,
		ЭтаФорма.Команды.ПереключитьХронометраж,
		ЭтаФорма.Элементы.ПереключитьХронометраж,
		ЭтаФорма.Элементы.УказатьТрудозатраты);
	
КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеДанныхИзНастроекНаСервере(Настройки)
	
	Если Не ЗначениеЗаполнено(ПереданноеСостояниеПроектныхЗадач) Тогда 
		СостояниеПроектныхЗадач = Настройки["СостояниеПроектныхЗадач"];
		ЗаполнитьДеревоЗадач();
	Иначе	
		СохраненноеСостояниеПроектныхЗадач = Настройки["СостояниеПроектныхЗадач"];
		СостояниеПроектныхЗадач = ПереданноеСостояниеПроектныхЗадач;
	КонецЕсли;
	
	УстановитьВидимость(Настройки["РежимПросмотра"]);
	
	УстановитьДоступностьПолейДереваЗадач(Настройки["ВариантРедактирования"]);
	
	ПоказыватьЗадачиПроцессов = Настройки["ПоказыватьЗадачиПроцессов"];
	Элементы.ГруппаЗадачиПроцессов.Видимость = ПоказыватьЗадачиПроцессов;
	Элементы.ДеревоЗадачПоказыватьЗадачиПроцессов.Пометка = ПоказыватьЗадачиПроцессов;
	
	ПоказатьСкрытьКнопкуОчисткиОтбора(Элементы.ПоИсполнителю, ПоИсполнителю);
	ПоказатьСкрытьКнопкуОчисткиОтбора(Элементы.СостояниеПроектныхЗадач, СостояниеПроектныхЗадач);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Для Каждого Строка Из ДеревоЗадач.ПолучитьЭлементы() Цикл
		Элементы.ДеревоЗадач.Развернуть(Строка.ПолучитьИдентификатор(), Истина);
	КонецЦикла;	
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	Если ОбщегоНазначенияДокументооборотКлиент.ПриЗакрытии(ЗавершениеРаботы) Тогда
		Возврат;
	КонецЕсли;
	
	ОбщегоНазначенияВызовСервера.ХранилищеОбщихНастроекСохранить(ЭтаФорма.ИмяФормы, 
		"ПоказатьПомеченныеНаУдаление", 
		ПоказатьПомеченныеНаУдаление);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(РезультатВыбора, ИсточникВыбора)
	
	Если ТипЗнч(РезультатВыбора) = Тип("Строка") И ЭтоАдресВременногоХранилища(РезультатВыбора) Тогда 
		
		МассивИсполнителей = ЗагрузитьИсполнителейИзВременногоХранилища(РезультатВыбора);
		
		ТекущиеДанные = Элементы.ДеревоЗадач.ТекущиеДанные;
		Исполнители = ТекущиеДанные.Исполнители; 
		Исполнители.Очистить();
		
		Для Каждого Исполнитель Из МассивИсполнителей Цикл
			НоваяСтрока = Исполнители.Добавить();
			НоваяСтрока.Исполнитель = Исполнитель;
			НоваяСтрока.ТекущийПланТрудозатраты = 
				ТекущиеДанные.ТекущийПланТрудозатраты / МассивИсполнителей.Количество();
		КонецЦикла;
		ТекущиеДанные.ИсполнителиСтр = ПредставлениеИсполнителя(Исполнители);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ИзмененаПроектнаяЗадача" И Параметр.Проект = Проект Тогда 
		
		Индекс = -1;
		РаботаСПроектамиКлиентСервер.НайтиЗадачуВДеревеПоСсылке(
			ДеревоЗадач.ПолучитьЭлементы(), Параметр.ПроектнаяЗадача, Индекс);
		
		НайденнаяСтрока = ДеревоЗадач.НайтиПоИдентификатору(Индекс);
		Если Параметр.Родитель <> НайденнаяСтрока.РодительскаяЗадача Тогда 
			ОбновитьДеревоЗадач();
		Иначе	
			ОбновитьИзмененныеСтрокиЗадач(Параметр.МассивИзмененныхЗадач);
		КонецЕсли;	
		
	КонецЕсли;	
		
	Если ИмяСобытия = "СозданаПроектнаяЗадача" И Параметр.Проект = Проект Тогда  	
		
		Если ЗначениеЗаполнено(Параметр.Родитель) Тогда 
			Индекс = -1;
			РаботаСПроектамиКлиентСервер.НайтиЗадачуВДеревеПоСсылке(
				ДеревоЗадач.ПолучитьЭлементы(), Параметр.Родитель, Индекс);
			
			НоваяСтрока = ДеревоЗадач.НайтиПоИдентификатору(Индекс).ПолучитьЭлементы().Добавить();
		Иначе
			НоваяСтрока = ДеревоЗадач.ПолучитьЭлементы().Добавить();
		КонецЕсли;	
		НоваяСтрока.Ссылка = Параметр.ПроектнаяЗадача;
		Элементы.ДеревоЗадач.ТекущаяСтрока = НоваяСтрока.ПолучитьИдентификатор();
		
		МассивИзмененныхЗадач = Новый Массив;
		МассивИзмененныхЗадач.Добавить(Параметр.ПроектнаяЗадача);
		
		Для Каждого Строка Из Параметр.МассивИзмененныхЗадач Цикл
			МассивИзмененныхЗадач.Добавить(Строка);
		КонецЦикла;
		
		Если ЗначениеЗаполнено(Параметр.Родитель) Тогда 
			МассивИзмененныхЗадач.Добавить(Параметр.Родитель);	
		КонецЕсли;	
		
		ОбновитьИзмененныеСтрокиЗадач(МассивИзмененныхЗадач);
		
	КонецЕсли;
	
	Если (ИмяСобытия = "БизнесПроцессСтартован" И Параметр.Проект = Проект) Тогда
		
		Если ЗначениеЗаполнено(Параметр.ПроектнаяЗадача) Тогда 
			МассивИзмененныхЗадач = Новый Массив;
		    МассивИзмененныхЗадач.Добавить(Параметр.ПроектнаяЗадача);
			ОбновитьИзмененныеСтрокиЗадач(МассивИзмененныхЗадач);
			
			Индекс = -1;
			РаботаСПроектамиКлиентСервер.НайтиЗадачуВДеревеПоСсылке(
				ДеревоЗадач.ПолучитьЭлементы(), Параметр.ПроектнаяЗадача, Индекс);
			
			НайденнаяСтрока = ДеревоЗадач.НайтиПоИдентификатору(Индекс);
			НайденнаяСтрока.Задачи = Истина;
			
			ОбновитьМиникарточку();
		КонецЕсли;	
		
	КонецЕсли;
	
	Если ИмяСобытия = "БизнесПроцессСтартован" И ТипЗнч(Параметр.СсылкаНаПредметБизнесПроцесса) = Тип("СправочникСсылка.ПроектныеЗадачи") Тогда 	
		СсылкаНаПредметБизнесПроцесса = Параметр.СсылкаНаПредметБизнесПроцесса;
		Если ОбщегоНазначенияДокументооборотВызовСервера.ЗначениеРеквизитаОбъекта(СсылкаНаПредметБизнесПроцесса, "Владелец") = Проект Тогда 
			
			Индекс = -1;
			РаботаСПроектамиКлиентСервер.НайтиЗадачуВДеревеПоСсылке(
				ДеревоЗадач.ПолучитьЭлементы(), СсылкаНаПредметБизнесПроцесса, Индекс);
			
			НайденнаяСтрока = ДеревоЗадач.НайтиПоИдентификатору(Индекс);
			НайденнаяСтрока.Задачи = Истина;
			
			ОбновитьМиникарточку();
		КонецЕсли;                                         
	КонецЕсли;	
		
	Если ИмяСобытия = "ЗаписьКонтроля" Тогда
		Если ЗначениеЗаполнено(Параметр.Предмет)
			И ТипЗнч(Параметр.Предмет) = Тип("СправочникСсылка.ПроектныеЗадачи") Тогда 
			ОбновитьДеревоЗадач();
		КонецЕсли;	
	КонецЕсли;
	
	Если ИмяСобытия = "ЗадачаВыполнена" Тогда
		ТекущиеДанные = Элементы.ДеревоЗадач.ТекущиеДанные;
		Если ТекущиеДанные <> Неопределено Тогда 
			Если ТипЗнч(Параметр) = Тип("ЗадачаСсылка.ЗадачаИсполнителя") Тогда 
				ПроектЗадачи = ОбщегоНазначенияДокументооборотВызовСервера.ЗначениеРеквизитаОбъекта(
					Параметр, "Проект");
				Если ЗначениеЗаполнено(ПроектЗадачи) И ПроектЗадачи = Проект Тогда 
					ПроектнаяЗадачаЗадачи = ОбщегоНазначенияДокументооборотВызовСервера.ЗначениеРеквизитаОбъекта(
						Параметр, "ПроектнаяЗадача");
					Если ПроектнаяЗадачаЗадачи = ТекущиеДанные.Ссылка Тогда 
						ОбновитьМиникарточку();
					КонецЕсли;
				КонецЕсли;
				
				ТипыПредметов = Новый Массив;
				ТипыПредметов.Добавить(Тип("СправочникСсылка.ПроектныеЗадачи"));
				ПредметыЗадачи = МультипредметностьКлиентСервер.ПолучитьМассивПредметовОбъекта(Параметр, ТипыПредметов, Ложь);
				Для Каждого ПредметЗадачи Из ПредметыЗадачи Цикл
					Если ПредметЗадачи = ТекущиеДанные.Ссылка Тогда
						ОбновитьМиникарточку();
					КонецЕсли;	
				КонецЦикла;	
			КонецЕсли;	
		КонецЕсли;
	КонецЕсли;	
	
КонецПроцедуры

&НаСервере
Процедура ПриСохраненииДанныхВНастройкахНаСервере(Настройки)
	
	Если ЗначениеЗаполнено(ПереданноеСостояниеПроектныхЗадач) Тогда 
		Настройки.Вставить("СостояниеПроектныхЗадач", СохраненноеСостояниеПроектныхЗадач);
	КонецЕсли;	
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура РежимПросмотраПриИзменении(Элемент)
	
	УстановитьВидимость(РежимПросмотра);
	
КонецПроцедуры

&НаКлиенте
Процедура СостояниеПроектныхЗадачПриИзменении(Элемент)
	
	Если Не ЗначениеЗаполнено(СостояниеПроектныхЗадач) Тогда 
		СостояниеПроектныхЗадач = "Все";
	КонецЕсли;	
		
	ОбновитьДеревоЗадач();
	
	Если СостояниеПроектныхЗадач = "Все" Тогда 
		Для Каждого Строка Из ДеревоЗадач.ПолучитьЭлементы() Цикл
			Элементы.ДеревоЗадач.Развернуть(Строка.ПолучитьИдентификатор(), Истина);
		КонецЦикла;
	КонецЕсли;
	
	ПоказатьСкрытьКнопкуОчисткиОтбора(Элементы.СостояниеПроектныхЗадач, СостояниеПроектныхЗадач);
	
КонецПроцедуры

&НаКлиенте
Процедура ДиаграммаГантаВыбор(Элемент, Значения, СтандартнаяОбработка, Дата)
	
	ЗадачаСсылка = Неопределено;
	
	Если ТипЗнч(Значения) = Тип("Массив") И Значения.Количество() > 0 Тогда
		
		Если ЗначениеЗаполнено(Значения[0].Точка.Значение) Тогда
			ЗадачаСсылка = Значения[0].Точка.Значение;
		КонецЕсли;	
		
	ИначеЕсли ТипЗнч(Значения) = Тип("ЗначениеДиаграммыГанта") Тогда	
		
		ЗадачаСсылка = Значения.Точка.Значение;
		
	КонецЕсли;
	
	
	Если ЗначениеЗаполнено(ЗадачаСсылка) Тогда
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("Ключ", ЗадачаСсылка);
		
		ОткрытьФорму("Справочник.ПроектныеЗадачи.ФормаОбъекта", ПараметрыФормы, ЭтаФорма);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДиаграммаГантаПриОкончанииРедактированияИнтервала(Элемент, Интервал, ОтменаРедактирования)
	
	Если Не РеквизитыПроекта.ДанныеДоступныДляИзменения Тогда 
		СообщениеТекст = НСтр("ru = 'У вас нет прав на изменение проектных задач.'; en = 'Insufficient permissions to change project tasks.'");
		ПоказатьПредупреждение(, СообщениеТекст);
		ОтменаРедактирования = Истина;
		Возврат;
	КонецЕсли;
	
	Если Не РольПолныеПрава Тогда 
		Если РеквизитыПроекта.ПроектЗавершен Тогда 
			СообщениеТекст = НСтр("ru = 'Проект завершен. Изменение плана запрещено.'; en = 'Project finished. Changing the plan is forbidden.'");
			ПоказатьПредупреждение(, СообщениеТекст);
			ОтменаРедактирования = Истина;
			Возврат;
		КонецЕсли;
		
		Если РеквизитыПроекта.РедактируетсяВСтороннейСистеме Тогда
			СообщениеТекст = НСтр("ru = 'Проект редактируется в сторонней системе. Изменение плана запрещено.'; en = 'Project is being edited in a thirt-party system. Changing the plan is forbidden.'");
			ПоказатьПредупреждение(, СообщениеТекст);
			ОтменаРедактирования = Истина;
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	ПараметрыОписания = Новый Структура;
	ПараметрыОписания.Вставить("Значение", Интервал.Значение.Точка.Значение);
	ПараметрыОписания.Вставить("Начало", Интервал.Начало);
	ПараметрыОписания.Вставить("Конец", Интервал.Конец);
	
	ОписаниеОповещения = Новый ОписаниеОповещения(
		"ДиаграммаГантаПриОкончанииРедактированияИнтервалаПродолжение",
		ЭтотОбъект, ПараметрыОписания);
	
	Если ЗначениеЗаполнено(ПараметрыОписания.Начало) 
		И РеквизитыПроекта.СпособПланирования =
			ПредопределенноеЗначение("Перечисление.СпособыПланированияПроекта.ОтДатыНачалаПроекта") 
		И РеквизитыПроекта.АвтоматическиРассчитыватьПланПроекта Тогда 
		
		ДанныеПроектнойЗадачи = РаботаСПроектами.ПолучитьДанныеПроектнойЗадачи(ПараметрыОписания.Значение);
		ПараметрыОписания.Вставить("ДанныеПроектнойЗадачи", ДанныеПроектнойЗадачи);
		
		Если ПараметрыОписания.Начало <> ДанныеПроектнойЗадачи.ТекущийПланНачало Тогда 
			
			Если ДанныеПроектнойЗадачи.ТипОграничения 
				= ПредопределенноеЗначение("Перечисление.ТипыОграниченийПроектныхЗадач.ФиксированноеНачало") 
			Или ДанныеПроектнойЗадачи.ТипОграничения 
				= ПредопределенноеЗначение("Перечисление.ТипыОграниченийПроектныхЗадач.НачалоНеРанее") Тогда 
				ПараметрыОписания.Вставить("ДатаОграничения", Интервал.Начало);
				ПараметрыОписания.Вставить("ТипОграничения", ДанныеПроектнойЗадачи.ТипОграничения);
				ВыполнитьОбработкуОповещения(ОписаниеОповещения, КодВозвратаДиалога.ОК);
			Иначе 
				ТекстВопроса = СтрШаблон( 
					НСтр("ru = 'Дата начала проектной задачи рассчитывается автоматически.
		                  |Установить для текущей задачи ограничение ""Фиксированное начало"" %1?';
		                  |en = 'Project task start date is calculated automatically.
		                  |Set for the task restriction ""Fixed start"" %1?'"),
					Формат(ПараметрыОписания.Начало, "ДФ='дд.ММ.гггг ЧЧ:мм'"));
					
				ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет,,КодВозвратаДиалога.Да);
			КонецЕсли;
		Иначе 
			ВыполнитьОбработкуОповещения(ОписаниеОповещения, КодВозвратаДиалога.ОК);
		КонецЕсли;
	Иначе 
		ВыполнитьОбработкуОповещения(ОписаниеОповещения, КодВозвратаДиалога.ОК);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДиаграммаГантаПриОкончанииРедактированияИнтервалаПродолжение(Результат, Параметры) Экспорт 
	
	Если Параметры.Свойство("ДатаОграничения") И ЗначениеЗаполнено(Параметры.ДатаОграничения) Тогда 
		ДатаОграничения = Параметры.ДатаОграничения;
		ТипОграничения = Параметры.ТипОграничения;
	Иначе 
		ДатаОграничения = Неопределено;
		ТипОграничения = Неопределено;
	КонецЕсли;
	
	Если Результат = КодВозвратаДиалога.Да Тогда 
		ТипОграничения = ПредопределенноеЗначение("Перечисление.ТипыОграниченийПроектныхЗадач.ФиксированноеНачало");
		ДатаОграничения = Параметры.Начало;
	КонецЕсли;
	
	Попытка
		ИзменитьЗадачуИзДиаграммыГанта(Параметры.Значение, Параметры.Начало, Параметры.Конец, 
			ТипОграничения, ДатаОграничения);
	Исключение	
		ОтменаРедактирования = Истина;
		ВызватьИсключение;
	КонецПопытки;
	
КонецПроцедуры

&НаКлиенте
Процедура ПоИсполнителюПриИзменении(Элемент)
	
	ОбновитьДеревоЗадач();
	
	ПоказатьСкрытьКнопкуОчисткиОтбора(Элементы.ПоИсполнителю, ПоИсполнителю);
	
КонецПроцедуры

&НаКлиенте
Процедура ПоИсполнителюНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("РежимРаботыФормы", 1);
	ПараметрыФормы.Вставить("УпрощенныйИнтерфейс", Истина);
	ПараметрыФормы.Вставить("ОтображатьСотрудников", Истина);
	ПараметрыФормы.Вставить("ОтображатьРоли", Истина);
	ПараметрыФормы.Вставить("ОтображатьКонтрагентов", Истина);
	ПараметрыФормы.Вставить("ВыбиратьКонтрагентов", Истина);
	ПараметрыФормы.Вставить("ЗаголовокФормы", НСтр("ru = 'Выбор исполнителя'; en = 'Select performer'"));
	
	Если ЗначениеЗаполнено(ПоИсполнителю) Тогда
		ПараметрыФормы.Вставить("ВыбранныеАдресаты", ПоИсполнителю);
	КонецЕсли;
	
	ОткрытьФорму("Справочник.АдреснаяКнига.ФормаСписка",
		ПараметрыФормы,
		Элементы.ПоИсполнителю,,,,,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ПоИсполнителюАвтоПодбор(Элемент, Текст, ДанныеВыбора, Ожидание, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Текст) Тогда 
		СтандартнаяОбработка = Ложь;
		ДополнениеТипа = Новый ОписаниеТипов("СправочникСсылка.ПолныеРоли");
		ДанныеВыбора = РаботаСПользователями.СформироватьДанныеВыбора(Текст, ДополнениеТипа);
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ПоИсполнителюОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Текст) Тогда 
		СтандартнаяОбработка = Ложь;
		ДополнениеТипа = Новый ОписаниеТипов("СправочникСсылка.ПолныеРоли");
		ДанныеВыбора = РаботаСПользователями.СформироватьДанныеВыбора(Текст, ДополнениеТипа);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыДеревоЗадач

&НаКлиенте
Процедура ДеревоЗадачПриАктивизацииСтроки(Элемент)
	
	Если Элементы.ГруппаСтраницы.ТекущаяСтраница <> Элементы.ГруппаЗадачиПроекта Тогда 
		Возврат;
	КонецЕсли;	
	
	Если Элемент.ТекущиеДанные = Неопределено Тогда
		ТекущаяПроектнаяЗадача = ПредопределенноеЗначение("Справочник.ПроектныеЗадачи.ПустаяСсылка");
		Элементы.ДеревоЗадачВверх.Доступность = Ложь;
		Элементы.ДеревоЗадачВниз.Доступность= Ложь;
		Элементы.ФормаНаУровеньВыше.Доступность = Ложь;
		Элементы.ФормаНаУровеньНиже.Доступность = Ложь;
		
		Элементы.ДеревоЗадачКонтекстноеМенюВверх.Доступность = Ложь;
		Элементы.ДеревоЗадачКонтекстноеМенюВниз.Доступность = Ложь;
		Элементы.ДеревоЗадачКонтекстноеМенюНаУровеньВыше.Доступность = Ложь;
		Элементы.ДеревоЗадачКонтекстноеМенюНаУровеньНиже.Доступность = Ложь;
		Возврат;
	КонецЕсли;
	
	Если КоличествоВыделенныхЗадач <> Элементы.ДеревоЗадач.ВыделенныеСтроки.Количество() 
		ИЛИ ТекущаяПроектнаяЗадача <> Элемент.ТекущиеДанные.Ссылка Тогда
		КоличествоВыделенныхЗадач = Элементы.ДеревоЗадач.ВыделенныеСтроки.Количество();
		ТекущаяПроектнаяЗадача = Элемент.ТекущиеДанные.Ссылка;
		УстановитьДоступностьКомандПеремещения(ТекущаяПроектнаяЗадача, ДеревоЗадач, Элементы);
	КонецЕсли;
	
	Если ВариантРедактирования = "ВСписке" Тогда 
		ТекущиеДанные = Элементы.ДеревоЗадач.ТекущиеДанные;
		Если ТекущиеДанные = Неопределено Тогда 
			Возврат;
		КонецЕсли;	
		
		Элементы.ТекущийПланНачалоЗадачи.ТолькоПросмотр = ТекущиеДанные.СуммарнаяЗадача;
		Элементы.ТекущийПланОкончаниеЗадачи.ТолькоПросмотр = ТекущиеДанные.СуммарнаяЗадача;
		Элементы.ТекущийПланДлительностьЗадачиСтр.ТолькоПросмотр = ТекущиеДанные.СуммарнаяЗадача;
			
		Элементы.НачалоФакт.ТолькоПросмотр = ТекущиеДанные.СуммарнаяЗадача;
		Элементы.ОкончаниеФакт.ТолькоПросмотр = ТекущиеДанные.СуммарнаяЗадача;
		Элементы.ДлительностьФактСтр.ТолькоПросмотр = ТекущиеДанные.СуммарнаяЗадача;
		
	КонецЕсли;	
	
	ОбновитьПараметрыУчетаВремениВФорме();
	
	Если ПоказыватьЗадачиПроцессов Тогда 
		ПодключитьОбработчикОжидания("ОбновитьМиникарточку", 0.2, Истина);
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоЗадачВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ТекущиеДанные = Элементы.ДеревоЗадач.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда 
		Возврат;
	КонецЕсли;	
	
	Если Поле = Элементы.ДеревоЗадачЗадачи Тогда
		ОткрытьФорму("ОбщаяФорма.ПроцессыИЗадачи",
			Новый Структура("Предмет", ТекущиеДанные.Ссылка),
			ЭтаФорма);
		Возврат;
	КонецЕсли;	
	
	Если Поле = Элементы.ДеревоЗадачСостояниеКонтроля Тогда
		КонтрольКлиент.ОбработкаКомандыКонтроль(ТекущиеДанные.Ссылка, ЭтаФорма);
		Возврат;
	КонецЕсли;
	
	Если ВариантРедактирования = "ВДиалоге" Или Не РеквизитыПроекта.ДанныеДоступныДляИзменения 
		Или РеквизитыПроекта.ПроектЗавершен Тогда 
		
		Если Поле = Элементы.ПредшественникиСтр Тогда
			ПараметрыФормы = Новый Структура("Ключ, ОткрытьЗакладкуПредшественники", ТекущиеДанные.Ссылка, Истина);
			ОткрытьФорму("Справочник.ПроектныеЗадачи.ФормаОбъекта", ПараметрыФормы, ЭтаФорма);
			Возврат;
		КонецЕсли;	
			
		Если Поле = Элементы.ИсполнителиСтр 
		 Или Поле = Элементы.ТекущийПланТрудозатратыСтр Тогда
			ПараметрыФормы = Новый Структура("Ключ, ОткрытьЗакладкуИсполнители", ТекущиеДанные.Ссылка, Истина);
			ОткрытьФорму("Справочник.ПроектныеЗадачи.ФормаОбъекта", ПараметрыФормы, ЭтаФорма);
			Возврат;
		КонецЕсли;
		
		Если Поле = Элементы.ТрудозатратыФактСтр Тогда 
			ПараметрыФормы = Новый Структура("Ключ, ОткрытьЗакладкуТрудозатраты", ТекущиеДанные.Ссылка, Истина);
			ОткрытьФорму("Справочник.ПроектныеЗадачи.ФормаОбъекта", ПараметрыФормы, ЭтаФорма);
			Возврат;
		КонецЕсли;
		
		ПараметрыФормы = Новый Структура("Ключ", ТекущиеДанные.Ссылка);
		ОткрытьФорму("Справочник.ПроектныеЗадачи.ФормаОбъекта", ПараметрыФормы, ЭтаФорма);
		Возврат;
		
	КонецЕсли;	
		
	СтандартнаяОбработка = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоЗадачПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	
	Отказ = Истина;
	
	Если Не РольПолныеПрава Тогда 
		Если Не РеквизитыПроекта.ДанныеДоступныДляИзменения Тогда 
			СообщениеТекст = НСтр("ru = 'У вас нет прав на добавление проектных задач.'; en = 'You have no permission to add project tasks.'");
			ПоказатьПредупреждение(, СообщениеТекст);
			Возврат;
		КонецЕсли;
		
		Если РеквизитыПроекта.ПроектЗавершен Тогда 
			СообщениеТекст = НСтр("ru = 'Проект завершен. Добавление задач запрещено.'; en = 'Project finished. Adding tasks is forbidden.'");
			ПоказатьПредупреждение(, СообщениеТекст);
			ОтменаРедактирования = Истина;
			Возврат;
		КонецЕсли;
		
		Если РеквизитыПроекта.РедактируетсяВСтороннейСистеме Тогда
			СообщениеТекст = НСтр("ru = 'Проект редактируется в сторонней системе. Добавление задач запрещено.'; en = 'Project is being edited in a thirt-party system. Adding tasks is forbidden.'");
			ПоказатьПредупреждение(, СообщениеТекст);
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	Если ВариантРедактирования = "ВСписке" Тогда 
		
		ТекущаяСтрока = Элементы.ДеревоЗадач.ТекущаяСтрока;
		Если ТекущаяСтрока = Неопределено Тогда 
			НовыйЭлемент = ДеревоЗадач.ПолучитьЭлементы().Добавить();
		Иначе
			ЭлементДерева = ДеревоЗадач.НайтиПоИдентификатору(ТекущаяСтрока);
			РодительЭлемента = ЭлементДерева.ПолучитьРодителя();
		
			Если РодительЭлемента = Неопределено Тогда 
				НовыйЭлемент = ДеревоЗадач.ПолучитьЭлементы().Добавить();
			Иначе
				НовыйЭлемент = РодительЭлемента.ПолучитьЭлементы().Добавить();
			КонецЕсли;
			
			Если Копирование Тогда 
				НовыйЭлемент.Копирование = Истина;
				
				НовыйЭлемент.РодительскаяЗадача = ЭлементДерева.РодительскаяЗадача;
				
				НовыйЭлемент.Наименование = ЭлементДерева.Наименование;
				НовыйЭлемент.ТекущийПланНачало = ЭлементДерева.ТекущийПланНачало;
				НовыйЭлемент.ТекущийПланОкончание = ЭлементДерева.ТекущийПланОкончание;
				
				НовыйЭлемент.ТекущийПланДлительность = ЭлементДерева.ТекущийПланДлительность;
				НовыйЭлемент.ТекущийПланЕдиницаДлительности = ЭлементДерева.ТекущийПланЕдиницаДлительности;
				
				НовыйЭлемент.ТекущийПланТрудозатраты = ЭлементДерева.ТекущийПланТрудозатраты;
				НовыйЭлемент.ТекущийПланЕдиницаТрудозатрат = ЭлементДерева.ТекущийПланЕдиницаТрудозатрат;
				НовыйЭлемент.ТекущийПланТрудозатратыСтр = ЭлементДерева.ТекущийПланТрудозатратыСтр;
				
				Для Каждого СтрокаИсполнитель Из ЭлементДерева.Исполнители Цикл
					НоваяСтрока = НовыйЭлемент.Исполнители.Добавить();
					НоваяСтрока.Исполнитель = СтрокаИсполнитель.Исполнитель;
					НоваяСтрока.ТекущийПланТрудозатраты = СтрокаИсполнитель.ТекущийПланТрудозатраты;
				КонецЦикла;	
				
				НовыйЭлемент.ИсполнителиСтр = ЭлементДерева.ИсполнителиСтр;
				
				Для Каждого СтрокаПредшественник Из ЭлементДерева.Предшественники Цикл
					НоваяСтрока = НовыйЭлемент.Предшественники.Добавить();
					НоваяСтрока.Предшественник = СтрокаПредшественник.Предшественник;
					НоваяСтрока.ТипЗависимости = СтрокаПредшественник.ТипЗависимости;
					НоваяСтрока.Задержка = СтрокаПредшественник.Задержка;
					НоваяСтрока.ЕдиницаЗадержки = СтрокаПредшественник.ЕдиницаЗадержки;
				КонецЦикла;
				
				НовыйЭлемент.ПредшественникиСтр = ЭлементДерева.ПредшественникиСтр;
				
			КонецЕсли;	
		КонецЕсли;	
		
		Элементы.ДеревоЗадач.ТекущаяСтрока = НовыйЭлемент.ПолучитьИдентификатор();
		Элементы.ДеревоЗадач.ИзменитьСтроку();
		
	Иначе
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("Проект", Проект);
	
		ТекущийДанные = Элементы.ДеревоЗадач.ТекущиеДанные;
		Если ТекущийДанные <> Неопределено Тогда 
			ПараметрыФормы.Вставить("Родитель", ТекущийДанные.РодительскаяЗадача);
			
			Если Копирование Тогда 
				ПараметрыФормы.Вставить("ЗначениеКопирования", ТекущийДанные.Ссылка);
			КонецЕсли;
		КонецЕсли;	
		
		ОткрытьФорму("Справочник.ПроектныеЗадачи.ФормаОбъекта", ПараметрыФормы, ЭтаФорма);
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоЗадачПередНачаломИзменения(Элемент, Отказ)
	
	Если ВариантРедактирования = "ВДиалоге" Или Не РеквизитыПроекта.ДанныеДоступныДляИзменения
		Или РеквизитыПроекта.ПроектЗавершен Тогда 
	
		Отказ = Истина;
	
		ТекущиеДанные = Элементы.ДеревоЗадач.ТекущиеДанные;
		Если ТекущиеДанные = Неопределено Тогда 
			Возврат;
		КонецЕсли;	
	
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("Ключ", ТекущиеДанные.Ссылка);
		
		ОткрытьФорму("Справочник.ПроектныеЗадачи.ФормаОбъекта", ПараметрыФормы, ЭтаФорма);
		
	Иначе	
		
		ТекущиеДанные = Элементы.ДеревоЗадач.ТекущиеДанные;
		Если ТекущиеДанные = Неопределено Тогда 
			Возврат;
		КонецЕсли;
		
		ЗаблокироватьСтрокуДляРедактирования(ТекущиеДанные.Ссылка, ЭтаФорма.УникальныйИдентификатор);
		
		ТекущиеДанные.НаименованиеЗадачи = ТекущиеДанные.Наименование;
			
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоЗадачПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	ТекущиеДанные = Элементы.ДеревоЗадач.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда 
		Возврат;
	КонецЕсли;	
	
	Если Не ЗначениеЗаполнено(ТекущиеДанные.Владелец) Тогда // изменяется новая строка
		ТекущиеДанные.Владелец = Проект;
		
		Если РеквизитыПроекта.СпособПланирования 
				= ПредопределенноеЗначение("Перечисление.СпособыПланированияПроекта.ОтДатыНачалаПроекта") Тогда 
			ТекущиеДанные.ТипОграничения 
				= ПредопределенноеЗначение("Перечисление.ТипыОграниченийПроектныхЗадач.КакМожноРаньше");
		Иначе	
			ТекущиеДанные.ТипОграничения 
				= ПредопределенноеЗначение("Перечисление.ТипыОграниченийПроектныхЗадач.КакМожноПозже");
		КонецЕсли;
		
		Если Не ТекущиеДанные.Копирование Тогда 
			РодительЭлемента = ТекущиеДанные.ПолучитьРодителя();
			
			Если РодительЭлемента = Неопределено Тогда 
				ЭлементыДерева = ДеревоЗадач.ПолучитьЭлементы();
			Иначе	
				ЭлементыДерева = РодительЭлемента.ПолучитьЭлементы();
			КонецЕсли;
			КоличествоЭлементов = ЭлементыДерева.Количество();
			
			УстановитьПредшественника = Ложь; // устанавливается связь, если 2 предыдущие задачи связаны
			Если КоличествоЭлементов >= 3 Тогда 
				ПоследняяЗадача = ЭлементыДерева[КоличествоЭлементов - 2];
				ПредпоследняяЗадача = ЭлементыДерева[КоличествоЭлементов - 3];
				
				Для Каждого Строка Из ПоследняяЗадача.Предшественники Цикл
					Если Строка.Предшественник = ПредпоследняяЗадача.Ссылка Тогда 
						УстановитьПредшественника = Истина;
						Прервать;
					КонецЕсли;	
				КонецЦикла;	
			КонецЕсли;	
			
			Если УстановитьПредшественника Тогда 
				НоваяСтрока = ТекущиеДанные.Предшественники.Добавить();
				НоваяСтрока.Предшественник = ПоследняяЗадача.Ссылка;
				НоваяСтрока.ТипЗависимости 
					= ПредопределенноеЗначение("Перечисление.ТипыЗависимостейПроектныхЗадач.ОкончаниеНачало");
				ТекущиеДанные.ПредшественникиСтр 
					= ПредставлениеПредшественников(ТекущиеДанные.Предшественники);
				
				Если ТекущиеДанные.ТипОграничения 
						= ПредопределенноеЗначение("Перечисление.ТипыОграниченийПроектныхЗадач.КакМожноРаньше") Тогда 
					ТекущиеДанные.ТекущийПланНачало = ПоследняяЗадача.ТекущийПланОкончание;
				Иначе
					Если РодительЭлемента = Неопределено Тогда 
						ТекущиеДанные.ТекущийПланОкончание = РеквизитыПроекта.ТекущийПланОкончание;
					Иначе	
						ТекущиеДанные.ТекущийПланОкончание = РодительЭлемента.ТекущийПланОкончание;
					КонецЕсли;	
				КонецЕсли;
			Иначе	
				Если РодительЭлемента = Неопределено Тогда 
					Если ТекущиеДанные.ТипОграничения 
							= ПредопределенноеЗначение("Перечисление.ТипыОграниченийПроектныхЗадач.КакМожноРаньше") Тогда 
						ТекущиеДанные.ТекущийПланНачало = РеквизитыПроекта.ТекущийПланНачало;
					Иначе
						ТекущиеДанные.ТекущийПланОкончание = РеквизитыПроекта.ТекущийПланОкончание;
					КонецЕсли;	
				Иначе
					Если ТекущиеДанные.ТипОграничения 
							= ПредопределенноеЗначение("Перечисление.ТипыОграниченийПроектныхЗадач.КакМожноРаньше") Тогда 
						ТекущиеДанные.ТекущийПланНачало = РодительЭлемента.ТекущийПланНачало;
					Иначе	
						ТекущиеДанные.ТекущийПланОкончание = РодительЭлемента.ТекущийПланОкончание;
					КонецЕсли;
				КонецЕсли;	
			КонецЕсли;
			
			Если РодительЭлемента <> Неопределено Тогда 
				ТекущиеДанные.РодительскаяЗадача = РодительЭлемента.Ссылка;
			КонецЕсли;
			
			ТекущиеДанные.ТекущийПланДлительность = 1;
			
			ПроектнаяЗадачаСтруктура = Новый Структура;
			ПроектнаяЗадачаСтруктура.Вставить("Владелец", ТекущиеДанные.Владелец);
			ПроектнаяЗадачаСтруктура.Вставить("ГрафикРаботы", ТекущиеДанные.ГрафикРаботы);
			
			Если ТекущиеДанные.ТипОграничения 
				= ПредопределенноеЗначение("Перечисление.ТипыОграниченийПроектныхЗадач.КакМожноРаньше") Тогда 
				ТекущиеДанные.ТекущийПланОкончание = РаботаСПроектами.РассчитатьОкончаниеПериода(ПроектнаяЗадачаСтруктура, 
					ТекущиеДанные.ТекущийПланНачало, 
					ТекущиеДанные.ТекущийПланДлительность, 
					ТекущиеДанные.ТекущийПланЕдиницаДлительности);
			Иначе
				ТекущиеДанные.ТекущийПланНачало = РаботаСПроектами.РассчитатьНачалоПериода(ПроектнаяЗадачаСтруктура, 
					ТекущиеДанные.ТекущийПланОкончание, 
					ТекущиеДанные.ТекущийПланДлительность, 
					ТекущиеДанные.ТекущийПланЕдиницаДлительности);
			КонецЕсли;			
			
		КонецЕсли;
		
		ПодписьТекПланДлительность = ПолучитьПодписьДлительности(
			ТекущиеДанные.ТекущийПланДлительность, ТекущиеДанные.ТекущийПланЕдиницаДлительности);
		ТекущиеДанные.ТекущийПланДлительностьСтр = Строка(ТекущиеДанные.ТекущийПланДлительность) + " " + ПодписьТекПланДлительность;
		
		ТекущиеДанные.ТекущийПланЕдиницаТрудозатрат = РеквизитыПроекта.ЕдиницаТрудозатратЗадач;
		ТекущиеДанные.ЕдиницаТрудозатратФакт = РеквизитыПроекта.ЕдиницаТрудозатратЗадач;
			
		ТекущиеДанные.ТекущийПланЕдиницаДлительности = РеквизитыПроекта.ЕдиницаДлительностиЗадач;
		ТекущиеДанные.ЕдиницаДлительностиФакт = РеквизитыПроекта.ЕдиницаДлительностиЗадач;
		
	КонецЕсли;	
	
	ДатаНачалаДоРедактирования = ТекущиеДанные.ТекущийПланНачало;
	ДатаОкончанияДоРедактирования = ТекущиеДанные.ТекущийПланОкончание;
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоЗадачПередОкончаниемРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования, Отказ)
	
	ТекущиеДанные = Элементы.ДеревоЗадач.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда 
		Возврат;
	КонецЕсли;	
	
	Если ОтменаРедактирования Тогда 
		
		ТекущаяСтрока = Элементы.ДеревоЗадач.ТекущаяСтрока;
		ЭлементДерева = ДеревоЗадач.НайтиПоИдентификатору(ТекущаяСтрока);
		
		Если Не ЗначениеЗаполнено(ТекущиеДанные.Ссылка) Тогда 
			
			Отказ = Истина;
			
			РодительЭлемента = ЭлементДерева.ПолучитьРодителя();
			Если РодительЭлемента = Неопределено Тогда 
				ИндексЭлемента = ДеревоЗадач.ПолучитьЭлементы().Индекс(ЭлементДерева);
				ДеревоЗадач.ПолучитьЭлементы().Удалить(ИндексЭлемента);
			Иначе
				ИндексЭлемента = РодительЭлемента.ПолучитьЭлементы().Индекс(ЭлементДерева);
				РодительЭлемента.ПолучитьЭлементы().Удалить(ИндексЭлемента);
			КонецЕсли;	
			Возврат;
			
		Иначе	
			
		КонецЕсли;	
		
	Иначе	
		
		Если Не ЗначениеЗаполнено(ТекущиеДанные.Наименование) Тогда 
			ТекстСообщения = НСтр("ru = 'Не заполнено поле ""Наименование""'; en = 'Field ""Description"" is not filled in'");
			ПоказатьПредупреждение(, ТекстСообщения);
			Отказ = Истина;
			Возврат;
		КонецЕсли;	
	
		Если Не ЗначениеЗаполнено(ТекущиеДанные.ТекущийПланНачало) Тогда 
			ТекстСообщения = НСтр("ru = 'Не заполнено поле ""Начало (план)""'; en = 'Field ""Start (plan)"" is not filled in'");
			ПоказатьПредупреждение(, ТекстСообщения);
			Отказ = Истина;
			Возврат;
		КонецЕсли;
	
		Если Не ЗначениеЗаполнено(ТекущиеДанные.ТекущийПланОкончание) Тогда 
			ТекстСообщения = НСтр("ru = 'Не заполнено поле ""Окончание (план)""'; en = 'Field ""Finish (plan)"" is not filled in'");
			ПоказатьПредупреждение(, ТекстСообщения);
			Отказ = Истина;
			Возврат;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоЗадачПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	ТекущиеДанные = Элементы.ДеревоЗадач.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда 
		Возврат;
	КонецЕсли;	
	
	Если ОтменаРедактирования Тогда 
		РазблокироватьСтрокуДляРедактирования(ТекущиеДанные.Ссылка, ЭтаФорма.УникальныйИдентификатор);
		
		МассивИзмененныхЗадач = Новый Массив;
		МассивИзмененныхЗадач.Добавить(ТекущиеДанные.Ссылка);
		ОбновитьИзмененныеСтрокиЗадач(МассивИзмененныхЗадач);
		Возврат;
	КонецЕсли;	
	
	Если Не ЗначениеЗаполнено(ТекущиеДанные.Ссылка) Тогда 
		НовыйЭлемент = Истина;
	Иначе
		НовыйЭлемент = Ложь;
	КонецЕсли;	
	
	ЗаписатьПроектнуюЗадачуИзСтрокиДерева(Элементы.ДеревоЗадач.ТекущаяСтрока);
	
	Если НовыйЭлемент Тогда 
		ПоказатьОповещениеПользователя(
			"Создание:", 
			ПолучитьНавигационнуюСсылку(ТекущиеДанные.Ссылка),
			Строка(ТекущиеДанные.Ссылка),
			БиблиотекаКартинок.Информация32);
	Иначе
		ПоказатьОповещениеПользователя(
			"Изменение:", 
			ПолучитьНавигационнуюСсылку(ТекущиеДанные.Ссылка),
			Строка(ТекущиеДанные.Ссылка),
			БиблиотекаКартинок.Информация32);
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоЗадачПередУдалением(Элемент, Отказ)
	
	Отказ = Истина;
	
	Если Не РольПолныеПрава Тогда 
		Если Не РеквизитыПроекта.ДанныеДоступныДляИзменения Тогда 
			СообщениеТекст = НСтр("ru = 'У вас нет прав на удаление проектных задач.'; en = 'Insufficient permissions to delete project tasks.'");
			ПоказатьПредупреждение(, СообщениеТекст);
			Возврат;
		КонецЕсли;
		
		Если РеквизитыПроекта.ПроектЗавершен Тогда 
			СообщениеТекст = НСтр("ru = 'Проект завершен. Удаление задач запрещено.'; en = 'Project finished. Deleting tasks is forbidden.'");
			ПоказатьПредупреждение(, СообщениеТекст);
			ОтменаРедактирования = Истина;
			Возврат;
		КонецЕсли;
	
		Если РеквизитыПроекта.РедактируетсяВСтороннейСистеме Тогда
			СообщениеТекст = НСтр("ru = 'Проект редактируется в сторонней системе. Удаление задач запрещено.'; en = 'Project is being edited in a thirt-party system. Deleting tasks is forbidden.'");
			ПоказатьПредупреждение(, СообщениеТекст);
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	ВыделенныеСтроки = Элемент.ВыделенныеСтроки;
	Если ВыделенныеСтроки = Неопределено Или ВыделенныеСтроки.Количество() <= 0 Тогда
		Возврат;
	КонецЕсли;	
	
	Режим = РежимДиалогаВопрос.ДаНет;
    ПараметрыОповещения = Новый Структура;
	ПараметрыОповещения.Вставить("ВыделенныеСтроки", ВыделенныеСтроки);
	
	Если ВыделенныеСтроки.Количество() = 1 Тогда 
		Если НЕ Элементы.ДеревоЗадач.ТекущиеДанные.ПометкаУдаления Тогда
			ТекстВопроса = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Пометить ""%1"" на удаление?'; en = 'Mark ""%1"" for deletion?'"),
				Строка(Элементы.ДеревоЗадач.ТекущиеДанные.Ссылка));
				
			ПараметрыОповещения.Вставить("ПометкаУдаления", Истина);
	        ОписаниеОповещения = Новый ОписаниеОповещения(
				"ДеревоЗадачПередУдалениемПродолжение",
				ЭтотОбъект,
				ПараметрыОповещения);	
				
			ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, Режим);
		Иначе
			ТекстВопроса = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Снять с ""%1"" пометку удаления?'; en = 'Remove deletion mark from ""%1"" ?'"),
				Строка(Элементы.ДеревоЗадач.ТекущиеДанные.Ссылка));
				
			ПараметрыОповещения.Вставить("ПометкаУдаления", Ложь);
	        ОписаниеОповещения = Новый ОписаниеОповещения(
				"ДеревоЗадачПередУдалениемПродолжение",
				ЭтотОбъект,
				ПараметрыОповещения);	
				
			ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, Режим);
		КонецЕсли;
	ИначеЕсли ВыделенныеСтроки.Количество() > 1 Тогда
		ПометкаУдаления = Истина;
		Для Каждого СтрокаТаблицы Из ВыделенныеСтроки Цикл
			СтрокаТаблицыПроектнаяЗадача = ДеревоЗадач.НайтиПоИдентификатору(СтрокаТаблицы);
			Если СтрокаТаблицыПроектнаяЗадача.ПометкаУдаления = Истина Тогда
				ПометкаУдаления = Ложь;
				Прервать;	
			КонецЕсли;
		КонецЦикла;
		
		Если ПометкаУдаления Тогда
			ПараметрыОповещения.Вставить("ПометкаУдаления", Истина);
	        ОписаниеОповещения = Новый ОписаниеОповещения(
				"ДеревоЗадачПередУдалениемПродолжение",
				ЭтотОбъект,
				ПараметрыОповещения);	
				
			ПоказатьВопрос(ОписаниеОповещения,
				НСтр("ru = 'Пометить выделенные элементы на удаление?'; en = 'Mark selected items for deletion?'"), Режим);
		Иначе
			ПараметрыОповещения.Вставить("ПометкаУдаления", Ложь);
	        ОписаниеОповещения = Новый ОписаниеОповещения(
				"ДеревоЗадачПередУдалениемПродолжение",
				ЭтотОбъект,
				ПараметрыОповещения);	
				
			ПоказатьВопрос(ОписаниеОповещения,
				НСтр("ru = 'Снять с выделенных элементов пометку на удаление?'; en = 'Remove deletion mark from selected items?'"), Режим);
		КонецЕсли;
	КонецЕсли;		
			
КонецПроцедуры

&НаКлиенте
Процедура ДеревоЗадачПередУдалениемПродолжение(Результат, Параметры) Экспорт 

	Если Результат = КодВозвратаДиалога.Нет Тогда
		Возврат;
	КонецЕсли;

	ВыделенныеСтроки = Параметры.ВыделенныеСтроки;
	ПометкаУдаления = Параметры.ПометкаУдаления;
	СписокРаскрытыхЗадач.Очистить();
	РаботаСПроектамиКлиент.ПолучитьМассивРаскрытыхЗадач(Элементы.ДеревоЗадач, ДеревоЗадач.ПолучитьЭлементы(), СписокРаскрытыхЗадач);
	ВременнаяТекущаяПроектнаяЗадача = ТекущаяПроектнаяЗадача;
	ПометитьНаУдалениеПроектныеЗадачи(ВыделенныеСтроки, ПометкаУдаления);
	РаботаСПроектамиКлиент.УстановитьРазвернутостьЭлементовДерева(Элементы.ДеревоЗадач, ДеревоЗадач, СписокРаскрытыхЗадач);		
	
	Если ПоказатьПомеченныеНаУдаление Тогда 
		РаботаСПроектамиКлиент.УстановитьТекущуюЗадачуВДеревеПоСсылке(Элементы.ДеревоЗадач, ДеревоЗадач, ВременнаяТекущаяПроектнаяЗадача);
	КонецЕсли;	

КонецПроцедуры

&НаКлиенте
Процедура ДеревоЗадачПроверкаПеретаскивания(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Поле)
	
	Если Строка <> Неопределено Тогда
		СтандартнаяОбработка = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоЗадачПеретаскивание(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Поле)
	
	СтандартнаяОбработка = Ложь;
	
	Если Не РольПолныеПрава Тогда 
		Если Не РеквизитыПроекта.ДанныеДоступныДляИзменения Тогда 
			СообщениеТекст = НСтр("ru = 'У вас нет прав на изменение проектных задач. Перетаскивание задач запрещено.'; en = 'Insufficient permissions to change project tasks. Dragging tasks is forbidden.'");
			ПоказатьПредупреждение(, СообщениеТекст);
			Возврат;
		КонецЕсли;
		
		Если РеквизитыПроекта.ПроектЗавершен Тогда 
			СообщениеТекст = НСтр("ru = 'Проект завершен. Перетаскивание задач запрещено.'; en = 'Project finished. Dragging tasks is forbidden.'");
			ПоказатьПредупреждение(, СообщениеТекст);
			ОтменаРедактирования = Истина;
			Возврат;
		КонецЕсли;
		
		Если РеквизитыПроекта.РедактируетсяВСтороннейСистеме Тогда
			СообщениеТекст = НСтр("ru = 'Проект редактируется в сторонней системе. Перетаскивание задач запрещено.'; en = 'Project is being edited in a thirt-party system. Dragging tasks is forbidden.'");
			ПоказатьПредупреждение(, СообщениеТекст);
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	СтрокаИсточник = ПараметрыПеретаскивания.Значение[0];
	СтрокаДереваИсточник = ДеревоЗадач.НайтиПоИдентификатору(СтрокаИсточник);
	Если СтрокаДереваИсточник = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	СсылкаИсточник = СтрокаДереваИсточник.Ссылка;
	
	Если Строка <> Неопределено Тогда
		СтрокаПриемник = Строка;
		СтрокаДереваПриемник = ДеревоЗадач.НайтиПоИдентификатору(СтрокаПриемник);
		СсылкаПриемник = СтрокаДереваПриемник.Ссылка;
		
		Если Не СтрокаДереваИсточник.ПометкаУдаления И СтрокаДереваПриемник.ПометкаУдаления Тогда 
			Возврат;
		КонецЕсли;	
	Иначе
		СсылкаПриемник = Неопределено;
	КонецЕсли;		
	
	Если СсылкаИсточник = СсылкаПриемник Тогда 
		Возврат;
	КонецЕсли;	
	
	СписокРаскрытыхЗадач.Очистить();
	РаботаСПроектамиКлиент.ПолучитьМассивРаскрытыхЗадач(
		Элементы.ДеревоЗадач, ДеревоЗадач.ПолучитьЭлементы(), СписокРаскрытыхЗадач);
	
	ДеревоЗадачПеретаскиваниеСервер(СсылкаИсточник, СсылкаПриемник);
	
	РаботаСПроектамиКлиент.УстановитьРазвернутостьЭлементовДерева(
		Элементы.ДеревоЗадач, ДеревоЗадач, СписокРаскрытыхЗадач);
		
	РаботаСПроектамиКлиент.УстановитьТекущуюЗадачуВДеревеПоСсылке(
		Элементы.ДеревоЗадач, ДеревоЗадач, СсылкаИсточник);	
		
	УстановитьДоступностьКомандПеремещения(
		ТекущаяПроектнаяЗадача, ДеревоЗадач, Элементы);	
		
КонецПроцедуры

&НаКлиенте
Процедура ПредшественникиСтрНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ТекущиеДанные = Элементы.ДеревоЗадач.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда 
		Возврат;
	КонецЕсли;	
	
	Выбранные = Новый Массив;
	Для Каждого Строка Из ТекущиеДанные.Предшественники Цикл
		Выбранные.Добавить(Строка.Предшественник);
	КонецЦикла;	
	
	ПараметрыФормы = Новый Структура;	
	ПараметрыФормы.Вставить("Отбор", Новый Структура("Владелец", Проект));
	ПараметрыФормы.Вставить("Заголовок", НСтр("ru = 'Подбор предшественников'; en = 'Filling predecessors'"));
	ПараметрыФормы.Вставить("Выбранные", Выбранные);
	
	ОткрытьФорму("Справочник.ПроектныеЗадачи.Форма.ФормаПодбора", ПараметрыФормы, Элемент);	
	
КонецПроцедуры

&НаКлиенте
Процедура ПредшественникиСтрАвтоПодбор(Элемент, Текст, ДанныеВыбора, Ожидание, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Текст) Тогда 
		СтандартнаяОбработка = Ложь;
		ТекстПоиска = ВыделитьТекстДляАвтоподбораПредшественника(Текст);
		ДанныеВыбора = РаботаСПроектами.СформироватьДанныеВыбораЗадачиПроекта(ТекстПоиска, Проект);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПредшественникиСтрОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Текст) Тогда 
		СтандартнаяОбработка = Ложь;
		ТекстПоиска = ВыделитьТекстДляАвтоподбораПредшественника(Текст);
		ДанныеВыбора = РаботаСПроектами.СформироватьДанныеВыбораЗадачиПроекта(ТекстПоиска, Проект);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПредшественникиСтрОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ТекущиеДанные = Элементы.ДеревоЗадач.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(ВыбранноеЗначение) = Тип("Массив") Тогда 
		
		Предшественники = ТекущиеДанные.Предшественники;
		Предшественники.Очистить();
		
		ДЛя Каждого ВыбранныйЭлемент Из ВыбранноеЗначение Цикл
			НоваяСтрока = Предшественники.Добавить();
			НоваяСтрока.Предшественник = ВыбранныйЭлемент;
			НоваяСтрока.ТипЗависимости 
				= ПредопределенноеЗначение("Перечисление.ТипыЗависимостейПроектныхЗадач.ОкончаниеНачало");
		КонецЦикла;	
		ТекущиеДанные.ПредшественникиСтр = ПредставлениеПредшественников(Предшественники);
		
	Иначе
		
		Предшественники = ТекущиеДанные.Предшественники;
		
		КодыПредшественников = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(
			Элементы.ПредшественникиСтр.ТекстРедактирования, ",");
		ВыполнитьУдалениеПредшественников(КодыПредшественников, Предшественники);
			
		ТекущиеДанные.Предшественники.Очистить();
		Для Каждого Строка Из Предшественники Цикл
			НоваяСтрока = ТекущиеДанные.Предшественники.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
		КонецЦикла;	
		
		Найден = Ложь;
		Для Каждого Строка Из ТекущиеДанные.Предшественники Цикл
			Если Строка.Предшественник = ВыбранноеЗначение Тогда 
				Найден = Истина;
				Прервать;
			КонецЕсли;	
		КонецЦикла;	
		
		Если Не Найден Тогда 
			НоваяСтрока = ТекущиеДанные.Предшественники.Добавить();
			НоваяСтрока.Предшественник = ВыбранноеЗначение;
			НоваяСтрока.ТипЗависимости 
				= ПредопределенноеЗначение("Перечисление.ТипыЗависимостейПроектныхЗадач.ОкончаниеНачало");
		КонецЕсли;		
		
		ТекущиеДанные.ПредшественникиСтр = ПредставлениеПредшественников(ТекущиеДанные.Предшественники);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПредшественникиСтрПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.ДеревоЗадач.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	Предшественники = ТекущиеДанные.Предшественники;
	
	Если Не ЗначениеЗаполнено(ТекущиеДанные.ПредшественникиСтр) Тогда 
		Предшественники.Очистить();
		ТекущиеДанные.ПредшественникиСтр = "";
	Иначе	
		КодыПредшественников = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(ТекущиеДанные.ПредшественникиСтр, ",");
		Если Предшественники.Количество() > КодыПредшественников.Количество() Тогда 
			ВыполнитьУдалениеПредшественников(КодыПредшественников, Предшественники);
			ТекущиеДанные.ПредшественникиСтр = ПредставлениеПредшественников(Предшественники);
		КонецЕсли;
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ИсполнителиСтрНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ТекущиеДанные = Элементы.ДеревоЗадач.ТекущиеДанные;
	Исполнители = ТекущиеДанные.Исполнители;
	АдресВременногоХранилища = ПоместитьИсполнителейВоВременноеХранилище(Исполнители, УникальныйИдентификатор);
	
	Если РеквизитыПроекта.ЗаполненаПроектнаяКоманда Тогда 
		
		ПараметрыВыбора = Новый Структура;
		ПараметрыВыбора.Вставить("Проект", Проект);
		ПараметрыВыбора.Вставить("АдресВременногоХранилища", АдресВременногоХранилища);
		
		ОткрытьФорму("Справочник.ПроектныеЗадачи.Форма.ФормаПодбораИсполнителей", ПараметрыВыбора, ЭтаФорма); 
		
	Иначе
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("РежимРаботыФормы", 1);
		ПараметрыФормы.Вставить("УпрощенныйИнтерфейс", Истина);
		ПараметрыФормы.Вставить("ОтображатьСотрудников", Истина);
		ПараметрыФормы.Вставить("ОтображатьРоли", Истина);
		ПараметрыФормы.Вставить("ОтображатьКонтрагентов", Истина);
		ПараметрыФормы.Вставить("ВыбиратьКонтрагентов", Истина);
		ПараметрыФормы.Вставить("ЗаголовокФормы", НСтр("ru = 'Выбор исполнителя'; en = 'Performer selection'"));
		
		Если Исполнители.Количество() = 1 Тогда
			ПараметрыФормы.Вставить("ВыбранныеАдресаты", Исполнители[0].Исполнитель);
		КонецЕсли;
		
		ОткрытьФорму("Справочник.АдреснаяКнига.ФормаСписка",
			ПараметрыФормы,
			Элементы.ИсполнителиСтр,,,,,
			РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ИсполнителиСтрОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
		
	ТекущиеДанные = Элементы.ДеревоЗадач.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	Найден = Ложь;
	Для Каждого Строка Из ТекущиеДанные.Исполнители Цикл
		Если Строка.Исполнитель = ВыбранноеЗначение Тогда 
			Найден = Истина;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Если Не Найден Тогда 
		НоваяСтрока = ТекущиеДанные.Исполнители.Добавить();
		НоваяСтрока.Исполнитель = ВыбранноеЗначение;
	КонецЕсли;
	Исполнители = ТекущиеДанные.Исполнители;
	
	ИменаИсполнителей = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(
		Элементы.ИсполнителиСтр.ТекстРедактирования, ",");
	Если Исполнители.Количество() > ИменаИсполнителей.Количество() Тогда 
		ВыполнитьУдалениеИсполнителей(ИменаИсполнителей, Исполнители);
		
		ТекущиеДанные.Исполнители.Очистить();
		Для Каждого Строка Из Исполнители Цикл
			НоваяСтрока = ТекущиеДанные.Исполнители.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
		КонецЦикла;
	КонецЕсли;
	ТекущиеДанные.ИсполнителиСтр = ПредставлениеИсполнителя(ТекущиеДанные.Исполнители);
	
	Для Каждого СтрокаИсполнители Из ТекущиеДанные.Исполнители Цикл
		СтрокаИсполнители.ТекущийПланТрудозатраты = ТекущиеДанные.ТекущийПланТрудозатраты / ТекущиеДанные.Исполнители.Количество();
	КонецЦикла;
		
КонецПроцедуры

&НаКлиенте
Процедура ИсполнителиСтрАвтоПодбор(Элемент, Текст, ДанныеВыбора, Ожидание, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Текст) Тогда 
		СтандартнаяОбработка = Ложь;
		ТекстПоиска = ВыделитьТекстДляАвтоподбораИсполнителя(Текст);
		Если РеквизитыПроекта.ЗаполненаПроектнаяКоманда Тогда 
			ДанныеВыбора = РаботаСПроектами.СформироватьДанныеВыбораИсполнителя(Проект, ТекстПоиска);
		Иначе
			ДополнениеТипа = Новый ОписаниеТипов("СправочникСсылка.ПолныеРоли");
			ДанныеВыбора = РаботаСПользователями.СформироватьДанныеВыбора(ТекстПоиска, ДополнениеТипа);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ИсполнителиСтрОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Текст) Тогда 
		СтандартнаяОбработка = Ложь;
		ТекстПоиска = ВыделитьТекстДляАвтоподбораИсполнителя(Текст);
		Если РеквизитыПроекта.ЗаполненаПроектнаяКоманда Тогда 
			ДанныеВыбора = РаботаСПроектами.СформироватьДанныеВыбораИсполнителя(Проект, ТекстПоиска);
		Иначе
			ДополнениеТипа = Новый ОписаниеТипов("СправочникСсылка.ПолныеРоли");
			ДанныеВыбора = РаботаСПользователями.СформироватьДанныеВыбора(ТекстПоиска, ДополнениеТипа);
		КонецЕсли;
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ИсполнителиСтрПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.ДеревоЗадач.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	Исполнители = ТекущиеДанные.Исполнители;
	
	Если Не ЗначениеЗаполнено(ТекущиеДанные.ИсполнителиСтр) Тогда 
		Исполнители.Очистить();
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ИсполнителиСтрОчистка(Элемент, СтандартнаяОбработка)
	
	ТекущиеДанные = Элементы.ДеревоЗадач.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	Исполнители = ТекущиеДанные.Исполнители;
	Исполнители.Очистить();
	
КонецПроцедуры

&НаКлиенте
Процедура НаименованиеЗадачиПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.ДеревоЗадач.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные.Наименование = ТекущиеДанные.НаименованиеЗадачи;
	
КонецПроцедуры

&НаКлиенте
Процедура ТекущийПланНачалоЗадачиПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.ДеревоЗадач.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ТекущиеДанные.ТекущийПланНачало) 
		И ТекущиеДанные.ТекущийПланНачало = НачалоДня(ТекущиеДанные.ТекущийПланНачало)
		И ИспользоватьГрафикиРаботы Тогда
		
		ГрафикРаботы = ?(ЗначениеЗаполнено(ТекущиеДанные.ГрафикРаботы), 
			ТекущиеДанные.ГрафикРаботы, 
			РеквизитыПроекта.ГрафикРаботы);
			
		ТекущиеДанные.ТекущийПланНачало = ГрафикиРаботы.ПолучитьПервоеРабочееВремяДня(
			ГрафикРаботы,
			ТекущиеДанные.ТекущийПланНачало);
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ТекущиеДанные.ТекущийПланНачало) Тогда 	
		ТекущиеДанные.ТекущийПланДлительность = 0;
		
	ИначеЕсли Не ЗначениеЗаполнено(ТекущиеДанные.ТекущийПланДлительность) Тогда 
		ТекущиеДанные.ТекущийПланОкончание = ТекущиеДанные.ТекущийПланНачало;
		
	ИначеЕсли ЗначениеЗаполнено(ТекущиеДанные.ТекущийПланДлительность) Тогда 	
		
		ПроектнаяЗадачаСтруктура = Новый Структура;
		ПроектнаяЗадачаСтруктура.Вставить("Владелец", ТекущиеДанные.Владелец);
		ПроектнаяЗадачаСтруктура.Вставить("ГрафикРаботы", ТекущиеДанные.ГрафикРаботы);
		
		ТекущиеДанные.ТекущийПланОкончание = РаботаСПроектами.РассчитатьОкончаниеПериода(ПроектнаяЗадачаСтруктура, 
			ТекущиеДанные.ТекущийПланНачало, 
			ТекущиеДанные.ТекущийПланДлительность, 
			ТекущиеДанные.ТекущийПланЕдиницаДлительности);
			
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ТекущиеДанные.ТекущийПланНачало) 
		И РеквизитыПроекта.СпособПланирования 
			= ПредопределенноеЗначение("Перечисление.СпособыПланированияПроекта.ОтДатыНачалаПроекта") 
		И ТекущиеДанные.ТекущийПланНачало <> ДатаНачалаДоРедактирования
		И РеквизитыПроекта.АвтоматическиРассчитыватьПланПроекта Тогда 
		
		Если ТекущиеДанные.ТипОграничения 
				= ПредопределенноеЗначение("Перечисление.ТипыОграниченийПроектныхЗадач.ФиксированноеНачало") 
			Или ТекущиеДанные.ТипОграничения 
				= ПредопределенноеЗначение("Перечисление.ТипыОграниченийПроектныхЗадач.НачалоНеРанее") Тогда 
			ТекущиеДанные.ДатаОграничения = ТекущиеДанные.ТекущийПланНачало;
		Иначе
			ПараметрыОповещения = Новый Структура;
			ПараметрыОповещения.Вставить("ТекущийПланНачало", ТекущиеДанные.ТекущийПланНачало);
			
			ОписаниеОповещения = Новый ОписаниеОповещения(
				"ТекущийПланНачалоПриИзмененииПродолжение",
				ЭтотОбъект, ПараметрыОповещения);
			
			ТекстВопроса = СтрШаблон( 
				НСтр("ru = 'Дата начала проектной задачи рассчитывается автоматически.
							|Установить для текущей задачи ограничение ""Фиксированное начало"" %1?';
							|en = 'Project task start date is calculated automatically.
							|Set for the task restriction ""Fixed start"" %1?'"),
							Формат(ТекущиеДанные.ТекущийПланНачало, "ДФ='дд.ММ.гггг ЧЧ:мм'"));
			
			ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет,,КодВозвратаДиалога.Нет);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТекущийПланНачалоПриИзмененииПродолжение(Результат, Параметры) Экспорт 
	
	ТекущиеДанные = Элементы.ДеревоЗадач.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	Если Результат = КодВозвратаДиалога.Да Тогда 
		ТекущиеДанные.ТипОграничения 
			= ПредопределенноеЗначение("Перечисление.ТипыОграниченийПроектныхЗадач.ФиксированноеНачало");
		ТекущиеДанные.ДатаОграничения = Параметры.ТекущийПланНачало;
		ЗаписатьПроектнуюЗадачуИзСтрокиДерева(Элементы.ДеревоЗадач.ТекущаяСтрока);
	Иначе	
		ТекущиеДанные.ТекущийПланНачало = ДатаНачалаДоРедактирования;
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура ТекущийПланОкончаниеЗадачиПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.ДеревоЗадач.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ТекущиеДанные.ТекущийПланОкончание) 
		И ТекущиеДанные.ТекущийПланОкончание = НачалоДня(ТекущиеДанные.ТекущийПланОкончание)
		И ИспользоватьГрафикиРаботы Тогда
		
		ГрафикРаботы = ?(ЗначениеЗаполнено(ТекущиеДанные.ГрафикРаботы),
			ТекущиеДанные.ГрафикРаботы,
			РеквизитыПроекта.ГрафикРаботы);
			
		ТекущиеДанные.ТекущийПланОкончание = ГрафикиРаботы.ПолучитьПоследнееРабочееВремяДня(
			ГрафикРаботы,
			ТекущиеДанные.ТекущийПланОкончание);
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ТекущиеДанные.ТекущийПланОкончание) Тогда 	
		ТекущиеДанные.ТекущийПланДлительность = 0;	
	
	ИначеЕсли Не ЗначениеЗаполнено(ТекущиеДанные.ТекущийПланДлительность) Тогда 
		ТекущиеДанные.ТекущийПланНачало = ТекущиеДанные.ТекущийПланОкончание;
		
	ИначеЕсли ЗначениеЗаполнено(ТекущиеДанные.ТекущийПланНачало) Тогда 	
		
		ПроектнаяЗадачаСтруктура = Новый Структура;
		ПроектнаяЗадачаСтруктура.Вставить("Владелец", ТекущиеДанные.Владелец);
		ПроектнаяЗадачаСтруктура.Вставить("ГрафикРаботы", ТекущиеДанные.ГрафикРаботы);
		
		ТекущиеДанные.ТекущийПланДлительность = РаботаСПроектами.РассчитатьДлительностьПериода(ПроектнаяЗадачаСтруктура, 
			ТекущиеДанные.ТекущийПланНачало, 
			ТекущиеДанные.ТекущийПланОкончание, 
			ТекущиеДанные.ТекущийПланЕдиницаДлительности);
			
		ТекущиеДанные.ТекущийПланДлительностьСтр = ТекущиеДанные.ТекущийПланДлительность;	
			
	КонецЕсли;	
	
	Если ЗначениеЗаполнено(ТекущиеДанные.ТекущийПланОкончание) 
		И РеквизитыПроекта.СпособПланирования 
			= ПредопределенноеЗначение("Перечисление.СпособыПланированияПроекта.ОтДатыОкончанияПроекта") 
		И ТекущиеДанные.ТекущийПланОкончание <> ДатаОкончанияДоРедактирования
		И РеквизитыПроекта.АвтоматическиРассчитыватьПланПроекта Тогда 
		
		Если ТекущиеДанные.ТипОграничения 
				= ПредопределенноеЗначение("Перечисление.ТипыОграниченийПроектныхЗадач.ФиксированноеОкончание") 
		 Или ТекущиеДанные.ТипОграничения 
		 		= ПредопределенноеЗначение("Перечисление.ТипыОграниченийПроектныхЗадач.ОкончаниеНеПозднее") Тогда 
			ТекущиеДанные.ДатаОграничения = ТекущиеДанные.ТекущийПланОкончание;
		Иначе 	
			ПараметрыОповещения = Новый Структура;
			ПараметрыОповещения.Вставить("ТекущийПланОкончание", ТекущиеДанные.ТекущийПланОкончание);
			
			ОписаниеОповещения = Новый ОписаниеОповещения(
				"ТекущийПланОкончаниеПриИзмененииПродолжение",
				ЭтотОбъект, ПараметрыОповещения);
			
			ТекстВопроса = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку( 
				НСтр("ru = 'Дата окончания проектной задачи рассчитывается автоматически.
							|Установить для текущей задачи ограничение ""Фиксированное окончание"" %1?';
							|en = 'Project task finish date is calculated automatically. 
							|Set for the task restriction ""Fixed end"" %1?'"),
							Формат(ТекущиеДанные.ТекущийПланОкончание, "ДФ='дд.ММ.гггг ЧЧ:мм'"));
			
			ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет,,КодВозвратаДиалога.Нет);
		КонецЕсли;
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ТекущийПланОкончаниеПриИзмененииПродолжение(Результат, Параметры) Экспорт 
	
	ТекущиеДанные = Элементы.ДеревоЗадач.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	Если Результат = КодВозвратаДиалога.Да Тогда 
		ТекущиеДанные.ТипОграничения 
			= ПредопределенноеЗначение("Перечисление.ТипыОграниченийПроектныхЗадач.ФиксированноеОкончание");
		ТекущиеДанные.ДатаОграничения = Параметры.ТекущийПланОкончание;
		ЗаписатьПроектнуюЗадачуИзСтрокиДерева(Элементы.ДеревоЗадач.ТекущаяСтрока);
	Иначе	
		ТекущиеДанные.ТекущийПланОкончание = ДатаОкончанияДоРедактирования;
	КонецЕсли;
		
КонецПроцедуры
	
&НаКлиенте
Процедура ТекущийПланДлительностьЗадачиСтрПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.ДеревоЗадач.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	НачальнаяДлительность = ТекущиеДанные.ТекущийПланДлительность;
	Попытка
		ЧисловаяЧасть = ПолучитьЧисловуюЧасть(ТекущиеДанные.ТекущийПланДлительностьСтр);
		ТекущийПланДлительность = Число(ЧисловаяЧасть);
	Исключение
		ТекстСообщения = НСтр("ru = 'Указано некорректное значение длительности'; en = 'Invalid value of duration is specified'");
		ПоказатьПредупреждение(, ТекстСообщения);
		ТекущиеДанные.ТекущийПланДлительностьСтр = НачальнаяДлительность;
		Возврат;
	КонецПопытки;	
	
	ТекущиеДанные.ТекущийПланДлительность = ТекущийПланДлительность;
	
	ПодписьТекПланДлительность = ПолучитьПодписьДлительности(
		ТекущиеДанные.ТекущийПланДлительность, ТекущиеДанные.ТекущийПланЕдиницаДлительности);
	ТекущиеДанные.ТекущийПланДлительностьСтр = Строка(ТекущиеДанные.ТекущийПланДлительность) + " " + ПодписьТекПланДлительность;
	
	ПроектнаяЗадачаСтруктура = Новый Структура;
	ПроектнаяЗадачаСтруктура.Вставить("Владелец", ТекущиеДанные.Владелец);
	ПроектнаяЗадачаСтруктура.Вставить("ГрафикРаботы", ТекущиеДанные.ГрафикРаботы);
	
	Если ТекущиеДанные.ТипОграничения 
		= ПредопределенноеЗначение("Перечисление.ТипыОграниченийПроектныхЗадач.КакМожноРаньше") Тогда 
		
		Если ЗначениеЗаполнено(ТекущиеДанные.ТекущийПланНачало) Тогда 
			ТекущиеДанные.ТекущийПланОкончание = РаботаСПроектами.РассчитатьОкончаниеПериода(ПроектнаяЗадачаСтруктура, 
				ТекущиеДанные.ТекущийПланНачало, 
				ТекущиеДанные.ТекущийПланДлительность, 
				ТекущиеДанные.ТекущийПланЕдиницаДлительности);
		КонецЕсли;
		
	Иначе
		
		Если ЗначениеЗаполнено(ТекущиеДанные.ТекущийПланОкончание) Тогда 
			ТекущиеДанные.ТекущийПланНачало = РаботаСПроектами.РассчитатьНачалоПериода(ПроектнаяЗадачаСтруктура, 
				ТекущиеДанные.ТекущийПланОкончание, 
				ТекущиеДанные.ТекущийПланДлительность, 
				ТекущиеДанные.ТекущийПланЕдиницаДлительности);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТекущийПланДлительностьЗадачиСтрРегулирование(Элемент, Направление, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ТекущиеДанные = Элементы.ДеревоЗадач.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные.ТекущийПланДлительность = ТекущиеДанные.ТекущийПланДлительность + Направление;
	
	ПодписьТекПланДлительность = ПолучитьПодписьДлительности(
		ТекущиеДанные.ТекущийПланДлительность, ТекущиеДанные.ТекущийПланЕдиницаДлительности);
	ТекущиеДанные.ТекущийПланДлительностьСтр = Строка(ТекущиеДанные.ТекущийПланДлительность) + " " + ПодписьТекПланДлительность;
	
	ПроектнаяЗадачаСтруктура = Новый Структура;
	ПроектнаяЗадачаСтруктура.Вставить("Владелец", ТекущиеДанные.Владелец);
	ПроектнаяЗадачаСтруктура.Вставить("ГрафикРаботы", ТекущиеДанные.ГрафикРаботы);
	
	Если ТекущиеДанные.ТипОграничения 
		= ПредопределенноеЗначение("Перечисление.ТипыОграниченийПроектныхЗадач.КакМожноРаньше") Тогда 
		
		Если ЗначениеЗаполнено(ТекущиеДанные.ТекущийПланНачало) Тогда 
			ТекущиеДанные.ТекущийПланОкончание = РаботаСПроектами.РассчитатьОкончаниеПериода(ПроектнаяЗадачаСтруктура, 
				ТекущиеДанные.ТекущийПланНачало, 
				ТекущиеДанные.ТекущийПланДлительность, 
				ТекущиеДанные.ТекущийПланЕдиницаДлительности);
		КонецЕсли;
		
	Иначе
		
		Если ЗначениеЗаполнено(ТекущиеДанные.ТекущийПланОкончание) Тогда 
			ТекущиеДанные.ТекущийПланНачало = РаботаСПроектами.РассчитатьНачалоПериода(ПроектнаяЗадачаСтруктура, 
				ТекущиеДанные.ТекущийПланОкончание, 
				ТекущиеДанные.ТекущийПланДлительность, 
				ТекущиеДанные.ТекущийПланЕдиницаДлительности);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТекущийПланТрудозатратыСтрПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.ДеревоЗадач.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	НачальныеТрудозатраты = ТекущиеДанные.ТекущийПланТрудозатраты;
	Попытка
		ЧисловаяЧасть = ПолучитьЧисловуюЧасть(ТекущиеДанные.ТекущийПланТрудозатратыСтр);
		ТекущийПланТрудозатраты = Число(ЧисловаяЧасть);
	Исключение
		ТекстСообщения = НСтр("ru = 'Указано некорректное значение трудозатрат'; en = 'Invalid value of time spent is specified'");
		ПоказатьПредупреждение(, ТекстСообщения);
		ТекущиеДанные.ТекущийПланТрудозатратыСтр = НачальныеТрудозатраты;
		Возврат;
	КонецПопытки;	
	ТекущиеДанные.ТекущийПланТрудозатраты = ТекущийПланТрудозатраты;
	
	Если ТекущиеДанные.ТекущийПланТрудозатраты > 0 Тогда 
		ПодписьТекПланТрудозатраты = ПолучитьПодписьТрудозатрат(
			ТекущиеДанные.ТекущийПланТрудозатраты, ТекущиеДанные.ТекущийПланЕдиницаТрудозатрат);
		ТекущиеДанные.ТекущийПланТрудозатратыСтр = Строка(ТекущиеДанные.ТекущийПланТрудозатраты) + " " + ПодписьТекПланТрудозатраты;
	Иначе	
		ТекущиеДанные.ТекущийПланТрудозатратыСтр = "";
	КонецЕсли;
	
	Исполнители = ТекущиеДанные.Исполнители;
	Для Каждого СтрокаИсполнители Из Исполнители Цикл
		СтрокаИсполнители.ТекущийПланТрудозатраты = ТекущиеДанные.ТекущийПланТрудозатраты / Исполнители.Количество();
	КонецЦикла;	
	
КонецПроцедуры

&НаКлиенте
Процедура ТекущийПланТрудозатратыСтрРегулирование(Элемент, Направление, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ТекущиеДанные = Элементы.ДеревоЗадач.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	ТекущийПланТрудозатраты = ТекущиеДанные.ТекущийПланТрудозатраты;
	
	ТекущиеДанные.ТекущийПланТрудозатраты = ТекущийПланТрудозатраты + Направление;
	
	Если ТекущиеДанные.ТекущийПланТрудозатраты > 0 Тогда 
		ПодписьТекПланТрудозатраты = ПолучитьПодписьТрудозатрат(
			ТекущиеДанные.ТекущийПланТрудозатраты, ТекущиеДанные.ТекущийПланЕдиницаТрудозатрат);
		ТекущиеДанные.ТекущийПланТрудозатратыСтр = Строка(ТекущиеДанные.ТекущийПланТрудозатраты) + " " + ПодписьТекПланТрудозатраты;
	Иначе	
		ТекущиеДанные.ТекущийПланТрудозатратыСтр = "";
	КонецЕсли;
	
	Исполнители = ТекущиеДанные.Исполнители;
	Для Каждого СтрокаИсполнители Из Исполнители Цикл
		СтрокаИсполнители.ТекущийПланТрудозатраты = ТекущиеДанные.ТекущийПланТрудозатраты / Исполнители.Количество();
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ДлительностьФактСтрПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.ДеревоЗадач.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	НачальнаяДлительность = ТекущиеДанные.ДлительностьФакт;
	Попытка
		ЧисловаяЧасть = ПолучитьЧисловуюЧасть(ТекущиеДанные.ДлительностьФактСтр);
		ДлительностьФакт = Число(ЧисловаяЧасть);
	Исключение
		ТекстСообщения = НСтр("ru = 'Указано некорректное значение длительности'; en = 'Invalid value of duration is specified'");
		ПоказатьПредупреждение(, ТекстСообщения);
		ТекущиеДанные.ДлительностьФактСтр = НачальнаяДлительность;
		Возврат;
	КонецПопытки;	
	ТекущиеДанные.ДлительностьФакт = ДлительностьФакт;
	
	Если ТекущиеДанные.ДлительностьФакт > 0 Тогда 
		ПодписьТекФактДлительность = ПолучитьПодписьДлительности(
			ТекущиеДанные.ДлительностьФакт, ТекущиеДанные.ЕдиницаДлительностиФакт);
		ТекущиеДанные.ДлительностьФактСтр = Строка(ТекущиеДанные.ДлительностьФакт) + " " + ПодписьТекФактДлительность;
	Иначе	
		ТекущиеДанные.ДлительностьФактСтр = "";
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ТекущиеДанные.НачалоФакт) Тогда 
		
		ПроектнаяЗадачаСтруктура = Новый Структура;
		ПроектнаяЗадачаСтруктура.Вставить("Владелец", ТекущиеДанные.Владелец);
		ПроектнаяЗадачаСтруктура.Вставить("ГрафикРаботы", ТекущиеДанные.ГрафикРаботы);
		
		ТекущиеДанные.ОкончаниеФакт = РаботаСПроектами.РассчитатьОкончаниеПериода(ПроектнаяЗадачаСтруктура, 
				ТекущиеДанные.НачалоФакт, 
				ТекущиеДанные.ДлительностьФакт, 
				ТекущиеДанные.ЕдиницаДлительностиФакт);
				
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДлительностьФактСтрРегулирование(Элемент, Направление, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ТекущиеДанные = Элементы.ДеревоЗадач.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	ДлительностьФакт = ТекущиеДанные.ДлительностьФакт;
	
	ТекущиеДанные.ДлительностьФакт = ДлительностьФакт + Направление;
	
	Если ТекущиеДанные.ДлительностьФакт > 0 Тогда 
		ПодписьТекФактДлительность = ПолучитьПодписьДлительности(
			ТекущиеДанные.ДлительностьФакт, ТекущиеДанные.ЕдиницаДлительностиФакт);
		ТекущиеДанные.ДлительностьФактСтр = Строка(ТекущиеДанные.ДлительностьФакт) + " " + ПодписьТекФактДлительность;
	Иначе	
		ТекущиеДанные.ДлительностьФактСтр = "";
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ТекущиеДанные.НачалоФакт) Тогда 
		
		ПроектнаяЗадачаСтруктура = Новый Структура;
		ПроектнаяЗадачаСтруктура.Вставить("Владелец", ТекущиеДанные.Владелец);
		ПроектнаяЗадачаСтруктура.Вставить("ГрафикРаботы", ТекущиеДанные.ГрафикРаботы);
		
		ТекущиеДанные.ОкончаниеФакт = РаботаСПроектами.РассчитатьОкончаниеПериода(ПроектнаяЗадачаСтруктура, 
				ТекущиеДанные.НачалоФакт, 
				ТекущиеДанные.ДлительностьФакт, 
				ТекущиеДанные.ЕдиницаДлительностиФакт);
				
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОсновныеЗадачиПередНачаломИзменения(Элемент, Отказ)
	
	Отказ = Истина;
	
	ТекущиеДанные = Элементы.ОсновныеЗадачи.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда 
		Возврат;
	КонецЕсли;	
		
	ПоказатьЗначение(, ТекущиеДанные.Ссылка);
	
КонецПроцедуры

&НаКлиенте
Процедура ВспомогательныеЗадачиПередНачаломИзменения(Элемент, Отказ)
	
	Отказ = Истина;
	
	ТекущиеДанные = Элементы.ВспомогательныеЗадачи.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда 
		Возврат;
	КонецЕсли;	
		
	ПоказатьЗначение(, ТекущиеДанные.Ссылка);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Обновить(Команда)
	
	ОбновитьДеревоЗадач();
	
КонецПроцедуры

&НаКлиенте
Процедура ТекущееСостояние(Команда)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("СформироватьПриОткрытии", Истина);
	ПараметрыФормы.Вставить("КлючВарианта", "ТекущееСостояние");
	ПараметрыФормы.Вставить("Проект", Проект);
	
	ОткрытьФорму("Отчет.ПроектныеЗадачи.Форма", ПараметрыФормы, ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура СкороДолжныЗавершиться(Команда)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("СформироватьПриОткрытии", Истина);
	ПараметрыФормы.Вставить("КлючВарианта", "ДолжныЗакончиться");
	ПараметрыФормы.Вставить("Проект", Проект);
	
	ОткрытьФорму("Отчет.ПроектныеЗадачи.Форма", ПараметрыФормы, ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура СкороДолжныНачаться(Команда)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("СформироватьПриОткрытии", Истина);
	ПараметрыФормы.Вставить("КлючВарианта", "ДолжныНачаться");
	ПараметрыФормы.Вставить("Проект", Проект);
	
	ОткрытьФорму("Отчет.ПроектныеЗадачи.Форма", ПараметрыФормы, ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ПланФактПоСрокам(Команда)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("СформироватьПриОткрытии", Истина);
	ПараметрыФормы.Вставить("КлючВарианта", "ПланФактПоСрокам");
	ПараметрыФормы.Вставить("Проект", Проект);
	
	ОткрытьФорму("Отчет.ПроектныеЗадачи.Форма", ПараметрыФормы, ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ПланФактПоТрудозатратам(Команда)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("СформироватьПриОткрытии", Истина);
	ПараметрыФормы.Вставить("КлючВарианта", "ПланФактПоТрудозатратам");
	ПараметрыФормы.Вставить("Проект", Проект);
	
	ОткрытьФорму("Отчет.ПроектныеЗадачи.Форма", ПараметрыФормы, ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура СвязатьЗадачи(Команда)
	
	Если Элементы.ДеревоЗадач.ВыделенныеСтроки.Количество() < 2 Тогда
		ПоказатьПредупреждение(,НСтр("ru = 'Для установки предшественника выделите две или более задачи.'; en = 'To set the predecessor, select two tasks or more.'"));
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные = Элементы.ДеревоЗадач.ТекущиеДанные;
	
	Если Элементы.ДеревоЗадач.ВыделенныеСтроки.Количество() = 2 Тогда 
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Установлен предшественник для задачи ""%1""'; en = 'Predecessor is set for task ""%1""'"),
			ТекущиеДанные.НаименованиеЗадачи);
		
		
	Иначе
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Установлены предшественники для задачи ""%1""'; en = 'Predecessors are set for task ""%1""'"),
			ТекущиеДанные.НаименованиеЗадачи);
	КонецЕсли;	
	
	Результат = СвязатьЗадачиСервер();
	Если Результат Тогда
		ПоказатьОповещениеПользователя(
			НСтр("ru = 'Установка предшественника'; en = 'Predecessor is set'"),
			ПолучитьНавигационнуюСсылку(ТекущиеДанные.Ссылка),
			ТекстСообщения,
			БиблиотекаКартинок.Информация32);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьКарточку(Команда)
	
	ТекущийДанные = Элементы.ДеревоЗадач.ТекущиеДанные;
	Если ТекущийДанные = Неопределено Тогда 
		Возврат;
	КонецЕсли;	
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Ключ", ТекущийДанные.Ссылка);
	
	ОткрытьФорму("Справочник.ПроектныеЗадачи.ФормаОбъекта", ПараметрыФормы, ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьПомеченныеНаУдаление(Команда)
		
	ПоказатьПомеченныеНаУдаление = Не ПоказатьПомеченныеНаУдаление;
	Элементы.ДеревоЗадачПоказатьПомеченныеНаУдаление.Пометка = ПоказатьПомеченныеНаУдаление;
	
	СписокРаскрытыхЗадач.Очистить();
	РаботаСПроектамиКлиент.ПолучитьМассивРаскрытыхЗадач(
		Элементы.ДеревоЗадач, ДеревоЗадач.ПолучитьЭлементы(), СписокРаскрытыхЗадач);
	ВременнаяТекущаяПроектнаяЗадача = ТекущаяПроектнаяЗадача;
	
	ЗаполнитьДеревоЗадач();
	РаботаСПроектамиКлиент.УстановитьРазвернутостьЭлементовДерева(
		Элементы.ДеревоЗадач, ДеревоЗадач, СписокРаскрытыхЗадач);		
	РаботаСПроектамиКлиент.УстановитьТекущуюЗадачуВДеревеПоСсылке(
		Элементы.ДеревоЗадач, ДеревоЗадач, ВременнаяТекущаяПроектнаяЗадача);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьИзMSProject(Команда)
	
	ПараметрыФормы = Новый Структура();
	ПараметрыФормы.Вставить("Проект", Проект);
	
	ОткрытьФорму("Обработка.ИмпортПроектаИзMicrosoftProject.Форма", ПараметрыФормы, ЭтаФорма);
		
КонецПроцедуры

&НаКлиенте
Процедура РедактироватьВСписке(Команда)
	
	ВариантРедактирования = "ВСписке";
	УстановитьДоступностьПолейДереваЗадач(ВариантРедактирования);
	
КонецПроцедуры

&НаКлиенте
Процедура РедактироватьВКарточке(Команда)
	
	ВариантРедактирования = "ВДиалоге";
	УстановитьДоступностьПолейДереваЗадач(ВариантРедактирования);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьПроект(Команда)
	
	ПоказатьЗначение(, Проект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьОсновнойПроцесс(Команда)
	
	ТекущиеДанные = Элементы.ОсновныеЗадачи.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда 
		Возврат;
	КонецЕсли;	
		
	ПоказатьЗначение(, ТекущиеДанные.БизнесПроцесс);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьВспомогательныйПроцесс(Команда)
	
	ТекущиеДанные = Элементы.ВспомогательныеЗадачи.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда 
		Возврат;
	КонецЕсли;	
		
	ПоказатьЗначение(, ТекущиеДанные.БизнесПроцесс);
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказыватьЗадачиПроцессов(Команда)
	
	ПоказыватьЗадачиПроцессов = Не ПоказыватьЗадачиПроцессов;
	Элементы.ГруппаЗадачиПроцессов.Видимость = ПоказыватьЗадачиПроцессов;
	Элементы.ДеревоЗадачПоказыватьЗадачиПроцессов.Пометка = ПоказыватьЗадачиПроцессов;
	
КонецПроцедуры

&НаКлиенте
Процедура Контроль(Команда)
	
	ТекущийДанные = Элементы.ДеревоЗадач.ТекущиеДанные;
	Если ТекущийДанные = Неопределено Тогда 
		Возврат;
	КонецЕсли;	
	
	КонтрольКлиент.ОбработкаКомандыКонтроль(ТекущийДанные.Ссылка, ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьПодчиненную(Команда)
	
	Если Не РольПолныеПрава Тогда 
		Если Не РеквизитыПроекта.ДанныеДоступныДляИзменения Тогда 
			СообщениеТекст = НСтр("ru = 'У вас нет прав на добавление проектных задач.'; en = 'You have no permission to add project tasks.'");
			ПоказатьПредупреждение(, СообщениеТекст);
			Возврат;
		КонецЕсли;
		
		Если РеквизитыПроекта.ПроектЗавершен Тогда 
			СообщениеТекст = НСтр("ru = 'Проект завершен. Добавление задач запрещено.'; en = 'Project finished. Adding tasks is forbidden.'");
			ПоказатьПредупреждение(, СообщениеТекст);
			ОтменаРедактирования = Истина;
			Возврат;
		КонецЕсли;

		Если РеквизитыПроекта.РедактируетсяВСтороннейСистеме Тогда
			СообщениеТекст = НСтр("ru = 'Проект редактируется в сторонней системе. Добавление задач запрещено.'; en = 'Project is being edited in a thirt-party system. Adding tasks is forbidden.'");
			ПоказатьПредупреждение(, СообщениеТекст);
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	Если ВариантРедактирования = "ВСписке" Тогда 
		
		ТекущаяСтрока = Элементы.ДеревоЗадач.ТекущаяСтрока;
		Если ТекущаяСтрока = Неопределено Тогда 
			НовыйЭлемент = ДеревоЗадач.ПолучитьЭлементы().Добавить();
		Иначе
			ЭлементДерева = ДеревоЗадач.НайтиПоИдентификатору(ТекущаяСтрока);
			НовыйЭлемент = ЭлементДерева.ПолучитьЭлементы().Добавить();
		КонецЕсли;	
		
		Элементы.ДеревоЗадач.ТекущаяСтрока = НовыйЭлемент.ПолучитьИдентификатор();
		Элементы.ДеревоЗадач.ИзменитьСтроку();
	Иначе
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("Проект", Проект);
		
		ТекущийДанные = Элементы.ДеревоЗадач.ТекущиеДанные;
		Если ТекущийДанные <> Неопределено Тогда 
			ПараметрыФормы.Вставить("Родитель", ТекущийДанные.Ссылка);
		КонецЕсли;	
		
		ОткрытьФорму("Справочник.ПроектныеЗадачи.ФормаОбъекта", ПараметрыФормы, ЭтаФорма);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьПоследующую(Команда)
	
	Если Не РольПолныеПрава Тогда 
		Если Не РеквизитыПроекта.ДанныеДоступныДляИзменения Тогда 
			СообщениеТекст = НСтр("ru = 'У вас нет прав на добавление проектных задач.'; en = 'You have no permission to add project tasks.'");
			ПоказатьПредупреждение(, СообщениеТекст);
			Возврат;
		КонецЕсли;
		
		Если РеквизитыПроекта.ПроектЗавершен Тогда 
			СообщениеТекст = НСтр("ru = 'Проект завершен. Добавление задач запрещено.'; en = 'Project finished. Adding tasks is forbidden.'");
			ПоказатьПредупреждение(, СообщениеТекст);
			ОтменаРедактирования = Истина;
			Возврат;
		КонецЕсли;
		
		Если РеквизитыПроекта.РедактируетсяВСтороннейСистеме Тогда
			СообщениеТекст = НСтр("ru = 'Проект редактируется в сторонней системе. Добавление задач запрещено.'; en = 'Project is being edited in a thirt-party system. Adding tasks is forbidden.'");
			ПоказатьПредупреждение(, СообщениеТекст);
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	Если ВариантРедактирования = "ВСписке" Тогда 
		
		ТекущаяСтрока = Элементы.ДеревоЗадач.ТекущаяСтрока;
		Если ТекущаяСтрока = Неопределено Тогда 
			НовыйЭлемент = ДеревоЗадач.ПолучитьЭлементы().Добавить();
		Иначе
			ЭлементДерева = ДеревоЗадач.НайтиПоИдентификатору(ТекущаяСтрока);
			РодительЭлемента = ЭлементДерева.ПолучитьРодителя();
			
			Если РодительЭлемента = Неопределено Тогда 
				НовыйЭлемент = ДеревоЗадач.ПолучитьЭлементы().Добавить();
			Иначе
				НовыйЭлемент = РодительЭлемента.ПолучитьЭлементы().Добавить();
			КонецЕсли;	
			
			Предшественники = НовыйЭлемент.Предшественники;
			
			СтрокаПредшественник = Предшественники.Добавить();
			СтрокаПредшественник.Предшественник = ЭлементДерева.Ссылка;
			СтрокаПредшественник.ТипЗависимости 
				= ПредопределенноеЗначение("Перечисление.ТипыЗависимостейПроектныхЗадач.ОкончаниеНачало");
			
			НовыйЭлемент.ПредшественникиСтр = ПредставлениеПредшественников(Предшественники);
		КонецЕсли;	
		
		Элементы.ДеревоЗадач.ТекущаяСтрока = НовыйЭлемент.ПолучитьИдентификатор();
		Элементы.ДеревоЗадач.ИзменитьСтроку();
		
	Иначе
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("Проект", Проект);
		
		ТекущийДанные = Элементы.ДеревоЗадач.ТекущиеДанные;
		Если ТекущийДанные <> Неопределено Тогда 
			ПараметрыФормы.Вставить("Родитель", ТекущийДанные.РодительскаяЗадача);
			ПараметрыФормы.Вставить("Предшественник", ТекущийДанные.Ссылка);
		КонецЕсли;	
		
		ОткрытьФорму("Справочник.ПроектныеЗадачи.ФормаОбъекта", ПараметрыФормы, ЭтаФорма);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НаУровеньНиже(Команда)
	
	Если Элементы.ДеревоЗадач.ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СписокРаскрытыхЗадач.Очистить();
	РаботаСПроектамиКлиент.ПолучитьМассивРаскрытыхЗадач(
		Элементы.ДеревоЗадач, ДеревоЗадач.ПолучитьЭлементы(), СписокРаскрытыхЗадач);
	ТекущаяЗадачаДоПеремещения = ТекущаяПроектнаяЗадача;
	МассивВыделенныхЗадач = Новый Массив;
	Для Каждого ВыделеннаяСтрока Из Элементы.ДеревоЗадач.ВыделенныеСтроки Цикл
		Ссылка = ДеревоЗадач.НайтиПоИдентификатору(ВыделеннаяСтрока).Ссылка;	
		МассивВыделенныхЗадач.Добавить(Ссылка);
	КонецЦикла;
	
	НаУровеньНижеСервер(
		Элементы.ДеревоЗадач.ТекущиеДанные.Ссылка, 
		Элементы.ДеревоЗадач.ТекущиеДанные.РодительскаяЗадача);
		
	РаботаСПроектамиКлиент.УстановитьРазвернутостьЭлементовДерева(
		Элементы.ДеревоЗадач, ДеревоЗадач, СписокРаскрытыхЗадач);		
	Элементы.ДеревоЗадач.ВыделенныеСтроки.Очистить();
	Для Каждого Ссылка Из МассивВыделенныхЗадач Цикл
		РаботаСПроектамиКлиент.УстановитьВыделеннуюЗадачуВДеревеПоСсылке(Элементы.ДеревоЗадач, ДеревоЗадач, Ссылка);	
	КонецЦикла;
	
	РаботаСПроектамиКлиент.УстановитьТекущуюЗадачуВДеревеПоСсылке(
		Элементы.ДеревоЗадач, ДеревоЗадач, ТекущаяЗадачаДоПеремещения);
		
	УстановитьДоступностьКомандПеремещения(
		ТекущаяПроектнаяЗадача, ДеревоЗадач, Элементы);
	
КонецПроцедуры

&НаКлиенте
Процедура НаУровеньВыше(Команда)
	
	Если Элементы.ДеревоЗадач.ТекущиеДанные = Неопределено 
		ИЛИ НЕ ЗначениеЗаполнено(Элементы.ДеревоЗадач.ТекущиеДанные.РодительскаяЗадача) Тогда
		Возврат;
	КонецЕсли;
	
	СписокРаскрытыхЗадач.Очистить();
	РаботаСПроектамиКлиент.ПолучитьМассивРаскрытыхЗадач(
		Элементы.ДеревоЗадач, ДеревоЗадач.ПолучитьЭлементы(), СписокРаскрытыхЗадач);
	ТекущаяЗадачаДоПеремещения = ТекущаяПроектнаяЗадача;
	МассивВыделенныхЗадач = Новый Массив;
	Для Каждого ВыделеннаяСтрока Из Элементы.ДеревоЗадач.ВыделенныеСтроки Цикл
		Ссылка = ДеревоЗадач.НайтиПоИдентификатору(ВыделеннаяСтрока).Ссылка;	
		МассивВыделенныхЗадач.Добавить(Ссылка);
	КонецЦикла;

	НаУровеньВышеСервер(
		Элементы.ДеревоЗадач.ТекущиеДанные.Ссылка, 
		Элементы.ДеревоЗадач.ТекущиеДанные.РодительскаяЗадача);
		
	РаботаСПроектамиКлиент.УстановитьРазвернутостьЭлементовДерева(
		Элементы.ДеревоЗадач, ДеревоЗадач, СписокРаскрытыхЗадач);		
	Элементы.ДеревоЗадач.ВыделенныеСтроки.Очистить();
	Для Каждого Ссылка Из МассивВыделенныхЗадач Цикл
		РаботаСПроектамиКлиент.УстановитьВыделеннуюЗадачуВДеревеПоСсылке(
			Элементы.ДеревоЗадач, ДеревоЗадач, Ссылка);	
	КонецЦикла;
	РаботаСПроектамиКлиент.УстановитьТекущуюЗадачуВДеревеПоСсылке(
		Элементы.ДеревоЗадач, ДеревоЗадач, ТекущаяЗадачаДоПеремещения);
    УстановитьДоступностьКомандПеремещения(
		ТекущаяПроектнаяЗадача, ДеревоЗадач, Элементы);

КонецПроцедуры

&НаКлиенте
Процедура Вверх(Команда)
	
	Если Элементы.ДеревоЗадач.ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТекущаяЗадачаДоПеремещения = ТекущаяПроектнаяЗадача;
	МассивВыделенныхЗадач = Новый Массив;
	Для Каждого ВыделеннаяСтрока Из Элементы.ДеревоЗадач.ВыделенныеСтроки Цикл
		Ссылка = ДеревоЗадач.НайтиПоИдентификатору(ВыделеннаяСтрока).Ссылка;	
		МассивВыделенныхЗадач.Добавить(Ссылка);
	КонецЦикла;
	
	ВверхСервер();
		
	Элементы.ДеревоЗадач.ВыделенныеСтроки.Очистить();
	Для Каждого Ссылка Из МассивВыделенныхЗадач Цикл
		РаботаСПроектамиКлиент.УстановитьВыделеннуюЗадачуВДеревеПоСсылке(
			Элементы.ДеревоЗадач, ДеревоЗадач, Ссылка);	
	КонецЦикла;
	
	РаботаСПроектамиКлиент.УстановитьТекущуюЗадачуВДеревеПоСсылке(
		Элементы.ДеревоЗадач, ДеревоЗадач, ТекущаяЗадачаДоПеремещения);
	
КонецПроцедуры

&НаКлиенте
Процедура Вниз(Команда)
	
	Если Элементы.ДеревоЗадач.ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТекущаяЗадачаДоПеремещения = ТекущаяПроектнаяЗадача;
	МассивВыделенныхЗадач = Новый Массив;
	Для Каждого ВыделеннаяСтрока Из Элементы.ДеревоЗадач.ВыделенныеСтроки Цикл
		Ссылка = ДеревоЗадач.НайтиПоИдентификатору(ВыделеннаяСтрока).Ссылка;	
		МассивВыделенныхЗадач.Добавить(Ссылка);
	КонецЦикла;
	
	ВнизСервер();
		
	Элементы.ДеревоЗадач.ВыделенныеСтроки.Очистить();
	Для Каждого Ссылка Из МассивВыделенныхЗадач Цикл
		РаботаСПроектамиКлиент.УстановитьВыделеннуюЗадачуВДеревеПоСсылке(
			Элементы.ДеревоЗадач, ДеревоЗадач, Ссылка);	
	КонецЦикла;
	
	РаботаСПроектамиКлиент.УстановитьТекущуюЗадачуВДеревеПоСсылке(
		Элементы.ДеревоЗадач, ДеревоЗадач, ТекущаяЗадачаДоПеремещения);
	
КонецПроцедуры

// СтандартныеПодсистемы.Печать
&НаКлиенте
Процедура Подключаемый_ВыполнитьКомандуПечати(Команда)
	
	УправлениеПечатьюКлиент.ВыполнитьПодключаемуюКомандуПечати(Команда, ЭтаФорма, Элементы.ДеревоЗадач);
	
КонецПроцедуры


&НаКлиенте
Процедура ПоказатьПодзадачи(Команда)
	
	ТекущаяСтрока = Элементы.ДеревоЗадач.ТекущаяСтрока;
	Если ТекущаяСтрока = Неопределено Тогда 
		Возврат;
	КонецЕсли;	
	
	Элементы.ДеревоЗадач.Развернуть(ТекущаяСтрока, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура СкрытьПодзадачи(Команда)
	
	ТекущаяСтрока = Элементы.ДеревоЗадач.ТекущаяСтрока;
	Если ТекущаяСтрока = Неопределено Тогда 
		Возврат;
	КонецЕсли;	
	
	Элементы.ДеревоЗадач.Свернуть(ТекущаяСтрока);
	
КонецПроцедуры

&НаКлиенте
Процедура Уровень1(Команда)
	
	ПоказатьУровень(1);
	
КонецПроцедуры

&НаКлиенте
Процедура Уровень2(Команда)
	
	ПоказатьУровень(2);
	
КонецПроцедуры

&НаКлиенте
Процедура Уровень3(Команда)
	
	ПоказатьУровень(3);
	
КонецПроцедуры

&НаКлиенте
Процедура Уровень4(Команда)
	
	ПоказатьУровень(4);
	
КонецПроцедуры

&НаКлиенте
Процедура Уровень5(Команда)
	
	ПоказатьУровень(5);
	
КонецПроцедуры

&НаКлиенте
Процедура Уровень6(Команда)
	
	ПоказатьУровень(6);
	
КонецПроцедуры

&НаКлиенте
Процедура Уровень7(Команда)
	
	ПоказатьУровень(7);
	
КонецПроцедуры

// Учет времени

&НаКлиенте
Процедура ПереключитьХронометраж(Команда)
	
	ТекущиеДанные = Элементы.ДеревоЗадач.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда 
		Возврат;
	КонецЕсли;

	ПараметрыОповещения = Неопределено;
	НуженДиалог = УчетВремениКлиент.НуженДиалогДляХронометража(ВключенХронометраж, 
		ДатаНачалаХронометража, ВидыРабот);
	
	Если НуженДиалог = Ложь Тогда
		
		ПереключитьХронометражСервер(ПараметрыОповещения);
		УчетВремениКлиент.ПоказатьОповещение(ПараметрыОповещения, ВключенХронометраж, ТекущиеДанные.Ссылка);
	
	Иначе
		ДлительностьРаботы = УчетВремениКлиент.ПолучитьДлительностьРаботы(ДатаНачалаХронометража);
		
		ОписаниеРаботы = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Работа с документом ""%1""'; en = 'Working with document ""%1""'"),
			ТекущиеДанные.Наименование);
		
		ПараметрыФормы = Новый Структура();
		ПараметрыФормы.Вставить("ДатаОтчета", ТекущаяДата());
		ПараметрыФормы.Вставить("ВидыРабот", ВидыРабот);
		ПараметрыФормы.Вставить("ОписаниеРаботы", ОписаниеРаботы);
		ПараметрыФормы.Вставить("ДлительностьРаботы", ДлительностьРаботы);
		ПараметрыФормы.Вставить("НачалоРаботы", ДатаНачалаХронометража);
		ПараметрыФормы.Вставить("Объект", ТекущиеДанные.Ссылка);
		ПараметрыФормы.Вставить("СпособУказанияВремени", СпособУказанияВремени);
		
		ОписаниеОповещения = Новый ОписаниеОповещения(
			"ПереключитьХронометражПродолжение",
			ЭтотОбъект,
			Новый Структура("Объект", ТекущиеДанные.Ссылка));
		
		ОткрытьФорму("РегистрСведений.ФактическиеТрудозатраты.Форма.ФормаДобавленияРаботы", ПараметрыФормы,,,,,
			ОписаниеОповещения, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПереключитьХронометражПродолжение(Результат, Параметры) Экспорт 
	
	Если Результат <> Неопределено Тогда
		ДобавитьВОтчетИОбновитьФорму(Результат, ПараметрыОповещения);
		УчетВремениКлиент.ПоказатьОповещение(ПараметрыОповещения, ВключенХронометраж, Параметры.Объект);
	Иначе
		ОтключитьХронометражСервер();
	КонецЕсли;  

КонецПроцедуры

&НаКлиенте
Процедура УказатьТрудозатраты(Команда)
	
	ТекущиеДанные = Элементы.ДеревоЗадач.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	ДатаОтчета = ТекущаяДата();
	
	УчетВремениКлиент.ДобавитьВОтчетКлиент(
		ДатаОтчета,
		ВключенХронометраж, 
		ДатаНачалаХронометража, 
		ДатаКонцаХронометража, 
		ВидыРабот, 
		ТекущиеДанные.Ссылка,
		СпособУказанияВремени,
		ЭтаФорма.Элементы.ПереключитьХронометраж,
		Ложь,
		ЭтаФорма); // Выполнена
		
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы_Отправить

&НаКлиенте
Процедура ПроцессИсполнение(Команда)
	
	ОткрытьПомощникСозданияОсновныхПроцессов("Исполнение");
	
КонецПроцедуры

&НаКлиенте
Процедура ПроцессОбработка(Команда)
	
	ТипыОпераций = Новый Массив;
	ТипыОпераций.Добавить("КомплексныйПроцесс");
	
	ОткрытьПомощникСозданияОсновныхПроцессов(ТипыОпераций);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьПомощникСозданияОсновныхПроцессов(ТипыОпераций)
	
	ТекущиеДанные = Элементы.ДеревоЗадач.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		ТекстПредупреждения = НСтр("ru = 'Не выбрана проектная задача.'; en = 'Project task is not selected.'");
		ПоказатьПредупреждение(, ТекстПредупреждения);
		Возврат
	КонецЕсли;
	
	ДопПараметры = Новый Структура;
	ДопПараметры.Вставить("ПроектнаяЗадача", ТекущиеДанные.Ссылка);
	ДопПараметры.Вставить("ТипыОпераций", ТипыОпераций);
	
	РежимОткрытияФормы = РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс;
	ОписаниеОповещения = Новый ОписаниеОповещения(
		"ОткрытьПомощникСозданияОсновныхПроцессов_Продолжение", ЭтотОбъект, ДопПараметры);

	ДанныеПроектнойЗадачи = РаботаСПроектами.ПолучитьСрокиПроектнойЗадачи(ТекущиеДанные.Ссылка);
	ОкончаниеФакт = ДанныеПроектнойЗадачи.ОкончаниеФакт;
	Если ОкончаниеФакт > Дата(1, 1, 1) Тогда
		
		ТекстВопроса = СтрШаблон(
			НСтр("ru = 'Проектная задача завершена. При отправке %1 задача вновь перейдет в стадию выполнения. Отправить %1?'; en = 'Project task is finished. Upon sending %1 the task will return to execution . Send %1?'"),
			?(ТипыОпераций = "Исполнение", НСтр("ru = 'на исполнение'; en = 'for performance'"), НСтр("ru = 'в обработку'; en = 'For processing'")));
			
		Режим = Новый СписокЗначений;
		Режим.Добавить(КодВозвратаДиалога.Да, НСтр("ru = 'Отправить'; en = 'Submit'"));
		Режим.Добавить(КодВозвратаДиалога.Нет, НСтр("ru = 'Отмена'; en = 'Cancel'"));
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, Режим, , КодВозвратаДиалога.Нет);
	Иначе 
		ВыполнитьОбработкуОповещения(ОписаниеОповещения, КодВозвратаДиалога.Да);  
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьПомощникСозданияОсновныхПроцессов_Продолжение(Результат, ДопПараметры) Экспорт
	
	Если Результат <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	РаботаСБизнесПроцессамиКлиент.ОткрытьПомощникСозданияОсновныхПроцессов(
		ДопПараметры.ТипыОпераций, ДопПараметры.ПроектнаяЗадача, ЭтаФорма, "ФормаСписка");
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьВидимость(Режим)
	
	Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаЗадачиПроекта;
	
	Элементы.ДеревоЗадачГруппаФакт.ОтображатьВШапке = Ложь;
	Элементы.ДеревоЗадачГруппаПлан.ОтображатьВШапке = Ложь;
	
	Элементы.ТекущийПланНачалоЗадачи.Заголовок = НСтр("ru = 'Начало'; en = 'Start'");
	Элементы.ТекущийПланДлительностьЗадачиСтр.Заголовок = НСтр("ru = 'Длит.'; en = 'Dur.'");
	Элементы.ТекущийПланОкончаниеЗадачи.Заголовок = НСтр("ru = 'Окончание'; en = 'Finish'");
	Элементы.ТекущийПланТрудозатратыСтр.Заголовок = НСтр("ru = 'Трудозатраты'; en = 'Time spent'");
	Элементы.ТекущийПланТрудозатратыСтр.Ширина = 12;
	
	Элементы.НачалоФакт.Заголовок = НСтр("ru = 'Начало'; en = 'Start'");
	Элементы.ДлительностьФактСтр.Заголовок = НСтр("ru = 'Длит.'; en = 'Dur.'");
	Элементы.ОкончаниеФакт.Заголовок = НСтр("ru = 'Окончание'; en = 'Finish'");
	Элементы.ТрудозатратыФактСтр.Заголовок = НСтр("ru = 'Трудозатраты'; en = 'Time spent'");
	Элементы.ТрудозатратыФактСтр.Ширина = 12;
	
	Элементы.Состояние.Видимость = Истина;
	Элементы.ИсполнителиСтр.Видимость = Истина;
	Элементы.ПредшественникиСтр.Видимость = Истина;
	
	Элементы.ДеревоЗадачЗадачи.ФиксацияВТаблице = ФиксацияВТаблице.Нет;
	Элементы.НаименованиеЗадачи.ФиксацияВТаблице = ФиксацияВТаблице.Нет;
	
	Если Режим = "Планирование" Тогда 
		
		Элементы.ТекущийПланНачалоЗадачи.Видимость = Истина;
		Элементы.ТекущийПланДлительностьЗадачиСтр.Видимость = Истина;
		Элементы.ТекущийПланОкончаниеЗадачи.Видимость = Истина;
		Элементы.ТекущийПланТрудозатратыСтр.Видимость = Истина;
		
		Элементы.НачалоФакт.Видимость = Ложь;
		Элементы.ДлительностьФактСтр.Видимость = Ложь;
		Элементы.ОкончаниеФакт.Видимость = Ложь;
		Элементы.ТрудозатратыФактСтр.Видимость = Ложь;
		
		Элементы.Состояние.Видимость = Ложь;
		
	ИначеЕсли Режим = "НаВыполнение" Тогда 
		
		Элементы.ТекущийПланНачалоЗадачи.Видимость = Истина;
		Элементы.ТекущийПланНачалоЗадачи.Заголовок = НСтр("ru = 'Начало (план)'; en = 'Start (plan)'");
		Элементы.ТекущийПланДлительностьЗадачиСтр.Видимость = Ложь;
		Элементы.ТекущийПланОкончаниеЗадачи.Видимость = Ложь;
		Элементы.ТекущийПланТрудозатратыСтр.Видимость = Ложь;
		
		Элементы.НачалоФакт.Видимость = Истина;
		Элементы.НачалоФакт.Заголовок = НСтр("ru = 'Начало (факт)'; en = 'Start (actual)'");
		Элементы.ДлительностьФактСтр.Видимость = Ложь;
		Элементы.ОкончаниеФакт.Видимость = Ложь;
		Элементы.ТрудозатратыФактСтр.Видимость = Ложь;
		
		Элементы.ПредшественникиСтр.Видимость = Ложь;
		
	ИначеЕсли Режим = "КонтрольВыполнения" Тогда 
		
		Элементы.ТекущийПланДлительностьЗадачиСтр.Видимость = Ложь;
		Элементы.ТекущийПланНачалоЗадачи.Видимость = Ложь;
		Элементы.ТекущийПланОкончаниеЗадачи.Видимость = Истина;
		Элементы.ТекущийПланОкончаниеЗадачи.Заголовок = НСтр("ru = 'Окончание (план)'; en = 'Finish (plan)'");
		Элементы.ТекущийПланТрудозатратыСтр.Видимость = Ложь;
		
		Элементы.НачалоФакт.Видимость = Ложь;
		Элементы.ДлительностьФактСтр.Видимость = Ложь;
		Элементы.ОкончаниеФакт.Видимость = Истина;
		Элементы.ОкончаниеФакт.Заголовок = НСтр("ru = 'Окончание (факт)'; en = 'Finish date (actual)'");
		Элементы.ТрудозатратыФактСтр.Видимость = Ложь;
		
		Элементы.ПредшественникиСтр.Видимость = Ложь;
		
	ИначеЕсли Режим = "Трудозатраты" Тогда 
		
		Элементы.ТекущийПланДлительностьЗадачиСтр.Видимость = Ложь;
		Элементы.ТекущийПланНачалоЗадачи.Видимость = Ложь;
		Элементы.ТекущийПланОкончаниеЗадачи.Видимость = Ложь;
		Элементы.ТекущийПланТрудозатратыСтр.Видимость = Истина;
		Элементы.ТекущийПланТрудозатратыСтр.Заголовок = НСтр("ru = 'Трудозатраты (план)'; en = 'Time spent (plan)'");
		Элементы.ТекущийПланТрудозатратыСтр.Ширина = 16;
		
		Элементы.НачалоФакт.Видимость = Ложь;
		Элементы.ДлительностьФактСтр.Видимость = Ложь;
		Элементы.ОкончаниеФакт.Видимость = Ложь;
		Элементы.ТрудозатратыФактСтр.Видимость = Истина;
		Элементы.ТрудозатратыФактСтр.Заголовок = НСтр("ru = 'Трудозатраты (факт)'; en = 'Time spent (actual)'");
		Элементы.ТрудозатратыФактСтр.Ширина = 16;
		
		Элементы.ПредшественникиСтр.Видимость = Ложь;
		
	ИначеЕсли Режим = "Полный" Тогда 
		
		Элементы.ДеревоЗадачГруппаФакт.ОтображатьВШапке = Истина;
		Элементы.ДеревоЗадачГруппаПлан.ОтображатьВШапке = Истина;
		
		Элементы.ТекущийПланДлительностьЗадачиСтр.Видимость = Истина;
		Элементы.ТекущийПланНачалоЗадачи.Видимость = Истина;
		Элементы.ТекущийПланОкончаниеЗадачи.Видимость = Истина;
		Элементы.ТекущийПланТрудозатратыСтр.Видимость = Истина;
		
		Элементы.НачалоФакт.Видимость = Истина;
		Элементы.ДлительностьФактСтр.Видимость = Истина;
		Элементы.ОкончаниеФакт.Видимость = Истина;
		Элементы.ТрудозатратыФактСтр.Видимость = Истина;
		
		Элементы.ДеревоЗадачЗадачи.ФиксацияВТаблице = ФиксацияВТаблице.Лево;
		Элементы.НаименованиеЗадачи.ФиксацияВТаблице = ФиксацияВТаблице.Лево;
		
	ИначеЕсли Режим = "ДиаграммаГанта" Тогда 	
		
		Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаДиаграммаГанта;
		ЗаполнитьДиаграммуГанта();
		
	КонецЕсли;	
	
КонецПроцедуры	

&НаСервере
Процедура ЗаполнитьДеревоЗадач()
	
	Дерево = РеквизитФормыВЗначение("ДеревоЗадач");
	Дерево.Строки.Очистить();
	
	Если ЗначениеЗаполнено(Проект) Тогда 
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ЗадачаИсполнителяПредметы.Предмет КАК Предмет,
		|	ЗадачаИсполнителяПредметы.Ссылка.Выполнена КАК Выполнена,
		|	ЗадачаИсполнителяПредметы.Ссылка.ПометкаУдаления КАК ПометкаУдаления
		|ПОМЕСТИТЬ ПредметыЗадач
		|ИЗ
		|	Задача.ЗадачаИсполнителя.Предметы КАК ЗадачаИсполнителяПредметы
		|ГДЕ
		|	ТИПЗНАЧЕНИЯ(ЗадачаИсполнителяПредметы.Предмет) = ТИП(Справочник.ПроектныеЗадачи)
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Предмет,
		|	Выполнена,
		|	ПометкаУдаления
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ЗадачаИсполнителя.ПроектнаяЗадача,
		|	ЗадачаИсполнителя.Выполнена,
		|	ЗадачаИсполнителя.ПометкаУдаления
		|ПОМЕСТИТЬ Задачи
		|ИЗ
		|	Задача.ЗадачаИсполнителя КАК ЗадачаИсполнителя
		|ГДЕ
		|	ЗадачаИсполнителя.Проект = &Проект
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ЗадачаИсполнителя.ПроектнаяЗадача,
		|	ЗадачаИсполнителя.Выполнена,
		|	ЗадачаИсполнителя.ПометкаУдаления
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ПроектныеЗадачи.Ссылка,
		|	ПроектныеЗадачи.ВерсияДанных,
		|	ПроектныеЗадачи.Родитель,
		|	ПроектныеЗадачи.ПометкаУдаления,
		|	ПроектныеЗадачи.Наименование,
		|	ПроектныеЗадачи.КодСДР,
		|	ПроектныеЗадачи.Владелец,
		|	ПроектныеЗадачи.ГрафикРаботы,
		|	ПроектныеЗадачи.ТекущийПланТрудозатраты,
		|	ПроектныеЗадачи.ТекущийПланЕдиницаТрудозатрат,
		|	ПроектныеЗадачи.ЕдиницаТрудозатратФакт,
		|	ПроектныеЗадачи.СуммарнаяЗадача,
		|	ПроектныеЗадачи.Веха,
		|	ПроектныеЗадачи.ТипОграничения,
		|	ПроектныеЗадачи.ДатаОграничения,
		|	ПроектныеЗадачи.Исполнители.(
		|		Ссылка,
		|		НомерСтроки,
		|		Исполнитель,
		|		ТекущийПланТрудозатраты
		|	),
		|	ПроектныеЗадачи.Предшественники.(
		|		Ссылка,
		|		НомерСтроки,
		|		Предшественник,
		|		ТипЗависимости,
		|		Задержка,
		|		ЕдиницаЗадержки
		|	),
		|	ЕСТЬNULL(СрокиПроектныхЗадач.ТекущийПланНачало, ДАТАВРЕМЯ(1, 1, 1)) КАК ТекущийПланНачало,
		|	ЕСТЬNULL(СрокиПроектныхЗадач.ТекущийПланОкончание, ДАТАВРЕМЯ(1, 1, 1)) КАК ТекущийПланОкончание,
		|	ЕСТЬNULL(СрокиПроектныхЗадач.ТекущийПланДлительность, 0) КАК ТекущийПланДлительность,
		|	ЕСТЬNULL(СрокиПроектныхЗадач.ТекущийПланЕдиницаДлительности, ЗНАЧЕНИЕ(Перечисление.ЕдиницыДлительности.ПустаяСсылка)) КАК ТекущийПланЕдиницаДлительности,
		|	ЕСТЬNULL(СрокиПроектныхЗадач.НачалоФакт, ДАТАВРЕМЯ(1, 1, 1)) КАК НачалоФакт,
		|	ЕСТЬNULL(СрокиПроектныхЗадач.ОкончаниеФакт, ДАТАВРЕМЯ(1, 1, 1)) КАК ОкончаниеФакт,
		|	ЕСТЬNULL(СрокиПроектныхЗадач.ДлительностьФакт, 0) КАК ДлительностьФакт,
		|	ЕСТЬNULL(СрокиПроектныхЗадач.ЕдиницаДлительностиФакт, ЗНАЧЕНИЕ(Перечисление.ЕдиницыДлительности.ПустаяСсылка)) КАК ЕдиницаДлительностиФакт,
		|	ВЫБОР
		|		КОГДА ИСТИНА В
		|				(ВЫБРАТЬ ПЕРВЫЕ 1
		|					ИСТИНА
		|				ИЗ
		|					Задачи КАК Задачи
		|				ГДЕ
		|					ПроектныеЗадачи.Ссылка = Задачи.ПроектнаяЗадача
		|					И НЕ Задачи.ПометкаУдаления
		|					И НЕ Задачи.Выполнена
		|	
		|				ОБЪЕДИНИТЬ ВСЕ
		|	
		|				ВЫБРАТЬ ПЕРВЫЕ 1
		|					ИСТИНА
		|				ИЗ
		|					ПредметыЗадач КАК ПредметыЗадач
		|				ГДЕ
		|					ПроектныеЗадачи.Ссылка = ПредметыЗадач.Предмет
		|					И НЕ ПредметыЗадач.ПометкаУдаления
		|					И НЕ ПредметыЗадач.Выполнена)
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК ЕстьЗадачи,
		|	ВЫБОР
		|		КОГДА КешИнформацииОбОбъектах.СостояниеКонтроля = ЗНАЧЕНИЕ(Перечисление.СостоянияКонтроля.НаКонтроле)
		|			ТОГДА ВЫБОР
		|					КОГДА КешИнформацииОбОбъектах.СрокИсполнения = ДАТАВРЕМЯ(1, 1, 1)
		|							ИЛИ КешИнформацииОбОбъектах.СрокИсполнения >= &ТекущаяДата
		|						ТОГДА 1
		|					ИНАЧЕ 3
		|				КОНЕЦ
		|		КОГДА КешИнформацииОбОбъектах.СостояниеКонтроля = ЗНАЧЕНИЕ(Перечисление.СостоянияКонтроля.СнятСКонтроля)
		|			ТОГДА 4
		|		КОГДА КешИнформацииОбОбъектах.СостояниеКонтроля = ЗНАЧЕНИЕ(Перечисление.СостоянияКонтроля.НаКонтролеНесколько)
		|			ТОГДА ВЫБОР
		|					КОГДА КешИнформацииОбОбъектах.СрокИсполнения = ДАТАВРЕМЯ(1, 1, 1)
		|							ИЛИ КешИнформацииОбОбъектах.СрокИсполнения >= &ТекущаяДата
		|						ТОГДА 5
		|					КОГДА КешИнформацииОбОбъектах.СрокИсполненияОбщий = ДАТАВРЕМЯ(1, 1, 1)
		|							ИЛИ КешИнформацииОбОбъектах.СрокИсполненияОбщий >= &ТекущаяДата
		|						ТОГДА 8
		|					ИНАЧЕ 6
		|				КОНЕЦ
		|		КОГДА КешИнформацииОбОбъектах.СостояниеКонтроля = ЗНАЧЕНИЕ(Перечисление.СостоянияКонтроля.СнятСКонтроляНесколько)
		|			ТОГДА 7
		|		КОГДА КешИнформацииОбОбъектах.СостояниеКонтроля = ЗНАЧЕНИЕ(Перечисление.СостоянияКонтроля.СмешанноНесколько)
		|			ТОГДА ВЫБОР
		|					КОГДА КешИнформацииОбОбъектах.СрокИсполнения = ДАТАВРЕМЯ(1, 1, 1)
		|							ИЛИ КешИнформацииОбОбъектах.СрокИсполнения >= &ТекущаяДата
		|						ТОГДА 9
		|					ИНАЧЕ 10
		|				КОНЕЦ
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК СостояниеКонтроля
		|ИЗ
		|	Справочник.ПроектныеЗадачи КАК ПроектныеЗадачи
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СрокиПроектныхЗадач КАК СрокиПроектныхЗадач
		|		ПО ПроектныеЗадачи.Ссылка = СрокиПроектныхЗадач.ПроектнаяЗадача
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КешИнформацииОбОбъектах КАК КешИнформацииОбОбъектах
		|		ПО ПроектныеЗадачи.Ссылка = КешИнформацииОбОбъектах.Объект
		|ГДЕ
		|	ПроектныеЗадачи.Владелец = &Проект
		|
		|УПОРЯДОЧИТЬ ПО
		|	ПроектныеЗадачи.НомерЗадачиВУровне ИЕРАРХИЯ";
		
		Запрос.УстановитьПараметр("Проект", Проект);
		Запрос.УстановитьПараметр("ТекущаяДата", НачалоДня(ТекущаяДатаСеанса()));
     
		Результат = Запрос.Выполнить();
		Выборка = Результат.Выбрать();
		ТекущаяДата = ТекущаяДатаСеанса();
		
		Пока Выборка.Следующий() Цикл
			
			Если Выборка.ПометкаУдаления И Не ПоказатьПомеченныеНаУдаление Тогда 
				Продолжить;
			КонецЕсли;	
			
			Если ЗначениеЗаполнено(СостояниеПроектныхЗадач) И СостояниеПроектныхЗадач <> "Все" Тогда 
				Если Не Выборка.СуммарнаяЗадача Тогда 
				
				Если СостояниеПроектныхЗадач = "Завершены" Тогда 
					Если ЗначениеЗаполнено(Выборка.ОкончаниеФакт) Тогда 
					Иначе	
						Продолжить;
					КонецЕсли;	
					
				ИначеЕсли СостояниеПроектныхЗадач = "НеЗавершены" Тогда 
					Если Не ЗначениеЗаполнено(Выборка.ОкончаниеФакт) Тогда 
					Иначе	
						Продолжить;
					КонецЕсли;	
					
				ИначеЕсли СостояниеПроектныхЗадач = "НеНачатыВовремя" Тогда 
					Если Не ЗначениеЗаполнено(Выборка.НачалоФакт) И Выборка.ТекущийПланНачало < ТекущаяДата Тогда 
					Иначе
						Продолжить;
					КонецЕсли;	
					
				ИначеЕсли СостояниеПроектныхЗадач = "ВремяНачалаНеНаступило" Тогда 
					Если Не ЗначениеЗаполнено(Выборка.НачалоФакт) И Выборка.ТекущийПланНачало >= ТекущаяДата Тогда 
					Иначе
						Продолжить;
					КонецЕсли;
					
				ИначеЕсли СостояниеПроектныхЗадач = "ВыполняютсяСПросрочкой" Тогда 	
					Если ЗначениеЗаполнено(Выборка.НачалоФакт) И Не ЗначениеЗаполнено(Выборка.ОкончаниеФакт) 
						И Выборка.ТекущийПланОкончание < ТекущаяДата Тогда 
					Иначе
						Продолжить;
					КонецЕсли;	
					
				ИначеЕсли СостояниеПроектныхЗадач = "ВыполняютсяБезПросрочки" Тогда 		
					Если ЗначениеЗаполнено(Выборка.НачалоФакт) И Не ЗначениеЗаполнено(Выборка.ОкончаниеФакт) 
						И Выборка.ТекущийПланОкончание >= ТекущаяДата Тогда 
					Иначе
						Продолжить;
					КонецЕсли;
					
				КонецЕсли;
				
				КонецЕсли;
			КонецЕсли;	
			
			Если ЗначениеЗаполнено(ПоИсполнителю) Тогда 
				Если Не Выборка.СуммарнаяЗадача Тогда 
				
				НайденИсполнитель = Ложь;
				
				Исполнители = Выборка.Исполнители.Выгрузить();
				Для Каждого Строка Из Исполнители Цикл
					Если ПоИсполнителю = Строка.Исполнитель Тогда
						НайденИсполнитель = Истина;
						Прервать;
					КонецЕсли;
				КонецЦикла;
				
				Если Не НайденИсполнитель Тогда 
					Продолжить;
				КонецЕсли;	
				
				КонецЕсли;
			КонецЕсли;
			
			Родитель = Выборка.Родитель;
			Если Не ЗначениеЗаполнено(Родитель) Тогда 
				НоваяСтрока = Дерево.Строки.Добавить();
			Иначе	
				НайденнаяСтрока = Дерево.Строки.Найти(Родитель, "Ссылка", Истина);	
				НоваяСтрока = НайденнаяСтрока.Строки.Добавить();
			КонецЕсли;
			
			НоваяСтрока.Предшественники.Колонки.Добавить("Предшественник");
			НоваяСтрока.Предшественники.Колонки.Добавить("ТипЗависимости");
			НоваяСтрока.Предшественники.Колонки.Добавить("Задержка");
			НоваяСтрока.Предшественники.Колонки.Добавить("ЕдиницаЗадержки");
			
			НоваяСтрока.Исполнители.Колонки.Добавить("Исполнитель");
			НоваяСтрока.Исполнители.Колонки.Добавить("ТекущийПланТрудозатраты");
			
			НоваяСтрока.Задачи = Выборка.ЕстьЗадачи;
			НоваяСтрока.ТекущаяДата = ТекущаяДата;
			
			ЗаполнитьСтрокуДереваИзПроектнойЗадачи(НоваяСтрока, Выборка);
			
		КонецЦикла;
		
		Если (ЗначениеЗаполнено(СостояниеПроектныхЗадач) И СостояниеПроектныхЗадач <> "Все")
			Или ЗначениеЗаполнено(ПоИсполнителю) Тогда 
			УдалитьСуммарныеСтрокиБезПодчиненных(Дерево);
		КонецЕсли;
		
	КонецЕсли;
	
	ЗначениеВРеквизитФормы(Дерево, "ДеревоЗадач");
	
	Индекс = -1;
	РаботаСПроектамиКлиентСервер.НайтиЗадачуВДеревеПоСсылке(
		ДеревоЗадач.ПолучитьЭлементы(), ТекущаяПроектнаяЗадача, Индекс);
	Если Индекс > -1 Тогда
		Элементы.ДеревоЗадач.ТекущаяСтрока = Индекс;
	КонецЕсли;
		
	ЗаполнитьДиаграммуГанта();
	
КонецПроцедуры

&НаСервере
Функция УдалитьСуммарныеСтрокиБезПодчиненных(Дерево, ТекущаяСтрока = Неопределено)
	
	Если ТекущаяСтрока = Неопределено Тогда 
		ТекущаяСтрока = Дерево;
	КонецЕсли;	
	
	КоличествоСтрок = ТекущаяСтрока.Строки.Количество();
	
	ВсеСуммарные = Истина;
	
	Инд = 1;
	Пока Инд <= КоличествоСтрок Цикл
		Строка = ТекущаяСтрока.Строки[КоличествоСтрок - Инд];
		Если Строка.СуммарнаяЗадача Тогда 
			Если Не УдалитьСуммарныеСтрокиБезПодчиненных(Дерево, Строка) Тогда 
				ВсеСуммарные = Ложь;
			КонецЕсли;	
		Иначе	
			ВсеСуммарные = Ложь;
		КонецЕсли;
		Инд = Инд + 1;
	КонецЦикла;
	
	Если ВсеСуммарные Тогда 
		Если ТекущаяСтрока <> Дерево Тогда 
			Если ТекущаяСтрока.СуммарнаяЗадача Тогда 
				Если ЗначениеЗаполнено(ТекущаяСтрока.Родитель) Тогда 
					ТекущаяСтрока.Родитель.Строки.Удалить(ТекущаяСтрока);
				Иначе
					Дерево.Строки.Удалить(ТекущаяСтрока);
				КонецЕсли;
			КонецЕсли;	
		КонецЕсли;
	КонецЕсли;
	
	Возврат ВсеСуммарные;
	
КонецФункции

&НаСервере
Процедура ЗаполнитьСтрокуДереваИзПроектнойЗадачи(ЭлементДерева, ПроектнаяЗадача)
	
	Если ТипЗнч(ЭлементДерева) = Тип("Число") Тогда 
		ЭлементДерева = ДеревоЗадач.НайтиПоИдентификатору(ЭлементДерева);
	КонецЕсли;	
	
	СписокРеквизитов1 = 
	"Наименование, 
	|Родитель,
	|Исполнители,
	|Предшественники,
	|КодСДР, 
	|Владелец, 
	|Ссылка,
	|ВерсияДанных,
	|ПометкаУдаления, 
	|ГрафикРаботы, 
	|ТекущийПланТрудозатраты, 
	|ТекущийПланЕдиницаТрудозатрат, 
	|ЕдиницаТрудозатратФакт,
	|ТипОграничения,
	|ДатаОграничения,
	|СуммарнаяЗадача,
	|Веха";
	
	СписокРеквизитов2 = 
	"ТекущийПланНачало, 
	|ТекущийПланОкончание, 
	|ТекущийПланДлительность, 
	|ТекущийПланЕдиницаДлительности, 
	|НачалоФакт,
	|ОкончаниеФакт,
	|ДлительностьФакт,
	|ЕдиницаДлительностиФакт,
	|СостояниеКонтроля";
	
	Если ТипЗнч(ПроектнаяЗадача) = Тип("СправочникСсылка.ПроектныеЗадачи") Тогда 
		Реквизиты = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ПроектнаяЗадача, СписокРеквизитов1);
		
		ДанныеПроектнойЗадачи = РаботаСПроектами.ПолучитьДанныеПроектнойЗадачи(ПроектнаяЗадача);
		СтруктураРеквизитов2 = Новый Структура(СписокРеквизитов2);
		Для Каждого Строка Из СтруктураРеквизитов2 Цикл
			Реквизиты.Вставить(Строка.Ключ, ДанныеПроектнойЗадачи[Строка.Ключ]);
		КонецЦикла;	
		
	Иначе
		Реквизиты = ПроектнаяЗадача;
	КонецЕсли;	
	
	СписокРеквизитов = СписокРеквизитов1 + "," + СписокРеквизитов2;
	СписокРеквизитов = СтрЗаменить(СписокРеквизитов, "Родитель,", "");
	СписокРеквизитов = СтрЗаменить(СписокРеквизитов, "Исполнители,", "");
	СписокРеквизитов = СтрЗаменить(СписокРеквизитов, "Предшественники,", "");
	
	ЗаполнитьЗначенияСвойств(ЭлементДерева, Реквизиты, СписокРеквизитов);
	
	ЭлементДерева.ИндексКартинки = ?(ЭлементДерева.ПометкаУдаления, 3, 2);
	ЭлементДерева.РодительскаяЗадача = Реквизиты.Родитель;
	ЭлементДерева.НаименованиеЗадачи = Реквизиты.КодСДР + "  " + Реквизиты.Наименование; 
	
	// плановая длительность
	ПодписьТекПланДлительность = ПолучитьПодписьДлительности(
		Реквизиты.ТекущийПланДлительность, Реквизиты.ТекущийПланЕдиницаДлительности);
	ЭлементДерева.ТекущийПланДлительностьСтр = Строка(Реквизиты.ТекущийПланДлительность) + " " + ПодписьТекПланДлительность;
	
	// фактическая длительность
	ЭлементДерева.ДлительностьФактСтр = "";
	Если Реквизиты.ДлительностьФакт > 0 Тогда 
		ПодписьТекФактДлительность = ПолучитьПодписьДлительности(
			Реквизиты.ДлительностьФакт, Реквизиты.ЕдиницаДлительностиФакт);
		ЭлементДерева.ДлительностьФактСтр = Строка(Реквизиты.ДлительностьФакт) + " " + ПодписьТекФактДлительность;
	КонецЕсли;
	
	// плановые трудозатраты
	ЭлементДерева.ТекущийПланТрудозатратыСтр = "";
	Если Реквизиты.ТекущийПланТрудозатраты > 0 Тогда 
		ПодписьТекПланТрудозатраты = ПолучитьПодписьТрудозатрат(
			Реквизиты.ТекущийПланТрудозатраты, Реквизиты.ТекущийПланЕдиницаТрудозатрат);
		ЭлементДерева.ТекущийПланТрудозатратыСтр = Строка(Реквизиты.ТекущийПланТрудозатраты) + " " + ПодписьТекПланТрудозатраты;
	КонецЕсли;	
	
	// фактические трудозатраты
	ТрудозатратыФакт = РаботаСПроектами.ПолучитьФактическиеТрудозатратыПроектнойЗадачи(Реквизиты.Ссылка);
	ЭлементДерева.ТрудозатратыФакт = ТрудозатратыФакт;
	Если ТрудозатратыФакт > 0 Тогда 
		ПодписьТрудозатратыФакт = ПолучитьПодписьТрудозатрат(ТрудозатратыФакт, Реквизиты.ЕдиницаТрудозатратФакт);
		ЭлементДерева.ТрудозатратыФактСтр = Строка(ТрудозатратыФакт) + " " + ПодписьТрудозатратыФакт;
	КонецЕсли;
	
	// состояние
	СостояниеСтр = "";
	Если ЗначениеЗаполнено(Реквизиты.ОкончаниеФакт) Тогда 
		СостояниеСтр = НСтр("ru = 'Выполнена'; en = 'Executed'");
   	ИначеЕсли ЗначениеЗаполнено(Реквизиты.НачалоФакт) Тогда 
		СостояниеСтр = НСтр("ru = 'Выполняется'; en = 'Executing'");
	Иначе   
		СостояниеСтр = НСтр("ru = 'Запланирована'; en = 'Scheduled'");
	КонецЕсли;	
	ЭлементДерева.Состояние = СостояниеСтр;
	
	// исполнители
	ЭлементДерева.Исполнители.Очистить();
	Если ТипЗнч(Реквизиты.Исполнители) = Тип("РезультатЗапроса") Тогда 
		ТаблИсполнители = Реквизиты.Исполнители.Выгрузить();
	Иначе	
		ТаблИсполнители = Реквизиты.Исполнители;
	КонецЕсли;
	Для Каждого СтрокаТаблЧасти Из ТаблИсполнители Цикл
		СтрокаТаблицыЗначений = ЭлементДерева.Исполнители.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаТаблицыЗначений, СтрокаТаблЧасти);
	КонецЦикла;	
	ЭлементДерева.ИсполнителиСтр = ПредставлениеИсполнителя(ЭлементДерева.Исполнители);
	
	// предшественники
	ЭлементДерева.Предшественники.Очистить();
	Если ТипЗнч(Реквизиты.Предшественники) = Тип("РезультатЗапроса") Тогда 
		ТаблПредшественники = Реквизиты.Предшественники.Выгрузить();
	Иначе	
		ТаблПредшественники = Реквизиты.Предшественники;
	КонецЕсли;
	Для Каждого СтрокаТаблЧасти Из ТаблПредшественники Цикл
		СтрокаТаблицыЗначений = ЭлементДерева.Предшественники.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаТаблицыЗначений, СтрокаТаблЧасти);
	КонецЦикла;	
	ЭлементДерева.ПредшественникиСтр = ПредставлениеПредшественников(ЭлементДерева.Предшественники);
	
	// задержка начала
	НачалоФакт = ?(ЗначениеЗаполнено(Реквизиты.НачалоФакт), Реквизиты.НачалоФакт, ТекущаяДатаСеанса()) ;
	ЭлементДерева.ЗадержкаНачала = (НачалоФакт > Реквизиты.ТекущийПланНачало);
	
	// задержка окончания
	ОкончаниеФакт = ?(ЗначениеЗаполнено(Реквизиты.ОкончаниеФакт), Реквизиты.ОкончаниеФакт, ТекущаяДатаСеанса()) ;
	ЭлементДерева.ЗадержкаОкончания = (ОкончаниеФакт > Реквизиты.ТекущийПланОкончание);
	
	// все предшественники выполнены
	//ВсеПредшественникиВыполнены = Истина;
	//Для Каждого СтрокаТаблЧасти Из ТаблПредшественники Цикл
	//	Если Не ЗначениеЗаполнено(СтрокаТаблЧасти.Предшественник) Тогда 
	//		Продолжить;
	//	КонецЕсли;
	//	
	//	ДанныеПредшественника = РаботаСПроектами.ПолучитьДанныеПроектнойЗадачи(СтрокаТаблЧасти.Предшественник);
	//	Если ДанныеПредшественника.ПометкаУдаления Тогда 
	//		Продолжить;
	//	КонецЕсли;	
	//	
	//	Если Не ЗначениеЗаполнено(ДанныеПредшественника.ОкончаниеФакт) Тогда 
	//		ВсеПредшественникиВыполнены = Ложь;
	//		Прервать;
	//	КонецЕсли;
	//КонецЦикла;
	//ЭлементДерева.ВсеПредшественникиВыполнены = ВсеПредшественникиВыполнены;
	
КонецПроцедуры	

&НаСервере
Процедура ЗаполнитьСтрокиДереваИзПроектныхЗадач(ДеревоЗадач, ПроектныеЗадачи)
	
	ДанныеПроектныхЗадач = РаботаСПроектами.ПолучитьДанныеПроектныхЗадач(ПроектныеЗадачи);
	Для Каждого ДанныеПроектнойЗадачи Из ДанныеПроектныхЗадач Цикл
		
		Индекс = -1;
		РаботаСПроектамиКлиентСервер.НайтиЗадачуВДеревеПоСсылке(
			ДеревоЗадач.ПолучитьЭлементы(), ДанныеПроектнойЗадачи.Ключ, Индекс);
		Если Индекс = -1 Тогда 
			Продолжить;
		КонецЕсли;
		ЭлементДерева = ДеревоЗадач.НайтиПоИдентификатору(Индекс);
		
		ЗаполнитьСтрокуДереваИзПроектнойЗадачи(ЭлементДерева, ДанныеПроектнойЗадачи.Значение);
		
	КонецЦикла;
	
КонецПроцедуры	

&НаСервере
Процедура ОбновитьИзмененныеСтрокиЗадач(МассивИзмененныхЗадач)

	ОбновленныеЗадачи = Новый Массив;
	
	Для Каждого ИзмененнаяЗадача Из МассивИзмененныхЗадач Цикл
		
		Если ОбновленныеЗадачи.Найти(ИзмененнаяЗадача) <> Неопределено Тогда 
			Продолжить;
		КонецЕсли;	
		ОбновленныеЗадачи.Добавить(ИзмененнаяЗадача);
		
	КонецЦикла;	
	
	ЗаполнитьСтрокиДереваИзПроектныхЗадач(ДеревоЗадач, ОбновленныеЗадачи);
	
	ЗаполнитьДиаграммуГанта();
	
КонецПроцедуры	

&НаСервереБезКонтекста
Процедура ЗаблокироватьСтрокуДляРедактирования(Ссылка, УникальныйИдентификатор)
	
	Если ЗначениеЗаполнено(Ссылка) Тогда 
		ЗаблокироватьДанныеДляРедактирования(Ссылка,,УникальныйИдентификатор);
	КонецЕсли;	
	
КонецПроцедуры	

&НаСервереБезКонтекста
Процедура РазблокироватьСтрокуДляРедактирования(Ссылка, УникальныйИдентификатор)
	
	Если ЗначениеЗаполнено(Ссылка) Тогда 
		РазблокироватьДанныеДляРедактирования(Ссылка, УникальныйИдентификатор);
	КонецЕсли;	
	
КонецПроцедуры	

&НаКлиенте
Процедура ОбновитьДеревоЗадач()
	
	СписокРаскрытыхЗадач.Очистить();
	РаботаСПроектамиКлиент.ПолучитьМассивРаскрытыхЗадач(
		Элементы.ДеревоЗадач, ДеревоЗадач.ПолучитьЭлементы(), СписокРаскрытыхЗадач);
		
	ЗаполнитьДеревоЗадач();
	
	РаботаСПроектамиКлиент.УстановитьРазвернутостьЭлементовДерева(
		Элементы.ДеревоЗадач, ДеревоЗадач, СписокРаскрытыхЗадач);
	РаботаСПроектамиКлиент.УстановитьТекущуюЗадачуВДеревеПоСсылке(
		Элементы.ДеревоЗадач, ДеревоЗадач, ТекущаяПроектнаяЗадача);
	
КонецПроцедуры	

&НаКлиенте
Процедура ОбновитьМиникарточку()

	Если Не ПоказыватьЗадачиПроцессов Тогда 
		Возврат;
	КонецЕсли;	
	
	ОсновныеЗадачи.Очистить();
	ВспомогательныеЗадачи.Очистить();
	
	ЗаголовокОсновныеЗадачи = НСтр("ru = 'Основные задачи:'; en = 'Main tasks:'");
	ЗаголовокВспомогательныеЗадачи = НСтр("ru = 'Вспомогательные задачи:'; en = 'Supplementary tasks:'");
	
	ТекущиеДанные = Элементы.ДеревоЗадач.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	Если Не ЗначениеЗаполнено(ТекущиеДанные.Ссылка) Тогда 
		Возврат;
	КонецЕсли;
	
	ДанныеМиникарточки = ПолучитьДанныеМиникарточки(ТекущиеДанные.Ссылка);
	
	Для Каждого Строка Из ДанныеМиникарточки.ОсновныеЗадачи Цикл
		НоваяСтрока = ОсновныеЗадачи.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
	КонецЦикла;
	КоличествоОсновныхЗадач = ДанныеМиникарточки.ОсновныеЗадачи.Количество();
	Если КоличествоОсновныхЗадач > 0 Тогда 
		ЗаголовокОсновныеЗадачи = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Основные задачи (%1):'; en = 'Main tasks (%1):'"),
			КоличествоОсновныхЗадач);
	КонецЕсли;
	
	Для Каждого Строка Из ДанныеМиникарточки.ВспомогательныеЗадачи Цикл
		НоваяСтрока = ВспомогательныеЗадачи.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
	КонецЦикла;
	КоличествоВспомогательныхЗадач = ДанныеМиникарточки.ВспомогательныеЗадачи.Количество();
	Если КоличествоВспомогательныхЗадач > 0 Тогда 
		ЗаголовокВспомогательныеЗадачи = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Вспомогательные задачи (%1):'; en = 'Supplementary tasks (%1):'"),
			КоличествоВспомогательныхЗадач);
	КонецЕсли;
	
КонецПроцедуры	

&НаСервереБезКонтекста
Функция ПолучитьДанныеМиникарточки(ПроектнаяЗадача) 
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ЗадачаИсполнителя.Исполнитель,
		|	ЗадачаИсполнителя.СрокИсполнения,
		|	ЗадачаИсполнителя.РольИсполнителя,
		|	ЗадачаИсполнителя.ТочкаМаршрута,
		|	ЗадачаИсполнителя.БизнесПроцесс,
		|	ВЫБОР
		|		КОГДА ЗадачаИсполнителя.СрокИсполнения <> ДАТАВРЕМЯ(1, 1, 1)
		|				И ЗадачаИсполнителя.СрокИсполнения < &ТекущаяДата
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК Просрочена,
		|	ЗадачаИсполнителя.Ссылка
		|ИЗ
		|	Задача.ЗадачаИсполнителя КАК ЗадачаИсполнителя
		|ГДЕ
		|	ЗадачаИсполнителя.ПроектнаяЗадача = &ПроектнаяЗадача
		|	И НЕ ЗадачаИсполнителя.Выполнена
		|	И НЕ ЗадачаИсполнителя.ПометкаУдаления
		|	И ЗадачаИсполнителя.СостояниеБизнесПроцесса = ЗНАЧЕНИЕ(Перечисление.СостоянияБизнесПроцессов.Активен)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ЗадачаИсполнителя.Исполнитель,
		|	ЗадачаИсполнителя.СрокИсполнения,
		|	ЗадачаИсполнителя.РольИсполнителя,
		|	ЗадачаИсполнителя.ТочкаМаршрута,
		|	ЗадачаИсполнителя.БизнесПроцесс,
		|	ВЫБОР
		|		КОГДА ЗадачаИсполнителя.СрокИсполнения <> ДАТАВРЕМЯ(1, 1, 1)
		|				И ЗадачаИсполнителя.СрокИсполнения < &ТекущаяДата
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК Просрочена,
		|	ЗадачаИсполнителя.Ссылка
		|ИЗ
		|	Задача.ЗадачаИсполнителя КАК ЗадачаИсполнителя
		|ГДЕ
		|	ЗадачаИсполнителя.Предметы.Предмет = &ПроектнаяЗадача
		|	И НЕ ЗадачаИсполнителя.Выполнена
		|	И НЕ ЗадачаИсполнителя.ПометкаУдаления
		|	И ЗадачаИсполнителя.СостояниеБизнесПроцесса = ЗНАЧЕНИЕ(Перечисление.СостоянияБизнесПроцессов.Активен)";
		
	Запрос.УстановитьПараметр("ПроектнаяЗадача", ПроектнаяЗадача);
	Запрос.УстановитьПараметр("ТекущаяДата", ТекущаяДатаСеанса());
	Результаты = Запрос.ВыполнитьПакет();
	
	ДанныеМиникарточки = Новый Структура;
	ДанныеМиникарточки.Вставить("ОсновныеЗадачи", ПолучитьДанныеЗадач(Результаты[0]));
	ДанныеМиникарточки.Вставить("ВспомогательныеЗадачи", ПолучитьДанныеЗадач(Результаты[1]));
	
	Возврат ДанныеМиникарточки;
	
КонецФункции	

&НаСервереБезКонтекста
Функция ПолучитьДанныеЗадач(РезультатЗапроса)
	
	МассивЗадачи = Новый Массив;
	
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		СтрокаЗадача = Новый Структура(
			"Исполнитель,
			|СрокИсполнения,
			|ТочкаМаршрута, 
			|Ссылка,
			|Просрочена,
			|БизнесПроцесс");
		
		Если ЗначениеЗаполнено(Выборка.Исполнитель) Тогда
			СтрокаИсполнитель = Строка(Выборка.Исполнитель);
		Иначе
			СтрокаИсполнитель = Строка(Выборка.РольИсполнителя);
		КонецЕсли;
		
		СтрокаЗадача.Исполнитель = СтрокаИсполнитель;
		СтрокаЗадача.СрокИсполнения = Выборка.СрокИсполнения; 
		СтрокаЗадача.ТочкаМаршрута = Выборка.ТочкаМаршрута; 
		СтрокаЗадача.Ссылка = Выборка.Ссылка; 
		СтрокаЗадача.БизнесПроцесс = Выборка.БизнесПроцесс; 
		СтрокаЗадача.Просрочена = Выборка.Просрочена; 
		
		МассивЗадачи.Добавить(СтрокаЗадача);
	КонецЦикла;	
	
	Возврат МассивЗадачи;
	
КонецФункции	

&НаСервере
Процедура ЗаполнитьДиаграммуГанта()
	
	Если Не Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаДиаграммаГанта Тогда 
		Возврат;
	КонецЕсли;	
	
	// Получаем диаграмму и запрещаем на время заполнения ее обновление.
	// В случае большого объема данных это существенно ускорит выполнение.
	Диаграмма = ДиаграммаГанта;
	Диаграмма.Обновление = Ложь;
	Диаграмма.Очистить();
	Диаграмма.ИнтервалыФона.Очистить();
	
	Для Каждого Шкала Из Диаграмма.ОбластьПостроения.ШкалаВремени.Элементы Цикл
		Шкала.Метки.Очистить();
	КонецЦикла;	
	
	Диаграмма.ОтображатьЛегенду = Ложь;
	Диаграмма.ОтображатьЗаголовок = Ложь;
	Диаграмма.ОбластьПостроения.Право = 1;
	
	// Создаем таблицу значений, в которую, по мере обхода запроса, будем заносить связи.
	Связи = Новый ТаблицаЗначений;
	Связи.Колонки.Добавить("Конец");
	Связи.Колонки.Добавить("Начало");
	Связи.Колонки.Добавить("ТипЗависимости");
	
	// Серия будет одна.
	Серия = Диаграмма.Серии.Добавить();
	
	// Заводим переменные для определения максимальной и минимальной дат работ.
	МаксимальнаяДата = NULL;
	МинимальнаяДата = NULL;
	
	Строки = ДеревоЗадач.ПолучитьЭлементы();
	Для Каждого СтрокаДерева Из Строки Цикл
		ЗаполнитьОднуТочкуДиаграммыГанта(
			СтрокаДерева, Связи, Серия, МаксимальнаяДата, МинимальнаяДата);
	КонецЦикла;
	
	// Теперь в интервале минимальной и максимальной дат заполняем интервалы выходных дней.
	Если МинимальнаяДата <> NULL Тогда
		
		День = 3600 * 24;
		Неделя = 3600 * 24 * 7;
		МаксимальнаяДата = МаксимальнаяДата + Неделя * 4;
		
		ГрафикРаботы = Проект.ГрафикРаботы;
		Если Не ЗначениеЗаполнено(ГрафикРаботы) Тогда
			ГрафикРаботы = ГрафикиРаботы.ПолучитьОсновнойГрафикРаботы();
		КонецЕсли;
		
		РабочиеДни = Справочники.Календари.ПрочитатьДанныеГрафикаИзРегистра(
			ГрафикРаботы.Календарь, Год(МинимальнаяДата));
		Если Год(МаксимальнаяДата) > Год(МинимальнаяДата) Тогда 
			ТекГод = Год(МинимальнаяДата) + 1;
			Пока ТекГод <= Год(МаксимальнаяДата) Цикл
				врРабочиеДни = Справочники.Календари.ПрочитатьДанныеГрафикаИзРегистра(ГрафикРаботы.Календарь, ТекГод);
				Для Каждого Элемент Из врРабочиеДни Цикл
					РабочиеДни.Добавить(Элемент);
				КонецЦикла;	
				ТекГод = ТекГод + 1;
			КонецЦикла;	
		КонецЕсли;	 
		
		ТекДата = МинимальнаяДата;
		Пока ТекДата <= МаксимальнаяДата Цикл;
			
			РабочийДень = (РабочиеДни.Найти(НачалоДня(ТекДата)) <> Неопределено);
			
			Если Не РабочийДень Тогда
			
				Кон = КонецДня(ТекДата);
				Нач = НачалоДня(ТекДата);
				Диаграмма.ИнтервалыФона.Добавить(Нач, Кон);
				
			КонецЕсли;	
			
			ТекДата = ТекДата + День;
			
		КонецЦикла;		
		
	КонецЕсли;
	
	// По заполненной таблице создаем связи.
	СоздатьСвязиНаДиаграммеГанта(Связи, Диаграмма, Серия);
	
	// Находим элемент шкалы времени для непериодических меток.
	ЭлементМеток = ПолучитьЭлементДляМеток(Диаграмма);
	
	// Создаем метку - текущая дата
	Метка = ЭлементМеток.Метки.Добавить(ТекущаяДата());
	Метка.Текст = НСтр("ru = 'Текущая дата'; en = 'Current date'");
	Метка.ЦветТекста = WebЦвета.Красный;
	Метка.ЦветЛинии = Метка.ЦветТекста;
	
	// Развертываем первую точку - рекурсивно. 
	Если Диаграмма.Точки.Количество() > 0 Тогда
		Диаграмма.РазвернутьТочку(Диаграмма.Точки.Получить(0), Истина);
	КонецЕсли;
	
	// Включаем обновление диаграммы.
	Диаграмма.Обновление = Истина;
	
	Диаграмма.ПоддержкаМасштаба = ПоддержкаМасштабаДиаграммыГанта.Авто;
	
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьОднуТочкуДиаграммыГанта(СтрокаДерева, Связи, Серия, МаксимальнаяДата, МинимальнаяДата)
	
	Диаграмма = ДиаграммаГанта;
	
	ЦветЗапланированного = WebЦвета.Васильковый;
	ЦветНеПросроченного = ЦветаСтиля.ОтметкаПоложительногоВыполненияЗадачи;
	ЦветПросроченного = ЦветаСтиля.ПоясняющийОшибкуТекст;
	ЦветВыполненного =  ЦветаСтиля.ЗавершенныйБизнесПроцесс; 
	ЦветНеНазначенного = WebЦвета.НейтральноКоричневый;
	ЦветРодителя = WebЦвета.Черный;
	
	Если ЗначениеЗаполнено(СтрокаДерева.НачалоФакт) И ЗначениеЗаполнено(СтрокаДерева.ОкончаниеФакт) Тогда 
		ДатаНачала = СтрокаДерева.НачалоФакт;
		ДатаОкончания = СтрокаДерева.ОкончаниеФакт;
	Иначе	
		ДатаНачала = СтрокаДерева.ТекущийПланНачало;
		ДатаОкончания = СтрокаДерева.ТекущийПланОкончание;
	КонецЕсли;	
	
	
	НужноДобавитьСтроку = Истина;
	
	// Если даты не заполнены и нет дочерних задач - не добавляем точку
	Если Не ЗначениеЗаполнено(ДатаНачала) И Не ЗначениеЗаполнено(ДатаОкончания)
		И СтрокаДерева.ПолучитьЭлементы().Количество() = 0 Тогда
		
		НужноДобавитьСтроку = Ложь;
		
	КонецЕсли;	
	
	Если НужноДобавитьСтроку Тогда
		
		// Поскольку серия одна, каждой работе соответствует одна точка и одно значение.
		
		// Инициализируем точку.
		Точка = Диаграмма.УстановитьТочку(СтрокаДерева.Ссылка, 
											?(СтрокаДерева.РодительскаяЗадача.Пустая(), 
				  							Неопределено,
											СтрокаДерева.РодительскаяЗадача));
											
		Точка.Текст = СтрокаДерева.Наименование;
		
		Диаграмма.РазвернутьТочку(Точка, Истина);
		
		// Инициализируем значение.
		Значение = Диаграмма.ПолучитьЗначение(Точка, Серия);
		
		Если ЗначениеЗаполнено(ДатаНачала) И ЗначениеЗаполнено(ДатаОкончания) Тогда	
			 
			Если ЗначениеЗаполнено(СтрокаДерева.ОкончаниеФакт) Тогда
				Значение.Редактирование = Ложь;
			Иначе	
				Значение.Редактирование = Истина;
			КонецЕсли;	
			
			// Добавляем интервал. В данном примере у каждой работы единственный интервал.
			Интервал = Значение.Добавить();
			Интервал.Начало = ДатаНачала;
			Интервал.Конец = ДатаОкончания;
			
			// Формируем подпись.
			УстановитьТекстЗначения(Интервал);
			
			ЭтоРодитель = (СтрокаДерева.ПолучитьЭлементы().Количество() <> 0);
			
			// Определяем статус задачи.
			Если ЭтоРодитель Тогда
				Значение.Цвет = ЦветРодителя;
			ИначеЕсли ЗначениеЗаполнено(СтрокаДерева.ОкончаниеФакт) Тогда
				Значение.Цвет = ЦветВыполненного;
			ИначеЕсли ДатаОкончания < ТекущаяДатаСеанса() Тогда
				Значение.Цвет = ЦветПросроченного;
			ИначеЕсли ДатаНачала < ТекущаяДатаСеанса() И Не ЗначениеЗаполнено(СтрокаДерева.НачалоФакт) Тогда 		
				Значение.Цвет = ЦветНеНазначенного;
			ИначеЕсли ЗначениеЗаполнено(СтрокаДерева.НачалоФакт) Тогда 	
				Значение.Цвет = ЦветНеПросроченного;
			Иначе
				Значение.Цвет = ЦветЗапланированного;
			КонецЕсли;
			
			// Если надо, корректируем максимальную и минимальную даты.
			Если МаксимальнаяДата = NULL Или МаксимальнаяДата < ДатаОкончания Тогда
				МаксимальнаяДата = ДатаОкончания;
			КонецЕсли;				
			
			Если МинимальнаяДата = NULL Или МинимальнаяДата > ДатаНачала Тогда
				МинимальнаяДата = ДатаНачала;
			КонецЕсли;	
			
			// Получаем связи данной работы и заносим в таблицу.
			Для Каждого СтрокаДереваПред Из СтрокаДерева.Предшественники Цикл
				Если ЗначениеЗаполнено(СтрокаДереваПред.Предшественник) И СтрокаДереваПред.Предшественник.ПометкаУдаления Тогда 
				   Продолжить;
				КонецЕсли; 
				
				Связь = Связи.Добавить();
				Связь.Начало = СтрокаДереваПред.Предшественник;
				Связь.Конец = СтрокаДерева.Ссылка;
				Связь.ТипЗависимости = СтрокаДереваПред.ТипЗависимости;
			КонецЦикла;
			
		КонецЕсли;			
		
		Если СтрокаДерева.Веха Тогда
			
			// Находим элемент шкалы времени для непериодических меток.
			ЭлементМеток = ПолучитьЭлементДляМеток(Диаграмма);
			
			// Создаем метку - для вехи 
			Метка = ЭлементМеток.Метки.Добавить(ДатаНачала);
			Метка.Текст = СтрокаДерева.Наименование;
			Метка.ЦветТекста = WebЦвета.Красный;
			Метка.ЦветЛинии = Метка.ЦветТекста;
			
		КонецЕсли;
		
		Строки = СтрокаДерева.ПолучитьЭлементы();
		Для Каждого Строка Из Строки Цикл
			ЗаполнитьОднуТочкуДиаграммыГанта(Строка, Связи, Серия, МаксимальнаяДата, МинимальнаяДата);
		КонецЦикла;
		
	КонецЕсли;	
	
КонецПроцедуры	

&НаСервере
Процедура СоздатьСвязиНаДиаграммеГанта(Связи, Диаграмма, Серия)
	
	ТекущаяТочка = NULL;
	Для Каждого Связь Из Связи Цикл 
		
		Если ТекущаяТочка = NULL Или ТекущаяТочка.Значение <> Связь.Конец Тогда
			ТекущаяТочка = Диаграмма.УстановитьТочку(Связь.Конец);
		КонецЕсли;			
		
		ТочкаНачала = Диаграмма.УстановитьТочку(Связь.Начало);
		Значение = Диаграмма.ПолучитьЗначение(ТочкаНачала, Серия);
		Для Каждого Интервал Из Значение Цикл
			ИнтервалНачала = Интервал;
			Прервать;
		КонецЦикла;				
		Значение = Диаграмма.ПолучитьЗначение(ТекущаяТочка, Серия);
		Для Каждого Интервал Из Значение Цикл
			ИнтервалКонца = Интервал;
			Прервать;
		КонецЦикла;	
		
		ИнтервалыРавны = Ложь;
		Если ИнтервалНачала <> Неопределено И ИнтервалКонца <> Неопределено Тогда
			Если (ИнтервалНачала.Значение <> Неопределено И ИнтервалКонца.Значение <> Неопределено И 
				ИнтервалНачала.Значение.Текст = ИнтервалКонца.Значение.Текст)
				И ИнтервалНачала.Конец = ИнтервалКонца.Конец 
				И ИнтервалНачала.Начало = ИнтервалКонца.Начало Тогда
				ИнтервалыРавны = Истина;
			КонецЕсли;	
		КонецЕсли;
		
		Если ИнтервалНачала = Неопределено ИЛИ ИнтервалКонца = Неопределено 
			ИЛИ ИнтервалыРавны Тогда
			Продолжить;
		КонецЕсли;
		
		СвязьДобавленная = ИнтервалНачала.Добавить(ИнтервалКонца);
		
		Если Связь.ТипЗависимости = Перечисления.ТипыЗависимостейПроектныхЗадач.НачалоНачало Тогда
			СвязьДобавленная.ТипСвязи = ТипСвязиДиаграммыГанта.НачалоНачало;
		ИначеЕсли Связь.ТипЗависимости = Перечисления.ТипыЗависимостейПроектныхЗадач.НачалоОкончание Тогда
			СвязьДобавленная.ТипСвязи = ТипСвязиДиаграммыГанта.НачалоКонец;
		ИначеЕсли Связь.ТипЗависимости = Перечисления.ТипыЗависимостейПроектныхЗадач.ОкончаниеНачало Тогда
			СвязьДобавленная.ТипСвязи = ТипСвязиДиаграммыГанта.КонецНачало;
		ИначеЕсли Связь.ТипЗависимости = Перечисления.ТипыЗависимостейПроектныхЗадач.ОкончаниеОкончание Тогда
			СвязьДобавленная.ТипСвязи = ТипСвязиДиаграммыГанта.КонецКонец;
		КонецЕсли;	
		
		Если ИнтервалНачала.Конец <= ИнтервалКонца.Начало Тогда
			СвязьДобавленная.Цвет = WebЦвета.Синий;
		Иначе			
			СвязьДобавленная.Цвет = WebЦвета.Красный;
		КонецЕсли;			
		
	КонецЦикла;				
	
КонецПроцедуры // СоздатьСвязи(Связи, Диаграмма, Серия)

&НаСервере
Функция ПолучитьЭлементДляМеток(Диаграмма)
	
	// Ищем элемент шкалы времени с ОтображатьПериодическиеМетки = Ложь
	Для Каждого Элемент Из Диаграмма.ОбластьПостроения.ШкалаВремени.Элементы Цикл
		
		Если Элемент.ОтображатьПериодическиеМетки = Ложь Тогда
			ЭлементМеток = Элемент;
			Возврат ЭлементМеток;
		КонецЕсли;			
		
	КонецЦикла;						
	
	ЭлементМеток = Диаграмма.ОбластьПостроения.ШкалаВремени.Элементы.Добавить();
	ЭлементМеток.Единица = ТипЕдиницыШкалыВремени.День;
	ЭлементМеток.ОтображатьПериодическиеМетки = Ложь;
	
	Возврат ЭлементМеток;
	
КонецФункции // ПолучитьЭлементДляМеток(Диаграмма)

&НаСервере
Процедура УстановитьТекстЗначения(Интервал)
	
	Интервал.Значение.Текст = Строка(День(Интервал.Начало)) + "." 
		+ Строка(Месяц(Интервал.Начало)) + "-" 
		+ Строка(День(Интервал.Конец)) + "." 
		+ Строка(Месяц(Интервал.Конец));
									 
КонецПроцедуры

&НаСервере
Процедура ИзменитьЗадачуИзДиаграммыГанта(Задача, ИнтервалНачало, ИнтервалКонец, ТипОграничения, ДатаОграничения)
	
	Если ЗначениеЗаполнено(ТипОграничения) И ЗначениеЗаполнено(ДатаОграничения) Тогда 
		ОбъектИсточник = Задача.ПолучитьОбъект();
		ОбъектИсточник.ТипОграничения = ТипОграничения;
		ОбъектИсточник.ДатаОграничения = ДатаОграничения;
		ОбъектИсточник.Записать();
	КонецЕсли;
	
	ДанныеПроектнойЗадачи = РаботаСПроектами.ПолучитьДанныеПроектнойЗадачи(Задача);
	Если ДанныеПроектнойЗадачи.Веха Тогда
		
		Если ИнтервалНачало <> ДанныеПроектнойЗадачи.ТекущийПланНачало Тогда
			ИнтервалКонец = ИнтервалНачало;
		КонецЕсли;	
		
		Если ИнтервалКонец <> ДанныеПроектнойЗадачи.ТекущийПланОкончание Тогда
			ИнтервалНачало = ИнтервалКонец;
		КонецЕсли;
		
		СтруктураДанных = Новый Структура;
		СтруктураДанных.Вставить("ТекущийПланНачало", ИнтервалНачало);
		СтруктураДанных.Вставить("ТекущийПланОкончание", ИнтервалКонец);
		СтруктураДанных.Вставить("ТекущийПланДлительность", 0);
				
		РаботаСПроектами.ЗаписатьСрокиПроектнойЗадачи(ДанныеПроектнойЗадачи.Ссылка, СтруктураДанных);
		
	Иначе
		
		Если ИнтервалНачало <> ДанныеПроектнойЗадачи.ТекущийПланНачало 
		   И ИнтервалКонец <> ДанныеПроектнойЗадачи.ТекущийПланОкончание Тогда // перенесли отрезок
		   
			СтруктураДанных = Новый Структура;
			СтруктураДанных.Вставить("ТекущийПланНачало", ИнтервалНачало);
			СтруктураДанных.Вставить("ТекущийПланОкончание", 
				РаботаСПроектами.РассчитатьОкончаниеПериода(ДанныеПроектнойЗадачи, 
				ИнтервалНачало, 
				ДанныеПроектнойЗадачи.ТекущийПланДлительность, 
				ДанныеПроектнойЗадачи.ТекущийПланЕдиницаДлительности));
			
			РаботаСПроектами.ЗаписатьСрокиПроектнойЗадачи(ДанныеПроектнойЗадачи.Ссылка, СтруктураДанных);
			
		Иначе // изменили отрезок
			
			ТекущийПланДлительность = РаботаСПроектами.РассчитатьДлительностьПериода(ДанныеПроектнойЗадачи, 
				ИнтервалНачало, 
				ИнтервалКонец, 
				ДанныеПроектнойЗадачи.ТекущийПланЕдиницаДлительности);
				
			Если ТекущийПланДлительность > 0 Тогда 
				СтруктураДанных = Новый Структура;
				СтруктураДанных.Вставить("ТекущийПланНачало", ИнтервалНачало);
				СтруктураДанных.Вставить("ТекущийПланОкончание", ИнтервалКонец);
				СтруктураДанных.Вставить("ТекущийПланДлительность", ТекущийПланДлительность);
				
				РаботаСПроектами.ЗаписатьСрокиПроектнойЗадачи(ДанныеПроектнойЗадачи.Ссылка, СтруктураДанных);
			КонецЕсли;	
			
		КонецЕсли;
		
	КонецЕсли;
	
	МассивИзмененныхЗадач = Новый Массив;
	РаботаСПроектами.РассчитатьПланПроекта(ДанныеПроектнойЗадачи.Ссылка, МассивИзмененныхЗадач);
	ОбновитьИзмененныеСтрокиЗадач(МассивИзмененныхЗадач);
	
КонецПроцедуры	

&НаСервереБезКонтекста
Функция ПредставлениеПредшественников(Знач Предшественники)
	
	ПредшественникиСтр = "";
	Для Каждого СтрокаТаблЧасти Из Предшественники Цикл
		
		Если СтрокаТаблЧасти.ТипЗависимости = Перечисления.ТипыЗависимостейПроектныхЗадач.ОкончаниеНачало Тогда
			ТипЗависимости = "";
		ИначеЕсли СтрокаТаблЧасти.ТипЗависимости = Перечисления.ТипыЗависимостейПроектныхЗадач.ОкончаниеОкончание Тогда
			ТипЗависимости = НСтр("ru = 'ОО'; en = 'FF'");
		ИначеЕсли СтрокаТаблЧасти.ТипЗависимости = Перечисления.ТипыЗависимостейПроектныхЗадач.НачалоОкончание Тогда
			ТипЗависимости = НСтр("ru = 'НО'; en = 'SF'");
		ИначеЕсли СтрокаТаблЧасти.ТипЗависимости = Перечисления.ТипыЗависимостейПроектныхЗадач.НачалоНачало Тогда
			ТипЗависимости = НСтр("ru = 'НН'; en = 'SS'");
		КонецЕсли;
		
		Задержка = "";
		Если СтрокаТаблЧасти.Задержка > 0 Тогда
			Задержка = " + " + СтрокаТаблЧасти.Задержка + " " 
				+ ПолучитьПодписьДлительности(СтрокаТаблЧасти.Задержка, СтрокаТаблЧасти.ЕдиницаЗадержки);
		КонецЕсли;
		
		ПредшественникКодСДР = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СтрокаТаблЧасти.Предшественник, "КодСДР");
		ПредшественникиСтр = ПредшественникиСтр + ПредшественникКодСДР + ТипЗависимости + Задержка + ", ";
		
	КонецЦикла;	
	
	Если ПредшественникиСтр <> "" Тогда 
		ПредшественникиСтр = Лев(ПредшественникиСтр, СтрДлина(ПредшественникиСтр)-2);
	КонецЕсли;
	
	Возврат ПредшественникиСтр;
	
КонецФункции	

&НаСервереБезКонтекста
Функция ПредставлениеИсполнителя(Знач Исполнители)
	
	ИсполнителиСтр = "";
	Для Каждого Строка Из Исполнители Цикл
		ПредставлениеИсполнителя = Строка(Строка.Исполнитель);
		ИсполнителиСтр = ИсполнителиСтр + ", " + ПредставлениеИсполнителя;
	КонецЦикла;
	
	Если ИсполнителиСтр <> "" Тогда 
		ИсполнителиСтр = Сред(ИсполнителиСтр, 3);
	КонецЕсли;	
	
	Возврат ИсполнителиСтр;
	
КонецФункции	

&НаКлиентеНаСервереБезКонтекста
Функция ПолучитьПодписьДлительности(Длительность, ЕдиницаДлительности)
	
	ПодписьДлительность = "";
	
	ЦелаяЧастьДлительность = Цел(Длительность);
	Если ЕдиницаДлительности = ПредопределенноеЗначение("Перечисление.ЕдиницыДлительности.Минута") Тогда
		ПодписьДлительность = ДелопроизводствоКлиентСервер.ПолучитьПодписьМинут(ЦелаяЧастьДлительность);
			
	ИначеЕсли ЕдиницаДлительности = ПредопределенноеЗначение("Перечисление.ЕдиницыДлительности.Час") Тогда
		ПодписьДлительность = ДелопроизводствоКлиентСервер.ПолучитьПодписьЧасов(ЦелаяЧастьДлительность);
			
	ИначеЕсли ЕдиницаДлительности = ПредопределенноеЗначение("Перечисление.ЕдиницыДлительности.День") Тогда
		ПодписьДлительность = ДелопроизводствоКлиентСервер.ПолучитьПодписьДней(ЦелаяЧастьДлительность);
			
	ИначеЕсли ЕдиницаДлительности = ПредопределенноеЗначение("Перечисление.ЕдиницыДлительности.Неделя") Тогда
		ПодписьДлительность = ДелопроизводствоКлиентСервер.ПолучитьПодписьНедель(ЦелаяЧастьДлительность);
			
	ИначеЕсли ЕдиницаДлительности = ПредопределенноеЗначение("Перечисление.ЕдиницыДлительности.Месяц") Тогда
		ПодписьДлительность = ДелопроизводствоКлиентСервер.ПолучитьПодписьМесяцев(ЦелаяЧастьДлительность);	
			
	Иначе
		ПодписьДлительность = НРег(Строка(ЕдиницаДлительности));
	КонецЕсли;
	
	Возврат ПодписьДлительность;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ПолучитьПодписьТрудозатрат(Трудозатраты, ЕдиницаТрудозатрат)
	
	ПодписьТрудозатраты = "";
	
	ЦелаяЧастьТрудозатраты = Цел(Трудозатраты);
	Если ЕдиницаТрудозатрат = ПредопределенноеЗначение("Перечисление.ЕдиницыТрудозатрат.ЧеловекоМинута") Тогда
		ПодписьТрудозатраты = НСтр("ru = 'чел-'; en = 'man-'") 
			+ ДелопроизводствоКлиентСервер.ПолучитьПодписьМинут(ЦелаяЧастьТрудозатраты);
			
	ИначеЕсли ЕдиницаТрудозатрат = ПредопределенноеЗначение("Перечисление.ЕдиницыТрудозатрат.ЧеловекоЧас") Тогда
		ПодписьТрудозатраты = НСтр("ru = 'чел-'; en = 'man-'") 
			+ ДелопроизводствоКлиентСервер.ПолучитьПодписьЧасов(ЦелаяЧастьТрудозатраты);
			
	ИначеЕсли ЕдиницаТрудозатрат = ПредопределенноеЗначение("Перечисление.ЕдиницыТрудозатрат.ЧеловекоДень") Тогда
		ПодписьТрудозатраты = НСтр("ru = 'чел-'; en = 'man-'") 
			+ ДелопроизводствоКлиентСервер.ПолучитьПодписьДней(ЦелаяЧастьТрудозатраты);
			
	ИначеЕсли ЕдиницаТрудозатрат = ПредопределенноеЗначение("Перечисление.ЕдиницыТрудозатрат.ЧеловекоНеделя") Тогда
		ПодписьТрудозатраты = НСтр("ru = 'чел-'; en = 'man-'") 
			+ ДелопроизводствоКлиентСервер.ПолучитьПодписьНедель(ЦелаяЧастьТрудозатраты);
			
	ИначеЕсли ЕдиницаТрудозатрат = ПредопределенноеЗначение("Перечисление.ЕдиницыТрудозатрат.ЧеловекоМесяц") Тогда
		ПодписьТрудозатраты = НСтр("ru = 'чел-'; en = 'man-'") 
			+ ДелопроизводствоКлиентСервер.ПолучитьПодписьМесяцев(ЦелаяЧастьТрудозатраты);	
			
	Иначе
		ПодписьТрудозатраты = НРег(Строка(ЕдиницаТрудозатрат));
	КонецЕсли;
	
	Возврат ПодписьТрудозатраты;
	
КонецФункции

&НаСервере
Процедура ЗаписатьПроектнуюЗадачуИзСтрокиДерева(ИндексСтроки)
	
	ЭлементДерева = ДеревоЗадач.НайтиПоИдентификатору(ИндексСтроки);
	
	Если ЗначениеЗаполнено(ЭлементДерева.Ссылка) Тогда 
		ПроектнаяЗадачаОбъект = ЭлементДерева.Ссылка.ПолучитьОбъект();
		
		Если ЭлементДерева.ВерсияДанных <> ЭлементДерева.Ссылка.ВерсияДанных Тогда 
			РазблокироватьСтрокуДляРедактирования(ПроектнаяЗадачаОбъект.Ссылка, ЭтаФорма.УникальныйИдентификатор);
			
			ЗаполнитьСтрокуДереваИзПроектнойЗадачи(ЭлементДерева, ПроектнаяЗадачаОбъект.Ссылка);
			
			ТекстСообщения = НСтр("ru = 'Данные были изменены другим пользователем'; en = 'Data were modified by another user'");
			ВызватьИсключение ТекстСообщения; 
		КонецЕсли;	
	Иначе
		ПроектнаяЗадачаОбъект = Справочники.ПроектныеЗадачи.СоздатьЭлемент();
		ПроектнаяЗадачаОбъект.Важность = Перечисления.ВариантыВажностиЗадачи.Обычная;
		
		ДанныеКода = РаботаСПроектами.ПолучитьКодСДРИНомерЗадачиВУровне(ЭлементДерева.Владелец, ЭлементДерева.РодительскаяЗадача);
		
		ПроектнаяЗадачаОбъект.КодСДР = ДанныеКода.КодСДР;
		ПроектнаяЗадачаОбъект.НомерЗадачиВУровне = ДанныеКода.НомерЗадачиВУровне;
	КонецЕсли;	
	
	Если ЭлементДерева.ТекущийПланДлительность = 0 Тогда 
		ПроектнаяЗадачаОбъект.Веха = Истина;
	Иначе
		ПроектнаяЗадачаОбъект.Веха = Ложь;
	КонецЕсли;	
	
	ПроектнаяЗадачаОбъект.ЕдиницаТрудозатратФакт = ЭлементДерева.ЕдиницаТрудозатратФакт;
	ПроектнаяЗадачаОбъект.ТекущийПланЕдиницаТрудозатрат = ЭлементДерева.ТекущийПланЕдиницаТрудозатрат;
	ПроектнаяЗадачаОбъект.ТекущийПланТрудозатраты = ЭлементДерева.ТекущийПланТрудозатраты;
	
	ПроектнаяЗадачаОбъект.Наименование = ЭлементДерева.Наименование;
	ПроектнаяЗадачаОбъект.Владелец = ЭлементДерева.Владелец;
	ПроектнаяЗадачаОбъект.Родитель = ЭлементДерева.РодительскаяЗадача;
	
	ПроектнаяЗадачаОбъект.Предшественники.Загрузить(ЭлементДерева.Предшественники.Выгрузить());
	ПроектнаяЗадачаОбъект.Исполнители.Загрузить(ЭлементДерева.Исполнители.Выгрузить());
	
	ПроектнаяЗадачаОбъект.ТипОграничения = ЭлементДерева.ТипОграничения;
	ПроектнаяЗадачаОбъект.ДатаОграничения = ЭлементДерева.ДатаОграничения;
	
	ПроектнаяЗадачаОбъект.ДополнительныеСвойства.Вставить("ПроверитьПредшественников", Истина);
	ПроектнаяЗадачаОбъект.Записать();
	
	СтруктураДанных = Новый Структура;
	СтруктураДанных.Вставить("НачалоФакт", ЭлементДерева.НачалоФакт);
	СтруктураДанных.Вставить("ОкончаниеФакт", ЭлементДерева.ОкончаниеФакт);
	СтруктураДанных.Вставить("ДлительностьФакт", ЭлементДерева.ДлительностьФакт);
	СтруктураДанных.Вставить("ЕдиницаДлительностиФакт", ЭлементДерева.ЕдиницаДлительностиФакт);
	
	СтруктураДанных.Вставить("ТекущийПланНачало", ЭлементДерева.ТекущийПланНачало);
	СтруктураДанных.Вставить("ТекущийПланОкончание", ЭлементДерева.ТекущийПланОкончание);
	СтруктураДанных.Вставить("ТекущийПланДлительность", ЭлементДерева.ТекущийПланДлительность);
	СтруктураДанных.Вставить("ТекущийПланЕдиницаДлительности", ЭлементДерева.ТекущийПланЕдиницаДлительности);
		
	РаботаСПроектами.ЗаписатьСрокиПроектнойЗадачи(ПроектнаяЗадачаОбъект.Ссылка, СтруктураДанных);
	
	ЭлементДерева.Ссылка = ПроектнаяЗадачаОбъект.Ссылка;
	ЭлементДерева.ВерсияДанных = ПроектнаяЗадачаОбъект.ВерсияДанных;
	
	РазблокироватьСтрокуДляРедактирования(ПроектнаяЗадачаОбъект.Ссылка, ЭтаФорма.УникальныйИдентификатор);
	
	// рассчитать план
	МассивИзмененныхЗадач = Новый Массив;
	РаботаСПроектами.РассчитатьПланПроекта(ПроектнаяЗадачаОбъект.Ссылка, МассивИзмененныхЗадач);
	
	Если МассивИзмененныхЗадач.Найти(ПроектнаяЗадачаОбъект.Родитель) = Неопределено Тогда 
		МассивИзмененныхЗадач.Добавить(ПроектнаяЗадачаОбъект.Родитель);
	КонецЕсли;	
	ОбновитьИзмененныеСтрокиЗадач(МассивИзмененныхЗадач);
	
	ИсторияРаботыПользователя.Добавить(ПроектнаяЗадачаОбъект.Ссылка);
	
КонецПроцедуры	

&НаКлиентеНаСервереБезКонтекста
Функция ПолучитьДанныеДоступностиКомандПеремещения(ДеревоЗадач, ЭлементыФормы)
	
	ДеревоЗадачВверх = Истина;
	ДеревоЗадачВниз = Истина;
	ДеревоЗадачНаУровеньВыше = Истина;
	ДеревоЗадачНаУровеньНиже = Истина;
	
	ВыделенныеСтроки = ЭлементыФормы.ДеревоЗадач.ВыделенныеСтроки;
	ВыделенныеСтрокиКоличество = ВыделенныеСтроки.Количество();
	
	Для Каждого ВыделеннаяСтрока Из ВыделенныеСтроки Цикл
		СтрокаТекущая = ДеревоЗадач.НайтиПоИдентификатору(ВыделеннаяСтрока);
		СтрокаРодитель = СтрокаТекущая.ПолучитьРодителя();
		
		ДеревоЗадачНаУровеньВыше = ДеревоЗадачНаУровеньВыше 
			И (СтрокаРодитель <> Неопределено)
			И ВыделенныеСтрокиКоличество = 1
			И Не СтрокаТекущая.ПометкаУдаления;
			
		Если СтрокаРодитель = Неопределено Тогда
			СтрокаРодитель = ДеревоЗадач;
		КонецЕсли;
		
		ДеревоЗадачНаУровеньНиже = ДеревоЗадачНаУровеньНиже 
			И СтрокаРодитель.ПолучитьЭлементы().Индекс(СтрокаТекущая) > 0
			И ВыделенныеСтрокиКоличество = 1
			И Не СтрокаТекущая.ПометкаУдаления;
		
		ДеревоЗадачВверх = ДеревоЗадачВверх
			И СтрокаРодитель.ПолучитьЭлементы().Индекс(СтрокаТекущая) > 0
			И Не СтрокаТекущая.ПометкаУдаления;
			
		ДеревоЗадачВниз = ДеревоЗадачВниз
			И СтрокаРодитель.ПолучитьЭлементы().Индекс(СтрокаТекущая) < 
				СтрокаРодитель.ПолучитьЭлементы().Количество() - 1
			И Не СтрокаТекущая.ПометкаУдаления;
			
	КонецЦикла;
	
	ДанныеВозврата = Новый Структура;
	ДанныеВозврата.Вставить("ДеревоЗадачВверх", 		ДеревоЗадачВверх);
	ДанныеВозврата.Вставить("ДеревоЗадачВниз", 			ДеревоЗадачВниз);
	ДанныеВозврата.Вставить("ДеревоЗадачНаУровеньВыше", ДеревоЗадачНаУровеньВыше);
	ДанныеВозврата.Вставить("ДеревоЗадачНаУровеньНиже", ДеревоЗадачНаУровеньНиже);
		
	Возврат ДанныеВозврата;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьДоступностьКомандПеремещения(ТекущаяПроектнаяЗадача, ДеревоЗадач, Элементы)
	
	ДанныеДоступности = ПолучитьДанныеДоступностиКомандПеремещения(ДеревоЗадач, Элементы);
	
	Элементы.ДеревоЗадачВверх.Доступность = ДанныеДоступности.ДеревоЗадачВверх;
	Элементы.ДеревоЗадачКонтекстноеМенюВверх.Доступность = ДанныеДоступности.ДеревоЗадачВверх;
	Элементы.ДеревоЗадачВниз.Доступность = ДанныеДоступности.ДеревоЗадачВниз;
	Элементы.ДеревоЗадачКонтекстноеМенюВниз.Доступность = ДанныеДоступности.ДеревоЗадачВниз;
	
	Элементы.ФормаНаУровеньВыше.Доступность = ДанныеДоступности.ДеревоЗадачНаУровеньВыше;
	Элементы.ДеревоЗадачКонтекстноеМенюНаУровеньВыше.Доступность = ДанныеДоступности.ДеревоЗадачНаУровеньВыше;
	Элементы.ФормаНаУровеньНиже.Доступность = ДанныеДоступности.ДеревоЗадачНаУровеньНиже;
	Элементы.ДеревоЗадачКонтекстноеМенюНаУровеньНиже.Доступность = ДанныеДоступности.ДеревоЗадачНаУровеньНиже;
	
КонецПроцедуры

&НаСервере
Функция ПометитьНаУдалениеПроектныеЗадачи(ВыделенныеСтроки, ПометкаУдаления)
	
	Для Каждого СтрокаТаблицы Из ВыделенныеСтроки Цикл
		СтрокаТаблицыПроектнаяЗадача = ДеревоЗадач.НайтиПоИдентификатору(СтрокаТаблицы);
		Если СтрокаТаблицыПроектнаяЗадача = Неопределено Тогда 
			Продолжить;
		КонецЕсли;	
		
		ПроектнаяЗадачаСсылка = СтрокаТаблицыПроектнаяЗадача.Ссылка;
		Если ПроектнаяЗадачаСсылка.Пустая() Тогда
			Продолжить;
		КонецЕсли;	
		
		ПроектнаяЗадачаОбъект = ПроектнаяЗадачаСсылка.ПолучитьОбъект();
		ПроектнаяЗадачаОбъект.УстановитьПометкуУдаления(ПометкаУдаления);
	КонецЦикла;
	
	ЗаполнитьДеревоЗадач();
	
КонецФункции

&НаСервере
Процедура ЗаполнитьДанныеОПоследователяхСпискаЗадач(МассивПоследователей, МассивЗадач)
	
	Для Каждого Элемент Из МассивЗадач Цикл
		МассивПоследователейЗадачи = РаботаСПроектами.ПолучитьВсехПоследователей(Элемент).ВыгрузитьКолонку("Ссылка");
		Для Каждого ЭлементПоследователь Из МассивПоследователейЗадачи Цикл 
			Если МассивПоследователей.Найти(ЭлементПоследователь) = Неопределено Тогда
				МассивПоследователей.Добавить(ЭлементПоследователь);	
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;	
	
КонецПроцедуры

&НаСервере
Процедура УстановитьДоступностьПолейДереваЗадач(Режим)
	
	Если Режим = "ВСписке" Тогда 
		Элементы.Состояние.ТолькоПросмотр = Истина;
		Элементы.ТрудозатратыФактСтр.ТолькоПросмотр = Истина;
		Элементы.ДеревоЗадачИзменить.Видимость = Ложь;
		Элементы.ДеревоЗадачРедактироватьВСписке.Пометка = Истина;
		Элементы.ДеревоЗадачРедактироватьВКарточке.Пометка = Ложь;
	Иначе
		Элементы.Состояние.ТолькоПросмотр = Ложь;
		Элементы.ТрудозатратыФактСтр.ТолькоПросмотр = Ложь;
		Элементы.ДеревоЗадачИзменить.Видимость = Истина;
		Элементы.ДеревоЗадачРедактироватьВСписке.Пометка = Ложь;
		Элементы.ДеревоЗадачРедактироватьВКарточке.Пометка = Истина;
	КонецЕсли;	
	
КонецПроцедуры	

&НаКлиенте
Функция ПолучитьЧисловуюЧасть(Знач Строка) 
	
	Строка = СокрЛП(Строка);
	Если ПустаяСтрока(Строка) Тогда 
		Возврат "0";
	КонецЕсли;	
	
	ЧисловаяЧасть = "";
	
	ДопустимыеСимволы = "0123456789., ";
	Для Инд = 1 По СтрДлина(Строка) Цикл
		Символ = Сред(Строка,Инд,1);
		Если Найти(ДопустимыеСимволы, Символ) > 0 Тогда 
			ЧисловаяЧасть = ЧисловаяЧасть + Символ;
		Иначе	
			Прервать;
		КонецЕсли;	
	КонецЦикла;	
	
	ЧисловаяЧасть = СтрЗаменить(ЧисловаяЧасть, " ", "");
	
	Возврат ЧисловаяЧасть;
	
КонецФункции	

&НаСервере
Функция СвязатьЗадачиСервер()
	
	ТекущаяСтрока = Элементы.ДеревоЗадач.ТекущаяСтрока;
	Последователь = ДеревоЗадач.НайтиПоИдентификатору(ТекущаяСтрока).Ссылка;
	
	Предшественники = Новый Массив;
	Для Каждого Строка Из Элементы.ДеревоЗадач.ВыделенныеСтроки Цикл
		Предшественник = ДеревоЗадач.НайтиПоИдентификатору(Строка).Ссылка;
		Если Предшественник <> Последователь Тогда 
			Предшественники.Добавить(Предшественник);
		КонецЕсли;	
	КонецЦикла;	
	
	СвязьУстановлена = Ложь;
	ОбъектПоследователь = Последователь.ПолучитьОбъект();
	Для Каждого Предшественник Из Предшественники Цикл
		Если ОбъектПоследователь.Предшественники.Найти(Предшественник, "Предшественник") = Неопределено Тогда 
			НоваяСтрока = ОбъектПоследователь.Предшественники.Добавить();
			НоваяСтрока.Предшественник = Предшественник;
			НоваяСтрока.ТипЗависимости = Перечисления.ТипыЗависимостейПроектныхЗадач.ОкончаниеНачало;
			СвязьУстановлена = Истина;
		КонецЕсли;	
	КонецЦикла;	
	
	Если СвязьУстановлена Тогда 
		ОбъектПоследователь.ДополнительныеСвойства.Вставить("ПроверитьПредшественников", Истина);
		ОбъектПоследователь.Записать();
		
		МассивИзмененныхЗадач = Новый Массив;
		РаботаСПроектами.РассчитатьПланПроекта(Последователь, МассивИзмененныхЗадач);
		ОбновитьИзмененныеСтрокиЗадач(МассивИзмененныхЗадач);
		
		Возврат Истина;
	Иначе	
		Возврат Ложь;
	КонецЕсли;	
	
КонецФункции

&НаСервере
Процедура НаУровеньНижеСервер(Ссылка, Родитель)
	
	// Вычисляем наличие соседней задачи, в которую помещается текущая задача
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПроектныеЗадачи.Ссылка
		|ИЗ
		|	Справочник.ПроектныеЗадачи КАК ПроектныеЗадачи
		|ГДЕ
		|	ПроектныеЗадачи.Родитель = &Родитель
		|	И ПроектныеЗадачи.Владелец = &Проект
		|	И НЕ ПроектныеЗадачи.ПометкаУдаления
		|	И ПроектныеЗадачи.НомерЗадачиВУровне = &НомерВУровне - 1";
		
	Запрос.УстановитьПараметр("Родитель", Родитель);
	Запрос.УстановитьПараметр("НомерВУровне", Ссылка.НомерЗадачиВУровне);
	Запрос.УстановитьПараметр("Проект", Ссылка.Владелец);
	
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	Выборка = Результат.Выбрать();
	Если Выборка.Следующий() Тогда
		ОбъектЗадача = Ссылка.ПолучитьОбъект();
		РодительЗадачи = ОбъектЗадача.Родитель;
		ОбъектЗадача.Родитель = Выборка.Ссылка;
		ОбъектЗадача.НомерЗадачиВУровне = РаботаСПроектами.ПолучитьМаксимальныйНомерЗадачиУровня(
			Ссылка.Владелец,
			ОбъектЗадача.Родитель) + 1;
		ОбъектЗадача.ДополнительныеСвойства.Вставить("ПроверитьПредшественников", Истина);
		ОбъектЗадача.Записать();
		
		РаботаСПроектами.ЗаполнитьКодыСДРПроектныхЗадач(Ссылка.Владелец);

		Если ЗначениеЗаполнено(РодительЗадачи) Тогда 
			РаботаСПроектами.РассчитатьПланПроекта(РодительЗадачи);
		КонецЕсли;	
		Если ЗначениеЗаполнено(ОбъектЗадача.Родитель) Тогда 
			РаботаСПроектами.РассчитатьПланПроекта(ОбъектЗадача.Родитель);
		КонецЕсли;
		
		ЗаполнитьДеревоЗадач();
		СписокРаскрытыхЗадач.Добавить(ОбъектЗадача.Родитель);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура НаУровеньВышеСервер(Ссылка, Родитель)
	
	// Выбор всех задачи одного с родителем уровня и сдвигаем номера на один вниз
	МассивЗадачУровняРодителя = 
		РаботаСПроектами.ПолучитьМассивЗадачОдногоУровняСУказанной(Ссылка.Владелец, Родитель);
		
	// Подготовка номера для вставки подчиненной задачи на один уровень вверх
	КоличествоЭлементов = МассивЗадачУровняРодителя.Количество(); 
	Для Счетчик = 0 по КоличествоЭлементов - 1 Цикл
		Индекс = КоличествоЭлементов - Счетчик - 1;
		Задача = МассивЗадачУровняРодителя[Индекс];
		Если Задача.НомерЗадачиВУровне > Родитель.НомерЗадачиВУровне Тогда
			ЗадачаОбъект = Задача.ПолучитьОбъект();
			ЗадачаОбъект.НомерЗадачиВУровне = ЗадачаОбъект.НомерЗадачиВУровне + 1;
		КонецЕсли;
	КонецЦикла;
		
	ЗадачаОбъект = Ссылка.ПолучитьОбъект();
	РодительЗадачи = ЗадачаОбъект.Родитель;
	ЗадачаОбъект.Родитель = Родитель.Родитель;
	ЗадачаОбъект.НомерЗадачиВУровне = Родитель.НомерЗадачиВУровне + 1;
	ЗадачаОбъект.ДополнительныеСвойства.Вставить("ПроверитьПредшественников", Истина);
	ЗадачаОбъект.Записать();
		
	РаботаСПроектами.ЗаполнитьКодыСДРПроектныхЗадач(Ссылка.Владелец);
	
	Если ЗначениеЗаполнено(РодительЗадачи) Тогда 
		РаботаСПроектами.РассчитатьПланПроекта(РодительЗадачи);
	КонецЕсли;	
	Если ЗначениеЗаполнено(ЗадачаОбъект.Родитель) Тогда 
		РаботаСПроектами.РассчитатьПланПроекта(ЗадачаОбъект.Родитель);
	КонецЕсли;
	
	ЗаполнитьДеревоЗадач();
	СписокРаскрытыхЗадач.Добавить(ЗадачаОбъект.Родитель);
	
КонецПроцедуры

&НаСервере
Процедура ВверхСервер()
	
	УстановитьПривилегированныйРежим(Истина);
	
	МассивСсылок = Новый Массив;
	Для Каждого ВыделеннаяСтрока Из Элементы.ДеревоЗадач.ВыделенныеСтроки Цикл
		Ссылка = ДеревоЗадач.НайтиПоИдентификатору(ВыделеннаяСтрока).Ссылка;
		Индекс = Неопределено;
		Для Каждого Элемент Из МассивСсылок Цикл
			Если Элемент.НомерЗадачиВУровне > Ссылка.НомерЗадачиВУровне Тогда
				Индекс = МассивСсылок.Найти(Элемент);
				Прервать;
			КонецЕсли;
		КонецЦикла;
		Если Индекс = Неопределено Тогда
			МассивСсылок.Добавить(Ссылка);
		Иначе
			МассивСсылок.Вставить(Индекс, Ссылка); 
		КонецЕсли;	
	КонецЦикла;
	
	Для Каждого Ссылка Из МассивСсылок Цикл
		// Получение задачи с номером в уровне, меньшим на 1, чем текущий
		Запрос = Новый Запрос;
		Запрос.Текст =
			"ВЫБРАТЬ
			|	ПроектныеЗадачи.Ссылка
			|ИЗ
			|	Справочник.ПроектныеЗадачи КАК ПроектныеЗадачи
			|ГДЕ
			|	ПроектныеЗадачи.Владелец = &Владелец
			|	И ПроектныеЗадачи.Родитель = &Родитель
			|	И Не ПометкаУдаления
			|	И ПроектныеЗадачи.НомерЗадачиВУровне = &НомерЗадачиВУровне";
		Запрос.УстановитьПараметр("Владелец", Ссылка.Владелец);
		Запрос.УстановитьПараметр("Родитель", Ссылка.Родитель);
		Запрос.УстановитьПараметр("НомерЗадачиВУровне", Ссылка.НомерЗадачиВУровне - 1);
		
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			
			// сдвиг строк в дереве
			Идентификатор = -1;
			РаботаСПроектамиКлиентСервер.НайтиЗадачуВДеревеПоСсылке(
				ДеревоЗадач.ПолучитьЭлементы(),
				Ссылка,
				Идентификатор);
				
			СтрокаТекущейЗадачи = ДеревоЗадач.НайтиПоИдентификатору(Идентификатор);
			СтрокаРодительскойЗадачи = СтрокаТекущейЗадачи.ПолучитьРодителя();
			Если СтрокаРодительскойЗадачи = Неопределено Тогда
				СтрокаРодительскойЗадачи = ДеревоЗадач;	
			КонецЕсли;
			
			ИндексТекущейЗадачи = СтрокаРодительскойЗадачи.ПолучитьЭлементы().Индекс(СтрокаТекущейЗадачи);
			СтрокаРодительскойЗадачи.ПолучитьЭлементы().Сдвинуть(ИндексТекущейЗадачи, -1);
			
			// изменение реквизитов объектов
			ТекущаяЗадачаОбъект = Ссылка.ПолучитьОбъект();
			ПредыдущаяЗадачаОбъект = Выборка.Ссылка.ПолучитьОбъект();
			
			Буфер = ТекущаяЗадачаОбъект.НомерЗадачиВУровне;
			ТекущаяЗадачаОбъект.НомерЗадачиВУровне = ПредыдущаяЗадачаОбъект.НомерЗадачиВУровне;
			ПредыдущаяЗадачаОбъект.НомерЗадачиВУровне = Буфер;

			БуферСДР = ТекущаяЗадачаОбъект.КодСДР;
			ТекущаяЗадачаОбъект.КодСДР = ПредыдущаяЗадачаОбъект.КодСДР;
			ПредыдущаяЗадачаОбъект.КодСДР = БуферСДР;
			
			Если ТекущаяЗадачаОбъект.Предшественники.Количество() = 1 Тогда 
				НайденнаяСтрока = ТекущаяЗадачаОбъект.Предшественники.Найти(
					ПредыдущаяЗадачаОбъект.Ссылка, "Предшественник");
				Если НайденнаяСтрока <> Неопределено Тогда 
					Буфер = Новый Структура("Предшественник, ТипЗависимости, Задержка, ЕдиницаЗадержки");
					ЗаполнитьЗначенияСвойств(Буфер, НайденнаяСтрока);
					
					ТекущаяЗадачаОбъект.Предшественники.Загрузить(
						ПредыдущаяЗадачаОбъект.Предшественники.Выгрузить());
					
					ПредыдущаяЗадачаОбъект.Предшественники.Очистить();
					НоваяСтрока = ПредыдущаяЗадачаОбъект.Предшественники.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяСтрока, Буфер);
					НоваяСтрока.Предшественник = ТекущаяЗадачаОбъект.Ссылка;
				КонецЕсли;	
			КонецЕсли;
			
			ТекущаяЗадачаОбъект.Записать();
			ПредыдущаяЗадачаОбъект.Записать();
			
			ИзмененныеЗадачи = Новый Массив;
			ИзмененныеЗадачи.Добавить(ТекущаяЗадачаОбъект.Ссылка);
			ИзмененныеЗадачи.Добавить(ПредыдущаяЗадачаОбъект.Ссылка);
			
			// заполнение кодов СДР
			РаботаСПроектами.ЗаполнитьКодыСДРПодчиненныхЗадач(ТекущаяЗадачаОбъект.Ссылка, ИзмененныеЗадачи);
			РаботаСПроектами.ЗаполнитьКодыСДРПодчиненныхЗадач(ПредыдущаяЗадачаОбъект.Ссылка, ИзмененныеЗадачи);
			
			// обновление последователей
			ЗаполнитьДанныеОПоследователяхСпискаЗадач(ИзмененныеЗадачи, ИзмененныеЗадачи);
			ОбновитьИзмененныеСтрокиЗадач(ИзмененныеЗадачи);
			
			СписокРаскрытыхЗадач.Добавить(ТекущаяПроектнаяЗадача.Родитель);
 		КонецЕсли;
	КонецЦикла;
	
	Если МассивСсылок.Количество() > 0 Тогда 
		РаботаСПроектами.ЗаполнитьПорядокПроектныхЗадач(Проект);
	КонецЕсли;
	
	УстановитьДоступностьКомандПеремещения(ТекущаяПроектнаяЗадача, ДеревоЗадач, Элементы);
	 
КонецПроцедуры

&НаСервере
Процедура ВнизСервер()
	
	МассивСсылок = Новый Массив;
	Для Каждого ВыделеннаяСтрока Из Элементы.ДеревоЗадач.ВыделенныеСтроки Цикл
		Ссылка = ДеревоЗадач.НайтиПоИдентификатору(ВыделеннаяСтрока).Ссылка;
		Индекс = Неопределено;
		Для Каждого Элемент Из МассивСсылок Цикл
			Если Элемент.НомерЗадачиВУровне < Ссылка.НомерЗадачиВУровне Тогда
				Индекс = МассивСсылок.Найти(Элемент);
				Прервать;
			КонецЕсли;
		КонецЦикла;
		Если Индекс = Неопределено Тогда
			МассивСсылок.Добавить(Ссылка);
		Иначе
			МассивСсылок.Вставить(Индекс, Ссылка); 
		КонецЕсли;	
	КонецЦикла;
	
	Для Каждого Ссылка Из МассивСсылок Цикл
		// Получение задачи с номером в уровне, меньшим на 1, чем текущий
		Запрос = Новый Запрос;
		Запрос.Текст =
			"ВЫБРАТЬ
			|	ПроектныеЗадачи.Ссылка
			|ИЗ
			|	Справочник.ПроектныеЗадачи КАК ПроектныеЗадачи
			|ГДЕ
			|	ПроектныеЗадачи.Владелец = &Владелец
			|	И ПроектныеЗадачи.Родитель = &Родитель
			|	И Не ПометкаУдаления
			|	И ПроектныеЗадачи.НомерЗадачиВУровне = &НомерЗадачиВУровне";
			
		Запрос.УстановитьПараметр("Владелец", Ссылка.Владелец);
		Запрос.УстановитьПараметр("Родитель", Ссылка.Родитель);
		Запрос.УстановитьПараметр("НомерЗадачиВУровне", Ссылка.НомерЗадачиВУровне + 1);
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			
			// сдвиг строк в дереве	
			Идентификатор = -1;
			РаботаСПроектамиКлиентСервер.НайтиЗадачуВДеревеПоСсылке(
				ДеревоЗадач.ПолучитьЭлементы(),
				Ссылка,
				Идентификатор);
			СтрокаТекущейЗадачи = ДеревоЗадач.НайтиПоИдентификатору(Идентификатор);
			СтрокаРодительскойЗадачи = СтрокаТекущейЗадачи.ПолучитьРодителя();
			Если СтрокаРодительскойЗадачи = Неопределено Тогда
				СтрокаРодительскойЗадачи = ДеревоЗадач;	
			КонецЕсли;
			ИндексТекущейЗадачи = СтрокаРодительскойЗадачи.ПолучитьЭлементы().Индекс(СтрокаТекущейЗадачи);
			СтрокаРодительскойЗадачи.ПолучитьЭлементы().Сдвинуть(ИндексТекущейЗадачи, 1);
			
			// изменение реквизитов объектов
			ТекущаяЗадачаОбъект = Ссылка.ПолучитьОбъект();
			СледующаяЗадачаОбъект = Выборка.Ссылка.ПолучитьОбъект();
			
			Буфер = ТекущаяЗадачаОбъект.НомерЗадачиВУровне;
			ТекущаяЗадачаОбъект.НомерЗадачиВУровне = СледующаяЗадачаОбъект.НомерЗадачиВУровне;
			СледующаяЗадачаОбъект.НомерЗадачиВУровне = Буфер;

			БуферСДР = ТекущаяЗадачаОбъект.КодСДР;
			ТекущаяЗадачаОбъект.КодСДР = СледующаяЗадачаОбъект.КодСДР;
			СледующаяЗадачаОбъект.КодСДР = БуферСДР;
			
			Если СледующаяЗадачаОбъект.Предшественники.Количество() = 1 Тогда 
				НайденнаяСтрока = СледующаяЗадачаОбъект.Предшественники.Найти(ТекущаяЗадачаОбъект.Ссылка, "Предшественник");
				Если НайденнаяСтрока <> Неопределено Тогда 
					Буфер = Новый Структура("Предшественник, ТипЗависимости, Задержка, ЕдиницаЗадержки");
					ЗаполнитьЗначенияСвойств(Буфер, НайденнаяСтрока);
					
					СледующаяЗадачаОбъект.Предшественники.Загрузить(ТекущаяЗадачаОбъект.Предшественники.Выгрузить());
					
					ТекущаяЗадачаОбъект.Предшественники.Очистить();
					НоваяСтрока = ТекущаяЗадачаОбъект.Предшественники.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяСтрока, Буфер);
					НоваяСтрока.Предшественник = СледующаяЗадачаОбъект.Ссылка;
				КонецЕсли;	
			КонецЕсли;
			
			ТекущаяЗадачаОбъект.Записать();
			СледующаяЗадачаОбъект.Записать();
			
			ИзмененныеЗадачи = Новый Массив;
			ИзмененныеЗадачи.Добавить(ТекущаяЗадачаОбъект.Ссылка);
			ИзмененныеЗадачи.Добавить(СледующаяЗадачаОбъект.Ссылка);
			
			// заполнение кодов СДР
			РаботаСПроектами.ЗаполнитьКодыСДРПодчиненныхЗадач(ТекущаяЗадачаОбъект.Ссылка, ИзмененныеЗадачи);
			РаботаСПроектами.ЗаполнитьКодыСДРПодчиненныхЗадач(СледующаяЗадачаОбъект.Ссылка, ИзмененныеЗадачи);
			
			// обновление последователей
			ЗаполнитьДанныеОПоследователяхСпискаЗадач(ИзмененныеЗадачи, ИзмененныеЗадачи);
			ОбновитьИзмененныеСтрокиЗадач(ИзмененныеЗадачи);
			
			СписокРаскрытыхЗадач.Добавить(ТекущаяПроектнаяЗадача.Родитель);		
		КонецЕсли;
	КонецЦикла;
	
	Если МассивСсылок.Количество() > 0 Тогда 
		РаботаСПроектами.ЗаполнитьПорядокПроектныхЗадач(Проект);
	КонецЕсли;

	УстановитьДоступностьКомандПеремещения(ТекущаяПроектнаяЗадача, ДеревоЗадач, Элементы);
	
КонецПроцедуры

&НаСервере
Процедура ДеревоЗадачПеретаскиваниеСервер(СсылкаИсточник, СсылкаПриемник)
	
	НачатьТранзакцию();
	Попытка
		
		ОбъектИсточник = СсылкаИсточник.ПолучитьОбъект();
		СтарыйРодительИсточника = ОбъектИсточник.Родитель;
		
		НовыйРодительИсточника = СсылкаПриемник;
		ОбъектИсточник.Родитель = НовыйРодительИсточника;
		
		ОбъектИсточник.НомерЗадачиВУровне = РаботаСПроектами.ПолучитьМаксимальныйНомерЗадачиУровня(
			ОбъектИсточник.Владелец,
			ОбъектИсточник.Родитель) + 1;
		ОбъектИсточник.ДополнительныеСвойства.Вставить("ПроверитьПредшественников", Истина);
		ОбъектИсточник.Записать();
		
		Если ЗначениеЗаполнено(СтарыйРодительИсточника) Тогда 
			РаботаСПроектами.РассчитатьПланПроекта(СтарыйРодительИсточника);
		КонецЕсли;	
		РаботаСПроектами.РассчитатьПланПроекта(СсылкаИсточник);
		
		РаботаСПроектами.ЗаполнитьКодыСДРПроектныхЗадач(ОбъектИсточник.Владелец);
		
		ЗаполнитьДеревоЗадач();
		СписокРаскрытыхЗадач.Добавить(СсылкаПриемник);
		
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		ВызватьИсключение;
	КонецПопытки;
	
КонецПроцедуры

&НаКлиенте
Функция ВыделитьТекстДляАвтоподбораПредшественника(Знач Текст)
	
	Текст = СокрЛП(Текст);
	
	ПозРазделителя = 0;
	Для Инд = 1 По СтрДлина(Текст) Цикл
		Символ = Сред(Текст, Инд, 1);
		Если Найти("0123456789. ", Символ) > 0 Тогда 
			Продолжить;
		ИначеЕсли Символ = "," Тогда 
			ПозРазделителя = Инд;
			Продолжить;
		Иначе	
			Возврат СокрЛП(Сред(Текст, ПозРазделителя+1));
		КонецЕсли;
	КонецЦикла;	
	
	Если ПозРазделителя = 0 Тогда 
		Возврат Текст;
	Иначе
		Возврат СокрЛП(Сред(Текст, ПозРазделителя+1));
	КонецЕсли;	
	
КонецФункции

&НаСервереБезКонтекста
Процедура ВыполнитьУдалениеПредшественников(КодыПредшественников, Предшественники)
	
	КоличествоСтрок = Предшественники.Количество();
	Для Инд = 1 по КоличествоСтрок Цикл
		Строка = Предшественники[КоличествоСтрок-Инд];
		Если КодыПредшественников.Найти(Строка.Предшественник.КодСДР) = Неопределено Тогда 
			Предшественники.Удалить(Строка);
		КонецЕсли;	
	КонецЦикла;	
	
КонецПроцедуры	

&НаСервереБезКонтекста
Процедура ВыполнитьУдалениеИсполнителей(ИменаИсполнителей, Исполнители)
	
	КоличествоСтрок = Исполнители.Количество();
	Для Инд = 1 по КоличествоСтрок Цикл
		Строка = Исполнители[КоличествоСтрок-Инд];
		Если ИменаИсполнителей.Найти(Строка.Исполнитель.Наименование) = Неопределено Тогда 
			Исполнители.Удалить(Строка);
		КонецЕсли;	
	КонецЦикла;	
	
КонецПроцедуры	

&НаКлиенте
Функция ВыделитьТекстДляАвтоподбораИсполнителя(Знач Текст)
	
	Текст = СокрЛП(Текст);
	
	ПозРазделителя = 0;
	Для Инд = 1 По СтрДлина(Текст) Цикл
		Символ = Сред(Текст, Инд, 1);
		Если Символ = "," Тогда 
			ПозРазделителя = Инд;
		КонецЕсли;
	КонецЦикла;	
	
	Если ПозРазделителя = 0 Тогда 
		Возврат Текст;
	Иначе
		Возврат СокрЛП(Сред(Текст, ПозРазделителя+1));
	КонецЕсли;	
	
КонецФункции

&НаСервереБезКонтекста
Функция ПоместитьИсполнителейВоВременноеХранилище(Исполнители, УникальныйИдентификатор)

	Возврат ПоместитьВоВременноеХранилище(
		Исполнители.Выгрузить().ВыгрузитьКолонку("Исполнитель"),
		УникальныйИдентификатор);
	
КонецФункции

&НаСервереБезКонтекста
Функция ЗагрузитьИсполнителейИзВременногоХранилища(АдресВременногоХранилища)
	
	Возврат ПолучитьИзВременногоХранилища(АдресВременногоХранилища);
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура ПоказатьСкрытьКнопкуОчисткиОтбора(Поле, Значение)
	
	Если ЗначениеЗаполнено(Значение) И Значение <> "Все" Тогда 
		Поле.КнопкаОчистки = Истина;
		#Если Клиент Тогда
			Поле.ЦветФона = ОбщегоНазначенияКлиент.ЦветСтиля("ФонУправляющегоПоля");
		#Иначе
			Поле.ЦветФона = ЦветаСтиля["ФонУправляющегоПоля"];
		#КонецЕсли	
	Иначе 
		Поле.КнопкаОчистки = Ложь;
		Поле.ЦветФона = Новый Цвет();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьУровень(Уровень, Знач ТекущийУровень = 0, СтрокаРодитель = Неопределено)
	
	Если СтрокаРодитель = Неопределено Тогда 
		СтрокаРодитель = ДеревоЗадач;
	КонецЕсли;	
	
	ТекущийУровень = ТекущийУровень + 1;
	
	Если Уровень <= ТекущийУровень Тогда 
		Для Каждого Строка Из СтрокаРодитель.ПолучитьЭлементы() Цикл
			Элементы.ДеревоЗадач.Свернуть(Строка.ПолучитьИдентификатор());
		КонецЦикла;	
	Иначе
		Для Каждого Строка Из СтрокаРодитель.ПолучитьЭлементы() Цикл
			Элементы.ДеревоЗадач.Развернуть(Строка.ПолучитьИдентификатор());
			ПоказатьУровень(Уровень, ТекущийУровень, Строка);
		КонецЦикла;	
	КонецЕсли;
	
КонецПроцедуры	

#КонецОбласти

#Область СлужебныеПроцедурыИФункции_Хронометраж

&НаСервере
Процедура ПереключитьХронометражСервер(ПараметрыОповещения) Экспорт
	
	Если ТекущаяПроектнаяЗадача = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	УчетВремени.ПереключитьХронометражСервер(
		ПараметрыОповещения,
		ДатаНачалаХронометража,
		ДатаКонцаХронометража,
		ВключенХронометраж,
		ТекущаяПроектнаяЗадача,
		ВидыРабот,
		ЭтаФорма.Команды.ПереключитьХронометраж,
		ЭтаФорма.Элементы.ПереключитьХронометраж);
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьВОтчетИОбновитьФорму(ПараметрыОтчета, ПараметрыОповещения) Экспорт
	
	УчетВремени.ДобавитьВОтчетИОбновитьФорму(
	    ПараметрыОтчета, 
		ПараметрыОповещения,
		ДатаНачалаХронометража,
		ДатаКонцаХронометража,
		ВключенХронометраж,
		ЭтаФорма.Команды.ПереключитьХронометраж,
		ЭтаФорма.Элементы.ПереключитьХронометраж);
	
КонецПроцедуры

&НаСервере
Процедура ОтключитьХронометражСервер() Экспорт
	
	Если ТекущаяПроектнаяЗадача = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	УчетВремени.ОтключитьХронометражСервер(
		ДатаНачалаХронометража,
		ДатаКонцаХронометража,
		ВключенХронометраж,
		ТекущаяПроектнаяЗадача,
		ЭтаФорма.Команды.ПереключитьХронометраж,
		ЭтаФорма.Элементы.ПереключитьХронометраж);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьПараметрыУчетаВремениВФорме()
	
	ТекущиеДанные = Элементы.ДеревоЗадач.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда 
		Элементы.ПереключитьХронометраж.Доступность = Ложь;
		Элементы.УказатьТрудозатраты.Доступность = Ложь;
		Возврат;
	КонецЕсли;
	ТекущаяПроектнаяЗадача = ТекущиеДанные.Ссылка;
	
	ПараметрыУчетаВремени = ПолучитьПараметрыУчетаВремени(ТекущиеДанные.Ссылка);
	
	ДатаНачалаХронометража = ПараметрыУчетаВремени.ДатаНачалаХронометража;
	ДатаКонцаХронометража = ПараметрыУчетаВремени.ДатаКонцаХронометража;
	ВключенХронометраж = ПараметрыУчетаВремени.ВключенХронометраж;
	ОпцияИспользоватьУчетВремени = ПараметрыУчетаВремени.ОпцияИспользоватьУчетВремени;
	ВидыРабот = ПараметрыУчетаВремени.ВидыРабот;
	СпособУказанияВремени = ПараметрыУчетаВремени.СпособУказанияВремени;
	
	Для Каждого СвойствоЭлемента Из ПараметрыУчетаВремени.ПереключитьХронометраж Цикл
		Элементы.ПереключитьХронометраж[СвойствоЭлемента.Ключ] = СвойствоЭлемента.Значение;
	КонецЦикла;
	
	Для Каждого СвойствоЭлемента Из ПараметрыУчетаВремени.УказатьТрудозатраты Цикл
		Элементы.УказатьТрудозатраты[СвойствоЭлемента.Ключ] = СвойствоЭлемента.Значение;
	КонецЦикла;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьПараметрыУчетаВремени(Документ)
	
	Результат = Новый Структура;
	
	ДатаНачалаХронометража = Неопределено;
	ДатаКонцаХронометража = Неопределено;
	ВключенХронометраж = Неопределено;
	ОпцияИспользоватьУчетВремени = Неопределено;
	ВидыРабот = Неопределено;
	СпособУказанияВремени = Неопределено;
	
	ПереключитьХронометражНеМеняяПодсказку = Новый Структура("Имя, Подсказка");
	
	ПереключитьХронометраж = Новый Структура("Доступность, Пометка, Видимость");
	ПереключитьХронометраж.Доступность = Истина;
	
	УказатьТрудозатраты = Новый Структура("Доступность");
	УказатьТрудозатраты.Доступность = Истина;
	
	УчетВремени.ПроинициализироватьПараметрыУчетаВремени(
		ДатаНачалаХронометража,
		ДатаКонцаХронометража,
		ВключенХронометраж,
		ОпцияИспользоватьУчетВремени,
		Документ,
		ВидыРабот,
		СпособУказанияВремени,
		ПереключитьХронометражНеМеняяПодсказку,
		ПереключитьХронометраж,
		УказатьТрудозатраты);
		
	Результат.Вставить("ПереключитьХронометраж", Новый Соответствие);
	Результат.Вставить("УказатьТрудозатраты", Новый Соответствие);
	
	Результат.ПереключитьХронометраж.Вставить(
		"Доступность",
		ПереключитьХронометраж.Доступность);
	Результат.ПереключитьХронометраж.Вставить(
		"Пометка",
		ПереключитьХронометраж.Пометка);
	Результат.УказатьТрудозатраты.Вставить(
		"Доступность",
		УказатьТрудозатраты.Доступность);
	
	Результат.Вставить("ДатаНачалаХронометража", ДатаНачалаХронометража);
	Результат.Вставить("ДатаКонцаХронометража", ДатаКонцаХронометража);
	Результат.Вставить("ВключенХронометраж", ВключенХронометраж);
	Результат.Вставить("ОпцияИспользоватьУчетВремени", ОпцияИспользоватьУчетВремени);
	Результат.Вставить("ВидыРабот", ВидыРабот);
	Результат.Вставить("СпособУказанияВремени", СпособУказанияВремени);
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти
