
////////////////////////////////////////////////////////////////////////////////
// Обработчики событий формы
//

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	РодительПерсональныхГруппДоступа = Справочники.ГруппыДоступа.РодительПерсональныхГруппДоступа(Истина);
	УпрощенныйИнтерфейсНастройкиПравДоступа = УправлениеДоступомПереопределяемый.УпрощенныйИнтерфейсНастройкиПравДоступа();
	Если УпрощенныйИнтерфейсНастройкиПравДоступа Тогда
		Элементы.ФормаСоздать.Видимость = Ложь;
		Элементы.ФормаСкопировать.Видимость = Ложь;
		Элементы.СписокКонтекстноеМенюСоздать.Видимость = Ложь;
		Элементы.СписокКонтекстноеМенюСкопировать.Видимость = Ложь;
	КонецЕсли;
	
	Список.Параметры.УстановитьЗначениеПараметра("Профиль", Параметры.Профиль);
	Если ЗначениеЗаполнено(Параметры.Профиль) Тогда
		Элементы.Профиль.Видимость = Ложь;
		Элементы.Список.Отображение = ОтображениеТаблицы.Список;
		Автозаголовок = Ложь;
		Заголовок = УправлениеДоступомДокументооборот.ЗаголовокПодчиненнойФормы(
			НСтр("ru = 'Группы доступа: %1 (%2)'; en = 'Group access: %1 (%2)'"), Параметры.Профиль);
		Элементы.ФормаСоздатьГруппу.Видимость = Ложь;
		Элементы.СписокКонтекстноеМенюСоздатьГруппу.Видимость = Ложь;
	КонецЕсли;
	
	Если НЕ ПравоДоступа("Чтение", Метаданные.Справочники.ПрофилиГруппДоступа) Тогда
		Элементы.Профиль.Видимость = Ложь;
	КонецЕсли;
	
	Если ПравоДоступа("Просмотр", Метаданные.Справочники.ВнешниеПользователи) Тогда
		ФрагментПолученияПредставленияВнешнегоПользователя = "ЕстьNULL(ВЫРАЗИТЬ(ГруппыДоступа.Пользователь КАК Справочник.ВнешниеПользователи).Наименование, &ОшибкаОбъектНеНайден)";
		Список.ТекстЗапроса = СтрЗаменить(Список.ТекстЗапроса, "&ОшибкаОбъектНеНайден", ФрагментПолученияПредставленияВнешнегоПользователя);
	КонецЕсли;
	
	Список.КомпоновщикНастроек.Настройки.Отбор.Элементы[0].Использование = 
		НЕ ПользователиСерверПовтИсп.ЭтоПолноправныйПользовательИБ();
	
	Список.Параметры.УстановитьЗначениеПараметра("РежимВыбора",            Параметры.РежимВыбора);
	Список.Параметры.УстановитьЗначениеПараметра("РежимВыбораТолькоГрупп", Параметры.ВыборГруппИЭлементов = ИспользованиеГруппИЭлементов.Группы);
	Список.Параметры.УстановитьЗначениеПараметра("ОшибкаОбъектНеНайден",   НСтр("ru = '<Объект не найден>'; en = '<Объект не найден>'"));
	
	Если Параметры.РежимВыбора Тогда
		
		// Отбор не помеченных на удаление
		Список.КомпоновщикНастроек.Настройки.Отбор.Элементы[1].Использование = Истина;
		
		Элементы.Список.РежимВыбора = Истина;
		Элементы.Список.ВыборГруппИЭлементов = Параметры.ВыборГруппИЭлементов;
		Если Параметры.ВыборГруппИЭлементов = ИспользованиеГруппИЭлементов.Группы Тогда
			Элементы.Список.Отображение = ОтображениеТаблицы.Дерево;
			Элементы.Список.НачальноеОтображениеДерева = НачальноеОтображениеДерева.РаскрыватьВерхнийУровень;
		КонецЕсли;
		
		АвтоЗаголовок = Ложь;
		Если Параметры.ЗакрыватьПриВыборе = Ложь Тогда
			// Режим подбора
			Элементы.Список.МножественныйВыбор = Истина;
			Элементы.Список.РежимВыделения = РежимВыделенияТаблицы.Множественный;
			
			Заголовок = НСтр("ru = 'Подбор групп доступа'; en = 'Selection of access groups'");
		Иначе
			Заголовок = НСтр("ru = 'Выбор группы доступа'; en = 'Select access groups'");
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Обработчики событий команд и элементов формы
//

&НаКлиенте
Процедура СписокПриАктивизацииСтроки(Элемент)
	
	Если Элементы.Список.ТекущиеДанные <> Неопределено Тогда
		ПереносДопустим = НЕ ЗначениеЗаполнено(Элементы.Список.ТекущиеДанные.Пользователь)
		                  И Элементы.Список.ТекущиеДанные.Ссылка <> РодительПерсональныхГруппДоступа;
		Если Элементы.Найти("ФормаПеренестиЭлемент") <> Неопределено Тогда
			Элементы.ФормаПеренестиЭлемент.Доступность = ПереносДопустим;
		КонецЕсли;
		Если Элементы.Найти("СписокКонтекстноеМенюПеренестиЭлемент") <> Неопределено Тогда
			Элементы.СписокКонтекстноеМенюПеренестиЭлемент.Доступность = ПереносДопустим;
		КонецЕсли;
		Если Элементы.Найти("СписокПеренестиЭлемент") <> Неопределено Тогда
			Элементы.СписокПеренестиЭлемент.Доступность = ПереносДопустим;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокВыборЗначения(Элемент, Значение, СтандартнаяОбработка)
	
	Если Значение = РодительПерсональныхГруппДоступа Тогда
		СтандартнаяОбработка = Ложь;
		ПоказатьПредупреждение(,НСтр("ru = 'Эта группа только для персональных групп доступа.'; en = 'This group only for personal access groups.'"));
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	
	Если Родитель = РодительПерсональныхГруппДоступа Тогда
		
		Если Группа Тогда
			ПоказатьПредупреждение(,НСтр("ru = 'В этой группе не используются подгруппы.'; en = 'In this group are not used by the subgroup.'"));
		ИначеЕсли УпрощенныйИнтерфейсНастройкиПравДоступа Тогда
			ПоказатьПредупреждение(,НСтр("ru = 'Персональные группы доступа
			                          |создаются только в форме ""Права доступа"".'; en = 'Personal access groups are only created in the form of ""access rights"".'"));
		Иначе
			ПоказатьПредупреждение(,НСтр("ru = 'Персональные группы доступа не используются.'; en = 'Personal access groups are used.'"));
		КонецЕсли;
		Отказ = Истина;
		
	ИначеЕсли НЕ Группа И УпрощенныйИнтерфейсНастройкиПравДоступа Тогда
		ПоказатьПредупреждение(,НСтр("ru = 'Используются только персональные группы доступа,
		                          |которые создаются только в форме ""Права доступа"".'; en = 'Use only personal access groups that are created only in the form of a ""right of access"".'"));
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПеретаскивание(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Поле)
	
	Если Строка = РодительПерсональныхГруппДоступа Тогда
		СтандартнаяОбработка = Ложь;
		ПоказатьПредупреждение(,НСтр("ru = 'Эта папка только для персональных групп доступа.'; en = 'This folder only for personal access groups.'"));
		
	ИначеЕсли ПараметрыПеретаскивания.Значение = РодительПерсональныхГруппДоступа Тогда
		СтандартнаяОбработка = Ложь;
		ПоказатьПредупреждение(,НСтр("ru = 'Папка персональных групп доступа не переносится.'; en = 'Personal folder access groups.'"));
	КонецЕсли;
	
КонецПроцедуры



