#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ДеревоЗначений = ПолучитьДеревоКатегорий();
	
	ЗначениеВРеквизитФормы(ДеревоЗначений, "ДеревоКатегорий");
	
	// Отображение удаленных
	НастройкиФормы = ОбщегоНазначения.ХранилищеСистемныхНастроекЗагрузить(
						ИмяФормы + "/ТекущиеДанные", "");
	
	Если НастройкиФормы = Неопределено Или 
			НастройкиФормы.Получить("ПоказыватьУдаленные") = Неопределено Тогда
			
		Элементы.ПоказыватьУдаленные.Пометка = ПоказыватьУдаленные;
		УстановитьОтбор();
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеДанныхИзНастроекНаСервере(Настройки)
	
	Если Настройки["ПоказыватьУдаленные"] <> Неопределено Тогда
		ПоказыватьУдаленные = Настройки["ПоказыватьУдаленные"];
		Элементы.ПоказыватьУдаленные.Пометка = ПоказыватьУдаленные;
		УстановитьОтбор();
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура СписокКатегорийПриАктивизацииСтроки(Элемент)
	
	ПодключитьОбработчикОжидания("СписокКатегорийОбработчикПриАктивизацииСтроки", 0.1, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоКатегорийПриАктивизацииСтроки(Элемент)
	
	ПодключитьОбработчикОжидания("ДеревоКатегорийОбработчикАктивизацииСтроки", 0.1, Истина);

КонецПроцедуры

&НаКлиенте
Процедура ДеревоКатегорийПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Отказ = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоКатегорийПередНачаломИзменения(Элемент, Отказ)
	
	Отказ = Истина;

КонецПроцедуры

&НаКлиенте
Процедура ДеревоКатегорийПередУдалением(Элемент, Отказ)
	
	Отказ = Истина;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФорм

&НаКлиенте
Процедура ПоказыватьУдаленные(Команда)
	
	ПоказыватьУдаленные = Не ПоказыватьУдаленные;
	Элементы.ПоказыватьУдаленные.Пометка = ПоказыватьУдаленные;
	УстановитьОтбор();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура СписокКатегорийОбработчикПриАктивизацииСтроки()
	
	ТекущаяКатегория = Элементы.СписокКатегорий.ТекущиеДанные;
	Если Не ТекущаяКатегория = Неопределено Тогда
		Список.Параметры.УстановитьЗначениеПараметра("Категория", ТекущаяКатегория.Ссылка);
	КонецЕсли;
КонеЦПроцедуры

&НаКлиенте
Процедура ДеревоКатегорийОбработчикАктивизацииСтроки()
	
	ТекущаяКатегория = Элементы.ДеревоКатегорий.ТекущиеДанные;
	
	Параметр = Список.Параметры.Элементы.Найти("Категория");
	
	Если НЕ Параметр = Неопределено Тогда
		
		Параметр.Использование = Ложь;
		
	КонецЕсли;
	
	Если Не ТекущаяКатегория = Неопределено И ЗначениеЗаполнено(ТекущаяКатегория.Ссылка) Тогда
		
		Список.Параметры.УстановитьЗначениеПараметра("Категория", ТекущаяКатегория.Ссылка);
		
	КонецЕсли;
	
КонеЦПроцедуры

&НаСервере
Функция ПолучитьДеревоКатегорий()
	
	Дерево = Новый ДеревоЗначений;
	Дерево.Колонки.Добавить("Ссылка", Новый ОписаниеТипов("СправочникСсылка.КатегорииОтчетов"));
	Дерево.Колонки.Добавить("Наименование", Новый ОписаниеТипов("Строка"));
	
	Запрос = Новый Запрос();
	Запрос.Текст =
			"ВЫБРАТЬ
			|	КатегорииОтчетов.Наименование КАК Наименование,
			|	КатегорииОтчетов.Ссылка КАК Ссылка
			|ИЗ
			|	Справочник.КатегорииОтчетов КАК КатегорииОтчетов
			|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.НастройкиВариантовОтчетовДокументооборот.Категории КАК НастройкиВариантовОтчетовДокументооборотКатегории
			|		ПО (НастройкиВариантовОтчетовДокументооборотКатегории.Категория = КатегорииОтчетов.Ссылка)
			|ГДЕ
			|	КатегорииОтчетов.ПометкаУдаления = ЛОЖЬ
			|
			|СГРУППИРОВАТЬ ПО
			|	КатегорииОтчетов.Наименование,
			|	КатегорииОтчетов.Ссылка
			|
			|УПОРЯДОЧИТЬ ПО
			|	КатегорииОтчетов.Наименование";
 
	Результат = Запрос.Выполнить();
	УстановитьПривилегированныйРежим(Истина);
	ВыборкаДетальныеЗаписи = Результат.Выбрать();

	// Добавим раздел "Все"
	Строка = Дерево.Строки.Добавить();
		
	Строка.Ссылка = Справочники.КатегорииОтчетов.ПустаяСсылка();
	Строка.Наименование = "Все";
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
				
		Строка = Дерево.Строки.Добавить();
			
		Строка.Ссылка = ВыборкаДетальныеЗаписи.Ссылка;
		Строка.Наименование = ВыборкаДетальныеЗаписи.Наименование;
		
	КонецЦикла;
	
	Возврат Дерево;

КонецФункции

&НаСервере
Процедура УстановитьОтбор()
	
	Если Не ПоказыватьУдаленные Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
			Список, "ПометкаУдаления", Ложь,
			ВидСравненияКомпоновкиДанных.Равно, , Истина);
	Иначе
		ОбщегоНазначенияКлиентСервер.УдалитьЭлементыГруппыОтбораДинамическогоСписка(
			Список, "ПометкаУдаления");
	КонецЕсли;

КонецПроцедуры

#КонецОбласти
