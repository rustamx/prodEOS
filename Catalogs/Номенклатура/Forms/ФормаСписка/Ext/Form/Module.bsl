
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ПоказыватьУдаленные = Ложь;
	ПоказатьУдаленные();
	
	Делопроизводство.СписокДокументовУсловноеОформлениеПомеченныхНаУдаление(Список);
	Элементы.СтавкаНДС.Видимость = Делопроизводство.УчитыватьНДС();
	
	УправлениеДоступностью();
	
КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеДанныхИзНастроекНаСервере(Настройки)
	
	ПоказыватьУдаленные = Настройки["ПоказыватьУдаленные"];
	ПоказатьУдаленные();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапки

&НаКлиенте
Процедура ДекорацияИзменитьНажатие(Элемент)
	
	ОписаниеОповещения = Новый ОписаниеОповещения("НастройкаПрограммыЗавершение", ЭтотОбъект);
	ОткрытьФорму("Обработка.НастройкаПрограммы.Форма.ОбменДанными",,,,,,
		ОписаниеОповещения, РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПоказыватьУдаленные(Команда)
	
	ПоказыватьУдаленные = Не ПоказыватьУдаленные;
	
	ПоказатьУдаленные();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура НастройкаПрограммыЗавершение(Результат, Параметры) Экспорт
	
	УправлениеДоступностью();
	
КонецПроцедуры

&НаСервере
Процедура УправлениеДоступностью()
	
	// Редактирование может быть запрещено, поскольку справочник обновляется загрузкой.
	Если ПравоДоступа("Редактирование", Метаданные.Справочники.Номенклатура) Тогда
		ТолькоПросмотр = Константы.ЗапретитьРедактированиеТоваровИУслуг.Получить();
		// Подчеркнем это недоступностью команд.
		Элементы.ГруппаКомандыРедактирования.Доступность = НЕ ТолькоПросмотр;
		Элементы.ГруппаКомандыРедактированияКонтекст.Доступность = НЕ ТолькоПросмотр;
		Элементы.ДекорацииОбъясняющиеЗапрет.Видимость = ТолькоПросмотр;
		Элементы.ДекорацияИзменить.Видимость = ТолькоПросмотр И ПравоДоступа("Использование", 
			Метаданные.Обработки.НастройкаПрограммы);
	Иначе
		Элементы.ДекорацииОбъясняющиеЗапрет.Видимость = Ложь;
		Элементы.ДекорацияИзменить.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПоказатьУдаленные()
	
	Если ПоказыватьУдаленные Тогда
		ОбщегоНазначенияКлиентСервер.УдалитьЭлементыГруппыОтбораДинамическогоСписка(Список, "ПометкаУдаления");
	Иначе
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список, "ПометкаУдаления", Ложь);
	КонецЕсли;
	
	Элементы.ПоказыватьУдаленные.Пометка = ПоказыватьУдаленные;
	
КонецПроцедуры

#КонецОбласти
