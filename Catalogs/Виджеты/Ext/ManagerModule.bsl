
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает массив доступных виджетов текущему пользователю.
//
// Параметры:
//   Массив
//     - СправочникСсылка.Виджеты
//
Функция ДоступныеВиджеты() Экспорт
	
	ПолноправныйПользователь = Пользователи.ЭтоПолноправныйПользователь();
	
	ДоступныеВиджеты = Новый СписокЗначений;
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьБизнесПроцессыИЗадачи")
		И (РольДоступна("РаботаСЗадачамиИБизнесПроцессами")
			Или ПолноправныйПользователь) Тогда
		
		ДоступныеВиджеты.Добавить(Справочники.Виджеты.МоиЗадачи);
		
		Если Константы.ДобавлятьРуководителямДоступПодчиненных.Получить()
			И (РольДоступна("РаботаСЗадачамиСотрудниковПодразделения")
				Или ПолноправныйПользователь) Тогда
			
			ДоступныеВиджеты.Добавить(Справочники.Виджеты.ЗадачиОтдела);
		КонецЕсли;
	КонецЕсли;
	
	ДоступныеВиджеты.Добавить(Справочники.Виджеты.МоиДокументы);
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьСВД")
		И (РольДоступна("РаботаСЭДО") Или ПолноправныйПользователь) Тогда
		
		ДоступныеВиджеты.Добавить(Справочники.Виджеты.СВД);
	КонецЕсли;
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьХранилищеФайлов") Тогда
		ДоступныеВиджеты.Добавить(Справочники.Виджеты.МоиФайлы);
	КонецЕсли;
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьВстроеннуюПочту")
		И (РольДоступна("РаботаСоВстроеннойЭлектроннойПочтой") Или ПолноправныйПользователь)
		И РегистрыСведений.ИспользованиеПочты.ПолучитьИспользованиеВстроеннойПочты() Тогда
		
		ДоступныеВиджеты.Добавить(Справочники.Виджеты.Почта);
	КонецЕсли;
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьУправлениеМероприятиями")
		И (РольДоступна("РаботаСМероприятиями") Или ПолноправныйПользователь) Тогда
		
		ДоступныеВиджеты.Добавить(Справочники.Виджеты.Мероприятия);
	КонецЕсли;
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьРабочийКалендарь") Тогда
		ДоступныеВиджеты.Добавить(Справочники.Виджеты.Календарь);
	КонецЕсли;
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьОбсуждения") Тогда
		ДоступныеВиджеты.Добавить(Справочники.Виджеты.Форум);
	КонецЕсли;
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьКонтрольОбъектов") Тогда
		ДоступныеВиджеты.Добавить(Справочники.Виджеты.Контроль);
	КонецЕсли;
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьЕжедневныеОтчеты") Тогда
		ДоступныеВиджеты.Добавить(Справочники.Виджеты.ЕжедневныеОтчеты);
	КонецЕсли;
	
	ДоступныеВиджеты.Добавить(Справочники.Виджеты.МоиДела);
	
	// Сортировка виджетов по наименованию
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Виджеты.Ссылка
		|ИЗ
		|	Справочник.Виджеты КАК Виджеты
		|ГДЕ
		|	Виджеты.Ссылка В(&Виджеты)
		|
		|УПОРЯДОЧИТЬ ПО
		|	Виджеты.Наименование";
	Запрос.УстановитьПараметр("Виджеты", ДоступныеВиджеты);
	
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
	
КонецФункции

#КонецОбласти

#КонецЕсли
