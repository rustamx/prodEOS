////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Параметры.Документ) Тогда
		ВызватьИсключение НСтр("ru = 'Не определен исходящий документ.'; en = 'Outgoing document is not determined.'");
	КонецЕсли;
	
	Файлы = РаботаСФайламиВызовСервера.ПолучитьВсеПодчиненныеФайлы(Параметры.Документ, Ложь);
	
	Для Каждого Файл Из Файлы Цикл
		НоваяСтрока = ФайлыДляОтправки.Добавить();
		НоваяСтрока.Пометка = Истина;
		НоваяСтрока.Файл = Файл;
		НоваяСтрока.Описание = Файл.ПолучитьОбъект().Описание;
	КонецЦикла;		
	
	// Вывод информационной панели только в том случае, если хотя бы один получатель
	// получит документ через Такском.
	Элементы.ГруппаИнформация.Видимость = Ложь;
	Для Каждого Получатель Из Параметры.Документ.Получатели Цикл
		
		ПравилоОтправкиСообщенийСВД = РаботаССВД.НайтиПравилоОтправкиСообщенийСВД(
			Параметры.Документ.Организация,
			Получатель.Получатель,
			Неопределено);
			
		Если Не ЗначениеЗаполнено(ПравилоОтправкиСообщенийСВД) Тогда
			Продолжить;
		КонецЕсли;
		
		ФорматСообщения = ПравилоОтправкиСообщенийСВД.Транспорт.ФорматСообщения;
		Если ФорматСообщения = ПредопределенноеЗначение("Справочник.ФорматыСообщенийСВД.ОператорЭДО1СТакском") Тогда
			Элементы.ГруппаИнформация.Видимость = Истина;
			Прервать;
		КонецЕсли;
			
	КонецЦикла;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

&НаКлиенте
Процедура Отправить(Команда)

	Результат = Новый Массив;
	
	Для Каждого ТекСтрока Из ФайлыДляОтправки Цикл
		Если ТекСтрока.Пометка Тогда
			Результат.Добавить(ТекСтрока.Файл);
		КонецЕсли;
	КонецЦикла;
	
	Закрыть(Результат);
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	Закрыть(Неопределено);
	
КонецПроцедуры


