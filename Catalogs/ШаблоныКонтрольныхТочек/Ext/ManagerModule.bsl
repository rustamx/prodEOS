Функция ПолучитьКодПоследнейКТ(ОбъектКТ, ГруппаКТ) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	КонтрольныеТочки.ЧисловойКод КАК ЧисловойКод,
		|	КонтрольныеТочки.КодКТ КАК КодКТ
		|ИЗ
		|	Справочник.ШаблоныКонтрольныхТочек КАК КонтрольныеТочки
		|ГДЕ
		|	КонтрольныеТочки.ПометкаУдаления = ЛОЖЬ
		|	И КонтрольныеТочки.ОбъектКТ = &ОбъектКТ
		|	И КонтрольныеТочки.ГруппаКТ = &ГруппаКТ
		|
		|УПОРЯДОЧИТЬ ПО
		|	ЧисловойКод УБЫВ";
		
	Запрос.УстановитьПараметр("ОбъектКТ", ОбъектКТ);
	Запрос.УстановитьПараметр("ГруппаКТ", ГруппаКТ);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		
		Префикс = Выборка.КодКТ;
		
		// Определяем и сохраняем числовой код, для использования его при создании 
		// новых контрольных точек
		СтрокаЧисел = "0123456789";
		Для Индекс = 1 По СтрДлина(Префикс) Цикл
			СимволПоиска = Сред(Префикс, Индекс, 1);
			Если СтрНайти(СтрокаЧисел, СимволПоиска) <> 0 Тогда
				Прервать;
			КонецЕсли;
		КонецЦикла;
	
		Префикс = Лев(Префикс, Индекс - 1);
		
		Возврат Новый Структура(
			"Число, Префикс, Длина", 
			Выборка.ЧисловойКод, Префикс, СтрДлина(Выборка.КодКТ));
		
	КонецЕсли;
	
	// +
	//Возврат Неопределено;
	Возврат Новый Структура(
			"Число, Префикс, Длина", 0, "", 0);
	
КонецФункции

// Возвращает строку, содержащую перечисление полей доступа через запятую.
Функция ПолучитьПоляДоступа() Экспорт
	
	Возврат 
		"Ссылка,
		|ОбъектКТ,
		|Ответственный";
	
КонецФункции

// Проверяет наличие метода.
// 
Функция ЕстьМетодЗаполнитьДескрипторыОбъекта() Экспорт
	
	Возврат Истина;
	
КонецФункции

// Заполняет переданную таблицу дескрипторов объекта.
// 
Процедура ЗаполнитьДескрипторыОбъекта(ОбъектДоступа, ТаблицаДескрипторов, ПротоколРасчетаПрав = Неопределено) Экспорт
	
	Если ТипЗнч(ОбъектДоступа.ОбъектКТ) = Тип("СправочникСсылка.ВидыПроектов") Тогда	
		
		// Доступ на чтение всем пользователям
		ДокументооборотПраваДоступа.ДобавитьИндивидуальныйДескриптор(
			ОбъектДоступа, 
			ТаблицаДескрипторов, 
			Справочники.РабочиеГруппы.ВсеПользователи, 
			Ложь); // Только чтение
		
	КонецЕсли;
	
КонецПроцедуры


