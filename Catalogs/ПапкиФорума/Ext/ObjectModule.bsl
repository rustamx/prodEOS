#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	ДатаСоздания = ТекущаяДатаСеанса();
	Ответственный = ПользователиКлиентСервер.ТекущийПользователь();
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если Предопределенный Тогда
		
		// Проверка установки пометки удаления для предопределенного элемента
		Если Предопределенный И ПометкаУдаления Тогда
			ОписаниеОшибки = НСтр("ru = 'Пометка на удаление предопределенного элемента справочника запрещена'; en = 'It is forbidden to mark for deletion predefined catalog elements'");
			ВызватьИсключение ОписаниеОшибки;
		КонецЕсли;
		
		// Проверка переноса предопределенного элемента
		Если ЗначениеЗаполнено(Родитель) Тогда
			ОписаниеОшибки = НСтр("ru = 'Перенос предопределенного раздела запрещен'; en = 'It is forbidden to move predefined sub-forum'");
			ВызватьИсключение ОписаниеОшибки;
		КонецЕсли;
		
	КонецЕсли;
	
	// Проверка переноса элемента в предопределенный раздел
	Если Родитель = Справочники.ПапкиФорума.ТемыДокументов Тогда
		ОписаниеОшибки = НСтр("ru = 'В предопределенном разделе запрещены подразделы'; en = 'It is forbidden to place sub-forum in predefined sub-forum'");
		ВызватьИсключение ОписаниеОшибки;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	ДатаСоздания = ТекущаяДатаСеанса();
	Ответственный = ПользователиКлиентСервер.ТекущийПользователь();
	
КонецПроцедуры

#КонецОбласти
