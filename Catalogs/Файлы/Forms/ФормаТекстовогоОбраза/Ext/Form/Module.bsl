
&НаСервере
Процедура ЗаписатьОбраз()
	
	НачатьТранзакцию();
	Попытка
	
		ВерсияОбъект = ОбъектСсылка.ТекущаяВерсия.ПолучитьОбъект();
		
		ИзвлеченныйТекст = Строка(ВерсияОбъект.ИзвлеченныйТекст.Получить());
		РаспознанныйТекст = Строка(ВерсияОбъект.РаспознанныйТекст.Получить());
		
		ОбаРеквизитаПусты = ПустаяСтрока(РаспознанныйТекст) И ПустаяСтрока(ИзвлеченныйТекст);
		ОбаРеквизитаНепусты = НЕ ПустаяСтрока(РаспознанныйТекст) И НЕ ПустаяСтрока(ИзвлеченныйТекст);
		
		Если ОбаРеквизитаПусты ИЛИ ОбаРеквизитаНепусты Тогда
			
			ВерсияОбъект.ИзвлеченныйТекст = Новый ХранилищеЗначения(Текст);
			ВерсияОбъект.РаспознанныйТекст = Новый ХранилищеЗначения("");
			ВерсияОбъект.ТекстХранилище = ВерсияОбъект.ИзвлеченныйТекст;
			
		Иначе	 // один непуст
			
			Если НЕ ПустаяСтрока(РаспознанныйТекст)	Тогда
				ВерсияОбъект.РаспознанныйТекст = Новый ХранилищеЗначения(Текст);
				ВерсияОбъект.ТекстХранилище = ВерсияОбъект.РаспознанныйТекст;
			ИначеЕсли НЕ ПустаяСтрока(ИзвлеченныйТекст) Тогда
				ВерсияОбъект.ИзвлеченныйТекст = Новый ХранилищеЗначения(Текст);
				ВерсияОбъект.ТекстХранилище = ВерсияОбъект.ИзвлеченныйТекст;
			КонецЕсли;
			
		КонецЕсли;	
		
		ВерсияОбъект.ДополнительныеСвойства.Вставить("ЗаписьПодписанногоОбъекта", Истина); // чтобы прошла запись ранее подписанного объекта
		ВерсияОбъект.Записать();
		
		// запишем текст в Файл
		ФайлОбъект = ОбъектСсылка.ПолучитьОбъект();
		ФайлОбъект.ТекстХранилище = ВерсияОбъект.ТекстХранилище;
		ФайлОбъект.ДополнительныеСвойства.Вставить("ЗаписьПодписанногоОбъекта", Истина); // чтобы прошла запись ранее подписанного объекта
		ФайлОбъект.Записать();
		
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		ВызватьИсключение;
	КонецПопытки;
	
КонецПроцедуры

&НаКлиенте
Процедура Записать(Команда)
	
	ЗаписатьВыполнить();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьВыполнить()
	
	Если ИзКарточкиФайла И ПустойТекстПриОткрытии Тогда
		
		Если Не ЗаданВопросНеРаспознан Тогда
			
			Если НЕ Распознан И СтатусРаспознаванияТекста = "НужноРаспознать" Тогда
				
				Обработчик = Новый ОписаниеОповещения("ПослеВопросаФайлНужноРаспознать", ЭтотОбъект);
				ПоказатьВопрос(Обработчик, НСтр("ru = 'Внимание! Файл еще не распознан. Сделанные вами изменения будут потеряны. Продолжить?'; en = 'Attention! The file is not recognized yet. Changes you made will be lost. Do you want to continue?'"), РежимДиалогаВопрос.ДаНет);
				ЗаданВопросНеРаспознан = Истина;
				Возврат;
			КонецЕсли;
			
		КонецЕсли;
		
		Если Не ЗаданВопросНеИзвлечен Тогда
			
			Если НЕ ИзвлеченТекст И СтатусИзвлеченияТекста = "НеИзвлечен" Тогда
				Обработчик = Новый ОписаниеОповещения("ПослеВопросаФайлНеИзвлечен", ЭтотОбъект);
				ПоказатьВопрос(Обработчик, НСтр("ru = 'Внимание! Текст файла еще не извлечен. Сделанные вами изменения будут потеряны. Продолжить?'; en = 'Attention! The file text is not extracted yet. Changes you made will be lost. Do you want to continue?'"), РежимДиалогаВопрос.ДаНет);
				ЗаданВопросНеИзвлечен = Истина;
				Возврат;
			КонецЕсли;	
			
		КонецЕсли;	
		
	КонецЕсли;	
	
	ЗаписатьОбраз();
	
	ОповеститьОбИзменении(ОбъектСсылка);
	
	Оповестить(
		"Запись_Файл", 
		Новый Структура("Событие, Файл, Владелец, ЕстьЗашифрованныеИлиЗанятыеФайлы, ИдентификаторРодительскойФормы", 
			"ДанныеФайлаИзменены", 
			ОбъектСсылка, Неопределено, Неопределено,
			Неопределено),
			ОбъектСсылка);
	
	Закрыть(КодВозвратаДиалога.Да);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеВопросаФайлНужноРаспознать(Ответ, ПараметрыВыполнения) Экспорт
	
	Если Ответ = КодВозвратаДиалога.Да Тогда
		ЗаписатьВыполнить();
	КонецЕсли;	
	
КонецПроцедуры	

&НаКлиенте
Процедура ПослеВопросаФайлНеИзвлечен(Ответ, ПараметрыВыполнения) Экспорт
	
	Если Ответ = КодВозвратаДиалога.Да Тогда
		ЗаписатьВыполнить();
	КонецЕсли;	
	
КонецПроцедуры	


&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	
	ОбъектСсылка = Параметры.ОбъектСсылка;
	
	Заголовок = НСтр("ru = 'Текстовый образ файла ""'; en = 'File text image ""'") + Строка(ОбъектСсылка) + """";
	
	СправочникОбъект = ОбъектСсылка.ПолучитьОбъект();
	Текст = СправочникОбъект.ТекстХранилище.Получить();
	
	Если Параметры.Свойство("ИзКарточкиФайла") Тогда
		ИзКарточкиФайла = Параметры.ИзКарточкиФайла;
	КонецЕсли;
	
	Если Параметры.Свойство("ИзвлеченТекст") Тогда
		ИзвлеченТекст = Параметры.ИзвлеченТекст;
	КонецЕсли;
	
	Если Параметры.Свойство("Распознан") Тогда
		Распознан = Параметры.Распознан;
	КонецЕсли;
	
	ПустойТекстПриОткрытии = ПустаяСтрока(Текст);
	
	Если ИзКарточкиФайла Тогда
		ДанныеФайла = РаботаСФайламиВызовСервера.ДанныеФайла(ОбъектСсылка);
		СтатусРаспознаванияТекста = ДанныеФайла.СтатусРаспознаванияТекста;
		СтатусИзвлеченияТекста = ДанныеФайла.СтатусИзвлеченияТекста;
	КонецЕсли;	
	
КонецПроцедуры


