
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Элементы.ПолучитьСоСканера.Доступность = Ложь;
	
	Если Параметры.Свойство("РежимСоздания") Тогда
		РежимСоздания = Параметры.РежимСоздания;
	КонецЕсли;	
	
	Если Параметры.Свойство("ДоступнаКомандаСканировать") Тогда
		Если Параметры.ДоступнаКомандаСканировать Тогда
			Элементы.ПолучитьСоСканера.Доступность = Истина;
		КонецЕсли;	
	КонецЕсли;
	
	ПриложениеЯвляетсяВебКлиентом = ОбщегоНазначенияДокументооборот.ПриложениеЯвляетсяВебКлиентом();

	Если ПриложениеЯвляетсяВебКлиентом Тогда
		Элементы.ПолучитьСоСканера.Видимость = Ложь;
	КонецЕсли;
	
	МаксРазмерФайла = ФайловыеФункции.ПолучитьМаксимальныйРазмерФайла();
	
	ТаблицаШаблонов = РаботаСШаблонамиДокументовСервер.ПолучитьПоследниеИспользованныеШаблоны();
	МассивШаблонов = ТаблицаШаблонов.ВыгрузитьКолонку("Шаблон");
	
	Если ТаблицаШаблонов.Количество() = 0 Тогда
		Элементы.ДекорацияОтступ.Видимость = Ложь;
	КонецЕсли;
	
	ЧислоОтображаемыхШаблонов = Мин(ТаблицаШаблонов.Количество(), 5);
	КлючСохраненияПоложенияОкна = "ФормаНовогоСШаблонами_" + Строка(ЧислоОтображаемыхШаблонов);
	
	Элементы.Группа1.Видимость = Ложь;
	Счетчик = 1;
	Для Каждого Строка Из ТаблицаШаблонов Цикл
		
		ТекущаяВерсияРазмер = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Строка.Шаблон, "ТекущаяВерсияРазмер");
		
		Если ТекущаяВерсияРазмер > МаксРазмерФайла Тогда
			Продолжить;
		КонецЕсли;	
		
		Шаблоны.Добавить(Строка.Шаблон);
		
		ИмяРеквизита = "ИндексКартинки" + Строка(Счетчик);
		ЭтаФорма[ИмяРеквизита] = Строка.ИндексКартинки;
		
		ИмяРеквизита = "Шаблон" + Строка(Счетчик);
		ЭтаФорма[ИмяРеквизита] = Строка(Строка.Шаблон);
		
		ИмяЭлемента = "Группа" + Строка(Счетчик);
		Элементы[ИмяЭлемента].Видимость = Истина;
		
		Счетчик = Счетчик + 1;
		Если Счетчик > 5 Тогда
			Прервать;
		КонецЕсли;	
		
	КонецЦикла;	
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура УстановитьПараметрыИспользования(РежимСозданияПараметр) Экспорт
	РежимСоздания = РежимСозданияПараметр;
КонецПроцедуры	

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗагрузитьСДиска(Команда)
	Закрыть(2);
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьСоСканера(Команда)
	Закрыть(3);
КонецПроцедуры

&НаКлиенте
Процедура СоздатьПоШаблону(Команда)
	Закрыть(1);
КонецПроцедуры

&НаКлиенте
Процедура Шаблон1Нажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Имя = Элемент.Имя;
	Если Найти(Имя, "Шаблон") <> 0 Тогда
		
		НомерСтрока = Сред(Имя, СтрДлина("Шаблон") + 1);
		Номер = Число(НомерСтрока) - 1;
		
		Если Номер < Шаблоны.Количество() Тогда
			ШаблонСтрока = Шаблоны[Номер];
			Закрыть(ШаблонСтрока.Значение);
		КонецЕсли;
		
	КонецЕсли;	
	
КонецПроцедуры

#КонецОбласти
