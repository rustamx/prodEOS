////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

#Область ОбработчикиКомандФормы_Отправить

&НаКлиенте
Процедура ПроцессСогласование(Команда)
	
	РаботаСБизнесПроцессамиКлиент.ОткрытьПомощникСозданияОсновныхПроцессов(
		"Согласование", Объект.Ссылка, ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ПроцессУтверждение(Команда)
	
	РаботаСБизнесПроцессамиКлиент.ОткрытьПомощникСозданияОсновныхПроцессов(
		"Утверждение", Объект.Ссылка, ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ПроцессИсполнение(Команда)
	
	РаботаСБизнесПроцессамиКлиент.ОткрытьПомощникСозданияОсновныхПроцессов(
		"Исполнение", Объект.Ссылка, ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ПроцессОзнакомление(Команда)
	
	РаботаСБизнесПроцессамиКлиент.ОткрытьПомощникСозданияОсновныхПроцессов(
		"Ознакомление", Объект.Ссылка, ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ПроцессОбработка(Команда)
	
	РаботаСБизнесПроцессамиКлиент.ОткрытьПомощникСозданияОсновныхПроцессов(
		"КомплексныйПроцесс", Объект.Ссылка, ЭтаФорма);
	
КонецПроцедуры

#КонецОбласти

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыФайлОснование = Параметры.ФайлОснование;
	
	Если Параметры.Свойство("ОткрытьЗакладкуЭП") Тогда
		
		ОткрытьЗакладкуЭП = Параметры.ОткрытьЗакладкуЭП;
		
		Если ОткрытьЗакладкуЭП Тогда
			Элементы.ГруппаДополнительныеДанныеСтраницы.ТекущаяСтраница
			 = Элементы.ГруппаЭП;
		КонецЕсли;
		
	КонецЕсли;
	
	Если Параметры.Свойство("ОткрытьЗакладкуЗадачи") Тогда
		ОткрытьЗакладкуЗадачи = Параметры.ОткрытьЗакладкуЗадачи;
	КонецЕсли;
	
	Если НЕ Параметры.ФайлОснование.Пустая() И Объект.ТекущаяВерсия.Пустая() Тогда
		
		Элементы.ИконкаФайлаБольшая.Видимость = Ложь;
		Элементы.ИконкаФайлаБольшаяНаОсновании.Видимость = Истина;
		ИндексКартинкиНаОсновании = Параметры.ФайлОснование.ИндексКартинки;
		
		ЗаполнитьПараметрыКарточки(Параметры.ФайлОснование);
		
		Если Параметры.ФайлОснование.ТекущаяВерсия.ТипХраненияФайла 
			= Перечисления.ТипыХраненияФайлов.ВТомахНаДиске Тогда
			
			НетФайлаШаблона = Ложь;
			ПолныйПуть = "";
			
			Если НЕ Параметры.ФайлОснование.ТекущаяВерсия.Том.Пустая() Тогда
				
				ПолныйПуть = ФайловыеФункции.ПолныйПутьТома(Параметры.ФайлОснование.ТекущаяВерсия.Том)
					+ Параметры.ФайлОснование.ТекущаяВерсия.ПутьКФайлу; 
				Файл = Новый Файл(ПолныйПуть);
				НетФайлаШаблона = Не Файл.Существует();
				
			Иначе
				НетФайлаШаблона = Истина;
			КонецЕсли;
			
			Если НетФайлаШаблона Тогда
				
				ТекстОшибки = СтрШаблон(
					НСтр("ru ='Отсутствует файл (""%1"") у шаблона.'; en = 'Template has no file (""%1"") .'"),
					ПолныйПуть);
				
				ВызватьИсключение ТекстОшибки;
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	ПротоколированиеРаботыПользователей.ЗаписатьОткрытие(Объект.Ссылка);
	
	ТекущийПользователь = ПользователиКлиентСервер.ТекущийПользователь();
	
	Если ПользователиСерверПовтИсп.ЭтоПолноправныйПользовательИБ() Тогда
		Элементы.ДатаСоздания.ТолькоПросмотр = Ложь;
		Элементы.Владелец.ТолькоПросмотр = Ложь;
	Иначе	
		Элементы.Владелец.ТолькоПросмотр = Истина;
		Элементы.Владелец.КнопкаВыбора = Ложь;
	КонецЕсли;
	
	КолонкиМассив = Новый Массив;
	Для Каждого ОписаниеКолонки Из РеквизитФормыВЗначение("ЭлектронныеПодписи").Колонки Цикл
		КолонкиМассив.Добавить(ОписаниеКолонки.Имя);
	КонецЦикла;
	ОписаниеКолонокТаблицыПодписей = Новый ФиксированныйМассив(КолонкиМассив);
	
	ИспользоватьКатегорииДанных = ПолучитьФункциональнуюОпцию("ИспользоватьКатегорииДанных");
	ИспользоватьАвтоматическуюКатегоризациюДанных 
		= ПолучитьФункциональнуюОпцию("ИспользоватьАвтоматическуюКатегоризациюДанных");
	ИспользоватьПроверкуКатегорий = ПолучитьФункциональнуюОпцию("ИспользоватьКатегорииДанных");
	ИспользоватьАвтозаполнениеФайлов = ПолучитьФункциональнуюОпцию("ИспользоватьАвтозаполнениеФайлов");
	ВставлятьШКвMSWordнаСервере = Константы.ИзменениеФайловMSWordТолькоНаСервере.Получить();
	ИспользоватьШК = ПолучитьФункциональнуюОпцию("ИспользоватьШтрихкоды");
	
	ВестиУчетПоПроектам = ПолучитьФункциональнуюОпцию("ВестиУчетПоПроектам");
	
	Если Параметры.Свойство("РежимСоздания") Тогда 
		РежимСоздания = Параметры.РежимСоздания;
	КонецЕсли;
	
	Если Параметры.Ключ = Неопределено Или Параметры.Ключ.Пустая() Тогда
		
		НовыйФайл = Истина;
		
		Если Параметры.ЗначениеКопирования.Пустая() Тогда
			Объект.ВладелецФайла = Параметры.ВладелецФайла;
		Иначе
			Объект.ТекущаяВерсия = Справочники.ВерсииФайлов.ПустаяСсылка();
			Параметры.ФайлОснование = Параметры.ЗначениеКопирования;
		КонецЕсли;
		
	КонецЕсли;
	
	ДокОснование = Параметры.ФайлОснование;
	Если Не ДокОснование.Пустая() Тогда
		
		Объект.ПолноеНаименование = ДокОснование.ПолноеНаименование;
		Объект.Наименование = Объект.ПолноеНаименование;
		Объект.ХранитьВерсии = ДокОснование.ХранитьВерсии;
		
		// копирование категорий
		Если ИспользоватьКатегорииДанных Тогда
			КатегорииИсточника = РаботаСКатегориямиДанных.ПолучитьКатегорииОбъекта(ДокОснование);
			Для Каждого Категория Из КатегорииИсточника Цикл
				НоваяСтрока = СписокКатегорийДанных.Добавить();
				НоваяСтрока.Значение = Категория.Ссылка;
				НоваяСтрока.ПолноеНаименование 
					= РаботаСКатегориямиДанных.ПолучитьПолныйПутьКатегорииДанных(Категория.Ссылка);
			КонецЦикла;
		КонецЕсли;
		
		// копирование проекта
		Если ВестиУчетПоПроектам Тогда 
			Объект.Проект = ДокОснование.Проект;
		КонецЕсли;	
	КонецЕсли;
	
	ДанныеФайлаКорректны = Ложь;
	Если Не Объект.Ссылка.Пустая() Тогда
		ДанныеФайла = РаботаСФайламиВызовСервера.ДанныеФайла(Объект.Ссылка);
		ДанныеФайлаКорректны = Истина;
		
		Если ДанныеФайла.СтатусРаспознаванияТекста = "Распознано" Тогда
			Распознан = Истина;
		КонецЕсли;
		
		Если ДанныеФайла.СтатусИзвлеченияТекста = "Извлечен" Тогда
			ИзвлеченТекст = Истина;
		КонецЕсли;
		
		Если ТипЗнч(Объект.ВладелецФайла) = Тип("СправочникСсылка.ПапкиФайлов") Тогда
			Элементы.ЗаполнитьДаннымиВладельца.Видимость = Ложь;
		КонецЕсли;
	КонецЕсли;
	
	ТипВладельца = ТипЗнч(Объект.ВладелецФайла);
	Элементы.Владелец.Заголовок = ТипВладельца;
	
	НовыйФайлЗаписан = Ложь;
	
	// Обработчик подсистемы "Свойства"
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.Свойства") Тогда
		ДополнительныеПараметры = Новый Структура;
		ДополнительныеПараметры.Вставить("ИмяЭлементаДляРазмещения", "ГруппаСвойства");
		МодульУправлениеСвойствами = ОбщегоНазначения.ОбщийМодуль("УправлениеСвойствами");
		МодульУправлениеСвойствами.ПриСозданииНаСервере(ЭтотОбъект, ДополнительныеПараметры);
	КонецЕсли;
	
	Основание = ПараметрыФайлОснование;
	//Копирование дополнительных реквизитов основания 
	Если ЗначениеЗаполнено(Основание) 
		И ТипЗнч(Основание) = Тип("СправочникСсылка.Файлы")
		И ЭтаФорма.Свойства_ИспользоватьСвойства Тогда
		
		Для Каждого Реквизит Из Основание.ДополнительныеРеквизиты Цикл
			СтруктураПоиска = Новый Структура("Свойство", Реквизит.Свойство);
			МассивСтрок = ЭтаФорма.Свойства_ОписаниеДополнительныхРеквизитов.НайтиСтроки(СтруктураПоиска);
			Если МассивСтрок.Количество() > 0 Тогда
				НоваяСтрока = МассивСтрок[0];
				ЭтаФорма[НоваяСтрока.ИмяРеквизитаЗначение] = Реквизит.Значение;
			КонецЕсли;
		КонецЦикла;
		ОбновитьЭлементыДополнительныхРеквизитов();
	КонецЕсли;
	
	Если Параметры.Свойство("КарточкаОткрытаПослеСозданияФайла") Тогда
		Если Параметры.КарточкаОткрытаПослеСозданияФайла Тогда
			Попытка
				ЗаблокироватьДанныеФормыДляРедактирования();
			Исключение
			КонецПопытки;
		КонецЕсли;
	КонецЕсли;
	
	Если ТипЗнч(Объект.ВладелецФайла) = Тип("СправочникСсылка.ПапкиФайлов") Тогда
		ОбновитьПолныйПуть();
		Элементы.СубъектыПерсональныхДанных.ТолькоПросмотр = Ложь;
		Элементы.ФормаПоказатьВСписке.Видимость = Истина;
	Иначе 
		Элементы.ФормаПоказатьВСписке.Видимость = Ложь;
	КонецЕсли;
	ОбновитьПредставлениеВладельца();
	
	Если Не Параметры.ФайлОснование.Пустая() Тогда
		ФайлОснованиеПодписан = Параметры.ФайлОснование.ПодписанЭП;
	КонецЕсли;
	
	Если ЭлектроннаяПодпись.ИспользоватьЭлектронныеПодписи() Тогда
		ЗаполнитьСписокПодписей();
	КонецЕсли;
	
	Если ЭлектроннаяПодпись.ИспользоватьШифрование() Тогда
		ЗаполнитьСписокШифрования();
	КонецЕсли;
	
	Если НЕ Параметры.ФайлОснование.Пустая() И Объект.ТекущаяВерсия.Пустая() Тогда
		Объект.ШаблонОснованиеДляСоздания = Параметры.ФайлОснование;
	КонецЕсли;
	
	// Чтение категорий данных
	Если ИспользоватьКатегорииДанных Тогда
		ПредопределенныеВсеКатегории = Справочники.КатегорииДанных.ВсеКатегории;
		СписокКатегорийДанныхЗначение = РеквизитФормыВЗначение("СписокКатегорийДанных");
		Элементы.ДекорацияПояснение.Видимость = Ложь;
		Если ТипЗнч(Объект.ВладелецФайла) = Тип("СправочникСсылка.ПапкиФайлов") Тогда
			РаботаСКатегориямиДанных.ЗагрузитьДанныеОКатегориях(Элементы, Объект, СписокКатегорийДанныхЗначение, ИспользоватьАвтоматическуюКатегоризациюДанных, Параметры); 
		ИначеЕсли ТипЗнч(Объект.ВладелецФайла) = Тип("СправочникСсылка.ВнутренниеДокументы")
			ИЛИ ТипЗнч(Объект.ВладелецФайла) = Тип("СправочникСсылка.ВходящиеДокументы")
			ИЛИ ТипЗнч(Объект.ВладелецФайла) = Тип("СправочникСсылка.ИсходящиеДокументы") Тогда
			РаботаСКатегориямиДанных.ЗагрузитьДанныеОКатегориях(Элементы, Объект.ВладелецФайла, СписокКатегорийДанныхЗначение, ИспользоватьАвтоматическуюКатегоризациюДанных, Параметры); 
			Элементы.СписокКатегорийДанныхОткрытьФормуПодбораКатегорий.Видимость = Ложь;
			Элементы.СписокКатегорийДанныхВывестиСписок.Видимость = Ложь;
			Элементы.СписокКатегорийДанныхОткрытьСписокОбъектовСВыбраннымиКатегориями.Видимость = Ложь;
			Элементы.СписокКатегорийДанных.ИзменятьСоставСтрок = Ложь;
			Элементы.ДекорацияПояснение.Видимость = Истина;
			Элементы.ДекорацияПояснение.Заголовок = СтрШаблон(
				НСтр("ru = 'Данный файл прикреплен к документу ""%1"".
				|В списке приведены категории документа.';
				|en = 'This file is attached to the document ""%1"".
				|Document categories are listed here.'"),
				Объект.ВладелецФайла.Заголовок);
			Элементы.ГруппаСтатусы.Видимость = Ложь;
		Иначе
			Элементы.Категории.Видимость = Ложь;
		КонецЕсли;
		ЗначениеВРеквизитФормы(СписокКатегорийДанныхЗначение, "СписокКатегорийДанных");
		Если РольДоступна(Метаданные.Роли.ПроверкаКатегорий) Тогда
			Если ИспользоватьАвтоматическуюКатегоризациюДанных Тогда
				АвтоматическаяКатегоризацияВыполнена = РаботаСКатегориямиДанных.ПолучитьФлагВыполненияАвтоматическойКатегоризации(Объект.Ссылка);
				Если АвтоматическаяКатегоризацияВыполнена Тогда
					Элементы.ГруппаАвтоКатегоризация.ТекущаяСтраница = Элементы.ГруппаАвтокатегоризацияВыполнена;
				Иначе
					Элементы.ГруппаАвтоКатегоризация.ТекущаяСтраница = Элементы.ГруппаАвтокатегоризацияНеВыполнена;
				КонецЕсли;
			Иначе
				Элементы.ГруппаАвтоКатегоризация.Видимость = Ложь;
			КонецЕсли;
			Если ИспользоватьПроверкуКатегорий Тогда
				КатегорииПроверены = РаботаСКатегориямиДанных.ПолучитьФлагПроверкиКатегорийОбъекта(Объект.Ссылка);
				Если КатегорииПроверены Тогда
					Элементы.ГруппаПроверкаКатегорий.ТекущаяСтраница = Элементы.ГруппаКатегорииПроверены;
				Иначе
					Элементы.ГруппаПроверкаКатегорий.ТекущаяСтраница = Элементы.ГруппаКатегорииНеПроверены;
				КонецЕсли;
			Иначе
				Элементы.ГруппаПроверкаКатегорий.Видимость = Ложь;
			КонецЕсли;
		Иначе
			Элементы.ГруппаАвтоКатегоризация.Видимость = Ложь;
			Элементы.ГруппаПроверкаКатегорий.Видимость = Ложь;
		КонецЕсли;		
	Иначе
		Элементы.Категории.Видимость = Ложь;
	КонецЕсли;
	
	Если ПолучитьФункциональнуюОпцию("ВестиУчетПоПроектам") 
		И ЗначениеЗаполнено(Объект.ВладелецФайла)
		И Объект.ВладелецФайла.Метаданные().Реквизиты.Найти("Проект") <> Неопределено Тогда 
		Если Объект.Ссылка.Пустая() Тогда 
			Объект.Проект = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.ВладелецФайла, "Проект");
		КонецЕсли; 
		Элементы.Проект.ТолькоПросмотр = Истина;
	КонецЕсли;	
	
	СписокРасширенийТекстовыхФайлов = ФайловыеФункции.ПолучитьСписокРасширенийТекстовыхФайлов();
	Если ФайловыеФункцииКлиентСервер.РасширениеФайлаВСписке(СписокРасширенийТекстовыхФайлов, Объект.ТекущаяВерсияРасширение) Тогда
		
		Если ЗначениеЗаполнено(Объект.ТекущаяВерсия) Тогда
			
			КодировкаЗначение = РаботаСФайламиВызовСервера.ПолучитьКодировкуВерсииФайла(Объект.ТекущаяВерсия);
			
			СписокКодировок = РаботаСоСтроками.ПолучитьСписокКодировок();
			ЭлементСписка = СписокКодировок.НайтиПоЗначению(КодировкаЗначение);
			Если ЭлементСписка = Неопределено Тогда
				Кодировка = КодировкаЗначение;
			Иначе
				Кодировка = ЭлементСписка.Представление;
			КонецЕсли;
			
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(Кодировка) Тогда
			Кодировка = НСтр("ru='По умолчанию'; en = 'Default'");
		КонецЕсли;
		
	Иначе
		Элементы.Кодировка.Видимость = Ложь;
	КонецЕсли;
	
	ДоступностьРедактированияПоСостояниюБП = Истина;
	ЭтоБизнесПроцесс = ОбщегоНазначения.ЭтоБизнесПроцесс(Объект.ВладелецФайла.Метаданные());
	Если ЭтоБизнесПроцесс Тогда
		СостояниеБП = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.ВладелецФайла, "Состояние");
		Если СостояниеБП <> Неопределено
			И СостояниеБП = Перечисления.СостоянияБизнесПроцессов.Прерван Тогда
			ДоступностьРедактированияПоСостояниюБП = Ложь;
		КонецЕсли;
	КонецЕсли;
	
	Если ПолучитьФункциональнуюОпцию("ВестиУчетСканКопийОригиналовДокументов")
	   И ДелопроизводствоКлиентСервер.ЭтоДокумент(Объект.ВладелецФайла) Тогда 
		Элементы.ЯвляетсяОригиналом.Видимость = Истина;
		Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
			ЯвляетсяОригиналом = Делопроизводство.ФайлЯвляетсяОригиналом(Объект.Ссылка);
		КонецЕсли;
		НачальныйЯвляетсяОригиналом = ЯвляетсяОригиналом;
		
		// доступность переключателя Это оригинал
		Если Константы.ОграничиватьДоступностьПолейПоСостоянию.Получить()
			И Не РольДоступна("ПолныеПрава") Тогда 
			ДоступныеПоля = Новый Массив; 
			НедоступныеПоля = Новый Массив; 
		
			Делопроизводство.ПолучитьДоступныеИНедоступныеПоСостояниюПоля(
				Объект.ВладелецФайла.ПолучитьОбъект(),
				ДоступныеПоля, НедоступныеПоля);
			
			ДоступностьПоляОригинал = (ДоступныеПоля.Найти("ДобавлениеОригиналов") <> Неопределено) 
				И (ДоступныеПоля.Найти("ДобавлениеФайлов") <> Неопределено);
				
			Элементы.ЯвляетсяОригиналом.ТолькоПросмотр = Не ДоступностьПоляОригинал;
		КонецЕсли;
	Иначе
		Элементы.ЯвляетсяОригиналом.Видимость = Ложь;
	КонецЕсли;
	
	// Параметры оповещения
	Параметры.Свойство("ПараметрыОповещения", ПараметрыОповещения);
	
	// Учет трудозатрат
	УчетВремени.ПроинициализироватьПараметрыУчетаВремени(
		ДатаНачалаХронометража,
		ДатаКонцаХронометража,
		ВключенХронометраж,
		ОпцияИспользоватьУчетВремени,
		Объект.Ссылка,
		ВидыРабот,
		СпособУказанияВремени,
		ЭтаФорма.Команды.ПереключитьХронометраж,
		ЭтаФорма.Элементы.ПереключитьХронометраж,
		ЭтаФорма.Элементы.УказатьТрудозатраты);
			
	РаботаСПоследнимиОбъектами.ЗаписатьОбращениеКОбъекту(Объект.Ссылка);
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		РазрешеноРедактирование = Истина;
	КонецЕсли;	
	
	КешИнформации = РегистрыСведений.КешИнформацииОбОбъектах.СоздатьМенеджерЗаписи();
	КешИнформации.Объект = Объект.Ссылка;
	КешИнформации.Прочитать();
	
	// Картинка Взаимодействие
	Элементы.КартинкаВзаимодействие.Видимость = ПолучитьФункциональнуюОпцию("ИспользоватьБизнесПроцессыИЗадачи");
	Если КешИнформации.ЕстьЗадачи Тогда 
		СтатусВзаимодействия = 1;
		Элементы.КартинкаВзаимодействие.Подсказка = НСтр("ru = 'По файлу выполняются задачи'; en = 'File tasks'");
	Иначе	
		СтатусВзаимодействия = 0;
		Элементы.КартинкаВзаимодействие.Подсказка = НСтр("ru = 'По файлу нет выполняющихся задач'; en = 'No file tasks'");
	КонецЕсли;
	
	Элементы.КартинкаЭП.Видимость = ПолучитьФункциональнуюОпцию("ИспользоватьЭлектронныеПодписи");
	ОбзорДокумента.ВывестиКартинкуЭП(ЭтаФорма);
	
	// Картинка Категории
	Элементы.КартинкаКатегории.Видимость = ПолучитьФункциональнуюОпцию("ИспользоватьКатегорииДанных");
	Если СписокКатегорийДанных.Количество() > 0 Тогда 
		СтатусКатегории = 1;
		Элементы.КартинкаКатегории.Подсказка = НСтр("ru = 'Есть категории'; en = 'Has categories'");
	Иначе
		СтатусКатегории = 0;
		Элементы.КартинкаКатегории.Подсказка = НСтр("ru = 'Нет категорий'; en = 'No categories'");
	КонецЕсли;	
	
	// Картинка контроль
	ОбзорДокумента.ВывестиКартинкуКонтроля(ЭтаФорма);
	
	// Картинка форум
	ОбзорДокумента.ВывестиКартинкуФорума(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	УстановитьДоступностьЭлементовФормы();
	
	НаименованиеДоЗаписи = Объект.Наименование;
	
	Если Не Параметры.ФайлОснование.Пустая() И ФайлОснованиеПодписан Тогда
		
		ПодключитьОбработчикОжидания("ПриОткрытииКлиент", 0.2, Истина);

	КонецЕсли;
	
	УстановитьДоступностьКомандСпискаЭП();
	УстановитьДоступностьКомандСпискаШифрования();
	
	Если ОбщегоНазначенияКлиентПовтИсп.ЭтоВебКлиентПодMacOS() Тогда
		Элементы.ФормаЭПИШифрование.Видимость = Ложь;
		Элементы.ЭлектронныеПодписи.ПоложениеКоманднойПанели = ПоложениеКоманднойПанелиЭлементаФормы.Нет;
		Элементы.СертификатыШифрования.ПоложениеКоманднойПанели = ПоложениеКоманднойПанелиЭлементаФормы.Нет;
	КонецЕсли;
	
	Оповестить("ОбновитьСписокПоследних");
	
	Если ОткрытьЗакладкуЗадачи Тогда
		ПодключитьОбработчикОжидания("ПриОткрытииКлиентОткрытьЗадачи", 0.2, Истина);
	КонецЕсли;	
	
	Если Объект.Ссылка.Пустая()
		И ТипЗнч(ВладелецФормы) = Тип("УправляемаяФорма")
		И ВладелецФормы.ИмяФормы = "Справочник.Проекты.Форма.ФайлыПоПроекту" Тогда 
		Объект.Проект = ВладелецФормы.Параметры.Проект;
	КонецЕсли;		
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытииКлиентОткрытьЗадачи()
	
	ОткрытьФорму("ОбщаяФорма.ПроцессыИЗадачи",
		Новый Структура("Предмет", Объект.Ссылка),
		ЭтаФорма,
		ЭтаФорма.КлючУникальности,
		ЭтаФорма.Окно,
		"e1cib/command/ОбщаяКоманда.ПроцессыИЗадачи");
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытииКлиент()
	
	ОписаниеОповещения = Новый ОписаниеОповещения(
		"ПриОткрытииКлиентПродолжениеПослеВопроса",
		ЭтотОбъект);
		
	Текст = СтрШаблон(
		НСтр("ru = 'Файл ""%1"" ПодписанЭП. 
		| Копирование сведений об ЭП в новый файл сделает его недоступным для изменения. 
		| Скопировать в новый файл сведения об ЭП?';
		|en = 'The file ""%1"" is signed. 
		|Copying information about DS to the new file will make it unavailable for editing. 
		|Copy to the new file information about DS?'"),
		Строка(ПараметрыФайлОснование));
	
	ПоказатьВопрос(ОписаниеОповещения, Текст, РежимДиалогаВопрос.ДаНет);
		
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытииКлиентПродолжениеПослеВопроса(Ответ, Параметры) Экспорт
	
	Если Ответ = КодВозвратаДиалога.Да Тогда
		КопироватьПодписиЭП = Истина;
	КонецЕсли;	
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	// СтандартныеПодсистемы.Свойства
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.Свойства") Тогда
		МодульУправлениеСвойствами = ОбщегоНазначения.ОбщийМодуль("УправлениеСвойствами");
		МодульУправлениеСвойствами.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.Свойства
	
	ЗаполнитьПараметрыКарточки(ТекущийОбъект);
	
	ЗапретитьРедактироватьФайлы = Ложь;
	РазрешеноРедактирование = Истина;
	
	Если ДелопроизводствоКлиентСервер.ЭтоДокумент(ТекущийОбъект.ВладелецФайла)
		И ЗначениеЗаполнено(ТекущийОбъект.ШаблонОснованиеДляСоздания)
		И Не РольДоступна("ПолныеПрава") Тогда 
		
		СозданПоШаблонуДокумента 
			= РегистрыСведений.ФайлыСозданныеПоШаблону.ПолучитьФайлСозданПоШаблону(ТекущийОбъект.Ссылка);
			
		Если СозданПоШаблонуДокумента Тогда
			
			ЗапретитьРедактироватьФайлы = Делопроизводство.ПолучитьСвойствоШаблонаЗапретитьРедактироватьФайлы(
				ТекущийОбъект.ВладелецФайла);
				
			Если ЗапретитьРедактироватьФайлы Тогда
				
				РазрешеноРедактирование = Ложь;
				ТолькоПросмотр = Истина;
				
			КонецЕсли;
					
		КонецЕсли;	
			
	КонецЕсли;	
	
	ХранитьВерсииНачальноеЗначение = ТекущийОбъект.ХранитьВерсии;	
	
КонецПроцедуры

// Заполняет параметры первой закладки
//
// Параметры:
// ТекущийОбъект - текущий объект
//
&НаСервере
Процедура ЗаполнитьПараметрыКарточки(ТекущийОбъект)

	СозданСтатус = СтрШаблон(
		НСтр("ru = '%1 (%2)'; en = '%1 (%2)'"),
		Формат(ТекущийОбъект.ДатаСоздания, "ДФ='dd.MM.yyyy HH:mm'"),
		Строка(ТекущийОбъект.Автор));
	ИзмененСтатус = СтрШаблон(
		НСтр("ru = '%1 (%2)'; en = '%1 (%2)'"),
		Формат(ТекущийОбъект.ТекущаяВерсияДатаСоздания, "ДФ='dd.MM.yyyy HH:mm'"),
		Строка(ТекущийОбъект.ТекущаяВерсияАвтор));
	
	ИмяИРасширение = ОбщегоНазначенияКлиентСервер.ПолучитьИмяСРасширением(ТекущийОбъект.ПолноеНаименование, 
		ТекущийОбъект.ТекущаяВерсияРасширение);
	
	РазмерСтрока = СтрШаблон(
		НСтр("ru = 'Размер: %1'; en = 'Size: %1'"), 
			РаботаСоСтроками.ПолучитьРазмерСтрокой(ТекущийОбъект.ТекущаяВерсияРазмер));
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если ОбщегоНазначенияДокументооборотКлиент.ПередЗакрытием(
		Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка, Модифицированность) Тогда
		Возврат;
	КонецЕсли;
	
	Если Не БылЗаданВопросФайлЗанятДляРедактирования И Не БылЗаданВопросОткрытьДляРедактирования Тогда
		
		Если ФайлРедактировался И ФайловыеФункцииКлиентПовтИсп.ПолучитьПерсональныеНастройкиРаботыСФайлами().ТекущийПользователь = Объект.Редактирует Тогда 
			
			Обработчик = Новый ОписаниеОповещения("ПослеВопросаФайлЗанятДляРедактирования", ЭтотОбъект);
			ПоказатьВопрос(Обработчик, НСтр("ru = 'Файл занят вами для редактирования. Закрыть карточку?'; en = 'File is locked for edit. Close the form?'"), РежимДиалогаВопрос.ДаНет, ,КодВозвратаДиалога.Нет);
			Отказ = Истина;
			БылЗаданВопросФайлЗанятДляРедактирования = Истина;
			Возврат;
		КонецЕсли;
		
	КонецЕсли;

	Если Не БылЗаданВопросОткрытьДляРедактирования Тогда
		
		Если РежимСоздания = "ИзШаблона" И НЕ Объект.ПодписанЭП И НовыйФайл
			И НовыйФайлЗаписан И (Не ФайлРедактировался) Тогда
			
			СтрокаВопроса = СтрШаблон(
			  НСтр("ru = 'Открыть файл ""%1"" для редактирования?'; en = 'Open the file ""%1"" for editing?'"),
			  СокрЛП(Объект.ПолноеНаименование) );
		  
			Обработчик = Новый ОписаниеОповещения("ПослеВопросаОткрытьДляРедактирования", ЭтотОбъект);			  
		  	ПоказатьВопрос(Обработчик, СтрокаВопроса, РежимДиалогаВопрос.ДаНет, ,КодВозвратаДиалога.Да);
		   
		  	Отказ = Истина;
		  	БылЗаданВопросОткрытьДляРедактирования = Истина;
		  	Возврат;
				
		КонецЕсли;
		  
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеВопросаФайлЗанятДляРедактирования(Ответ, ПараметрыВыполнения) Экспорт
	
	Если Ответ <> КодВозвратаДиалога.Нет Тогда 
		Закрыть();
	Иначе
		БылЗаданВопросФайлЗанятДляРедактирования = Ложь;
	КонецЕсли;	
	
КонецПроцедуры	

&НаКлиенте
Процедура ПослеВопросаОткрытьДляРедактирования(Ответ, ПараметрыВыполнения) Экспорт
	
	Если Ответ <> КодВозвратаДиалога.Да Тогда 
		Закрыть();
	Иначе	
		Обработчик = Новый ОписаниеОповещения("ЗакрытьФорму", ЭтотОбъект);
		РаботаСФайламиКлиент.РедактироватьСОповещением(Обработчик, Объект.Ссылка, УникальныйИдентификатор);
	КонецЕсли;	
	
КонецПроцедуры	

&НаКлиенте
Процедура ЗакрытьФорму(Ответ, ПараметрыВыполнения) Экспорт
	
	Закрыть();
	
КонецПроцедуры	

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСОбытия = "ФайлОткрыт" И Параметр = Объект.Ссылка Тогда
		НовыйФайл = Ложь;
	КонецЕсли;

	Если ИмяСобытия = "Запись_Файл" И Параметр.Событие = "ФайлРедактировался" 
		И Источник = Объект.Ссылка Тогда
		ФайлРедактировался = Истина;
	КонецЕсли;

	Если ИмяСобытия = "Запись_Файл" И Параметр.Событие = "ДанныеФайлаИзменены" Тогда
		
		ФайлВОповещении = Источник;
		
		Если ФайлВОповещении = Объект.Ссылка Тогда
			Если ДанныеФайла <> Неопределено Тогда
				ДанныеФайлаКорректны = Ложь;
			КонецЕсли;
			
			Прочитать();
			УстановитьДоступностьЭлементовФормы();
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если ИмяСобытия = "ОбъектЗашифрован" И Параметр = Объект.Ссылка Тогда
		
		Если ДанныеФайла <> Неопределено Тогда
			ДанныеФайлаКорректны = Ложь;
		КонецЕсли;
		
		Прочитать();
	КонецЕсли;
	
	Если ИмяСобытия = "АктивнаяВерсияИзменена" И Параметр = Объект.Ссылка Тогда
		Прочитать();
	КонецЕсли;
	
	Если ИмяСобытия = "РедактироваласьКатегория" Тогда
		ОбновитьДанныеОКатегориях(Параметр);
	КонецЕсли;
	
	Если ИмяСобытия = "ОбновленаИнформацияОСертификатахШифрования" И Параметр = Объект.Ссылка Тогда
		ЗаполнитьСписокШифрования();
	КонецЕсли;
	
	// СтандартныеПодсистемы.Свойства
	Если УправлениеСвойствамиКлиент.ОбрабатыватьОповещения(ЭтотОбъект, ИмяСобытия, Параметр) Тогда
		ОбновитьЭлементыДополнительныхРеквизитов();
		УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	Если Не Отказ Тогда
		ОценкаПроизводительностиКлиентСервер.НачатьЗамерВремени("ФайлыВыполнениеКомандыЗаписать");
	КонецЕсли;
	
	Объект.Наименование = Объект.ПолноеНаименование;
	
	ПараметрыЗаписи.Вставить("ЭтоНовый", Объект.Ссылка.Пустая());
	
	ОбщегоНазначенияДокументооборотКлиент.УдалитьПустыеСтрокиТаблицы(Объект.СубъектыПерсональныхДанных, "ФизическоеЛицо");
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	ПараметрыЗаписи.Вставить("ЭтоНовыйОбъект", Не ЗначениеЗаполнено(ТекущийОбъект.Ссылка));
	
	// СтандартныеПодсистемы.Свойства
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.Свойства") Тогда
		МодульУправлениеСвойствами = ОбщегоНазначения.ОбщийМодуль("УправлениеСвойствами");
		МодульУправлениеСвойствами.ПередЗаписьюНаСервере(ЭтотОбъект, ТекущийОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.Свойства
	
	ТекущийОбъект.ДополнительныеСвойства.Вставить("УникальныйИдентификатор", УникальныйИдентификатор);
	
КонецПроцедуры

&НаСервере
Процедура ПриЗаписиНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Если ПолучитьФункциональнуюОпцию("ВестиУчетСканКопийОригиналовДокументов")
	   И ДелопроизводствоКлиентСервер.ЭтоДокумент(Объект.ВладелецФайла) Тогда 
		Делопроизводство.СохранитьСведенияОбОригиналеФайла(ТекущийОбъект.Ссылка, 
			ТекущийОбъект.ВладелецФайла, 
			ЯвляетсяОригиналом);
	КонецЕсли;	
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	ПротоколированиеРаботыПользователей.ЗаписатьСоздание(Объект.Ссылка, ПараметрыЗаписи.ЭтоНовыйОбъект);
	ПротоколированиеРаботыПользователей.ЗаписатьИзменение(Объект.Ссылка);
	
	Если НаименованиеДоЗаписи <> ТекущийОбъект.Наименование Тогда
		Если ТекущийОбъект.ТекущаяВерсия.ТипХраненияФайла = Перечисления.ТипыХраненияФайлов.ВТомахНаДиске Тогда
			
			РаботаСФайламиВызовСервера.ПереименоватьФайлВерсииНаДиске(ТекущийОбъект.ТекущаяВерсия, 
				НаименованиеДоЗаписи, ТекущийОбъект.Наименование, УникальныйИдентификатор);
				
		КонецЕсли;
	КонецЕсли;
		
	Если НовыйФайл Тогда 
		ДанныеФайла = РаботаСФайламиВызовСервера.ДанныеФайла(Объект.Ссылка);
		ДанныеФайлаКорректны = Истина;
	КонецЕсли;

	Если НЕ Параметры.ФайлОснование.Пустая() И Объект.ТекущаяВерсия.Пустая() Тогда
		СоздатьКопиюВерсии(Объект.Ссылка, Параметры.ФайлОснование, КопироватьПодписиЭП, Истина);
		Если ЗначениеЗаполнено(АдресДвоичныхДанныхЗаполненногоФайлаВоВременномХранилище) Тогда
			АвтозаполнениеШаблоновФайловСервер.ОбновитьВерсиюИзДвоичныхДанных(ПолучитьИзВременногоХранилища(АдресДвоичныхДанныхЗаполненногоФайлаВоВременномХранилище), 
					ТекущийОбъект.Ссылка, 
					"Автозаполнение полей файла",
					УникальныйИдентификатор);
			АвтозаполнениеВыполнялось = Истина;
			Прочитать();
		КонецЕсли;
		Модифированность = Истина;
	КонецЕсли;
	
	//Считываем настройки автозаполнения для дальнейшего использования
	ИспользоватьАвтозаполнениеФайлов = ПолучитьФункциональнуюОпцию("ИспользоватьАвтозаполнениеФайлов");
	Если ИспользоватьАвтозаполнениеФайлов И НовыйФайл Тогда
		НастройкиАвтозаполнения = АвтозаполнениеШаблоновФайловСервер.ПолучитьНастройкиАвтозаполненияШаблоновФайлов(Объект, Объект.ВладелецФайла, Истина);
	КонецЕсли;
	
	Если ИспользоватьКатегорииДанных 
		И ТипЗнч(Объект.ВладелецФайла) = Тип("СправочникСсылка.ПапкиФайлов") Тогда
		РаботаСКатегориямиДанных.ЗаписатьСписокКатегорийУОбъекта(СписокКатегорийДанных, Объект.Ссылка);
		Если ИспользоватьАвтоматическуюКатегоризациюДанных Тогда
			АвтоматическаяКатегоризацияВыполнена = РаботаСКатегориямиДанных.ПолучитьФлагВыполненияАвтоматическойКатегоризации(Объект.Ссылка);
			Если АвтоматическаяКатегоризацияВыполнена Тогда
				Элементы.ГруппаАвтоКатегоризация.ТекущаяСтраница = Элементы.ГруппаАвтокатегоризацияВыполнена;
			Иначе
				Элементы.ГруппаАвтоКатегоризация.ТекущаяСтраница = Элементы.ГруппаАвтокатегоризацияНеВыполнена;
			КонецЕсли;
		Иначе
			Элементы.ГруппаАвтоКатегоризация.Видимость = Ложь;
		КонецЕсли;
		Если ИспользоватьПроверкуКатегорий Тогда
			КатегорииПроверены = РаботаСКатегориямиДанных.ПолучитьФлагПроверкиКатегорийОбъекта(Объект.Ссылка);
			Если КатегорииПроверены Тогда
				Элементы.ГруппаПроверкаКатегорий.ТекущаяСтраница = Элементы.ГруппаКатегорииПроверены;
			Иначе
				Элементы.ГруппаПроверкаКатегорий.ТекущаяСтраница = Элементы.ГруппаКатегорииНеПроверены;
			КонецЕсли;
		Иначе
			Элементы.ГруппаПроверкаКатегорий.Видимость = Ложь;
		КонецЕсли;
	КонецЕсли;
	
	Если ИспользоватьШК Тогда
		
		ДанныеОШтрихкодеФайлаСервер = ШтрихкодированиеСервер.ПолучитьДанныеДляВставкиШтрихкодаВОбъект(Объект.Ссылка);
		Если ДанныеОШтрихкодеФайлаСервер <> Неопределено
			И ДанныеОШтрихкодеФайлаСервер.Свойство("ДвоичныеДанныеФайла")
			И ИспользоватьШК Тогда
			Попытка
				ВставкаШКВыполнялась = ШтрихкодированиеКлиентСервер.ВставитьШтрихкодВместоТэга(Объект.Ссылка,
					"ВставитьИзображениеШтрихкода", 
					Ложь, 
					ДанныеОШтрихкодеФайлаСервер.ДвоичныеДанныеИзображения, 
					ДанныеОШтрихкодеФайлаСервер.ДвоичныеДанныеФайла,
					ДанныеОШтрихкодеФайлаСервер.Расширение,
					ДанныеОШтрихкодеФайлаСервер.ФайлРедактируется,
					Ложь,
					ДанныеОШтрихкодеФайлаСервер.НастройкиШтрихкода.ВысотаШК,
					УникальныйИдентификатор);
				Прочитать();	
			Исключение
				Инфо = ИнформацияОбОшибке();
				Если Инфо.Описание = "ТэгНеНайден" Тогда
					ВставкаШКВыполнялась = Истина;
				КонецЕсли;
			КонецПопытки;	
		КонецЕсли;
	КонецЕсли;
	
	Если ПараметрыЗаписи.Свойство("ЭтоНовыйОбъект") И ПараметрыЗаписи.ЭтоНовыйОбъект = Истина Тогда
		РаботаСПоследнимиОбъектами.ЗаписатьОбращениеКОбъекту(Объект.Ссылка);
	КонецЕсли;
	
	Если ПараметрыЗаписи.Свойство("ЭтоНовыйОбъект") И ПараметрыЗаписи.ЭтоНовыйОбъект = Истина
		И ЗначениеЗаполнено(ПараметрыФайлОснование) Тогда
		РаботаСШаблонамиДокументовСервер.ЗаписатьИспользованиеШаблона(ПараметрыФайлОснование);
	КонецЕсли;	
	
	Если ЗначениеЗаполнено(ПараметрыФайлОснование) Тогда
		Элементы.ИконкаФайлаБольшая.Видимость = Истина;
		Элементы.ИконкаФайлаБольшаяНаОсновании.Видимость = Ложь;
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	#Если НЕ Вебклиент Тогда
		Если НовыйФайл
			И (ТипЗнч(Объект.ВладелецФайла) = Тип("СправочникСсылка.ВнутренниеДокументы")
				ИЛИ ТипЗнч(Объект.ВладелецФайла) = Тип("СправочникСсылка.ВходящиеДокументы") 
				ИЛИ ТипЗнч(Объект.ВладелецФайла) = Тип("СправочникСсылка.ИсходящиеДокументы"))
			И НастройкиАвтозаполнения <> Неопределено
			И НастройкиАвтозаполнения.МассивЗамен <> Неопределено 
			И НастройкиАвтозаполнения.МассивЗамен.Количество() > 0
			И НЕ АвтозаполнениеВыполнялось Тогда
			
			ДанныеВозврата = АвтозаполнениеШаблоновФайловКлиентСервер.ЗаполнитьПоляФайлаДаннымиВладельца(
				Истина,
				Объект.Ссылка,
				Истина,
				УникальныйИдентификатор,
				Объект.ВладелецФайла);
				
			Если ДанныеВозврата.Свойство("ДвоичныеДанныеЗаполненногоФайла") Тогда
				ДанныеОШтрихкодеФайла = Неопределено;
				ДвоичныеДанныеЗаполненногоФайла = ДанныеВозврата.ДвоичныеДанныеЗаполненногоФайла;
				Если НастройкиАвтозаполнения.Свойство("ДанныеОШтрихкоде", ДанныеОШтрихкодеФайла) И ИспользоватьШК Тогда
					ДвоичныеДанныеЗаполненногоФайлаСоШтрихкодом = ШтрихкодированиеКлиентСервер.ВставитьШтрихкодВместоТэга(
						Объект.Ссылка, 
						"ВставитьИзображениеШтрихкода",
						Истина,
						НастройкиАвтозаполнения.ДанныеОШтрихкоде.ДвоичныеДанныеИзображения,
						ДвоичныеДанныеЗаполненногоФайла,
						НастройкиАвтозаполнения.РасширениеФайла,
						Ложь,
						Ложь,
						НастройкиАвтозаполнения.ДанныеОШтрихкоде.НастройкиШтрихкода.ВысотаШК,
						УникальныйИдентификатор,
						Истина);
				КонецЕсли;
			КонецЕсли;
			Прочитать();
			Состояние();
		КонецЕсли;
	#КонецЕсли
	
	Если ПараметрыЗаписи.ЭтоНовый Тогда
		Если ЗначениеЗаполнено(ПараметрыОповещения) И ПараметрыОповещения.ТипСобытия = "Создание" Тогда
			ПараметрОповещения = Новый Структура;
			ПараметрОповещения.Вставить("Ссылка", Объект.Ссылка);
			ПараметрОповещения.Вставить("ПараметрСобытия", ПараметрыОповещения.ПараметрСобытия);
			Оповестить(ПараметрыОповещения.ИмяСобытия, ПараметрОповещения);
		КонецЕсли;
	КонецЕсли;
	
	Если НовыйФайл И Не НовыйФайлЗаписан Тогда 
		
		НовыйФайлЗаписан = Истина;
		
		ПараметрыОповещения = Новый Структура;
		ПараметрыОповещения.Вставить("Владелец", Объект.ВладелецФайла);
		ПараметрыОповещения.Вставить("Файл", Объект.Ссылка);
		ПараметрыОповещения.Вставить("ИдентификаторРодительскойФормы", Неопределено);
		ПараметрыОповещения.Вставить("Событие", "СозданФайл");
		Если ТипЗнч(ЭтаФорма.ВладелецФормы) = Тип("УправляемаяФорма") Тогда 
			ПараметрыОповещения.ИдентификаторРодительскойФормы = ЭтаФорма.ВладелецФормы.УникальныйИдентификатор;
		КонецЕсли;
		
		Оповестить("Запись_Файл", ПараметрыОповещения, Объект.Ссылка);
	Иначе
		Если НаименованиеДоЗаписи <> Объект.Наименование Тогда
			// в кеше обновить файл
			РаботаСФайламиКлиент.ОбновитьИнформациюВРабочемКаталоге(Объект.ТекущаяВерсия, Объект.Наименование);
			НаименованиеДоЗаписи = Объект.Наименование;
		КонецЕсли;
	КонецЕсли;
	
	ПараметрыОповещения = Новый Структура;
	ПараметрыОповещения.Вставить("Файл", Объект.Ссылка);
	ПараметрыОповещения.Вставить("Владелец", Объект.ВладелецФайла);
	
	Если НачальныйЯвляетсяОригиналом <> ЯвляетсяОригиналом Тогда 
		ПараметрыОповещения.Вставить("ИзменилсяПризнакОригинала", Истина);
		НачальныйЯвляетсяОригиналом = ЯвляетсяОригиналом;
	КонецЕсли;	
	
	Оповестить("ФайлИзменен", ПараметрыОповещения);
	
	Если ЗначениеЗаполнено(ПараметрыФайлОснование) Тогда
		ПараметрыПриложения["СтандартныеПодсистемы.ЕстьИспользованныеШаблоныФайлов"] = Истина;
	КонецЕсли;	
	
	УстановитьДоступностьЭлементовФормы();
	УстановитьДоступностьКомандСпискаЭП();
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)

	// СтандартныеПодсистемы.Свойства
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.Свойства") Тогда
		МодульУправлениеСвойствами = ОбщегоНазначения.ОбщийМодуль("УправлениеСвойствами");
		МодульУправлениеСвойствами.ОбработкаПроверкиЗаполнения(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.Свойства

	Если Объект.ХранитьВерсии <> ХранитьВерсииНачальноеЗначение И Не Объект.ХранитьВерсии
		И ЗначениеЗаполнено(Объект.ШаблонОснованиеДляСоздания)
		И ДелопроизводствоКлиентСервер.ЭтоДокумент(Объект.ВладелецФайла)
		И Не РольДоступна("ПолныеПрава") Тогда
		
		ТекстСообщения = НСтр("ru = 'Запрещено снимать флаг ""Хранить версии"" для файлов, созданных по шаблону.'; en = 'It is forbidden to unselect ""Keep versions"" for files created from the template.'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,"Объект.ХранитьВерсии",,Отказ);
		
	КонецЕсли;	
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ТАБЛИЦЫ ФОРМЫ ТАБЛИЦА ПОДПИСЕЙ

&НаКлиенте
Процедура ТаблицаПодписейВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	РаботаСЭПКлиент.ОткрытьПодпись(Элементы.ЭлектронныеПодписи.ТекущиеДанные, УникальныйИдентификатор);
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаПодписейПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)

	Отказ = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаПодписейПередНачаломИзменения(Элемент, Отказ)
	
	Отказ = Истина;
	РаботаСЭПКлиент.ОткрытьПодпись(Элементы.ЭлектронныеПодписи.ТекущиеДанные, УникальныйИдентификатор);
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаПодписейПередУдалением(Элемент, Отказ)
	
	Отказ = Истина;
	УдалитьПодписиЗапуск();

КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

// Функция предназначена для копирования последней версии
// из Файла-источника в Файл-приемник
// Параметры:
//	Приемник - ссылка на "Файл" куда копируется прилинкованный Файл
//	Источник - ссылка на "Файл" откуда копируется прилинкованный Файл
&НаСервере
Функция СоздатьКопиюВерсии(Приемник, Источник, КопироватьПодписиЭП, ЗаполнятьШаблон = Ложь)	

	Если Источник.ТекущаяВерсия.Пустая() Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ХранилищеФайла = Неопределено;
	Если Источник.ТекущаяВерсия.ТипХраненияФайла = Перечисления.ТипыХраненияФайлов.ВИнформационнойБазе Тогда 
		ХранилищеФайла = РаботаСФайламиВызовСервера.ПолучитьХранилищеФайлаИзИнформационнойБазы(Источник.ТекущаяВерсия);
	КонецЕсли;	
	
	СведенияОФайле = РаботаСФайламиКлиентСервер.СведенияОФайле("ФайлСВерсией");
	СведенияОФайле.ИмяБезРасширения = Объект.Наименование;
	СведенияОФайле.Размер = Источник.ТекущаяВерсия.Размер;
	СведенияОФайле.РасширениеБезТочки = Источник.ТекущаяВерсия.Расширение;
	СведенияОФайле.АдресВременногоХранилищаФайла = ХранилищеФайла;
	СведенияОФайле.АдресВременногоХранилищаТекста = Источник.ТекущаяВерсия.ТекстХранилище;
	СведенияОФайле.СсылкаНаВерсиюИсточник = Источник.ТекущаяВерсия;
	СведенияОФайле.ВремяИзменения = Источник.ТекущаяВерсия.ДатаМодификацииФайла;
	СведенияОФайле.ВремяИзмененияУниверсальное = Источник.ТекущаяВерсия.ДатаМодификацииУниверсальная;
	Версия = РаботаСФайламиВызовСервера.СоздатьВерсию(Приемник, СведенияОФайле);
	
	// Обновим форму Файла (ведь запись может произойти и не при закрытии формы)
	Объект.ТекущаяВерсия = Версия.Ссылка;
	
	// Обновим запись в информационной базе
	РаботаСФайламиВызовСервера.ОбновитьВерсиюВФайле(Приемник, 
		Версия, 
		Источник.ТекущаяВерсия.ТекстХранилище, 
		УникальныйИдентификатор);
		
	Кодировка = РаботаСФайламиВызовСервера.ПолучитьКодировкуВерсииФайла(Источник.ТекущаяВерсия);
		
	Если Кодировка <> Неопределено Тогда
		РаботаСФайламиВызовСервера.ЗаписатьКодировкуВерсииФайла(Приемник.ТекущаяВерсия, Кодировка);
	КонецЕсли;	
		
	ПараметрыРаспознавания = РаботаСФайламиВызовСервера.ПодготовитьПараметрыРаспознавания();
	Если ПараметрыРаспознавания <> Неопределено И ПараметрыРаспознавания.Свойство("РаспознатьПослеДобавления") И ПараметрыРаспознавания.РаспознатьПослеДобавления Тогда
		РаспознатьНемедленно = Ложь;
		ОписаниеОшибки = "";
		РаспознанныйТекст = "";
		РаботаСФайламиВызовСервера.РаспознатьФайл(Объект.Ссылка, 
			ПараметрыРаспознавания, 
			ОписаниеОшибки, 
			РаспознанныйТекст, 
			УникальныйИдентификатор,  
			РаспознатьНемедленно);
	КонецЕсли;
		
	Прочитать();		

	Если КопироватьПодписиЭП Тогда
		
		ФайлОбъект = Объект.Ссылка.ПолучитьОбъект();
		ФайлОбъект.ПодписанЭП = Истина;
		ФайлОбъект.Записать();
		
		ВерсияОбъект = Объект.ТекущаяВерсия.ПолучитьОбъект();
		
		РаботаСЭП.СкопироватьВсеПодписи(Источник.ТекущаяВерсия, Объект.ТекущаяВерсия);
		
		ВерсияОбъект.ПодписанЭП = Истина;
		ВерсияОбъект.Записать();
		
		ПрочитатьИЗаполнитьПодписи();
		
	КонецЕсли;

	Если Источник.Зашифрован Тогда
		
		ФайлОбъект = Объект.Ссылка.ПолучитьОбъект();
		ФайлОбъект.Зашифрован = Истина;
		
		Для Каждого Строка Из Источник.СертификатыШифрования Цикл
			НоваяСтрока = ФайлОбъект.СертификатыШифрования.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
		КонецЦикла;
		
		// чтобы прошла запись ранее подписанного объекта
		ФайлОбъект.ДополнительныеСвойства.Вставить("ЗаписьПодписанногоОбъекта", Истина);
		ФайлОбъект.Записать();
		
		ВерсияОбъект = Объект.ТекущаяВерсия.ПолучитьОбъект();
		ВерсияОбъект.Зашифрован = Истина;
		// чтобы прошла запись ранее подписанного объекта
		ВерсияОбъект.ДополнительныеСвойства.Вставить("ЗаписьПодписанногоОбъекта", Истина);
		ВерсияОбъект.Записать();
		
		ПрочитатьИЗаполнитьШифрование();
		
	КонецЕсли;	
	
	Если ИспользоватьАвтозаполнениеФайлов 
		И ЗаполнятьШаблон 
		И (ТипЗнч(Объект.ВладелецФайла) = Тип("СправочникСсылка.ВнутренниеДокументы")
		ИЛИ ТипЗнч(Объект.ВладелецФайла) = Тип("СправочникСсылка.ПапкиФайлов") 
		ИЛИ ТипЗнч(Объект.ВладелецФайла) = Тип("СправочникСсылка.ИсходящиеДокументы")) Тогда
		РезультатЗаполнения = АвтозаполнениеШаблоновФайловКлиентСервер.ЗаполнитьПоляФайлаДаннымиВладельца(Ложь, Объект.Ссылка, Истина, УникальныйИдентификатор);
		Если РезультатЗаполнения.Результат Тогда
			АвтозаполнениеВыполнялось = Истина;
		КонецЕсли;
		Прочитать();
	КонецЕсли;

КонецФункции // СоздатьКопиюВерсии()

// Устанавливает доступность команд и элементов формы
&НаКлиенте
Процедура УстановитьДоступностьЭлементовФормы()
	
	ДоступныДействияСФайлом = Не Объект.ТекущаяВерсия.Пустая() И Не Объект.Ссылка.Пустая();
	
	РедактируетТекущийПользователь = (Объект.Редактирует = ПользователиКлиентСервер.ТекущийПользователь());
	РедактируетДругой = Не Объект.Редактирует.Пустая() И Не РедактируетТекущийПользователь;
	
	Элементы.ХранитьВерсии.Доступность = ДоступныДействияСФайлом И Не Объект.ПометкаУдаления;
	Элементы.ОтменитьРедактирование.Доступность = Не Объект.Редактирует.Пустая();
	Элементы.ОткрытьКаталогФайла.Доступность = ДоступныДействияСФайлом;
	Элементы.СохранитьКак.Доступность = ДоступныДействияСФайлом;
	
	Элементы.Редактировать.Доступность = НЕ Объект.ПодписанЭП И Не РедактируетДругой И ДоступностьРедактированияПоСостояниюБП И РазрешеноРедактирование;
	Элементы.ФормаРедактировать.Доступность = НЕ Объект.ПодписанЭП И Не РедактируетДругой И ДоступностьРедактированияПоСостояниюБП И РазрешеноРедактирование;
	
	Элементы.ЗакончитьРедактирование.Доступность = ДоступныДействияСФайлом И РазрешеноРедактирование И РедактируетТекущийПользователь;
	Элементы.ФормаЗакончитьРедактирование.Доступность = ДоступныДействияСФайлом И РазрешеноРедактирование И РедактируетТекущийПользователь;
	
	//Элементы.ПолноеНаименование.ТолькоПросмотр = НЕ Объект.Редактирует.Пустая();
	Элементы.Занять.Доступность = Объект.Редактирует.Пустая() И (ДоступныДействияСФайлом) И НЕ Объект.ПодписанЭП И РазрешеноРедактирование;
	Элементы.СохранитьИзменения.Доступность = Не Объект.Редактирует.Пустая() И РазрешеноРедактирование;
	
	Элементы.ОбновитьИзФайлаНаДиске.Доступность = ДоступныДействияСФайлом И НЕ Объект.ПодписанЭП И РазрешеноРедактирование;
	
	Элементы.ФормаПодписать.Доступность = (ДоступныДействияСФайлом И Объект.Редактирует.Пустая() И НЕ Объект.Зашифрован) ИЛИ НЕ ДоступныДействияСФайлом;
	Элементы.ФормаЗашифровать.Доступность = (ДоступныДействияСФайлом И Объект.Редактирует.Пустая() И НЕ Объект.Зашифрован) ИЛИ НЕ ДоступныДействияСФайлом;
	
	Элементы.ФормаДобавитьЭПИзФайла.Доступность = ДоступныДействияСФайлом И Объект.Редактирует.Пустая() И НЕ Объект.Зашифрован;
	Элементы.ФормаСохранитьВместеСЭП.Доступность = ДоступныДействияСФайлом И Объект.ПодписанЭП;
	Элементы.ФормаРасшифровать.Доступность = ДоступныДействияСФайлом И Объект.Зашифрован;
	
	Элементы.ФормаПросмотретьТекстовыйОбраз.Доступность = ДоступныДействияСФайлом;
	
	Расширение = Объект.ТекущаяВерсияРасширение;
	ИспользоватьImageMagickДляРаспознаванияPDF = ФайловыеФункцииКлиентПовтИсп.ПолучитьПерсональныеНастройкиРаботыСФайлами().ИспользоватьImageMagickДляРаспознаванияPDF;
	РасширениеПоддерживается = РаботаСФайламиКлиентСервер.ЭтотФайлМожноРаспознать(Расширение, ИспользоватьImageMagickДляРаспознаванияPDF);
	
	Если ДоступныДействияСФайлом Тогда
		Элементы.ФормаРаспознать.Доступность = Объект.Редактирует.Пустая() И ДоступныДействияСФайлом И РасширениеПоддерживается;
	Иначе
		Элементы.ФормаРаспознать.Доступность = Ложь;
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ПолноеНаименованиеПриИзменении(Элемент)
	
	Объект.ПолноеНаименование = СокрЛП(Объект.ПолноеНаименование);
	Попытка
		ФайловыеФункцииКлиент.КорректноеИмяФайла(Объект.ПолноеНаименование, Истина);
	Исключение
		Информация = ИнформацияОбОшибке();
		ПоказатьПредупреждение(, Информация.Описание);
	КонецПопытки;
	
	Объект.Наименование = СокрЛП(Объект.ПолноеНаименование);
	
	ИмяИРасширение = ОбщегоНазначенияКлиентСервер.ПолучитьИмяСРасширением(Объект.ПолноеНаименование, 
		Объект.ТекущаяВерсияРасширение);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьВыполнить()
	
	Записать();
	Прочитать();
	
КонецПроцедуры

&НаКлиенте
Процедура СкопироватьВыполнить()
	
	РаботаСФайламиКлиент.СкопироватьФайл(Объект.ВладелецФайла, Объект.Ссылка);

КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСписокШифрования()
	
	СертификатыШифрования.Очистить();
	
	Если Объект.Зашифрован Тогда
		МассивСертификатовШифрования = РаботаСФайламиВызовСервера.ПолучитьМассивСертификатовШифрования(Объект.Ссылка);	
		Для Каждого СтруктураСертификата Из МассивСертификатовШифрования Цикл
			НоваяСтрока = СертификатыШифрования.Добавить();
			НоваяСтрока.Представление = СтруктураСертификата.Представление;
			НоваяСтрока.Отпечаток = СтруктураСертификата.Отпечаток;
			Если СтруктураСертификата.Сертификат <> Неопределено Тогда
				НоваяСтрока.АдресСертификата = ПоместитьВоВременноеХранилище(СтруктураСертификата.Сертификат, УникальныйИдентификатор);
			КонецЕсли;	
		КонецЦикла;	
	КонецЕсли;
	
	ТекстЗаголовка = НСтр("ru = 'Зашифрован для'; en = 'Encrypted for'");
	Если СертификатыШифрования.Количество() <> 0 Тогда
		ТекстЗаголовка = ТекстЗаголовка + " (" + Строка(СертификатыШифрования.Количество()) + ")";
	КонецЕсли;	
	Элементы.ГруппаШифрование.Заголовок = ТекстЗаголовка;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСписокПодписей()
	
	ЭлектронныеПодписи.Очистить();
		
	Если Объект.ПодписанЭП Тогда
		РаботаСЭП.ЗаполнитьСписокПодписейФайла(Объект.ТекущаяВерсия, ЭлектронныеПодписи, , , Истина);
	КонецЕсли;
	
	Если ЭлектронныеПодписи.Количество() = 0 Тогда
		ТекстЗаголовка = НСтр("ru = 'ЭП'; en = 'DS'");
	Иначе
		ТекстЗаголовка = СтрШаблон(
			НСтр("ru = 'ЭП (%1)'; en = 'DS (%1)'"),
			Формат(ЭлектронныеПодписи.Количество(), "ЧГ="));
	КонецЕсли;
	Элементы.ГруппаЭП.Заголовок = ТекстЗаголовка;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьПредставлениеВладельца()
	
	ТипВладельца = ТипЗнч(Объект.ВладелецФайла);
	ТипВладельцаСтрока = Строка(ТипВладельца);
	ПолныйПутьОписание = Строка(Объект.ВладелецФайла);
	
	Если ТипВладельца = Тип("СправочникСсылка.ПапкиФайлов") Тогда
		ТипВладельцаСтрока = НСтр("ru='Папка'; en = 'Folder'");
		ПолныйПутьОписание = ПолныйПуть;
	ИначеЕсли ТипВладельца = Тип("ДокументСсылка.ВходящееПисьмо") Тогда
		ТипВладельцаСтрока = НСтр("ru='Вх письмо'; en = 'Incoming email'");
	ИначеЕсли ТипВладельца = Тип("ДокументСсылка.ИсходящееПисьмо") Тогда
		ТипВладельцаСтрока = НСтр("ru='Исх письмо'; en = 'Outgoing email'");		
	ИначеЕсли ТипВладельца = Тип("СправочникСсылка.ВходящиеДокументы") Тогда
		ТипВладельцаСтрока = НСтр("ru='Вх документ'; en = 'Incoming document'");
	ИначеЕсли ТипВладельца = Тип("СправочникСсылка.ИсходящиеДокументы") Тогда
		ТипВладельцаСтрока = НСтр("ru='Исх документ'; en = 'Outgoing document'");
	ИначеЕсли ТипВладельца = Тип("СправочникСсылка.ВнутренниеДокументы") Тогда
		ТипВладельцаСтрока = НСтр("ru='Внутр документ'; en = 'Internal document'");
	КонецЕсли;	
	
	Элементы.ПолныйПутьОписание.Заголовок = ТипВладельцаСтрока;
	
КонецПроцедуры	

&НаСервере
Процедура ОбновитьПолныйПуть()
	
	ПапкаРодитель = Объект.ВладелецФайла;
	
	Если ТипЗнч(ПапкаРодитель) = Тип("СправочникСсылка.ПапкиФайлов") Тогда
		
		Если ЗначениеЗаполнено(ПапкаРодитель) Тогда
		
			ПолныйПуть = "";
			
			Пока ЗначениеЗаполнено(ПапкаРодитель) Цикл
				
				Если Не ПустаяСтрока(ПолныйПуть) Тогда
					ПолныйПуть = "\" + ПолныйПуть;
				КонецЕсли;	
				
				ПолныйПуть = Строка(ПапкаРодитель) + ПолныйПуть;
				
				ПапкаРодитель = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ПапкаРодитель, "Родитель");
				Если Не ЗначениеЗаполнено(ПапкаРодитель) Тогда
					Прервать;
				КонецЕсли;	
				
			КонецЦикла;

			Элементы.Владелец.Подсказка = ПолныйПуть;
			
		КонецЕсли;	
			
	КонецЕсли;	
	
КонецПроцедуры	

// Выполняет открытие файла на просмотр
&НаКлиенте
Процедура ОткрытьФайлВыполнить()
	
	Если Объект.Ссылка.Пустая() Тогда
		ОчиститьСообщения();
		Если Не Записать() Тогда 
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	ДанныеФайла = РаботаСФайламиВызовСервера.ДанныеФайлаДляОткрытия(Объект.Ссылка, Неопределено, 
		УникальныйИдентификатор);
		
	Если ДанныеФайла.Зашифрован И (ДанныеФайла.МассивСертификатовШифрования.Количество() = 0) Тогда
		Если Модифицированность Тогда
			Если Не Записать() Тогда 
				Возврат;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;	

	КомандыРаботыСФайламиКлиент.Открыть(ДанныеФайла, УникальныйИдентификатор);
	
КонецПроцедуры

&НаКлиенте
Процедура Редактировать(Команда)
	
	Если Объект.Ссылка.Пустая() Тогда
		ОчиститьСообщения();
		Если Не Записать() Тогда 
			Возврат;
		КонецЕсли;	
	КонецЕсли;	
	
	Если Модифицированность Тогда
		Записать();
	КонецЕсли;
	
	Обработчик = Новый ОписаниеОповещения("ПрочитатьИУстановитьДоступностьЭлементовФормы", ЭтотОбъект);
	РаботаСФайламиКлиент.РедактироватьСОповещением(Обработчик, Объект.Ссылка, УникальныйИдентификатор);
	
КонецПроцедуры

&НаКлиенте
Процедура ПрочитатьИУстановитьДоступностьЭлементовФормы(Результат, ПараметрыВыполнения) Экспорт
	Прочитать();
	УстановитьДоступностьЭлементовФормы();
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьДанныеФайлаЕслиНекорректны()
	
	Если ДанныеФайла = Неопределено ИЛИ НЕ ДанныеФайлаКорректны Тогда
		ДанныеФайла = РаботаСФайламиВызовСервера.ДанныеФайла(Объект.Ссылка);
		ДанныеФайлаКорректны = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗакончитьРедактирование(Команда)
	
	Если Объект.Редактирует.Пустая() Тогда
		
		СтрокаСообщения = Новый ФорматированнаяСтрока(
			НСтр("ru = 'Нельзя закончить редактирование файла '; en = 'You cannot finish editing a file '"),
			Новый ФорматированнаяСтрока(Строка(Объект.Ссылка), Новый Шрифт(,,Истина)),
			НСтр("ru=', т.к. он не занят для редактирования.'; en = ', because it is not locked for editing.'") );	
			
		ПоказатьПредупреждение(,СтрокаСообщения);	
		
		Возврат;
		
	КонецЕсли;	
	
	Если Модифицированность Тогда
		Записать();
	КонецЕсли;	
	
	ПолучитьДанныеФайлаЕслиНекорректны();
	
	Обработчик = Новый ОписаниеОповещения("ПрочитатьИУстановитьДоступностьЭлементовФормы", ЭтотОбъект);
	ПараметрыОбновленияФайла = РаботаСФайламиКлиент.ПараметрыОбновленияФайла(Обработчик, ДанныеФайла.Ссылка, УникальныйИдентификатор);
	ПараметрыОбновленияФайла.ХранитьВерсии = ДанныеФайла.ХранитьВерсии;
	ПараметрыОбновленияФайла.РедактируетТекущийПользователь = ДанныеФайла.РедактируетТекущийПользователь;
	ПараметрыОбновленияФайла.Редактирует = ДанныеФайла.Редактирует;
	РаботаСФайламиКлиент.ЗакончитьРедактированиеСОповещением(ПараметрыОбновленияФайла);
	
КонецПроцедуры

&НаКлиенте
Процедура Занять(Команда)
	
	Если Модифицированность Тогда
		Записать();
	КонецЕсли;	
	
	Обработчик = Новый ОписаниеОповещения("ПрочитатьИУстановитьДоступностьЭлементовФормы", ЭтотОбъект);
	
	РаботаСФайламиКлиент.ЗанятьСОповещением(Обработчик, Объект.Ссылка, УникальныйИдентификатор);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтменитьРедактирование(Команда)
	
	ОтменитьРедактированиеВыполнить();
		
КонецПроцедуры

&НаКлиенте
Процедура ОтменитьРедактированиеВыполнить() Экспорт
	
	Если Объект.Редактирует.Пустая() Тогда
		
		СтрокаСообщения = Новый ФорматированнаяСтрока(
			НСтр("ru = 'Нельзя освободить файл '; en = 'You cannot unlock file '"),
			Новый ФорматированнаяСтрока(Строка(Объект.Ссылка), Новый Шрифт(,,Истина)),
			НСтр("ru=', т.к. он не занят для редактирования.'; en = ', because it is not locked for editing.'") );	
			
		ПоказатьПредупреждение(,СтрокаСообщения);	
		
		Возврат;
		
	КонецЕсли;	
	
	Если Модифицированность Тогда
		Записать();
	КонецЕсли;	
	
	ПолучитьДанныеФайлаЕслиНекорректны();
	
	Обработчик = Новый ОписаниеОповещения("ПрочитатьИУстановитьДоступностьЭлементовФормы", ЭтотОбъект);
	
	ПараметрыОсвобожденияФайла = РаботаСФайламиКлиент.ПараметрыОсвобожденияФайла(Обработчик, ДанныеФайла.Ссылка);
	ПараметрыОсвобожденияФайла.ХранитьВерсии = ДанныеФайла.ХранитьВерсии;	
	ПараметрыОсвобожденияФайла.РедактируетТекущийПользователь = ДанныеФайла.РедактируетТекущийПользователь;	
	ПараметрыОсвобожденияФайла.Редактирует = ДанныеФайла.Редактирует;	
	ПараметрыОсвобожденияФайла.УникальныйИдентификатор = УникальныйИдентификатор;	
	РаботаСФайламиКлиент.ОсвободитьФайлСОповещением(ПараметрыОсвобожденияФайла);
		
КонецПроцедуры

&НаКлиенте
Процедура СохранитьИзменения(Команда)
	
	СохранитьИзмененияВыполнить();
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьИзмененияВыполнить() Экспорт
	
	Если Объект.Редактирует.Пустая() Тогда
		
		СтрокаСообщения = Новый ФорматированнаяСтрока(
			НСтр("ru = 'Нельзя сохранить изменения файла '; en = 'You cannot save changes to a file '"),
			Новый ФорматированнаяСтрока(Строка(Объект.Ссылка), Новый Шрифт(,,Истина)),
			НСтр("ru=', т.к. он не занят для редактирования.'; en = ', because it is not locked for editing.'") );	
			
		ПоказатьПредупреждение(,СтрокаСообщения);	
		
		Возврат;
		
	КонецЕсли;	
	
	Если Модифицированность Тогда
		Записать();
	КонецЕсли;	
	
	Обработчик = Новый ОписаниеОповещения("ПрочитатьИУстановитьДоступностьЭлементовФормы", ЭтотОбъект);
	РаботаСФайламиКлиент.СохранитьИзмененияФайлаСОповещением(Обработчик, Объект.Ссылка, УникальныйИдентификатор);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьКаталогФайла(Команда)
	
	ОткрытьКаталогФайлаВыполнить();
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьКаталогФайлаВыполнить() Экспорт
	
	ДанныеФайла = РаботаСФайламиВызовСервера.ДанныеФайлаДляОткрытия(Объект.Ссылка, Неопределено, УникальныйИдентификатор);
	КомандыРаботыСФайламиКлиент.ОткрытьКаталогФайла(ДанныеФайла);
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьКак(Команда)
	
	ДанныеФайла = РаботаСФайламиВызовСервера.ДанныеФайлаДляСохранения(Объект.Ссылка, Неопределено, УникальныйИдентификатор);
	КомандыРаботыСФайламиКлиент.СохранитьКак(ДанныеФайла, УникальныйИдентификатор);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьИзФайлаНаДиске(Команда)
	
	ОбновитьИзФайлаНаДискеВыполнить();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьИзФайлаНаДискеВыполнить() Экспорт
	
	Если Объект.Ссылка.Пустая() Тогда
		ОчиститьСообщения();
		Если Не Записать() Тогда 
			Возврат;
		КонецЕсли;	
	КонецЕсли;	
	
	Если Модифицированность Тогда
		Записать();
	КонецЕсли;	
	
	ДанныеФайла = РаботаСФайламиВызовСервера.ДанныеФайла(Объект.Ссылка);
	
	Обработчик = Новый ОписаниеОповещения("ПрочитатьИУстановитьДоступностьЭлементовФормы", ЭтотОбъект);
	
	РаботаСФайламиКлиент.ОбновитьИзФайлаНаДискеСОповещением(
		Обработчик,
		ДанныеФайла,
		УникальныйИдентификатор);
	
КонецПроцедуры

&НаКлиенте
Процедура Распознать(Команда)
	
	Обработчик = Новый ОписаниеОповещения("ОбновитьКарточку", ЭтотОбъект);
	КомандыРаботыСФайламиКлиент.РаспознатьФайлСОповещением(Обработчик, Объект.Ссылка, УникальныйИдентификатор);
	
КонецПроцедуры

Процедура ОбновитьКарточку(Результат, ПараметрыВыполнения) Экспорт
	
	Прочитать();
	
КонецПроцедуры

&НаСервере
Процедура СортироватьСписокИУстановитьКоличествоКатегорийВЗаголовок()
	
	СписокКатегорийДанных.Сортировать("ПолноеНаименование");
	Элементы.Категории.Заголовок = РаботаСКатегориямиДанныхКлиентСервер.ПолучитьЗаголовокВкладкиКатегории(СписокКатегорийДанных);	
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьДанныеОКатегориях(Параметр)
	
	Для Каждого ВыбраннаяКатегория Из СписоккатегорийДанных Цикл
		ВыбраннаяКатегория.ПолноеНаименование = РаботаСКатегориямиДанных.ПолучитьПолныйПутьКатегорииДанных(ВыбраннаяКатегория.Значение);
	КонецЦикла;
	
КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ ПОДСИСТЕМЫ СВОЙСТВ

&НаКлиенте
Процедура Подключаемый_РедактироватьСоставСвойств()
	
	Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("СтандартныеПодсистемы.Свойства") Тогда
		МодульУправлениеСвойствамиКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("УправлениеСвойствамиКлиент");
		МодульУправлениеСвойствамиКлиент.РедактироватьСоставСвойств(ЭтотОбъект, Объект.Ссылка);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьЭлементыДополнительныхРеквизитов()
	
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.Свойства") Тогда
		МодульУправлениеСвойствами = ОбщегоНазначения.ОбщийМодуль("УправлениеСвойствами");
		МодульУправлениеСвойствами.ОбновитьЭлементыДополнительныхРеквизитов(ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьЗависимостиДополнительныхРеквизитов()
      УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры
 
&НаКлиенте
Процедура Подключаемый_ПриИзмененииДополнительногоРеквизита(Элемент)
      УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры

&НаСервере
Процедура ПрочитатьИЗаполнитьПодписи()
	
	Прочитать();
	ДанныеФайла = РаботаСФайламиВызовСервера.ДанныеФайла(Объект.Ссылка);
	ДанныеФайлаКорректны = Истина;
	ЗаполнитьСписокПодписей();
	
КонецПроцедуры	

&НаСервере
Процедура ПрочитатьИЗаполнитьШифрование()
	Прочитать();
	ДанныеФайла = РаботаСФайламиВызовСервера.ДанныеФайла(Объект.Ссылка);
	ДанныеФайлаКорректны = Истина;
	ЗаполнитьСписокШифрования();
КонецПроцедуры	

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ ПОДСИСТЕМЫ СВОЙСТВ

&НаКлиенте
Процедура ВладелецПриИзменении(Элемент)
	
	Если ТипЗнч(Объект.ВладелецФайла) = Тип("СправочникСсылка.ПапкиФайлов") Тогда
		ОбновитьПолныйПуть();
		Элементы.СубъектыПерсональныхДанных.ТолькоПросмотр = Ложь;
		Элементы.Категории.Видимость = Истина;
		Элементы.ЗаполнитьДаннымиВладельца.Видимость = Ложь;
	Иначе	
		Элементы.СубъектыПерсональныхДанных.ТолькоПросмотр = Истина;
		Элементы.Категории.Видимость = Ложь;
		СписокКатегорийДанных.Очистить();
		Элементы.ЗаполнитьДаннымиВладельца.Видимость = Истина;
	КонецЕсли;	
	
	ТипВладельца = ТипЗнч(Объект.ВладелецФайла);
	Элементы.Владелец.Заголовок = ТипВладельца;
	
	ОбновитьПредставлениеВладельца();
	
КонецПроцедуры

&НаКлиенте
Функция ТекстВопросаНезаписанныхДанных(НазваниеКоманды)
	
	Возврат СтрШаблон(
		НСтр("ru = 'Данные еще не записаны.
             |Выполнение действия ""%1"" возможно только после записи данных.
             |Данные будут записаны.';
             |en = 'The data have not yet been saved.
             |Performing the action ""%1"" is possible only after the data is saved.
             |Data will be saved.'"),
		НазваниеКоманды);
	
КонецФункции

&НаКлиенте
Процедура Подписать(Команда)
	
	Если Объект.Ссылка.Пустая() Тогда
		
		ОписаниеОповещения = Новый ОписаниеОповещения(
			"ПодписатьПослеОтветаНаВопросСохранения",
			ЭтотОбъект);
		
		ТекстВопроса = ТекстВопросаНезаписанныхДанных(НСтр("ru = 'Подписать'; en = 'Sign'"));
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ОКОтмена);
		Возврат;
		
	Иначе
		
		Если Модифицированность Тогда
			Если Не Записать() Тогда
				Возврат;
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
	ПодписатьПродолжение();
	
КонецПроцедуры

&НаКлиенте
Процедура ПодписатьПослеОтветаНаВопросСохранения(Ответ, ПараметрыВыполнения) Экспорт
	
	Если Ответ <> КодВозвратаДиалога.ОК Тогда 
		Возврат;
	КонецЕсли;
    	
	Если Не Записать() Тогда 
		Возврат;
	КонецЕсли;
	
	ПоказатьОповещениеПользователя(
		НСтр("ru = 'Создание:'; en = 'Added:'"), 
		ПолучитьНавигационнуюСсылку(Объект.Ссылка),
		Строка(Объект.Ссылка),
		БиблиотекаКартинок.Информация32);
		
	ПодписатьПродолжение();
	
КонецПроцедуры

&НаКлиенте
Процедура ПодписатьПродолжение()
	
	МассивФайлов = Новый Массив;
	МассивФайлов.Добавить(Объект.Ссылка);
	
	ДанныеФайла = РаботаСФайламиВызовСервера.ДанныеФайла(Объект.Ссылка);
	
	ПараметрыВыполнения = Новый Структура;
	ПараметрыВыполнения.Вставить("ВладелецФайла", ДанныеФайла.Владелец);
	ОписаниеОповещения = Новый ОписаниеОповещения("ПодписатьЗавершение", ЭтотОбъект, ПараметрыВыполнения);
	
	РаботаСФайламиСлужебныйКлиент.ПодписатьФайл(МассивФайлов, УникальныйИдентификатор, ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ПодписатьЗавершение(Результат, ПараметрыВыполнения) Экспорт
	
	Если Не Результат Тогда
		Возврат;
	КонецЕсли;
	
	Оповестить("ПрисоединенныйФайлПодписан", ПараметрыВыполнения.ВладелецФайла);
	
	ПодписатьЗавершениеВызовСервера();
	
	УстановитьДоступностьЭлементовФормы();
	УстановитьДоступностьКомандСпискаЭП();
	
КонецПроцедуры

&НаСервере
Процедура ПодписатьЗавершениеВызовСервера()
	
	ПрочитатьИЗаполнитьПодписи();
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьЭПИзФайла(Команда)
	
	РаботаСФайламиСлужебныйКлиент.ДобавитьПодписьИзФайла(
		Объект.Ссылка,
		УникальныйИдентификатор,
		Новый ОписаниеОповещения("ДобавитьЭПИзФайлаЗавершение", ЭтотОбъект));
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьЭПИзФайлаЗавершение(Результат, ПараметрыВыполнения) Экспорт
	
	Если Результат = Истина Тогда
		УстановитьДоступностьЭлементовФормы();
		ЗаполнитьСписокПодписей();
		УстановитьДоступностьКомандСпискаЭП();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьВместеСЭП(Команда)
	
	РаботаСФайламиСлужебныйКлиент.СохранитьФайлВместеСПодписью(Объект.Ссылка, УникальныйИдентификатор);
	
КонецПроцедуры

&НаКлиенте
Процедура Зашифровать(Команда)
	
	Если Объект.Ссылка.Пустая() Тогда 
		
		ОписаниеОповещения = Новый ОписаниеОповещения(
			"ЗашифроватьПослеОтветаНаВопросЗаписать",
			ЭтотОбъект);
			
		ТекстВопроса = ТекстВопросаНезаписанныхДанных(НСтр("ru = 'Зашифровать'; en = 'Encrypt'"));
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ОКОтмена);
		Возврат;
		
	КонецЕсли;
	
	ЗашифроватьПродолжение();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗашифроватьПослеОтветаНаВопросЗаписать(Ответ, ПараметрыВыполнения) Экспорт
	
	Если Ответ <> КодВозвратаДиалога.ОК Тогда 
		Возврат;
	КонецЕсли;
    	
	Если Не Записать() Тогда 
		Возврат;
	КонецЕсли;
	
	ПоказатьОповещениеПользователя(
		НСтр("ru = 'Создание:'; en = 'Added:'"), 
		ПолучитьНавигационнуюСсылку(Объект.Ссылка),
		Строка(Объект.Ссылка),
		БиблиотекаКартинок.Информация32);
		
	ЗашифроватьПродолжение();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗашифроватьПродолжение() Экспорт
	
	ДанныеФайла = РаботаСФайламиВызовСервера.ПолучитьДанныеФайлаИКоличествоВерсий(Объект.Ссылка);
	
	ПараметрыОбработчика = Новый Структура;
	ПараметрыОбработчика.Вставить("ДанныеФайла", ДанныеФайла);
	ПараметрыОбработчика.Вставить("ОбъектСсылка", Объект.Ссылка);
	Обработчик = Новый ОписаниеОповещения("ЗашифроватьПослеШифрованияНаКлиенте", ЭтотОбъект, ПараметрыОбработчика);
	
	РаботаСФайламиСлужебныйКлиент.Зашифровать(Обработчик, ДанныеФайла, УникальныйИдентификатор);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗашифроватьПослеШифрованияНаКлиенте(Результат, ПараметрыВыполнения) Экспорт
	
	Если Не Результат.Успех Тогда
		Возврат;
	КонецЕсли;
	
	ФайловыеФункцииКлиент.ПроинициализироватьПутьКРабочемуКаталогу();
	ИмяРабочегоКаталога = ФайловыеФункцииКлиентПовтИсп.ПолучитьПерсональныеНастройкиРаботыСФайлами().ПутьКЛокальномуКэшуФайлов;
	
	МассивФайловВРабочемКаталогеДляУдаления = Новый Массив;
	ЕстьЗашифрованныеИлиЗанятыеФайлы = Неопределено;
	
	ЗашифроватьСервер(
		Результат.МассивДанныхДляЗанесенияВБазу,
		Результат.МассивОтпечатков,
		МассивФайловВРабочемКаталогеДляУдаления,
		ИмяРабочегоКаталога,
		ПараметрыВыполнения.ОбъектСсылка,
		ЕстьЗашифрованныеИлиЗанятыеФайлы);
	
	РаботаСФайламиКлиент.ИнформироватьОШифровании(
		МассивФайловВРабочемКаталогеДляУдаления,
		ПараметрыВыполнения.ДанныеФайла.Владелец,
		ПараметрыВыполнения.ОбъектСсылка,
		ЕстьЗашифрованныеИлиЗанятыеФайлы);
	
	УстановитьДоступностьЭлементовФормы();
	УстановитьДоступностьКомандСпискаШифрования();
	
КонецПроцедуры

&НаСервере
Процедура ЗашифроватьСервер(МассивДанныхДляЗанесенияВБазу, МассивОтпечатков, 
	МассивФайловВРабочемКаталогеДляУдаления,
	ИмяРабочегоКаталога, ОбъектСсылка, ЕстьЗашифрованныеИлиЗанятыеФайлы)
	
	Зашифровать = Истина;
	РаботаСФайламиВызовСервера.ЗанестиИнформациюОШифровании(
		ОбъектСсылка,
		Зашифровать,
		МассивДанныхДляЗанесенияВБазу,
		Неопределено,  // УникальныйИдентификатор
		ИмяРабочегоКаталога,
		МассивФайловВРабочемКаталогеДляУдаления,
		МассивОтпечатков);
	
	Прочитать();
	ЗаполнитьСписокШифрования();
	
	ЕстьЗашифрованныеИлиЗанятыеФайлы = РаботаСФайламиВызовСервера.ЕстьЗашифрованныеИлиЗанятыеФайлы(Объект.ВладелецФайла);	
	
КонецПроцедуры

&НаКлиенте
Процедура Расшифровать(Команда)
	
	ДанныеФайла = РаботаСФайламиВызовСервера.ПолучитьДанныеФайлаИКоличествоВерсий(Объект.Ссылка);
	
	ПараметрыОбработчика = Новый Структура;
	ПараметрыОбработчика.Вставить("ДанныеФайла", ДанныеФайла);
	ПараметрыОбработчика.Вставить("ОбъектСсылка", Объект.Ссылка);
	Обработчик = Новый ОписаниеОповещения("РасшифроватьПослеРасшифровкиНаКлиенте", ЭтотОбъект, ПараметрыОбработчика);
	
	РаботаСФайламиСлужебныйКлиент.Расшифровать(Обработчик, ДанныеФайла.Ссылка, УникальныйИдентификатор, ДанныеФайла);
	
КонецПроцедуры

&НаКлиенте
Процедура РасшифроватьПослеРасшифровкиНаКлиенте(Результат, ПараметрыВыполнения) Экспорт
	
	Если Не Результат.Успех Тогда
		Возврат;
	КонецЕсли;
	
	ФайловыеФункцииКлиент.ПроинициализироватьПутьКРабочемуКаталогу();
	ИмяРабочегоКаталога = ФайловыеФункцииКлиентПовтИсп.ПолучитьПерсональныеНастройкиРаботыСФайлами().ПутьКЛокальномуКэшуФайлов;
	
	ЕстьЗашифрованныеИлиЗанятыеФайлы = Неопределено;
	
	РасшифроватьСервер(
		Результат.МассивДанныхДляЗанесенияВБазу,
		ИмяРабочегоКаталога,
		ПараметрыВыполнения.ОбъектСсылка,
		ЕстьЗашифрованныеИлиЗанятыеФайлы);
	
	РаботаСФайламиКлиент.ИнформироватьОРасшифровке(
		ПараметрыВыполнения.ДанныеФайла.Владелец,
		Объект.Ссылка,
		ЕстьЗашифрованныеИлиЗанятыеФайлы);
	
	УстановитьДоступностьЭлементовФормы();
	УстановитьДоступностьКомандСпискаШифрования();
	
КонецПроцедуры

&НаСервере
Процедура РасшифроватьСервер(МассивДанныхДляЗанесенияВБазу, 
	ИмяРабочегоКаталога, ОбъектСсылка, ЕстьЗашифрованныеИлиЗанятыеФайлы)
	
	Зашифровать = Ложь;
	МассивОтпечатков = Новый Массив;
	МассивФайловВРабочемКаталогеДляУдаления = Новый Массив;
	
	РаботаСФайламиВызовСервера.ЗанестиИнформациюОШифровании(
		ОбъектСсылка,
		Зашифровать,
		МассивДанныхДляЗанесенияВБазу,
		Неопределено,  // УникальныйИдентификатор
		ИмяРабочегоКаталога,
		МассивФайловВРабочемКаталогеДляУдаления,
		МассивОтпечатков);
		
	Прочитать();
	ЗаполнитьСписокШифрования();
	
	ЕстьЗашифрованныеИлиЗанятыеФайлы = РаботаСФайламиВызовСервера.ЕстьЗашифрованныеИлиЗанятыеФайлы(Объект.ВладелецФайла);	
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьПодпись(Команда)
	
	РаботаСЭПКлиент.ОткрытьПодпись(Элементы.ЭлектронныеПодписи.ТекущиеДанные, УникальныйИдентификатор);
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьЭП(Команда)
	
	Если Элементы.ЭлектронныеПодписи.ВыделенныеСтроки.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ПолучитьДанныеФайлаЕслиНекорректны();
	СтруктураВозврата = РаботаСФайламиВызовСервера.ДанныеФайлаИДвоичныеДанные(Объект.Ссылка);
	ФайловыеФункцииСлужебныйКлиент.ПроверитьПодписи(ЭтотОбъект,
		СтруктураВозврата.ДвоичныеДанные, Элементы.ЭлектронныеПодписи.ВыделенныеСтроки,
		ЭтаФорма.УникальныйИдентификатор);
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьВсе(Команда)
	
	ПолучитьДанныеФайлаЕслиНекорректны();
	СтруктураВозврата = РаботаСФайламиВызовСервера.ДанныеФайлаИДвоичныеДанные(Объект.Ссылка);
	ФайловыеФункцииСлужебныйКлиент.ПроверитьПодписи(ЭтотОбъект, СтруктураВозврата.ДвоичныеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьПодпись(Команда)
	
	Если Элементы.ЭлектронныеПодписи.ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Элементы.ЭлектронныеПодписи.ТекущиеДанные.Объект <> Неопределено И (НЕ Элементы.ЭлектронныеПодписи.ТекущиеДанные.Объект.Пустая()) Тогда
		ФайловыеФункцииКлиент.СохранитьПодпись(Элементы.ЭлектронныеПодписи.ТекущиеДанные.АдресПодписи);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УдалитьПодписиИОбновитьСписок(РеквизитПодписанИзменен)
	
	ИндексыСтрок = Новый Массив;
	
	Для Каждого Элемент Из Элементы.ЭлектронныеПодписи.ВыделенныеСтроки Цикл
		
		ДанныеСтроки = ЭлектронныеПодписи.НайтиПоИдентификатору(Элемент);
		
		Если ДанныеСтроки.Объект <> Неопределено И (НЕ ДанныеСтроки.Объект.Пустая()) Тогда
			ИндексыСтрок.Добавить(ДанныеСтроки.УникальныйИдентификатор);
		КонецЕсли;
		
	КонецЦикла;
	
	РаботаСФайламиСлужебныйВызовСервера.УдалитьПодписиВерсииФайла(
		Объект.ТекущаяВерсия,
		ИндексыСтрок, 
		РеквизитПодписанИзменен,
		УникальныйИдентификатор);
	
	ЗаполнитьСписокПодписей();
	
КонецПроцедуры

&НаКлиенте
Процедура УдалитьПодпись(Команда)
	
	УдалитьПодписиЗапуск();
	
КонецПроцедуры

&НаКлиенте
Процедура УдалитьПодписиЗапуск()
	
	Если Элементы.ЭлектронныеПодписи.ВыделенныеСтроки.Количество() > 1 Тогда
		ТекстВопроса = НСтр("ru = 'Удалить выделенные подписи?'; en = 'Delete the selected signatures?'");
	Иначе
		ТекстВопроса = НСтр("ru = 'Удалить выделенную подпись?'; en = 'Delete the selected signature?'");
	КонецЕсли;
	
	Обработчик = Новый ОписаниеОповещения("УдалитьЗавершение", ЭтотОбъект);
	ПоказатьВопрос(Обработчик, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
	
КонецПроцедуры

&НаКлиенте
Процедура УдалитьЗавершение(Ответ, ПараметрыВыполнения) Экспорт
	
	Если Ответ = КодВозвратаДиалога.Нет Тогда
		Возврат;
	КонецЕсли;
	
	РеквизитПодписанИзменен = Ложь;
	УдалитьПодписиИОбновитьСписок(РеквизитПодписанИзменен);
	
	Если РеквизитПодписанИзменен Тогда
		ОповеститьОбИзменении(Объект.Ссылка);
		Прочитать();
	КонецЕсли;
	
	Оповестить("ПрисоединенныйФайлПодписан", Объект.ВладелецФайла);
	
	УстановитьДоступностьКомандСпискаЭП();
	УстановитьДоступностьЭлементовФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьДоступностьКомандСпискаЭП()
	
	ЕстьПодписи = (ЭлектронныеПодписи.Количество() <> 0);
	
	Элементы.ТаблицаПодписейПроверить.Доступность = ЕстьПодписи;
	Элементы.ТаблицаПодписейПроверитьВсе.Доступность = ЕстьПодписи;
	Элементы.ТаблицаПодписейОткрыть.Доступность = ЕстьПодписи;
	Элементы.ТаблицаПодписейУдалить.Доступность = ЕстьПодписи;
	Элементы.ТаблицаПодписейСохранить.Доступность = ЕстьПодписи;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьДоступностьКомандСпискаШифрования()
	
	Элементы.СертификатыШифрованияОткрытьСертификатШифрования.Доступность = Объект.Зашифрован;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьТекстовыйОбраз(Команда)
	
	Если Объект.Зашифрован Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Нельзя открыть текстовый образ зашифрованного файла.'; en = 'Cannot open encrypted file text image.'"));
		Возврат;
	КонецЕсли;	
	
	ПараметрыФормы = Новый Структура("ОбъектСсылка, ИзКарточкиФайла, ИзвлеченТекст, Распознан", 
		Объект.Ссылка, Истина, ИзвлеченТекст, Распознан);
	ОткрытьФорму("Справочник.Файлы.Форма.ФормаТекстовогоОбраза", ПараметрыФормы);
	
КонецПроцедуры

&НаКлиенте
Процедура СертификатыШифрованияВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	ОткрытьСертификатШифрованияВыполнить();
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьСертификатШифрования(Команда)
	
	ОткрытьСертификатШифрованияВыполнить();
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьСертификатШифрованияВыполнить()
	
	ТекущиеДанные = Элементы.СертификатыШифрования.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ОбщегоНазначенияКлиент.ПодсистемаСуществует("СтандартныеПодсистемы.ЭлектроннаяПодпись") Тогда
		Возврат;
	КонецЕсли;
	
	МодульЭлектроннаяПодписьКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("ЭлектроннаяПодписьКлиент");
	
	Если ПустаяСтрока(ТекущиеДанные.АдресСертификата) Тогда
		МодульЭлектроннаяПодписьКлиент.ОткрытьСертификат(ТекущиеДанные.Отпечаток);
	Иначе
		МодульЭлектроннаяПодписьКлиент.ОткрытьСертификат(ТекущиеДанные.АдресСертификата);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьСписокПодписей(Команда)
	
	ЗаполнитьСписокПодписей();
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуПодбораКатегорий(Команда)
		
	ОписаниеОповещения = Новый ОписаниеОповещения(
		"ОткрытьФормуПодбораКатегорийПродолжение",
		ЭтотОбъект,
		Новый Структура);

	РаботаСКатегориямиДанныхКлиент.ОткрытьФормуПодбораКатегорийДляСпискаКатегорий(
		СписокКатегорийДанных, ОписаниеОповещения); 
		
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуПодбораКатегорийПродолжение(СписокКатегорийДанных, Параметры)Экспорт 
	
	Модифицированность = Параметры.Модифицированность Или Модифицированность;
	Элементы.Категории.Заголовок = 
		РаботаСКатегориямиДанныхКлиентСервер.ПолучитьЗаголовокВкладкиКатегории(СписокКатегорийДанных);
		
КонецПроцедуры

&НаКлиенте
Процедура СписокКатегорийДанныхПослеУдаления(Элемент)
	
	СортироватьСписокИУстановитьКоличествоКатегорийВЗаголовок();
	Модифицированность = Истина;

КонецПроцедуры

&НаКлиенте
Процедура СписокКатегорийДанныхПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	
	Отказ = Истина;
	ОткрытьФормуПодбораКатегорий(Неопределено);
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьШКВФайл(Команда)
	
	ДанныеОШтрихкодеФайла = ШтрихкодированиеСервер.ПолучитьДанныеДляВставкиШтрихкодаВОбъект(Объект.Ссылка, Ложь);
	ШтрихкодированиеКлиент.СохранитьИзображениеШК(ДанныеОШтрихкодеФайла.ДвоичныеДанныеИзображения);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьДаннымиВладельца(Команда)
	
	Если Объект.Ссылка.Пустая() Тогда 
		Если Не Записать() Тогда 
			Возврат;
		КонецЕсли;
		
		ПоказатьОповещениеПользователя(
			НСтр("ru = 'Создание:'; en = 'Added:'"), 
			ПолучитьНавигационнуюСсылку(Объект.Ссылка),
			Строка(Объект.Ссылка),
			БиблиотекаКартинок.Информация32);
	Иначе
		Если Модифицированность Тогда
			Если Не Записать() Тогда 
				Возврат;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	ДанныеВыполнения = АвтозаполнениеШаблоновФайловКлиентСервер.ЗаполнитьПоляФайлаДаннымиВладельца(Истина,
		Объект.Ссылка, 
		Истина, 
		УникальныйИдентификатор);
	Если ДанныеВыполнения.Результат Тогда
		Прочитать();
		Текст = НСтр("ru = 'Поля в файле обновлены данными владельца.'; en = 'Fields in the file are updated with the data of the file owner.'");
	Иначе
		ВызватьИсключение(ДанныеВыполнения.Описание);
	КонецЕсли;
	ПоказатьПредупреждение(, Текст);
	
КонецПроцедуры

&НаКлиенте
Процедура Подписаться(Команда)
	
	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ПараметрыФормы = Новый Структура("ОбъектПодписки", Объект.Ссылка);
		ОткрытьФорму("ОбщаяФорма.ПодпискаНаУведомленияПоОбъекту", ПараметрыФормы);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УдалитьКатегорииИзСпискаВыбранных()
	
	Если Элементы.СписокКатегорийДанных.ВыделенныеСтроки.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	МассивСтрокДляУдаления = РаботаСКатегориямиДанных.ФильтроватьСписокКатегорийДляУдаления(Объект.Ссылка, СписокКатегорийДанных, Элементы.СписокКатегорийДанных.ВыделенныеСтроки);
	Для Каждого СтрокаДляУдаления Из МассивСтрокДляУдаления Цикл
		СписокКатегорийДанных.Удалить(СписокКатегорийДанных.НайтиПоИдентификатору(СтрокаДляУдаления));
	КонецЦикла;
	Если МассивСтрокДляУдаления.Количество() > 0 Тогда
		Модифицированность = Истина;
		СортироватьСписокИУстановитьКоличествоКатегорийВЗаголовок();
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьСписокОбъектовСВыбраннымиКатегориями(Команда)
	
	Если СписокКатегорийДанных.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	МассивКатегорий = Новый Массив();
	УникальныйИдентификаторСпискаКатегорий = "";
	Для Каждого ВыбраннаяКатегория Из СписокКатегорийДанных Цикл
		МассивКатегорий.Добавить(ВыбраннаяКатегория.Значение);
		УникальныйИдентификаторСпискаКатегорий = УникальныйИдентификаторСпискаКатегорий + "," + ВыбраннаяКатегория.ПолноеНаименование;
	КонецЦикла;
	
	ПараметрыФормы = Новый Структура("СписокВыбранныхКатегорий", МассивКатегорий);
	
	ОткрытьФорму("Справочник.КатегорииДанных.ФормаСписка", ПараметрыФормы, Элементы.СписокКатегорийДанных, УникальныйИдентификаторСпискаКатегорий);
	
КонецПроцедуры	

&НаКлиенте
Процедура СписокКатегорийДанныхВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ПараметрыФормы = Новый Структура("Ключ", Элемент.ТекущиеДанные.Значение);
	ОткрытьФорму("Справочник.КатегорииДанных.ФормаОбъекта", ПараметрыФормы, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокКатегорийДанныхПередНачаломИзменения(Элемент, Отказ)
	
	Отказ = Истина;
	ПараметрыФормы = Новый Структура("Ключ", Элемент.ТекущиеДанные.Значение);
	ОткрытьФорму("Справочник.КатегорииДанных.ФормаОбъекта", ПараметрыФормы, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокКатегорийДанныхПередУдалением(Элемент, Отказ)
	
	Отказ = Истина;
	УдалитьКатегорииИзСпискаВыбранных();
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// УЧЕТ ВРЕМЕНИ

&НаСервере
Процедура ПереключитьХронометражСервер(ПараметрыОповещения)
	
	УчетВремени.ПереключитьХронометражСервер(
		ПараметрыОповещения,
		ДатаНачалаХронометража,
		ДатаКонцаХронометража,
		ВключенХронометраж,
		Объект.Ссылка,
		ВидыРабот,
		ЭтаФорма.Команды.ПереключитьХронометраж,
		ЭтаФорма.Элементы.ПереключитьХронометраж);
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьВОтчетИОбновитьФорму(ПараметрыОтчета, ПараметрыОповещения) Экспорт
	
	УчетВремени.ДобавитьВОтчетИОбновитьФорму(
		ПараметрыОтчета, 
		ПараметрыОповещения,
		ДатаНачалаХронометража,
		ДатаКонцаХронометража,
		ВключенХронометраж,
		ЭтаФорма.Команды.ПереключитьХронометраж,
		ЭтаФорма.Элементы.ПереключитьХронометраж);
	
КонецПроцедуры

&НаСервере
Процедура ОтключитьХронометражСервер() Экспорт
	
	УчетВремени.ОтключитьХронометражСервер(
	ДатаНачалаХронометража,
	ДатаКонцаХронометража,
	ВключенХронометраж,
	Объект.Ссылка,
	ЭтаФорма.Команды.ПереключитьХронометраж,
	ЭтаФорма.Элементы.ПереключитьХронометраж);
	
КонецПроцедуры

&НаКлиенте
Процедура ПереключитьХронометраж(Команда)
	
	ПараметрыОповещения = Неопределено;
	НуженДиалог = УчетВремениКлиент.НуженДиалогДляХронометража(ВключенХронометраж, 
		ДатаНачалаХронометража, ВидыРабот);
	
	Если НуженДиалог = Ложь Тогда
		
		ПереключитьХронометражСервер(ПараметрыОповещения);
		УчетВремениКлиент.ПоказатьОповещение(ПараметрыОповещения, ВключенХронометраж, Объект.Ссылка);
	
	Иначе
		ДлительностьРаботы = УчетВремениКлиент.ПолучитьДлительностьРаботы(ДатаНачалаХронометража);
		
		ОписаниеРаботы = СтрШаблон(
			НСтр("ru = 'Работа с файлом ""%1""'; en = 'Working with the file ""%1""'"),
			Объект.Наименование);
		
		ПараметрыФормы = Новый Структура();
		ПараметрыФормы.Вставить("ДатаОтчета", ТекущаяДата());
		ПараметрыФормы.Вставить("ВидыРабот", ВидыРабот);
		ПараметрыФормы.Вставить("ОписаниеРаботы", ОписаниеРаботы);
		ПараметрыФормы.Вставить("ДлительностьРаботы", ДлительностьРаботы);
		ПараметрыФормы.Вставить("НачалоРаботы", ДатаНачалаХронометража);
		ПараметрыФормы.Вставить("Объект", Объект.Ссылка);
		ПараметрыФормы.Вставить("СпособУказанияВремени", СпособУказанияВремени);
		
		ОписаниеОповещения = Новый ОписаниеОповещения(
			"ПереключитьХронометражПродолжение",
			ЭтотОбъект);
		
		ОткрытьФорму("РегистрСведений.ФактическиеТрудозатраты.Форма.ФормаДобавленияРаботы", ПараметрыФормы,,,,,
			ОписаниеОповещения, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПереключитьХронометражПродолжение(Результат, Параметры) Экспорт 
	
	Если Результат <> Неопределено Тогда
		ДобавитьВОтчетИОбновитьФорму(Результат, ПараметрыОповещения);
		УчетВремениКлиент.ПоказатьОповещение(ПараметрыОповещения, ВключенХронометраж, Объект.Ссылка);
	Иначе
		ОтключитьХронометражСервер();
	КонецЕсли;  

КонецПроцедуры

&НаКлиенте
Процедура УказатьТрудозатраты(Команда)
	
	ДатаОтчета = ТекущаяДата();
	
	УчетВремениКлиент.ДобавитьВОтчетКлиент(
		ДатаОтчета,
		ВключенХронометраж, 
		ДатаНачалаХронометража, 
		ДатаКонцаХронометража, 
		ВидыРабот, 
		Объект.Ссылка,
		СпособУказанияВремени,
		ЭтаФорма.Элементы.ПереключитьХронометраж,
		Ложь,
		ЭтаФорма); // Выполнена
		
КонецПроцедуры

&НаКлиенте
Процедура ЯвляетсяОригиналомПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(Объект.ШаблонОснованиеДляСоздания) Тогда 
		ЯвляетсяОригиналом = Не ЯвляетсяОригиналом;
		ПоказатьПредупреждение(, НСтр("ru = 'Нельзя отметить как оригинал файл, созданный из шаблона.'; en = 'You cannot mark as original file created from the template.'"));
		Возврат;
	КонецЕсли;	
	
	Если ЯвляетсяОригиналом Тогда 
		
		Если Не ДелопроизводствоКлиентСервер.ЭтоРасширениеСканКопии(Объект.ТекущаяВерсияРасширение) Тогда 
			
			ОписаниеОповещения = Новый ОписаниеОповещения(
				"ЯвляетсяОригиналомПриИзмененииПродолжение",
				ЭтотОбъект);
			
			ТекстВопроса = НСтр("ru = 'Выбранный файл, возможно, не является скан-копией. 
			|Вы действительно хотите отметить его как оригинал?';
			|en = 'The selected file might not be a scanned copy. 
			|Are you sure you want to mark it as the original?'");
			
			ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет,,КодВозвратаДиалога.Нет, 
				НСтр("ru = 'Отметка оригинала'; en = 'The original stamp'"));
				
		КонецЕсли;	
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ЯвляетсяОригиналомПриИзмененииПродолжение(Результат, Параметры) Экспорт 

	Если Результат <> КодВозвратаДиалога.Да Тогда
		ЯвляетсяОригиналом = Не ЯвляетсяОригиналом;
	КонецЕсли;
	
КонецПроцедуры

// Отрабатывает нажатие на элементе КартинкаЭП
&НаКлиенте
Процедура КартинкаЭПНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если Объект.ПодписанЭП Тогда
		Элементы.ГруппаДополнительныеДанныеСтраницы.ТекущаяСтраница = Элементы.ГруппаЭП;
	Иначе
		Элементы.ГруппаДополнительныеДанныеСтраницы.ТекущаяСтраница = Элементы.ГруппаШифрование;
	КонецЕсли;	
	
КонецПроцедуры

// Отрабатывает нажатие на кнопке "..."
&НаКлиенте
Процедура КомандаЕще(Команда)
	
	СписокЗначений = Новый СписокЗначений;
	
	РедактируетТекущийПользователь = (Объект.Редактирует = ПользователиКлиентСервер.ТекущийПользователь());
	РедактируетДругой = Не Объект.Редактирует.Пустая() И Не РедактируетТекущийПользователь;
	
	СписокЗначений.Добавить("ОткрытьКаталогФайла", 
		НСтр("ru = 'Открыть каталог файла'; en = 'Open file directory'"),, 
		БиблиотекаКартинок.Папка);
	
	Если РазрешеноРедактирование Тогда
		
		Если Не РедактируетДругой Тогда
			СписокЗначений.Добавить("ОбновитьИзФайлаНаДиске", 
				НСтр("ru = 'Обновить из файла на диске'; en = 'Update from file on disk'"),, 
				БиблиотекаКартинок.ОбновитьФайлИзФайлаНаДиске);
		КонецЕсли;	
		
		СписокЗначений.Добавить("ОтменитьРедактирование", 
			НСтр("ru = 'Отменить редактирование'; en = 'Cancel editing'"),, 
			БиблиотекаКартинок.ОсвободитьФайл);
		
		Если Не РедактируетДругой Тогда
			СписокЗначений.Добавить("СохранитьИзменения", 
				НСтр("ru = 'Сохранить изменения'; en = 'Save changes'"),, 
				БиблиотекаКартинок.ОпубликоватьФайл);
		КонецЕсли;	
		
	КонецЕсли;
	
	ОписаниеОповещения = Новый ОписаниеОповещения(
		"КомандаЕщеВыбораЗначенияПродолжение",
		ЭтотОбъект);
	
	ПоказатьВыборИзМеню(ОписаниеОповещения, СписокЗначений);
	
КонецПроцедуры

// Обработчик команды с подменю в карточке файла - продолжение
&НаКлиенте
Процедура КомандаЕщеВыбораЗначенияПродолжение(Результат, Параметры) Экспорт
	
	Если Результат = Неопределено Или ТипЗнч(Результат) <> Тип("ЭлементСпискаЗначений") Тогда
		Возврат;
	КонецЕсли;	
	
	Если Результат.Значение = "ОткрытьКаталогФайла" Тогда
		ОткрытьКаталогФайлаВыполнить();
		Возврат;
	ИначеЕсли Результат.Значение = "ОбновитьИзФайлаНаДиске" Тогда
		ОбновитьИзФайлаНаДискеВыполнить();
		Возврат;
	ИначеЕсли Результат.Значение = "ОтменитьРедактирование" Тогда
		ОтменитьРедактированиеВыполнить();
		Возврат;
	ИначеЕсли Результат.Значение = "СохранитьИзменения" Тогда
		СохранитьИзмененияВыполнить();
		Возврат;
	КонецЕсли;	
	
КонецПроцедуры

// Отрабатывает нажатие на элементе Категории
&НаКлиенте
Процедура КартинкаКатегорииНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Элементы.ГруппаДополнительныеДанныеСтраницы.ТекущаяСтраница = Элементы.Категории;
	
КонецПроцедуры

// Отрабатывает нажатие на элементе Контроль
&НаКлиенте
Процедура КартинкаКонтрольНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	КонтрольКлиент.ОбработкаКомандыКонтроль(Объект.Ссылка, ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура КартинкаФорумНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если Объект.Ссылка.Пустая() Тогда 
		ПоказатьПредупреждение(, НСтр("ru = 'Для перехода по ссылке документ необходимо записать.'; en = 'To navigate the link you need to save document.'"));
		Возврат;
	КонецЕсли;	
	
	ОткрытьФорму("Справочник.ТемыОбсуждений.Форма.ФормаПросмотраТемы", 
		Новый Структура("Документ", Объект.Ссылка), ЭтаФорма, ЭтаФорма.КлючУникальности, ЭтаФорма.Окно);
	
КонецПроцедуры

// Отрабатывает нажатие на элементе КартинкаВзаимодействие
&НаКлиенте
Процедура КартинкаВзаимодействиеНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если Объект.Ссылка.Пустая() Тогда 
		ПоказатьПредупреждение(, НСтр("ru = 'Для перехода по ссылке файл необходимо записать.'; en = 'To navigate the link you need to save file.'"));
		Возврат;
	КонецЕсли;	
	
	ОткрытьФорму("ОбщаяФорма.ПроцессыИЗадачи",
		Новый Структура("Предмет", Объект.Ссылка), ЭтаФорма, ЭтаФорма.КлючУникальности, ЭтаФорма.Окно);
	
КонецПроцедуры

// Отрабатывает нажатие на элементе ПолныйПутьОписание
&НаКлиенте
Процедура ПолныйПутьОписаниеНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если ЗначениеЗаполнено(Объект.ВладелецФайла) Тогда
		
		Если ТипЗнч(Объект.ВладелецФайла) = Тип("СправочникСсылка.ПапкиФайлов") Тогда
			
			ПараметрыФормы = Новый Структура("Папка", Объект.ВладелецФайла);
			ОткрытьФорму("Справочник.Файлы.Форма.ХранилищеФайлов", ПараметрыФормы, , Объект.ВладелецФайла);
			
		Иначе
			
			ПоказатьЗначение(, Объект.ВладелецФайла);
			
		КонецЕсли;	
		
	КонецЕсли;	
	
КонецПроцедуры

// Отрабатывает нажатие на элементе ИконкаФайлаБольшая
&НаКлиенте
Процедура ИконкаФайлаБольшаяНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОткрытьФайлВыполнить();
	
КонецПроцедуры

// Отрабатывает событие Открытие для элемента Владелец
&НаКлиенте
Процедура ВладелецОткрытие(Элемент, СтандартнаяОбработка)
	
	Если ТипЗнч(Объект.ВладелецФайла) = Тип("СправочникСсылка.ПапкиФайлов") Тогда
		СтандартнаяОбработка = Ложь;
		
		ПараметрыФормы = Новый Структура("Папка", Объект.ВладелецФайла);
		ОткрытьФорму("Справочник.Файлы.Форма.ХранилищеФайлов", ПараметрыФормы, , Объект.ВладелецФайла);
		
	КонецЕсли;	
	
КонецПроцедуры

// Отрабатывает команду ПоказатьВСписке
&НаКлиенте
Процедура ПоказатьВСписке(Команда)
	
	Если ЗначениеЗаполнено(Объект.ВладелецФайла) Тогда
		
		Если ТипЗнч(Объект.ВладелецФайла) = Тип("СправочникСсылка.ПапкиФайлов") Тогда
			
			ПараметрыФормы = Новый Структура("Папка, Файл", Объект.ВладелецФайла, Объект.Ссылка);
			ОткрытьФорму("Справочник.Файлы.Форма.ХранилищеФайлов", ПараметрыФормы, , Объект.ВладелецФайла);
			
		КонецЕсли;	
		
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ПраваДоступа(Команда)
	
	ДокументооборотПраваДоступаКлиент.ОткрытьФормуПравДоступа(ЭтаФорма);
	
КонецПроцедуры


