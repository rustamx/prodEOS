
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьПривилегированныйРежим(Истина);
	
	КарточкаКонтроля = Параметры.Ключ;
	Предмет = Параметры.Предмет;
	
	Если ЗначениеЗаполнено(КарточкаКонтроля.СрокИсполнения) Тогда 
		СрокИсполнения = Формат(КарточкаКонтроля.СрокИсполнения, "ДЛФ=D");
	Иначе 	
		СрокИсполнения = НСтр("ru = 'не указан'; en = 'not specified'");
	КонецЕсли;
	
	HTMLТекст = "<html>
	|<head>
	|<style type=""text/css"">
	|	body {
	|		overflow:    auto;
	|		margin-top:  6px; 		 
	|		margin-left: 6px; 
	|		font-family: MS Shell Dlg, Microsoft Sans Serif, sans-serif; 
	|		font-size:   8pt;}
	| 	a:link {
	|		color: #006699; text-decoration: none;}
	|	a:visited {
	|		color: #006699; text-decoration: none;}
	|	a:hover {
	|		color: #006699; text-decoration: underline;}
	|	p {
	|		margin-top: 6px;}
	|</style>
	|<body>";
	
	Если КарточкаКонтроля.СнятСКонтроля Тогда 
		HTMLТекст = HTMLТекст + "<B>" + НСтр("ru = 'Снят с контроля'; en = 'Monitoring stopped'") + "</B>";	
		HTMLТекст = HTMLТекст + "<p>";
		ДобавитьРеквизит(HTMLТекст, НСтр("ru = 'Контролер:'; en = 'Monitor:'"), Строка(КарточкаКонтроля.Контролер));
		HTMLТекст = HTMLТекст + "<p>";
		ДобавитьРеквизит(HTMLТекст, НСтр("ru = 'Дата:'; en = 'Date:'"), Формат(КарточкаКонтроля.ДатаСнятияСКонтроля, "ДЛФ=D"));
	Иначе
		HTMLТекст = HTMLТекст + "<B>" + НСтр("ru = 'На контроле'; en = 'Monitored'") + "</B>";	
		HTMLТекст = HTMLТекст + "<p>";
		ДобавитьРеквизит(HTMLТекст, НСтр("ru = 'Контролер:'; en = 'Monitor:'"), Строка(КарточкаКонтроля.Контролер));
		HTMLТекст = HTMLТекст + "<p>";
		ДобавитьРеквизит(HTMLТекст, НСтр("ru = 'Срок:'; en = 'Due date:'"), СрокИсполнения);
	КонецЕсли;
	
	ИнформацияОКонтроле = HTMLТекст;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Добавить(Команда)
	
	ПараметрыФормы = Новый Структура("Основание", Предмет);
	ОткрытьФорму("Справочник.Контроль.Форма.ФормаНового", ПараметрыФормы, ВладелецФормы);
	
	Закрыть();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ДобавитьПодпись(HTMLТекст, Подпись)
	
	Если ЗначениеЗаполнено(Подпись) Тогда 
		HTMLТекст = HTMLТекст + "<FONT color=#413003>";
		HTMLТекст = HTMLТекст + Подпись + " ";
		HTMLТекст = HTMLТекст + "</FONT>";
	КонецЕсли;
	
КонецПроцедуры

Процедура ДобавитьЗначение(HTMLТекст, Значение, Цвет)
	
	Если ТипЗнч(Значение) = Тип("Строка") 
	 Или ТипЗнч(Значение) = Тип("Дата")	
	 Или ТипЗнч(Значение) = Тип("Число")
	 Или ТипЗнч(Значение) = Тип("Булево") Тогда 
		Если ЗначениеЗаполнено(Цвет) Тогда 
			HTMLТекст = HTMLТекст + "<FONT color=#"+Цвет+">";
			HTMLТекст = HTMLТекст + Значение;
			HTMLТекст = HTMLТекст + "</FONT>";
		Иначе	
			HTMLТекст = HTMLТекст + Значение;
		КонецЕсли;	
	Иначе
		HTMLТекст = HTMLТекст + СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			"<A href=v8doc:%1>%2</A>",
			ПолучитьНавигационнуюСсылку(Значение),
			Строка(Значение));
	КонецЕсли;
	
КонецПроцедуры

Процедура ДобавитьРеквизит(HTMLТекст, Подпись, Значение, Цвет = "")
	
	ДобавитьПодпись(HTMLТекст, Подпись);
	ДобавитьЗначение(HTMLТекст, Значение, Цвет); 
	HTMLТекст = HTMLТекст + "<br>";
	
КонецПроцедуры

#КонецОбласти
