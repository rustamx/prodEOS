#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаПолученияДанныхВыбора(ДанныеВыбора, Параметры, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	МассивПолей = ра_ОбщегоНазначения.ПолучитьПоляВводаПоСтроке(ПустаяСсылка().Метаданные());
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ИСТИНА
	|ИЗ
	|	РегистрСведений.ra_DostupnyePloshchadki КАК ra_DostupnyePloshchadki
	|ГДЕ
	|	ra_DostupnyePloshchadki.Polzovatel = &Polzovatel");
	Запрос.УстановитьПараметр("Polzovatel", ПараметрыСеанса.ТекущийПользователь);
	
	Если Не Параметры.Свойство("НеОтбирать") и Не Запрос.Выполнить().Пустой() Тогда
		
		Запрос = ра_ОбщегоНазначения.ЗапросДанныхВыбораПоТексту(
			"ВЫБРАТЬ
			|	ra_Ploshchadki.Ссылка КАК Ссылка,
			|	ra_Ploshchadki.Наименование КАК Наименование,
			|	ra_Ploshchadki.ПометкаУдаления КАК ПометкаУдаления
			|ИЗ
			|	РегистрСведений.ra_DostupnyePloshchadki КАК ra_DostupnyePloshchadki
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ra_Ploshchadki КАК ra_Ploshchadki
			|		ПО ra_DostupnyePloshchadki.Ploshchadka = ra_Ploshchadki.Ссылка
			|			И (ra_DostupnyePloshchadki.Polzovatel = &Polzovatel)",
			Параметры,
			МассивПолей);
			
		Запрос.УстановитьПараметр("Polzovatel", ПараметрыСеанса.ТекущийПользователь);
		
		ра_ОбщегоНазначения.ЗаполнитьДанныеВыбораПоЗапросу(ДанныеВыбора, Параметры, Запрос, МассивПолей, СтрСоединить(МассивПолей, ","));
				
	Иначе
		
		ра_ОбщегоНазначения.ПолучитьСтандартныеДанныеВыбора(Справочники.ra_Ploshchadki, ДанныеВыбора, Параметры, СтандартнаяОбработка, МассивПолей, СтрСоединить(МассивПолей, ","));
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПолученияПредставления(Данные, Представление, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Для каждого КлючИЗнач из Данные Цикл
		Представление = КлючИЗнач.Значение;	
	КонецЦикла;
	
	ра_ОбщегоНазначения.ОбработатьПустоеПредставление(Представление);
	
КонецПроцедуры

Процедура ОбработкаПолученияПолейПредставления(Поля, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Если ТекущийЯзык() = Метаданные.Языки.Английский Тогда
		Поля.Добавить("NaimenovanieEn");	
	Иначе
		Поля.Добавить("Наименование");	
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
