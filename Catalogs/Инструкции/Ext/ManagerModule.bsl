#Область ОбработчикиСобытий

Процедура ОбработкаПолученияФормы(ВидФормы, Параметры, ВыбраннаяФорма, ДополнительнаяИнформация, СтандартнаяОбработка)
	
	ЕстьТолькоПравоЧтенияИнструкции = РаботаСИнструкциями.ПравоТолькоЧтения();
	
	Если Не ЕстьТолькоПравоЧтенияИнструкции Тогда
		Если ВидФормы = "ФормаОбъекта" Тогда
			ВыбраннаяФорма = "ФормаЭлемента";
			СтандартнаяОбработка = Ложь;
		ИначеЕсли ВидФормы = "ФормаСписка" Тогда
			ВыбраннаяФорма = "ФормаДерева";
			СтандартнаяОбработка = Ложь;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область УправлениеДоступом

Функция ПолучитьПоляДоступа() Экспорт
	
	Возврат "Ссылка,ДоступНаЧтение";
	
КонецФункции

// Проверяет наличие метода.
// 
Функция ЕстьМетодЗаполнитьДескрипторыОбъекта() Экспорт
	
	Возврат Истина;
	
КонецФункции

// Заполняет переданную таблицу дескрипторов объекта.
// 
Процедура ЗаполнитьДескрипторыОбъекта(ОбъектДоступа, ТаблицаДескрипторов, ПротоколРасчетаПрав = Неопределено) Экспорт
	
	ТаблицаДоступа = ОбъектДоступа.ДоступНаЧтение.Выгрузить();
	Если ТаблицаДоступа.Количество() > 0 Тогда
		Для Каждого СтрДоступа Из ТаблицаДоступа Цикл
			ДокументооборотПраваДоступа.ДобавитьИндивидуальныйДескриптор(
				ОбъектДоступа, ТаблицаДескрипторов, СтрДоступа.Пользователь, Ложь);
		КонецЦикла;
	Иначе
		ДокументооборотПраваДоступа.ДобавитьИндивидуальныйДескриптор(
			ОбъектДоступа, ТаблицаДескрипторов, Справочники.РабочиеГруппы.ВсеПользователи, Ложь);
	КонецЕсли;
	
	Если ПротоколРасчетаПрав <> Неопределено Тогда
		Если ТаблицаДоступа.Количество() > 0 Тогда
			ЗаписьПротокола = Новый Структура("Элемент, Описание",
				ОбъектДоступа.Ссылка, НСтр("ru = 'Пользователи на закладке ""Доступ""'; en = 'Users on the ""Access"" tab'"));
			ПротоколРасчетаПрав.Добавить(ЗаписьПротокола);
		Иначе
			ЗаписьПротокола = Новый Структура("Элемент, Описание",
				Справочники.РабочиеГруппы.ВсеПользователи, НСтр("ru = 'Все пользователи'; en = 'All users'"));
			ПротоколРасчетаПрав.Добавить(ЗаписьПротокола);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
