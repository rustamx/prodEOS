
/////////////////////////
///Процедуры и функции///
/////////////////////////

&НаСервере
Процедура ЗаполнитьДеревоРеквизитовОбъекта(ТипОбъекта)
	
	ИндексЗначенияПеречисления = Перечисления.ТипыОбъектов.Индекс(ТипОбъекта);
	ИмяЗначенияПеречисления = Метаданные.Перечисления.ТипыОбъектов.ЗначенияПеречисления[ИндексЗначенияПеречисления].Имя;

	Дерево = РеквизитФормыВЗначение("ДеревоРеквизитовОбъекта");
	
	Дерево.Строки.Очистить();
	
	ПолныйПуть = "Предмет";
	НоваяСтрокаВладелецФайла = Дерево.Строки.Добавить();
	НоваяСтрокаВладелецФайла.НаименованиеРеквизита = "Предмет";
	НоваяСтрокаВладелецФайла.ТипРеквизита = ТипОбъекта;
	Если Найти(НРег(Строка(ТипОбъекта)), "документ") > 0 Тогда
		Элементы.ДеревоРеквизитовОбъекта.Заголовок = НСтр("ru = 'Реквизиты документа'; en = 'Document attributes'");
	ИначеЕсли Найти(НРег(Строка(ТипОбъекта)), "файл") > 0 Тогда
		Элементы.ДеревоРеквизитовОбъекта.Заголовок = НСтр("ru = 'Реквизиты файла'; en = 'File attributes'");
	Иначе
		Элементы.ДеревоРеквизитовОбъекта.Заголовок = НСтр("ru = 'Реквизиты объекта'; en = 'Object attributes'");
	КонецЕсли;
	НоваяСтрокаВладелецФайла.ПолныйПуть = ПолныйПуть;
	
	Если Метаданные.Справочники.Найти(ИмяЗначенияПеречисления) <> Неопределено Тогда
		СправочникОбъект = Метаданные.Справочники[ИмяЗначенияПеречисления];
	ИначеЕсли Метаданные.БизнесПроцессы.Найти(ИмяЗначенияПеречисления) <> Неопределено Тогда
		СправочникОбъект = Метаданные.БизнесПроцессы[ИмяЗначенияПеречисления];
	КонецЕсли;
	Для Каждого СтандартныйРеквизит Из СправочникОбъект.СтандартныеРеквизиты Цикл
		Строка = НоваяСтрокаВладелецФайла.Строки.Добавить();
		Строка.НаименованиеРеквизита = СтандартныйРеквизит.Имя;
		Строка.ТипРеквизита = СтандартныйРеквизит.Тип;
		Строка.ПолныйПуть = ПолныйПуть + "." + СтандартныйРеквизит.Имя;
	КонецЦикла;
	Для Каждого Реквизит Из СправочникОбъект.Реквизиты Цикл
		Строка = НоваяСтрокаВладелецФайла.Строки.Добавить();
		Строка.НаименованиеРеквизита = Реквизит.Имя;
		Строка.ТипРеквизита = Реквизит.Тип;
		Строка.ПолныйПуть = ПолныйПуть + "." + Реквизит.Имя;
	КонецЦикла;
	Для Каждого ТабличнаяЧасть Из СправочникОбъект.ТабличныеЧасти Цикл
		СтрокаТабличнаяЧасть = НоваяСтрокаВладелецФайла.Строки.Добавить();
		СтрокаТабличнаяЧасть.НаименованиеРеквизита = ТабличнаяЧасть.Имя;
		СтрокаТабличнаяЧасть.ТипРеквизита = "Табличная часть";
		СтрокаТабличнаяЧасть.ПолныйПуть = ПолныйПуть + "." + ТабличнаяЧасть.Имя + "[0]";
		Для Каждого СтандартныйРеквизитТЧ Из ТабличнаяЧасть.СтандартныеРеквизиты Цикл
			СтрокаРеквизитТЧ = СтрокаТабличнаяЧасть.Строки.Добавить();
			СтрокаРеквизитТЧ.НаименованиеРеквизита = СтандартныйРеквизитТЧ.Имя;
			СтрокаРеквизитТЧ.ТипРеквизита = СтандартныйРеквизитТЧ.Тип;
			СтрокаРеквизитТЧ.ПолныйПуть = ПолныйПуть + "." + ТабличнаяЧасть.Имя + "[0]" + "." + СтандартныйРеквизитТЧ.Имя; 
		КонецЦикла;
		Для Каждого РеквизитТЧ Из ТабличнаяЧасть.Реквизиты Цикл
			СтрокаРеквизитТЧ = СтрокаТабличнаяЧасть.Строки.Добавить();
			СтрокаРеквизитТЧ.НаименованиеРеквизита = РеквизитТЧ.Имя;
			СтрокаРеквизитТЧ.ТипРеквизита = РеквизитТЧ.Тип;
			СтрокаРеквизитТЧ.ПолныйПуть = ПолныйПуть + "." + ТабличнаяЧасть.Имя + "[0]" + "." + РеквизитТЧ.Имя;
		КонецЦикла; 
	КонецЦикла;
	
	ЗначениеВРеквизитФормы(Дерево,"ДеревоРеквизитовОбъекта");
	
КонецПроцедуры

&НаСервере
Процедура ОчиститьДеревоРеквизитовОбъекта()
	
	Дерево = РеквизитФормыВЗначение("ДеревоРеквизитовОбъекта");
	Дерево.Строки.Очистить();
	ЗначениеВРеквизитФормы(Дерево,"ДеревоРеквизитовОбъекта");
	Элементы.ДеревоРеквизитовОбъекта.Заголовок = НСтр("ru = 'Реквизиты объекта'; en = 'Object attributes'");
	
КонецПроцедуры

&НаСервере
Процедура НастроитьКомпоновщикУсловияИЗаполнитьДеревоРеквизитовОбъекта(Знач ТипОбъекта, Настройки = Неопределено)
	
	ИндексЗначенияПеречисления = Перечисления.ТипыОбъектов.Индекс(ТипОбъекта);
	ИмяЗначенияПеречисления = Метаданные.Перечисления.ТипыОбъектов.ЗначенияПеречисления[ИндексЗначенияПеречисления].Имя;
	
	СхемаКомпоновкиДанных = Справочники.УсловияМаршрутизации.ПолучитьМакет(ИмяЗначенияПеречисления);
	Если Настройки = Неопределено Тогда
		Настройки = СхемаКомпоновкиДанных.НастройкиПоУмолчанию;
	КонецЕсли;

	URLСхемы = ПоместитьВоВременноеХранилище(СхемаКомпоновкиДанных, УникальныйИдентификатор);
	ИсточникНастроек = Новый ИсточникДоступныхНастроекКомпоновкиДанных(URLСхемы);
	Компоновщик.Инициализировать(ИсточникНастроек);
	Компоновщик.ЗагрузитьНастройки(Настройки);
	
	Если ЗначениеЗаполнено(ТипОбъекта) Тогда
		ЗаполнитьДеревоРеквизитовОбъекта(ТипОбъекта);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура НастроитьКомпоновщикКомбинацииУсловий(Настройки = Неопределено)
	
	СхемаКомпоновкиДанных = Справочники.УсловияМаршрутизации.ПолучитьМакет("Условия");
	
	Если Настройки = Неопределено Тогда
		Настройки = СхемаКомпоновкиДанных.НастройкиПоУмолчанию;
	КонецЕсли;
	
	URLСхемы = ПоместитьВоВременноеХранилище(СхемаКомпоновкиДанных, УникальныйИдентификатор);
	ИсточникНастроек = Новый ИсточникДоступныхНастроекКомпоновкиДанных(URLСхемы);
	КомпоновщикУсловий.Инициализировать(ИсточникНастроек);
	КомпоновщикУсловий.ЗагрузитьНастройки(Настройки);
	//скрываем поле сравнения - в него будет ставиться всегда "Равно"
	Элементы.КомпоновщикУсловийНастройкиОтборВидСравнения.Видимость = Ложь;
	//делаем недоступным для редактирования поле "Левое значение"
	Элементы.КомпоновщикУсловийНастройкиОтборЛевоеЗначение.ТолькоПросмотр = Истина;
	Элементы.КомпоновщикУсловийНастройкиОтбор.Шапка = Ложь;
	
КонецПроцедуры
	
&НаСервере
Функция ПроверитьНаличиеУсловияВДереве(ИщемПустоеЗначение)
	
	Результат = Ложь;
	Если Объект.СпособЗаданияУсловия = Перечисления.СпособыЗаданияУсловия.КомбинацияИзДругихУсловий Тогда
		ЭлементыНастройкиОтбора = КомпоновщикУсловий.Настройки.Отбор.Элементы;
		ИскомоеУсловие = ?(ИщемПустоеЗначение, Неопределено, Объект.Ссылка);
		Результат = ВыполнитьПоискУсловияВОдномУровнеКомбинацииУсловий(ЭлементыНастройкиОтбора, ИскомоеУсловие);
	КонецЕсли;
	Возврат Результат;
	
КонецФункции

&НаСервере
Функция ВыполнитьПоискУсловияВОдномУровнеКомбинацииУсловий(Элементы, ИскомоеУсловие)
	
	Для Каждого ЭлементОтбора Из Элементы Цикл
		Если ТипЗнч(ЭлементОтбора) = Тип("ГруппаЭлементовОтбораКомпоновкиДанных") Тогда		
			Если ВыполнитьПоискУсловияВОдномУровнеКомбинацииУсловий(ЭлементОтбора.Элементы, ИскомоеУсловие) Тогда
				Возврат Истина;
			КонецЕсли;
		Иначе			
			Если (ЗначениеЗаполнено(ИскомоеУсловие) 
				И ЭлементОтбора.ПравоеЗначение = ИскомоеУсловие)
				ИЛИ (НЕ ЗначениеЗаполнено(ИскомоеУсловие) 
				И НЕ ЗначениеЗаполнено(ЭлементОтбора.ПравоеЗначение)) Тогда
				Возврат Истина;
			ИначеЕсли  ЭлементОтбора.ПравоеЗначение <> Неопределено
				И ЭлементОтбора.ПравоеЗначение.СпособЗаданияУсловия = Перечисления.СпособыЗаданияУсловия.КомбинацияИзДругихУсловий
				И ВыполнитьПоискУсловияВОдномУровнеКомбинацииУсловий(ЭлементОтбора.ПравоеЗначение.НастройкаКомбинацииУсловий.Получить().Отбор.Элементы, ИскомоеУсловие) Тогда
					Возврат Истина;
			КонецЕсли;
		КонецЕсли;	
	КонецЦикла;

	Возврат Ложь;
	
КонецФункции

&НаСервере
Функция ПроверитьНаличиеПустыхСтрокОтбораВДереве()
	
	Результат = Ложь;
	Если Объект.СпособЗаданияУсловия = Перечисления.СпособыЗаданияУсловия.ВРежимеКонструктора Тогда
		ЭлементыНастройкиОтбора = Компоновщик.Настройки.Отбор.Элементы;
		Результат = ВыполнитьПоискПустойСтрокиВОдномУровнеНастройкиОтбора(ЭлементыНастройкиОтбора);
	КонецЕсли;
	Возврат Результат;		
	
КонецФункции

&НаСервере
Функция ВыполнитьПоискПустойСтрокиВОдномУровнеНастройкиОтбора(Элементы)
	
	Для Каждого ЭлементОтбора Из Элементы Цикл
		Если ТипЗнч(ЭлементОтбора) = Тип("ГруппаЭлементовОтбораКомпоновкиДанных") Тогда		
			Если ВыполнитьПоискПустойСтрокиВОдномУровнеНастройкиОтбора(ЭлементОтбора.Элементы) Тогда
				Возврат Истина;
			КонецЕсли;
		Иначе			
			Если (НЕ ЗначениеЗаполнено(ЭлементОтбора.ПравоеЗначение)
				ИЛИ НЕ ЗначениеЗаполнено(Строка(ЭлементОтбора.ЛевоеЗначение)))
				И ЭлементОтбора.ВидСравнения <> ВидСравненияКомпоновкиДанных.Заполнено
				И ЭлементОтбора.ВидСравнения <> ВидСравненияКомпоновкиДанных.НеЗаполнено Тогда
				Возврат Истина;
				
			КонецЕсли;
		КонецЕсли;	
	КонецЦикла;

	Возврат Ложь;
	
КонецФункции

&НаСервере
Процедура УдалитьУсловиеИзКомбинацииУсловий(ИмяУсловия)
	
	Если Объект.СпособЗаданияУсловия = Перечисления.СпособыЗаданияУсловия.КомбинацияИзДругихУсловий Тогда
		ЭлементыНастройкиОтбора = КомпоновщикУсловий.Настройки.Отбор.Элементы;
		ВыполнитьПоискИУдалениеДляОдногоУровня(ЭлементыНастройкиОтбора, ИмяУсловия);
	КонецЕсли;	
	
КонецПроцедуры

&НаСервере
Процедура ВыполнитьПоискИУдалениеДляОдногоУровня(Элементы, ИмяУсловия)
	
	Для Каждого ЭлементОтбора Из Элементы Цикл
		Если ТипЗнч(ЭлементОтбора) = Тип("ГруппаЭлементовОтбораКомпоновкиДанных") Тогда		
			ВыполнитьПоискИУдалениеДляОдногоУровня(ЭлементОтбора.Элементы, ИмяУсловия);
		ИначеЕсли ЭлементОтбора.ПравоеЗначение <> Неопределено Тогда
			СпособЗаданияУсловия = ЭлементОтбора.ПравоеЗначение.СпособЗаданияУсловия;
				
			Если ЭлементОтбора.ПравоеЗначение.Наименование = ИмяУсловия Тогда
				ЭлементОтбора.ПравоеЗначение = Справочники.УсловияМаршрутизации.ПустаяСсылка();
			ИначеЕсли СпособЗаданияУсловия = Перечисления.СпособыЗаданияУсловия.КомбинацияИзДругихУсловий Тогда
            	ВыполнитьПоискИУдалениеДляОдногоУровня(ЭлементОтбора.ПравоеЗначение.НастройкаКомбинацииУсловий.Получить().Отбор.Элементы, ИмяУсловия);
			КонецЕсли;
			
		КонецЕсли;	
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция ПроверитьУникальностьУсловия()
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 1
	               |	Условия.Ссылка          
				   |ИЗ
	               |	Справочник.УсловияМаршрутизации КАК Условия
	               |ГДЕ
	               |	Условия.Наименование = &Наименование
	               |	И Условия.Ссылка <> &Ссылка";
	
	Запрос.УстановитьПараметр("Наименование", Объект.Наименование);
	Запрос.УстановитьПараметр("Ссылка", Объект.Ссылка);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
	   Возврат Выборка;	 
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции	

/////////////////////////
///Обработчики событий///
/////////////////////////                      

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Объект.Ссылка.Пустая()
		И (НЕ Параметры.Свойство("ЗначениеКопирования")
		ИЛИ НЕ ЗначениеЗаполнено(Параметры.ЗначениеКопирования.Наименование)) Тогда
		Если Параметры.Свойство("ТипОбъекта") Тогда
			Объект.ТипОбъекта = Параметры.ТипОбъекта;
			НастроитьКомпоновщикУсловияИЗаполнитьДеревоРеквизитовОбъекта(Объект.ТипОбъекта);
		КонецЕсли;
		Объект.СпособЗаданияУсловия = Перечисления.СпособыЗаданияУсловия.ВРежимеКонструктора;
		НастроитьКомпоновщикКомбинацииУсловий();
	ИначеЕсли Объект.Ссылка.Пустая()
		И Параметры.Свойство("ЗначениеКопирования")
		И ЗначениеЗаполнено(Параметры.ЗначениеКопирования.Наименование) Тогда
		Настройки = Параметры.ЗначениеКопирования.НастройкаУсловия.Получить();
		НастройкиКомбинацииУсловий = Параметры.ЗначениеКопирования.НастройкаКомбинацииУсловий.Получить();
		НастроитьКомпоновщикУсловияИЗаполнитьДеревоРеквизитовОбъекта(Объект.ТипОбъекта, Настройки);
		НастроитьКомпоновщикКомбинацииУсловий(НастройкиКомбинацииУсловий);
		
	Иначе
		Настройки = Объект.Ссылка.НастройкаУсловия.Получить();
		НастройкиКомбинацииУсловий = Объект.Ссылка.НастройкаКомбинацииУсловий.Получить();
		НастроитьКомпоновщикУсловияИЗаполнитьДеревоРеквизитовОбъекта(Объект.ТипОбъекта, Настройки);
		НастроитьКомпоновщикКомбинацииУсловий(НастройкиКомбинацииУсловий);
	КонецЕсли;
	
	Элементы.Страницы.ТекущаяСтраница = Элементы["Страница" + ОбщегоНазначения.ИмяЗначенияПеречисления(Объект.СпособЗаданияУсловия)];
	Если Объект.СпособЗаданияУсловия = Перечисления.СпособыЗаданияУсловия.НаВстроенномЯзыке Тогда
		Элементы.ГруппаОтветственныйИКомментарий.Видимость = Ложь;
	КонецЕсли;
	
	Элементы.КомпоновщикНастройкиОтборСгруппироватьЭлементыОтбора.Заголовок = НСтр("ru = 'Сгруппировать правила'; en = 'Group rules'");
	Элементы.КомпоновщикНастройкиОтборКонтекстноеМенюСгруппироватьЭлементыОтбора.Заголовок = НСтр("ru = 'Сгруппировать правила'; en = 'Group rules'");
    Элементы.КомпоновщикНастройкиОтборДобавитьЭлементОтбора.Заголовок = НСтр("ru = 'Добавить правило'; en = 'Add rule'");
	Элементы.КомпоновщикНастройкиОтборКонтекстноеМенюДобавитьЭлементОтбора.Заголовок = НСтр("ru = 'Добавить правило'; en = 'Add rule'");
	
	//скрываем кнопку "Очистить" у колонки "Поле" в компоновщике отбора, который используется в режиме "Конструктор"
	Элементы.КомпоновщикНастройкиОтборЛевоеЗначение.КнопкаОчистки = Ложь;
		
	//Настройка действия при изменении вида сравнения
	Элементы.КомпоновщикНастройкиОтборВидСравнения.УстановитьДействие("ПриИзменении", "ВидСравненияПриИзменении");
		
	//Настройка действия при начале изменения правого значения сравнения
	Элементы.КомпоновщикНастройкиОтборПравоеЗначение.УстановитьДействие("НачалоВыбора", "ПравоеЗначениеНачалоВыбора");
		
	Элементы.КомпоновщикУсловийНастройкиОтборДобавитьЭлементОтбора.Заголовок = НСтр("ru = 'Добавить условие'; en = 'Add rule'");
	Элементы.КомпоновщикУсловийНастройкиОтборКонтекстноеМенюДобавитьЭлементОтбора.Заголовок = НСтр("ru = 'Добавить условие'; en = 'Add rule'");
	
	Элементы.ГруппаКомпоновщик.Доступность = ЗначениеЗаполнено(Объект.ТипОбъекта);
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоРеквизитовОбъектаВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Объект.ВыражениеУсловия = Объект.ВыражениеУсловия + " " + Элемент.ТекущиеДанные.ПолныйПуть;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ВыбранПроектИлиПроектнаяЗадача" Тогда
		
		Если ТипЗнч(Параметр) = Тип("Структура") Тогда	
			Предмет = Параметр.ПроектнаяЗадача;	
		Иначе
			Предмет = Параметр;
		КонецЕсли;	
		
		ПроверитьВыражениеСервер();
		
	КонецЕсли;	
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	//проверка на уникальность имени условия
	РезультатПроверки = ПроверитьУникальностьУсловия();
	Если РезультатПроверки <> Неопределено Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			НСтр("ru='Данное Наименование уже указано для другого условия'; en = 'This description is already specified for another rule'"),, "Объект.Наименование",, Отказ);
		Возврат;
	КонецЕсли;
	
	Если Объект.СпособЗаданияУсловия = Перечисления.СпособыЗаданияУсловия.КомбинацияИзДругихУсловий
		И КомпоновщикУсловий.Настройки.Отбор.Элементы.Количество() = 0 Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			НСтр("ru = 'Не указано ни одного условия'; en = 'Not a single rule is specified'"),,"КомпоновщикУсловий.Настройки.Отбор",, Отказ);
	ИначеЕсли Объект.СпособЗаданияУсловия = Перечисления.СпособыЗаданияУсловия.ВРежимеКонструктора
		И Компоновщик.Настройки.Отбор.Элементы.Количество() = 0 Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			НСтр("ru = 'Не указано ни одного правила'; en = 'Not a single rule is specified'"),,"Компоновщик.Настройки.Отбор",, Отказ);
	ИначеЕсли Объект.СпособЗаданияУсловия = Перечисления.СпособыЗаданияУсловия.НаВстроенномЯзыке
		И НЕ ЗначениеЗаполнено(Объект.ВыражениеУсловия) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			НСтр("ru = 'Не задано выражение'; en = 'Expression is not specified'"),,"Объект.ВыражениеУсловия",, Отказ);
	КонецЕсли;
	
	Если Объект.СпособЗаданияУсловия = Перечисления.СпособыЗаданияУсловия.КомбинацияИзДругихУсловий
		И ПроверитьНаличиеУсловияВДереве(Истина) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			НСтр("ru = 'Заполнены не все значения в комбинации условий'; en = 'Not all values are filled in in combination of conditions'"),,"КомпоновщикУсловий.Настройки.Отбор",, Отказ);
		Возврат;
	КонецЕсли;
	
	Если Объект.СпособЗаданияУсловия = Перечисления.СпособыЗаданияУсловия.ВРежимеКонструктора
		И ПроверитьНаличиеПустыхСтрокОтбораВДереве() Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			НСтр("ru = 'Среди правил есть некорректно заполненные'; en = 'There are incorrectly filled rules'"),,"Компоновщик.Настройки.Отбор",, Отказ);
		Возврат;
	КонецЕсли;
	
	Если Объект.СпособЗаданияУсловия = Перечисления.СпособыЗаданияУсловия.КомбинацияИзДругихУсловий Тогда
		ТекущийОбъект.ПредставлениеОтбора = Строка(КомпоновщикУсловий.Настройки.Отбор);
	ИначеЕсли Объект.СпособЗаданияУсловия = Перечисления.СпособыЗаданияУсловия.ВРежимеКонструктора Тогда
		ТекущийОбъект.ПредставлениеОтбора = Строка(Компоновщик.Настройки.Отбор);
	ИначеЕсли Объект.СпособЗаданияУсловия = Перечисления.СпособыЗаданияУсловия.НаВстроенномЯзыке Тогда
		ТекущийОбъект.ПредставлениеОтбора = "";
	КонецЕсли;
	
	ТекущийОбъект.ПредставлениеОтбора = СтрЗаменить(ТекущийОбъект.ПредставлениеОтбора, "( ", "(");
	ТекущийОбъект.ПредставлениеОтбора = СтрЗаменить(ТекущийОбъект.ПредставлениеОтбора, " )", ")");
	
	ТекущийОбъект.НастройкаУсловия = Новый ХранилищеЗначения(Компоновщик.ПолучитьНастройки());
	ТекущийОбъект.НастройкаКомбинацииУсловий = Новый ХранилищеЗначения(КомпоновщикУсловий.ПолучитьНастройки());
	
КонецПроцедуры

&НаКлиенте
Процедура КомпоновщикУсловийНастройкиОтборПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	
	НовыйЭлементОтбора = КомпоновщикУсловий.Настройки.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
    ПолеОтбора = Новый ПолеКомпоновкиДанных("Результат_проверки_условия");
    НовыйЭлементОтбора.ЛевоеЗначение = ПолеОтбора;
    НовыйЭлементОтбора.Использование = Истина;
    НовыйЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	Отказ = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура КомпоновщикУсловийНастройкиОтборПриИзменении(Элемент)
	
	Если Элемент.ТекущиеДанные.ЛевоеЗначение <> Неопределено Тогда
		ДобавленноеУсловие = Элемент.ТекущиеДанные.ПравоеЗначение;
		Результат = ПроверитьНаличиеУсловияВДереве(Ложь);
		Если Результат Тогда
			ТекстПредупреждения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Нельзя добавить условие ""%1"". Произошло зацикливание условий.'; en = 'Unable to add condition ""%1"". Looping conditions found.'"),
				ДобавленноеУсловие);
			ПоказатьПредупреждение(, ТекстПредупреждения);
			УдалитьУсловиеИзКомбинацииУсловий(ДобавленноеУсловие);
		КонецЕсли;
	КонецЕсли;
	Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура КомпоновщикНастройкиОтборПриИзменении(Элемент)
	
	Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура СпособЗаданияУсловияПриИзменении(Элемент)
		
	Если ЗначениеЗаполнено(Объект.СпособЗаданияУсловия) Тогда
		Если Объект.СпособЗаданияУсловия = ПредопределенноеЗначение("Перечисление.СпособыЗаданияУсловия.ВРежимеКонструктора") Тогда
			Элементы.Страницы.ТекущаяСтраница = Элементы["СтраницаВРежимеКонструктора"];
		ИначеЕсли Объект.СпособЗаданияУсловия = ПредопределенноеЗначение("Перечисление.СпособыЗаданияУсловия.НаВстроенномЯзыке") Тогда
			Элементы.Страницы.ТекущаяСтраница = Элементы["СтраницаНаВстроенномЯзыке"];
		ИначеЕсли Объект.СпособЗаданияУсловия = ПредопределенноеЗначение("Перечисление.СпособыЗаданияУсловия.КомбинацияИзДругихУсловий") Тогда
			Элементы.Страницы.ТекущаяСтраница = Элементы["СтраницаКомбинацияИзДругихУсловий"];
		КонецЕсли;
		Элементы.ГруппаОтветственныйИКомментарий.Видимость = 
			(Объект.СпособЗаданияУсловия = ПредопределенноеЗначение("Перечисление.СпособыЗаданияУсловия.НаВстроенномЯзыке"));
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьВыражениеПродолжение(ИзмененныйТекст, Параметры) Экспорт
	
	Если ЗначениеЗаполнено(ИзмененныйТекст) Тогда
		Объект.ВыражениеУсловия = ИзмененныйТекст;
	КонецЕсли;	
		
КонецПроцедуры

&НаКлиенте
Процедура ВставитьРеквизитИзДерева(Команда)
	
	Если Элементы.ДеревоРеквизитовОбъекта.ТекущиеДанные <> Неопределено Тогда
		Объект.ВыражениеУсловия = Объект.ВыражениеУсловия + " " + Элементы.ДеревоРеквизитовОбъекта.ТекущиеДанные.ПолныйПуть;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТипОбъектаОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	ПроверитьДоступКТипуДокументаИЗагрузитьНастройки(ВыбранноеЗначение, СтандартнаяОбработка);
	Если НЕ СтандартнаяОбработка Тогда
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Недостаточно прав для создания условия маршрутизации для предмета типа ""%1"".'; en = 'Not enough permissions to create routing rule for subject of type ""%1"".'"),
			Строка(ВыбранноеЗначение));
		ПоказатьПредупреждение(, ТекстСообщения);	
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПроверитьДоступКТипуДокументаИЗагрузитьНастройки(ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если НЕ ЗначениеЗаполнено(ВыбранноеЗначение) Тогда
		ОчиститьДеревоРеквизитовОбъекта();
		Компоновщик.Настройки.Отбор.Элементы.Очистить();
		Возврат;
	КонецЕсли;
	
	ИндексЗначенияПеречисления = Перечисления.ТипыОбъектов.Индекс(ВыбранноеЗначение);
	ИмяЗначенияПеречисления = Метаданные.Перечисления.ТипыОбъектов.ЗначенияПеречисления[ИндексЗначенияПеречисления].Имя;
	Если Метаданные.Справочники.Найти(ИмяЗначенияПеречисления) <> Неопределено Тогда
		СтандартнаяОбработка = ПравоДоступа("Чтение", Метаданные.Справочники[ИмяЗначенияПеречисления]); 
	ИначеЕсли Метаданные.БизнесПроцессы.Найти(ИмяЗначенияПеречисления) <> Неопределено Тогда
		СтандартнаяОбработка = ПравоДоступа("Чтение", Метаданные.БизнесПроцессы[ИмяЗначенияПеречисления]); 
	КонецЕсли;
	Если СтандартнаяОбработка Тогда
		Элементы.ГруппаКомпоновщик.Доступность = ЗначениеЗаполнено(ВыбранноеЗначение);
		НастроитьКомпоновщикУсловияИЗаполнитьДеревоРеквизитовОбъекта(ВыбранноеЗначение);
		Элементы.КомпоновщикНастройкиОтбор.Обновить();
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////
///Обработчики событий отбора СКД///
////////////////////////////////////

&НаКлиенте
Процедура ВидСравненияПриИзменении(Элемент)
	
	СтандартнаяОбработка = Ложь;
	ЭлементОтбора = Компоновщик.Настройки.Отбор.ПолучитьОбъектПоИдентификатору(Элементы.КомпоновщикНастройкиОтбор.ТекущаяСтрока);
	Если ЭлементОтбора = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(ЭлементОтбора.ПравоеЗначение) = Тип("СправочникСсылка.Пользователи")
		ИЛИ 
		(ТипЗнч(ЭлементОтбора.ПравоеЗначение) = Тип("СписокЗначений")
			И ЭлементОтбора.ПравоеЗначение.ТипЗначения.СодержитТип(Тип("СправочникСсылка.Пользователи"))) Тогда
		
		ПараметрыФормы = Новый Структура;
		Если ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.ВИерархии
			ИЛИ ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.НеВИерархии Тогда
			ЭлементОтбора.ПравоеЗначение = ПредопределенноеЗначение("Справочник.РабочиеГруппы.ПустаяСсылка");
		КонецЕсли;
		
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура ПравоеЗначениеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ЭлементОтбора = Компоновщик.Настройки.Отбор.ПолучитьОбъектПоИдентификатору(
		Элементы.КомпоновщикНастройкиОтбор.ТекущаяСтрока);
	Если ЭлементОтбора = Неопределено Тогда
		Возврат;
	КонецЕсли;
	Если ТипЗнч(ЭлементОтбора.ПравоеЗначение) = Тип("СправочникСсылка.РабочиеГруппы") Тогда
	    СтандартнаяОбработка = Ложь;
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("РежимВыбора", Истина);
		ОписаниеОповещения = Новый ОписаниеОповещения(
			"ПравоеЗначениеНачалоВыбораПродолжение",
			ЭтотОбъект);
		ОткрытьФорму(
			"Справочник.РабочиеГруппы.ФормаВыбора", 
			ПараметрыФормы, 
			Элемент,,,,
			ОписаниеОповещения,
			РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	ИначеЕсли ТипЗнч(ЭлементОтбора.ПравоеЗначение) = Тип("СправочникСсылка.ЗначенияСвойствОбъектов") Тогда
		СтандартнаяОбработка = Ложь;
		ИмяДопРеквизита = Строка(ЭлементОтбора.ЛевоеЗначение);
		ИмяДопРеквизита = Сред(ИмяДопРеквизита, Найти(ИмяДопРеквизита, "["));
		ИмяДопРеквизита = Сред(ИмяДопРеквизита, 2, СтрДлина(ИмяДопРеквизита) - 2);
		ВладелецЗначения = ПолучитьВладельцаЗначенияДопРеквизита(ИмяДопРеквизита);

		ПараметрыФормы = Новый Структура();
		ПараметрыФормы.Вставить("РежимВыбора", Истина);
		ПараметрыФормы.Вставить("Отбор", Новый Структура("Владелец", ВладелецЗначения));
		ОткрытьФорму("Справочник.ЗначенияСвойствОбъектов.Форма.ФормаСписка", ПараметрыФормы, Элемент);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПравоеЗначениеНачалоВыбораПродолжение(Результат, Параметры) Экспорт
	
	ВыбранноеПравоеЗначение = Результат;
	
	ПодключитьОбработчикОжидания("УстановитьПравоеЗначениеЭлементаОтбора", 0.2, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьПравоеЗначениеЭлементаОтбора()
	
	ЭлементОтбора = Компоновщик.Настройки.Отбор.ПолучитьОбъектПоИдентификатору(
		Элементы.КомпоновщикНастройкиОтбор.ТекущаяСтрока);
	ЭлементОтбора.ПравоеЗначение = ВыбранноеПравоеЗначение;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьВладельцаЗначенияДопРеквизита(ИмяДопРеквизита)
	
	Возврат ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.НайтиПоНаименованию(ИмяДопРеквизита);	
	
КонецФункции

&НаКлиенте
Процедура ПредметПриИзменении(Элемент)
	
	ПроверитьВыражениеСервер();
	
КонецПроцедуры

&НаСервере
Процедура ПроверитьВыражениеСервер()
	
	Результат = Неопределено;
	ИтогПроверки = "";
	
	Попытка
		Выполнить(Объект.ВыражениеУсловия);
	Исключение
		Результат = Ложь;
		Инфо = ИнформацияОбОшибке();
		
		Описание = "";
		Если ТипЗнч(Инфо.Причина) = Тип("ИнформацияОбОшибке") Тогда
			Описание = Инфо.Причина.Описание;
		Иначе
			Описание = Инфо.Описание;
		КонецЕсли;
		
		ИтогПроверки = НСтр("ru = 'Ошибка.'; en = 'Error.'") + Символы.ВК + Описание;
		Возврат;
	КонецПопытки;		
	
	Если ТипЗнч(Результат) <> Тип("Булево") Тогда
		ИтогПроверки = НСтр("ru = 'Ошибка.
			|Переменной ""Результат"" необходимо присвоить значение типа ""Булево""';
			|en = 'Error. 
			|Value of type ""Boolean"" must be assigned to variable ""Result"".'");	
		Возврат;
	КонецЕсли;
	
	ИтогПроверки = ?(Результат, НСтр("ru = 'Истина'; en = 'True'"), НСтр("ru = 'Ложь'; en = 'False'"));
		
КонецПроцедуры

&НаСервереБезКонтекста
// Возвращает тип по типу объекта
// Параметры:
//  ТипОбъекта - ПеречислениеСсылка.ТипыОбъектов
//
// Возвращаемое значение - Тип
Функция ТипПоТипуОбъекта(ТипОбъекта)
	
	ИндексЗначенияПеречисления = Перечисления.ТипыОбъектов.Индекс(ТипОбъекта);
	ИмяЗначенияПеречисления = Метаданные.Перечисления.ТипыОбъектов.ЗначенияПеречисления[ИндексЗначенияПеречисления].Имя;
	ТипВыбранный = ТипЗнч(Справочники[ИмяЗначенияПеречисления].ПустаяСсылка());
	Возврат ТипВыбранный;
	
КонецФункции	

&НаКлиенте
Процедура ПредметНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ТипВыбранный = ТипПоТипуОбъекта(Объект.ТипОбъекта);
	
	Если ТипВыбранный = Тип("СправочникСсылка.ПроектныеЗадачи") Тогда
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("ВыбиратьТолькоПроектнуюЗадачу", Истина);
		
		Если ТипВыбранный = Тип("СправочникСсылка.ПроектныеЗадачи") Тогда
			ПараметрыФормы.Вставить("ПроектнаяЗадача", Предмет);
		ИначеЕсли ТипВыбранный = Тип("СправочникСсылка.Проекты") Тогда
			ПараметрыФормы.Вставить("Проект", Предмет);
		КонецЕсли;
		
		ОткрытьФорму("ОбщаяФорма.ВыборПроектаЗадачи", ПараметрыФормы);
		
	Иначе
		
		ПолноеИмя = ПолноеИмяПоТипу(ТипВыбранный);
		
		ОписаниеОповещения = Новый ОписаниеОповещения("ПредметСтрокойНажатиеЗавершение", ЭтотОбъект);
		РежимОткрытияОкна = РежимОткрытияОкнаФормы.БлокироватьОкноВладельца;
		ОткрытьФорму(ПолноеИмя + ".ФормаВыбора", , , , , , ОписаниеОповещения, РежимОткрытияОкна);
		
	КонецЕсли;	
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолноеИмяПоТипу(ТипВыбранный)
	
	ПолноеИмя = Метаданные.НайтиПоТипу(ТипВыбранный).ПолноеИмя();
	Возврат ПолноеИмя;
	
КонецФункции	

&НаКлиенте
Процедура ПредметСтрокойНажатиеЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если ЗначениеЗаполнено(Результат) Тогда
		Предмет = Результат;
		ПроверитьВыражениеСервер();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	Оповестить("Запись_Условие");
	
КонецПроцедуры


