//////////////////////////////////////////////////////////////////////////////// 
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ПротоколированиеРаботыПользователей.ЗаписатьОткрытие(Объект.Ссылка);
	ИспользоватьУчетПоОрганизациям = ПолучитьФункциональнуюОпцию("ИспользоватьУчетПоОрганизациям");
	
	Если ЗначениеЗаполнено(Объект.Родитель)Тогда 
		ПодразделениеРодителя = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.Родитель, "Подразделение");
		
		Если ЗначениеЗаполнено(ПодразделениеРодителя) Тогда 
			МассивПодразделений = ЗаполнитьСписокВыбораПодразделения(ПодразделениеРодителя);
			Элементы.Подразделение.СписокВыбора.ЗагрузитьЗначения(МассивПодразделений);	
			Элементы.Подразделение.РежимВыбораИзСписка = Истина;
		КонецЕсли;	
	КонецЕсли;

	Если Объект.Ссылка.Пустая() Тогда
		Если Не ЗначениеЗаполнено(Объект.Год) Тогда
			Объект.Год = Год(ТекущаяДата());
		КонецЕсли;	
		
		Если ЗначениеЗаполнено(Объект.Родитель)Тогда 
			Если ИспользоватьУчетПоОрганизациям Тогда				
				Организация = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.Родитель, "Организация");
				Объект.Организация = Организация;
			КонецЕсли;

			Объект.Подразделение = ПодразделениеРодителя;
		КонецЕсли;

	КонецЕсли;	
		
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	ПараметрыЗаписи.Вставить("ЭтоНовыйОбъект", НЕ ЗначениеЗаполнено(ТекущийОбъект.Ссылка));
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	ПротоколированиеРаботыПользователей.ЗаписатьСоздание(Объект.Ссылка, ПараметрыЗаписи.ЭтоНовыйОбъект);
	ПротоколированиеРаботыПользователей.ЗаписатьИзменение(Объект.Ссылка);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ШАПКИ ФОРМЫ

&НаКлиенте
Процедура РодительПриИзменении(Элемент)
	
	ПодразделениеРодителя = Неопределено;
	
	Если ЗначениеЗаполнено(Объект.Родитель) Тогда	
		ПодразделениеРодителя = ОбщегоНазначенияДокументооборотВызовСервера.ЗначениеРеквизитаОбъекта(
			Объект.Родитель, "Подразделение");
		Объект.Подразделение = ПодразделениеРодителя;
	КонецЕсли;	
	
	Если ЗначениеЗаполнено(ПодразделениеРодителя) Тогда 
		МассивПодразделений = ЗаполнитьСписокВыбораПодразделения(ПодразделениеРодителя);
		Элементы.Подразделение.РежимВыбораИзСписка = Истина;
		Элементы.Подразделение.СписокВыбора.ЗагрузитьЗначения(МассивПодразделений);
	Иначе 
		Элементы.Подразделение.РежимВыбораИзСписка = Ложь;
		Элементы.Подразделение.СписокВыбора.Очистить();
	КонецЕсли;	

КонецПроцедуры

&НаКлиенте
Процедура РодительНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	Если ИспользоватьУчетПоОрганизациям Тогда 
		СтандартнаяОбработка = Ложь;
	
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("Организация", Объект.Организация);
		ОткрытьФорму("Справочник.РазделыНоменклатурыДел.ФормаВыбора", ПараметрыФормы, Элемент);
	КонецЕсли;	

КонецПроцедуры

&НаКлиенте
Процедура РодительОчистка(Элемент, СтандартнаяОбработка)
	
	ПодразделениеРодителя = Неопределено;
	Элементы.Подразделение.РежимВыбораИзСписка = Ложь;
	Элементы.Подразделение.СписокВыбора.Очистить();	

КонецПроцедуры

&НаКлиенте
Процедура РодительАвтоПодбор(Элемент, Текст, ДанныеВыбора, Параметры, Ожидание, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Текст)
		И ИспользоватьУчетПоОрганизациям 
		И ЗначениеЗаполнено(Объект.Организация) Тогда
		СтандартнаяОбработка = Ложь;
		ДанныеВыбора = СформироватьДанныеВыбораРодителя(Текст, Объект.Организация, Объект.Год);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура РодительОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, Параметры, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Текст)
		И ИспользоватьУчетПоОрганизациям 
		И ЗначениеЗаполнено(Объект.Организация) Тогда
		СтандартнаяОбработка = Ложь;
		ДанныеВыбора = СформироватьДанныеВыбораРодителя(Текст, Объект.Организация, Объект.Год);
	КонецЕсли;

КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаСервереБезКонтекста
Функция СформироватьДанныеВыбораРодителя(Текст, Организация, Год)
	
	ДанныеВыбора = Новый СписокЗначений;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
			"ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	РазделыНоменклатурыДел.Ссылка КАК Ссылка,
			|	РазделыНоменклатурыДел.Наименование
			|ИЗ
			|	Справочник.РазделыНоменклатурыДел КАК РазделыНоменклатурыДел
			|ГДЕ
			|	РазделыНоменклатурыДел.Наименование ПОДОБНО &Текст
			|	И РазделыНоменклатурыДел.Организация = &Организация
			|	И РазделыНоменклатурыДел.Год = &Год";
					
	Запрос.УстановитьПараметр("Текст", Текст + "%");
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("Год", Год);

	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ДанныеВыбора.Добавить(Выборка.Ссылка, Строка(Выборка.Наименование));
	КонецЦикла;
		
	Возврат ДанныеВыбора;
	
КонецФункции	

&НаСервереБезКонтекста
Функция ЗаполнитьСписокВыбораПодразделения(Подразделение) 
	
	Массив = Новый Массив;

	Запрос = Новый Запрос;
	Запрос.Текст = 
			"ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	СтруктураПредприятия.Ссылка
			|ИЗ
			|	Справочник.СтруктураПредприятия КАК СтруктураПредприятия
			|ГДЕ
			|	НЕ СтруктураПредприятия.ПометкаУдаления
			|	И СтруктураПредприятия.Ссылка В ИЕРАРХИИ(&Подразделение)";
					
	Запрос.УстановитьПараметр("Подразделение", Подразделение);

	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Массив.Добавить(Выборка.Ссылка);
	КонецЦикла;
	
	Возврат Массив;
	
КонецФункции


