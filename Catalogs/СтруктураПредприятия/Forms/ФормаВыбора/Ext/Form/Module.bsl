#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Отбор.Свойство("ПометкаУдаления") И Не Параметры.Отбор.ПометкаУдаления Тогда 
		Элементы.ПоказыватьУдаленные.Видимость = Ложь;
	Иначе 
		ПоказыватьУдаленные = Ложь;
		ПоказатьУдаленные();
	КонецЕсли;
	
	Делопроизводство.СписокДокументовУсловноеОформлениеПомеченныхНаУдаление(Список);
	
	// СтандартныеПодсистемы.Печать
	УправлениеПечатью.ПриСозданииНаСервере(ЭтаФорма);
	// Конец СтандартныеПодсистемы.Печать
	
	Параметры.Отбор.Свойство("ра_Организация", ра_Организация);
	Параметры.Свойство("СоздаватьСтруктуру", СоздаватьСтруктуру);
	
	Если ЗначениеЗаполнено(ра_Организация) Тогда
		
		Параметры.Отбор.Удалить("ра_Организация");
			
		Элементы.Список.НачальноеОтображениеДерева = НачальноеОтображениеДерева.РаскрыватьВсеУровни;
		Элементы.Список.РодительВерхнегоУровня     = Справочники.СтруктураПредприятия.ПолучитьПервоеПодразделениеОрганизации(ра_Организация);

	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеДанныхИзНастроекНаСервере(Настройки)
	
	Если Не Параметры.Отбор.Свойство("ПометкаУдаления") Тогда
		ПоказыватьУдаленные = Настройки["ПоказыватьУдаленные"];
		ПоказатьУдаленные();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПоказыватьУдаленные(Команда)
	
	ПоказыватьУдаленные = Не ПоказыватьУдаленные;
	
	ПоказатьУдаленные();
	
КонецПроцедуры

// СтандартныеПодсистемы.Печать
&НаКлиенте
Процедура Подключаемый_ВыполнитьКомандуПечати(Команда)
	
	УправлениеПечатьюКлиент.ВыполнитьПодключаемуюКомандуПечати(Команда, ЭтаФорма, Элементы.Список);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПоказатьУдаленные()
	
	Если ПоказыватьУдаленные Тогда
		ОбщегоНазначенияКлиентСервер.УдалитьЭлементыГруппыОтбораДинамическогоСписка(Список, "ПометкаУдаления");
	Иначе
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список, "ПометкаУдаления", Ложь);
	КонецЕсли;
	
	Элементы.ПоказыватьУдаленные.Пометка = ПоказыватьУдаленные;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если ЗначениеЗаполнено(ра_Организация)
		И Не ЗначениеЗаполнено(Элементы.Список.РодительВерхнегоУровня) Тогда
		
		Отказ = Истина;
		Если СоздаватьСтруктуру Тогда
			ОписаниеОповещения = Новый ОписаниеОповещения("ОбработкаОтветаПоПредоставлениюДоступа", ЭтотОбъект);
			ПоказатьВопрос(ОписаниеОповещения, НСтр(СтрШаблон("ru = 'Предоставить доступ организации %1?'", ра_Организация)), РежимДиалогаВопрос.ОКОтмена);
		Иначе
			ПоказатьПредупреждение(,НСтр(СтрШаблон("ru = 'Организации %1 не предоставлен доступ'", ра_Организация)));
		КонецЕсли;
		
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОтветаПоПредоставлениюДоступа(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.ОК Тогда
		ОткрытьФорму("Справочник.СтруктураПредприятия.ФормаОбъекта", Новый Структура("Основание", ра_Организация), ВладелецФормы);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
