
////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Элементы.Состав.ТолькоПросмотр = Истина;
	ЭтоНовый = Объект.Ссылка.Пустая();
	ЗаполнитьСоставОбъектов();
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

&НаСервере
Процедура ЗаполнитьСоставОбъектов()

	УстановитьПривилегированныйРежим(Истина);
	
	ДеревоСостав = РеквизитФормыВЗначение("Состав");
	ДеревоСостав.Строки.Очистить();
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	ИдентификаторыОбъектовМетаданных.ПолноеИмя
		|ИЗ
		|	Справочник.ОбластиДелегированияПрав.Состав КАК ОбластиДелегированияПравСостав
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ИдентификаторыОбъектовМетаданных КАК ИдентификаторыОбъектовМетаданных
		|		ПО ОбластиДелегированияПравСостав.ОбъектМетаданных = ИдентификаторыОбъектовМетаданных.Ссылка
		|ГДЕ
		|	ОбластиДелегированияПравСостав.Ссылка = &Ссылка");
		
	Запрос.УстановитьПараметр("Ссылка", Объект.Ссылка);
	ИменаМетаданных = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("ПолноеИмя");
	
	Отбор = Новый Структура;
	Отбор.Вставить("ПолноеИмя", ИменаМетаданных);
	
	ДеревоМетаданных = ОбщегоНазначения.ДеревоМетаданныхКонфигурации(Отбор);
	ЗначениеВРеквизитФормы(ДеревоМетаданных, "Состав");
	
КонецПроцедуры	


