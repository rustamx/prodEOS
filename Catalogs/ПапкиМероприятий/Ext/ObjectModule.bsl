
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

// Обновление адресной книги
Перем ОбновитьДанныеПапкиМероприятийВАдреснойКниге;

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	ДатаСоздания = ТекущаяДатаСеанса();
	Ответственный = ПользователиКлиентСервер.ТекущийПользователь();
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	РеквизитыПапкиПоСсылке = Неопределено;
	
	Если НЕ Ссылка.Пустая() Тогда
		
		РеквизитыПапкиПоСсылке = 
			ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Ссылка, "ПометкаУдаления, Родитель, Наименование");
		
		ПредыдущаяПометкаУдаления = РеквизитыПапкиПоСсылке.ПометкаУдаления;
		
		Если ПометкаУдаления <> ПредыдущаяПометкаУдаления Тогда 
			
			Если НЕ ДокументооборотПраваДоступа.ПолучитьПраваПоОбъекту(Ссылка).Удаление Тогда
				ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
									 НСтр("ru = 'У вас нет права ""Пометка на удаление"" папки ""%1"".'; en = 'Not enough permissions to delete the folder ""%1"".'"),
									 Строка(Ссылка));
			КонецЕсли;	
			
			Запрос = Новый Запрос;
			Запрос.Текст = 
				"ВЫБРАТЬ РАЗРЕШЕННЫЕ
				|	Мероприятия.Ссылка,
				|	Мероприятия.ПометкаУдаления
				|ИЗ
				|	Справочник.Мероприятия КАК Мероприятия
				|ГДЕ
				|	Мероприятия.Папка = &Ссылка";
			Запрос.УстановитьПараметр("Ссылка", Ссылка);
			
			Выборка = Запрос.Выполнить().Выбрать();
			Пока Выборка.Следующий() Цикл
				Если ПометкаУдаления <> Выборка.ПометкаУдаления Тогда 
					Объект = Выборка.Ссылка.ПолучитьОбъект();
					Объект.Заблокировать();
					Объект.УстановитьПометкуУдаления(ПометкаУдаления);
				КонецЕсли;
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЕсли;
	
	// Обновление адресной книги
	ОбновитьДанныеПапкиМероприятийВАдреснойКниге = Ложь;
	Если ЭтоНовый() Тогда
		ОбновитьДанныеПапкиМероприятийВАдреснойКниге = Истина;
	Иначе
		Если РеквизитыПапкиПоСсылке.Родитель <> Родитель
			Или РеквизитыПапкиПоСсылке.ПометкаУдаления <> ПометкаУдаления
			Или РеквизитыПапкиПоСсылке.Наименование <> Наименование Тогда
			
			ОбновитьДанныеПапкиМероприятийВАдреснойКниге = Истина;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	// Обновление адресной книги
	Если ОбновитьДанныеПапкиМероприятийВАдреснойКниге Тогда
		Справочники.АдреснаяКнига.ОбновитьДанныеОбъекта(
			Ссылка,
			Родитель,
			Справочники.АдреснаяКнига.ПоМероприятиям,
			Ссылка);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
