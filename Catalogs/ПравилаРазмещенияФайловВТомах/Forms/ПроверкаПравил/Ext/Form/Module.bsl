
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого Строка Из Параметры.Правила Цикл
		СтрокаТаблицы = Правила.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаТаблицы, Строка);
	КонецЦикла;	
	
КонецПроцедуры

&НаКлиенте
Процедура ВерсияВладелецСтрокаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОписаниеОповещения = Новый ОписаниеОповещения(
		"ВерсияВладелецСтрокаНачалоВыбораПродолжение",
		ЭтотОбъект);
		
	ОткрытьФорму(
		"Справочник.ВерсииФайлов.ФормаВыбора",,,,,,
		ОписаниеОповещения, 
		РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс);
	
КонецПроцедуры

&НаКлиенте
Процедура ВерсияВладелецСтрокаНачалоВыбораПродолжение(Результат, Параметры) Экспорт
	
	Версия = Результат;
	ВерсияПриИзменении();
	
КонецПроцедуры

&НаСервере
Процедура ВерсияПриИзменении()
	
	ТаблицаПравил = Новый ТаблицаЗначений;
	ТаблицаПравил.Колонки.Добавить("ГруппаТомов", Новый ОписаниеТипов("СправочникСсылка.ТомаХраненияФайлов"));
	ТаблицаПравил.Колонки.Добавить("Условие", Новый ОписаниеТипов("ХранилищеЗначения"));
	
	Для Каждого Строка Из Правила Цикл
		
		СтрокаТаблицы = ТаблицаПравил.Добавить();
		СтрокаТаблицы.ГруппаТомов = Строка.ГруппаТомов;

		Если ЗначениеЗаполнено(Строка.НавигационнаяСсылкаНаУсловие) Тогда
			СтрокаТаблицы.Условие = ПолучитьИзВременногоХранилища(Строка.НавигационнаяСсылкаНаУсловие);
		КонецЕсли;
		
	КонецЦикла;
	
	ВерсияВладелецСтрока = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Версия, "Владелец");
	ГруппаТомов = ФайловыеФункции.ВыбратьГруппуТомовДляРазмещенияВерсииИзТаблицыПравил(Версия, ТаблицаПравил);
	
КонецПроцедуры

