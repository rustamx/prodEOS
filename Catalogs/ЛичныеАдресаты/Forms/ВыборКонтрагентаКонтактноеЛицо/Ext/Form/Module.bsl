
&НаКлиенте
Перем ТекущийЭлементФормы;

&НаКлиенте
Процедура ОбработкаОжиданияКонтактныеЛица()
	
	Если Элементы.Контрагенты.ТекущаяСтрока <> Неопределено Тогда 
		СписокКонтактныеЛица.Параметры.УстановитьЗначениеПараметра("Контрагент", Элементы.Контрагенты.ТекущаяСтрока);
	КонецЕсли;	
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	СписокКонтактныеЛица.Параметры.УстановитьЗначениеПараметра("Контрагент", Элементы.Контрагенты.ТекущаяСтрока);
	
	ЛичныйАдресат = Параметры.ЛичныйАдресат;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ЗапомнитьТекущийЭлемент();
	ПодключитьОбработчикОжидания("ЗапомнитьТекущийЭлемент", 1);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапомнитьТекущийЭлемент()
	
	Если ТекущийЭлемент = Элементы.Контрагенты 
		Или ТекущийЭлемент = Элементы.СписокКонтактныеЛица Тогда
		ТекущийЭлементФормы = ТекущийЭлемент;
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура КонтрагентыПриАктивизацииСтроки(Элемент)
	
	ПодключитьОбработчикОжидания("ОбработкаОжиданияКонтактныеЛица", 0.2, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокКонтактныеЛицаВыборЗначения(Элемент, Значение, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОповеститьОВыборе(Значение);
	
КонецПроцедуры

&НаКлиенте
Процедура Выбрать(Команда)
	
	Если ЭтаФорма.ТекущийЭлемент = Элементы.СписокКонтактныеЛица Тогда 
		
		Если Элементы.СписокКонтактныеЛица.ТекущаяСтрока = Неопределено Тогда 
			Возврат;
		КонецЕсли;	
	
		ОповеститьОВыборе(Элементы.СписокКонтактныеЛица.ТекущаяСтрока);
		
	Иначе
		
		Если Элементы.Контрагенты.ТекущаяСтрока = Неопределено Тогда 
			Возврат;
		КонецЕсли;	
	
		ОповеститьОВыборе(Элементы.Контрагенты.ТекущаяСтрока);
		
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура СписокКонтактныеЛицаПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	
	Если Копирование Тогда 
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные = Элементы.Контрагенты.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда 
		Возврат;
	КонецЕсли;	
	
	Если ТекущиеДанные.ЭтоГруппа Тогда
		Возврат;
	КонецЕсли;	
	
	Отказ = Истина;
	
	ЗначенияЗаполнения = Новый Структура;
	ЗначенияЗаполнения.Вставить("Владелец", ТекущиеДанные.Ссылка);

	ПараметрыФормы = Новый Структура("ЛичныйАдресат", ЛичныйАдресат);
	ПараметрыФормы.Вставить("ЗначенияЗаполнения", ЗначенияЗаполнения);
		
	ОткрытьФорму("Справочник.КонтактныеЛица.ФормаОбъекта", ПараметрыФормы, Элемент);	
	
КонецПроцедуры

&НаКлиенте
Процедура КонтрагентыПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	
	Если Копирование Тогда 
		Возврат;
	КонецЕсли;
	
	Отказ = Истина;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ЛичныйАдресат", ЛичныйАдресат);
		
	ОткрытьФорму("Справочник.Контрагенты.ФормаОбъекта", ПараметрыФормы, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура КонтрагентыВыборЗначения(Элемент, Значение, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОповеститьОВыборе(Значение);
	
КонецПроцедуры

&НаКлиенте
Процедура КонтрагентыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОповеститьОВыборе(ВыбраннаяСтрока);
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьЭлемент(Команда)
	
	Если ТекущийЭлементФормы = Неопределено 
		Или ТекущийЭлементФормы = Элементы.Контрагенты Тогда
		
		Элементы.Контрагенты.ДобавитьСтроку();
		
	ИначеЕсли ТекущийЭлементФормы = Элементы.СписокКонтактныеЛица Тогда
		
		Элементы.СписокКонтактныеЛица.ДобавитьСтроку();
		
	КонецЕсли;
	
КонецПроцедуры

