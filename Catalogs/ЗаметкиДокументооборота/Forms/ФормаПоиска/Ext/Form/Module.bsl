
&НаКлиенте
Процедура СтрокаПоискаПриИзменении(Элемент)
	
	НайтиЗаметки();
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьПоиск(Команда)
	
	НайтиЗаметки();
	
КонецПроцедуры

&НаКлиенте
Процедура НайтиЗаметки()
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(Список.Отбор,
		"СодержаниеТекст",
		СтрокаПоиска,
		ВидСравненияКомпоновкиДанных.Содержит);	

КонецПроцедуры		

&НаКлиенте
Процедура СписокПриАктивизацииСтроки(Элемент)
	
	Если ЗначениеЗаполнено(Элементы.Список.ТекущаяСтрока) Тогда
		ПодключитьОбработчикОжидания("ОтложенныйВыборЗаметки", 0.2, Истина);	
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ОтложенныйВыборЗаметки()
	
	ОбновитьСодержаниеВыбраннойЗаметки();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьСодержаниеВыбраннойЗаметки()
	
	ДанныеЗаметки = ПолучитьДанныеВыбраннойЗаметки(Элементы.Список.ТекущаяСтрока);
	СодержаниеЗаметки = ДанныеЗаметки.Содержание;
	
	ТемыЗаметки = "";
	Для каждого Эл Из ДанныеЗаметки.Темы Цикл
		Если Не ПустаяСтрока(ТемыЗаметки) Тогда
			ТемыЗаметки = ТемыЗаметки + ", ";
		КонецЕсли;	
		ТемыЗаметки = ТемыЗаметки + Эл;
	КонецЦикла;	
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьДанныеВыбраннойЗаметки(Заметка)
	
	ДанныеЗаметки = Новый Структура();
	ДанныеЗаметки.Вставить("Содержание", Заметка.Содержание.Получить());
	ДанныеЗаметки.Вставить("Темы", РегистрыСведений.ТемыЗаметокДокументооборота.ПолучитьТемыЗаметки(Заметка));	
	
	Возврат ДанныеЗаметки;
	
КонецФункции


