#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Ссылка) И ПометкаУдаления Тогда
		
		УчетнаяЗапись = ПолучитьСвязаннуюУчетнуюЗапись();
		
		Если УчетнаяЗапись <> Неопределено Тогда
			
			СообщениеОбОшибке = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Папку ""%1"" нельзя удалить, т.к. она используется учетной записью ""%2"".'; en = 'The folder ""%1"" cannot be deleted because it is used by the account ""%2"".'"),
				ПолучитьПолныйПутьПапки(),
				УчетнаяЗапись);
			
			ВызватьИсключение СообщениеОбОшибке;
			
			Возврат;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Ссылка) Тогда
		ПредыдущаяПометкаУдаления = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Ссылка, "ПометкаУдаления");
		Если ПометкаУдаления <> ПредыдущаяПометкаУдаления Тогда 
			РаботаСФайламиВызовСервера.ПометитьНаУдалениеПисьмаВПапке(Ссылка, ПометкаУдаления);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Функция ПолучитьСвязаннуюУчетнуюЗапись()
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ПапкиУчетныхЗаписей.УчетнаяЗапись КАК УчетнаяЗапись
		|ИЗ
		|	РегистрСведений.ПапкиУчетныхЗаписей КАК ПапкиУчетныхЗаписей
		|ГДЕ
		|	ПапкиУчетныхЗаписей.Папка = &Папка
		|	И НЕ ПапкиУчетныхЗаписей.УчетнаяЗапись.ПометкаУдаления");
	
	Запрос.УстановитьПараметр("Папка", Ссылка);
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Возврат РезультатЗапроса.Выгрузить()[0].УчетнаяЗапись;
	
КонецФункции

Функция ПолучитьПолныйПутьПапки()
	
	УстановитьПривилегированныйРежим(Истина);
	
	ТекПапка = Ссылка;
	Результат = Строка(ТекПапка);
	Родитель = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ТекПапка, "Родитель");
	Пока ЗначениеЗаполнено(Родитель) Цикл
		ТекПапка = Родитель;
		Результат = Строка(ТекПапка) + "\" + Результат;
		Родитель = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ТекПапка, "Родитель");
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

Процедура ПриКопировании(ОбъектКопирования)
	
	ПредопределеннаяПапка = Ложь;
	
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	ЗаполнитьОбъектЗначениямиПоУмолчанию();
	
КонецПроцедуры

// Инициализирует новую папку писем значениями по умолчанию.
//
Процедура ЗаполнитьОбъектЗначениямиПоУмолчанию() Экспорт
	
	ВидПапки = Перечисления.ВидыПапокПисем.Общая;
	ВариантОтображенияКоличестваПисем = Перечисления.ВариантыОтображенияКоличестваПисемВПапке.Непрочтенные;
	
КонецПроцедуры

#КонецЕсли
