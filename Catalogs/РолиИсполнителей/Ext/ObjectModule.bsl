#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

// Обновление адресной книги
Перем ОбновитьДанныеРолиВАдреснойКниге;
Перем ОбновитьДанныеОтображенияРолиВАдреснойКниге;
Перем ОбновитьГруппуВсеПользователиВАдреснойКниге;
Перем ОбновитьСловаПоискаПоРолиВАдреснойКниге;
Перем ОбновитьДоступностьВПоискеПоРоли;
Перем ОбновитьПодчиненныеПолныеРоли;

#Область ОбработчикиСобытий

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Если НЕ ИспользуетсяСОбъектамиАдресации И НЕ ИспользуетсяБезОбъектовАдресации Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			НСтр("ru = 'Не указаны допустимые способы назначения исполнителей на роль: совместно с объектами адресации, без них или обоими способами.'; en = 'Allowed ways of assigning performers are not specified: with addressing objects, without them or using both ways.'"),
		 	ЭтотОбъект, "ИспользуетсяБезОбъектовАдресации",,Отказ);
		Возврат;
	КонецЕсли;
	
	Если НЕ ИспользуетсяСОбъектамиАдресации Тогда
		Возврат;
	КонецЕсли;
	
	ЗаданыТипыОсновногоОбъектаАдресации = ТипыОсновногоОбъектаАдресации <> Неопределено И НЕ ТипыОсновногоОбъектаАдресации.Пустая();
	Если НЕ ЗаданыТипыОсновногоОбъектаАдресации Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Не указаны типы основного объекта адресации.'; en = 'Types of the main addressing object are not specified.'"),
		 	ЭтотОбъект, "ТипыОсновногоОбъектаАдресации",,Отказ);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
		
	Если ТипыОсновногоОбъектаАдресации <> Неопределено И ТипыОсновногоОбъектаАдресации.Пустая() Тогда
		ТипыОсновногоОбъектаАдресации = Неопределено;
	КонецЕсли;
	
	Если ТипыДополнительногоОбъектаАдресации <> Неопределено И ТипыДополнительногоОбъектаАдресации.Пустая() Тогда
		ТипыДополнительногоОбъектаАдресации = Неопределено;
	КонецЕсли;
	
	// Обновление адресной книги и подчиненных полных ролей.
	
	ОбновитьДанныеРолиВАдреснойКниге = Ложь;
	ОбновитьДанныеОтображенияРолиВАдреснойКниге = Ложь;
	ОбновитьСловаПоискаПоРолиВАдреснойКниге = Ложь;
	ОбновитьДоступностьВПоискеПоРоли = Ложь;
	ОбновитьГруппуВсеПользователиВАдреснойКниге = Ложь;
	
	Если ЭтоНовый() Тогда
		
		ОбновитьСловаПоискаПоРолиВАдреснойКниге = Истина;
		ОбновитьГруппуВсеПользователиВАдреснойКниге = Истина;
		ОбновитьДанныеРолиВАдреснойКниге = Истина;
		
		ОбновитьПодчиненныеПолныеРоли = Ложь;
		
	Иначе
		
		РеквизитыРолиПоСсылке = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Ссылка,
			"Наименование, ПометкаУдаления,
			|ИспользуетсяБезОбъектовАдресации, ИспользуетсяСОбъектамиАдресации,
			|ТипыОсновногоОбъектаАдресации, ТипыДополнительногоОбъектаАдресации");
			
		Если РеквизитыРолиПоСсылке.ПометкаУдаления <> ПометкаУдаления Тогда
			ОбновитьДанныеОтображенияРолиВАдреснойКниге = Истина;
			ОбновитьДоступностьВПоискеПоРоли = Истина;
			ОбновитьДанныеРолиВАдреснойКниге = Истина;
		КонецЕсли;
		
		Если РеквизитыРолиПоСсылке.Наименование <> Наименование Тогда
			ОбновитьСловаПоискаПоРолиВАдреснойКниге = Истина;
			ОбновитьДанныеОтображенияРолиВАдреснойКниге = Истина;
			ОбновитьДанныеРолиВАдреснойКниге = Истина;
		КонецЕсли;
		
		ОбновитьПодчиненныеПолныеРоли = 
			РеквизитыРолиПоСсылке.ПометкаУдаления <> ПометкаУдаления
			Или РеквизитыРолиПоСсылке.ИспользуетсяБезОбъектовАдресации <> ИспользуетсяБезОбъектовАдресации
			Или РеквизитыРолиПоСсылке.ИспользуетсяСОбъектамиАдресации <> ИспользуетсяСОбъектамиАдресации
			Или РеквизитыРолиПоСсылке.ТипыОсновногоОбъектаАдресации <> ТипыОсновногоОбъектаАдресации
			Или РеквизитыРолиПоСсылке.ТипыДополнительногоОбъектаАдресации <> ТипыДополнительногоОбъектаАдресации;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	// Обновление адресной книги
	Если ОбновитьДанныеРолиВАдреснойКниге Тогда
		Справочники.АдреснаяКнига.ОбновитьДанныеОбъекта(Ссылка, Неопределено, Справочники.АдреснаяКнига.Роли);
	КонецЕсли;
	Если ОбновитьДанныеОтображенияРолиВАдреснойКниге Тогда
		Справочники.АдреснаяКнига.ОбновитьДанныеОтображенияПодчиненногоОбъекта(Ссылка);
	КонецЕсли;
	Если ОбновитьГруппуВсеПользователиВАдреснойКниге Тогда
		СоставГруппыВсеПользователи = РаботаСПользователями.ПолучитьВсехПользователей();
		Справочники.АдреснаяКнига.РасширитьСписокПользователейРолями(
			СоставГруппыВсеПользователи, Ложь);
		Справочники.АдреснаяКнига.ОбновитьСписокПодчиненныхОбъектов(
			Справочники.РабочиеГруппы.ВсеПользователи,
			Неопределено,
			СоставГруппыВсеПользователи,
			Справочники.АдреснаяКнига.ПоРабочимГруппам);
	КонецЕсли;
	Если ОбновитьСловаПоискаПоРолиВАдреснойКниге Тогда
		РегистрыСведений.ПоискВАдреснойКниге.ОбновитьСловаПоискаПоРоли(ЭтотОбъект);
	КонецЕсли;
	Если ОбновитьДоступностьВПоискеПоРоли Тогда
		РегистрыСведений.ПоискВАдреснойКниге.ОбновитьДоступностьВПоиске(ЭтотОбъект)
	КонецЕсли;
	
	Если ОбновитьПодчиненныеПолныеРоли Тогда
		Справочники.ПолныеРоли.ОбновитьПометкуУдаленияСогласноВладельцу(Ссылка,
			ПометкаУдаления,
			ИспользуетсяБезОбъектовАдресации,
			ИспользуетсяСОбъектамиАдресации,
			ТипыОсновногоОбъектаАдресации,
			ТипыДополнительногоОбъектаАдресации);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
