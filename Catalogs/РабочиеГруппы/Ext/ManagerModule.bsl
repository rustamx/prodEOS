
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает структуру полей рабочей группы.
//
// Возвращаемое значение:
//   Структура
//     Наименование
//     Родитель
//
Функция ПолучитьСтруктуруРабочихГрупп() Экспорт
	
	СвойстваЭлемента = Новый Структура;
	СвойстваЭлемента.Вставить("Наименование");
	СвойстваЭлемента.Вставить("Родитель");
	
	Возврат СвойстваЭлемента;
	
КонецФункции

// Создает и записывает в БД элемент справочника.
//
// Параметры:
//   СтруктураРабочейГруппы - Структура - структура полей рабочей группы.
//
Функция СоздатьРабочуюГруппу(СтруктураРабочейГруппы) Экспорт
	
	НовыйЭлемент = СоздатьЭлемент();
	ЗаполнитьЗначенияСвойств(НовыйЭлемент, СтруктураРабочейГруппы);
	НовыйЭлемент.Записать();
	
	Возврат НовыйЭлемент.Ссылка;
	
КонецФункции

// Возвращает состав пользователей, входящих в указанный контейнер.
//
// Параметры:
//   Контейнер - СправочникСсылка.РабочиеГруппы - контейнер пользователей.
//
// Возвращаемое значение:
//   Массив - массив значений СправочникСсылка.Пользователи - состав контейнера.
//
Функция СоставКонтейнераПользователей(Контейнер) Экспорт
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	ИсточникДанных.Пользователь КАК Пользователь 
		|ИЗ
		|	РегистрСведений.СоставыГруппПользователей КАК ИсточникДанных
		|ГДЕ
		|	ИсточникДанных.ГруппаПользователей = &Контейнер
		|	И НЕ ИсточникДанных.Пользователь.ПометкаУдаления
		|");
	Запрос.УстановитьПараметр("Контейнер", Контейнер);
	
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Пользователь");
	
КонецФункции

// Возвращает контейнеры типа РабочиеГруппы, которым принадлежит указанный пользователь.
//
// Параметры:
//   Пользователь - СправочникСсылка.Пользователи - проверяемый пользователь.
//
// Возвращаемое значение:
//   Массив - массив значений СправочникСсылка.РабочиеГруппы - контейнеры, которым принадлежит пользователь.
//
Функция КонтейнерыПользователя(Пользователь) Экспорт
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	ИсточникДанных.ГруппаПользователей КАК Контейнер 
		|ИЗ
		|	РегистрСведений.СоставыГруппПользователей КАК ИсточникДанных
		|ГДЕ
		|	ИсточникДанных.Пользователь = &Пользователь
		|");
	Запрос.УстановитьПараметр("Пользователь", Пользователь);
	
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Контейнер");
	
КонецФункции

// Вернет Истина, у этого объекта метаданных есть функция ПолучитьАдресФото
Функция ЕстьФункцияПолученияФото() Экспорт
	
	Возврат Ложь;
	
КонецФункции

#КонецОбласти

#КонецЕсли
