
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Если Не Параметры.Свойство("СписокЗначений")
		ИЛИ Параметры.СписокЗначений.Количество() <> 2 Тогда
		
		ВызватьИсключение(НСтр("ru = 'Некорректные параметры'; en = 'Incorrect parameters'"));
	
	КонецЕсли;
	
	Дата1 = Параметры.СписокЗначений[0].Значение;
	Дата2 = Параметры.СписокЗначений[1].Значение;
	
	ДатаПосле = Дата1;
	ДатаДо = Дата2;
	
	ИспользоватьДатуПосле = ЗначениеЗаполнено(ДатаПосле);
	ИспользоватьДатуДо = ЗначениеЗаполнено(ДатаДо);
	Элементы.ДатаПосле.Доступность = ИспользоватьДатуПосле;
	Элементы.ДатаПосле1.Доступность = ИспользоватьДатуПосле;
	Элементы.ДатаДо.Доступность = ИспользоватьДатуДо;
	Элементы.ДатаДо1.Доступность = ИспользоватьДатуДо;
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	Закрыть(Неопределено);
	
КонецПроцедуры

&НаКлиенте
Процедура Готово(Команда)
	
	Если ЗначениеЗаполнено(ДатаДо) И ДатаДо < ДатаПосле Тогда
		Текст = НСтр("ru = 'Значение в поле ""Дата до"" должно быть больше значения в поле ""Дата после""'; en = 'The value of ""To"" date must be greater than the value of ""From"" date.'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			Текст,
			,
			,
			"ДатаДо");
		Возврат;
	КонецЕсли;
	
	Если (ЗначениеЗаполнено(ДатаДо) Или ЗначениеЗаполнено(ДатаДо)) И ДатаДо = ДатаПосле Тогда
		Текст = НСтр("ru = 'Необходимо ввести разные значения в поля ""Дата до"" и ""Дата после""'; en = 'You must enter different values in the fields ""From"" and ""To"" date.'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			Текст,
			,
			,
			"ДатаДо");
		Возврат;
	КонецЕсли;
	
	СписокВозврата = Новый СписокЗначений;
	СписокВозврата.Добавить(ДатаПосле);
	СписокВозврата.Добавить(ДатаДо);
		
	СтруктураВозврата = Новый Структура;
	СтруктураВозврата.Вставить("Список", СписокВозврата);
	
	Закрыть(СтруктураВозврата);
	
КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьДатуПослеПриИзменении(Элемент)
	
	Элементы.ДатаПосле.Доступность = ИспользоватьДатуПосле;
	Элементы.ДатаПосле1.Доступность = ИспользоватьДатуПосле;
	Если Не ИспользоватьДатуПосле Тогда
		ДатаПосле = Дата(1,1,1);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьДатуДоПриИзменении(Элемент)
	
	Элементы.ДатаДо.Доступность = ИспользоватьДатуДо;
	Элементы.ДатаДо1.Доступность = ИспользоватьДатуДо;
	Если Не ИспользоватьДатуДо Тогда
		ДатаДо = Дата(1,1,1);
	КонецЕсли;
	
КонецПроцедуры



