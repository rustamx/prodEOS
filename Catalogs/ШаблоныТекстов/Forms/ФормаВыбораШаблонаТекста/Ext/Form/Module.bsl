
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ТекущийПользователь = ПользователиКлиентСервер.ТекущийПользователь();
	
	Если Параметры.Свойство("ОбластьПрименения") И ЗначениеЗаполнено(Параметры.ОбластьПрименения) Тогда 
		ОбластьПрименения = Параметры.ОбластьПрименения;
	Иначе 
		ОбластьПрименения = Перечисления.ОбластиПримененияШаблоновТекстов.Почта;
	КонецЕсли;
	
	Если ОбластьПрименения = Перечисления.ОбластиПримененияШаблоновТекстов.Почта Тогда 
		ЭтаФорма.Заголовок = НСтр("ru = 'Выбор шаблона текста письма'; en = 'Select email text template'");
	ИначеЕсли ОбластьПрименения = Перечисления.ОбластиПримененияШаблоновТекстов.Резолюции Тогда
		ЭтаФорма.Заголовок = НСтр("ru = 'Выбор шаблона резолюции'; en = 'Select resolution template'");
		КоличествоЭлементов = КоличествоЭлементовСправочника(ТекущийПользователь);
	КонецЕсли;
	
	КонтейнерыПользователя = ПользователиДокументооборот.КонтейнерыПользователя(ТекущийПользователь);
	
	Список.Параметры.УстановитьЗначениеПараметра("КонтейнерыПользователя", КонтейнерыПользователя);
	Список.Параметры.УстановитьЗначениеПараметра("Пользователь", ТекущийПользователь);
	Список.Параметры.УстановитьЗначениеПараметра("ОбластьПрименения", ОбластьПрименения);
	Список.Параметры.УстановитьЗначениеПараметра("Исполнители",
		Справочники.ШаблоныТекстов.ПолучитьПользователейДелегирующихПрава(ТекущийПользователь));
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	Если ОбщегоНазначенияДокументооборотКлиент.ПриЗакрытии(ЗавершениеРаботы) Тогда
		Возврат;
	КонецЕсли;
	
	Если ОбластьПрименения = ПредопределенноеЗначение("Перечисление.ОбластиПримененияШаблоновТекстов.Резолюции") 
		И КоличествоЭлементов > КоличествоЭлементовСправочника(ТекущийПользователь) Тогда 
		ПараметрОповещения = Новый Структура;
		Оповестить("БылУдаленШаблонРезолюции");
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок
 
&НаКлиенте
Процедура СписокВыборЗначения(Элемент, Значение, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ТекстШаблона = ПолучитьТекстШаблона(Значение);
	Закрыть(ТекстШаблона);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Отказ = Истина;
	ПараметрыФормы = Новый Структура;
	
	Если Копирование Тогда 
		ПараметрыФормы.Вставить("ЗначениеКопирования", Элементы.Список.ТекущаяСтрока);
	Иначе 
		ПараметрыФормы.Вставить("ОбластьПрименения", ОбластьПрименения);
	КонецЕсли;	
	
	Открытьформу("Справочник.ШаблоныТекстов.ФормаОбъекта", ПараметрыФормы, 
		Элементы.Список, Новый УникальныйИдентификатор);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Функция ПолучитьТекстШаблона(Шаблон)
	
	Возврат Справочники.ШаблоныТекстов.ПолучитьТекстШаблона(Шаблон);
	
КонецФункции

&НаСервереБезКонтекста
Функция КоличествоЭлементовСправочника(ТекущийПользователь)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	КОЛИЧЕСТВО(*) КАК Количество
		|ИЗ
		|	Справочник.ШаблоныТекстов КАК СправочникШаблоныТекстов
		|ГДЕ
		|	СправочникШаблоныТекстов.ОбластьПрименения = ЗНАЧЕНИЕ(Перечисление.ОбластиПримененияШаблоновТекстов.Резолюции)
		|	И НЕ СправочникШаблоныТекстов.ПометкаУдаления
		|	И (СправочникШаблоныТекстов.Автор = &Пользователь
		|			ИЛИ СправочникШаблоныТекстов.Автор В (&Исполнители)
		|			ИЛИ СправочникШаблоныТекстов.ОбщийШаблон
		|				И СправочникШаблоныТекстов.Пользователи.ПользовательИлиГруппа.Ссылка В (&МассивГруппПользователей))";
	
	
	МассивГруппПользователей = ПользователиСерверПовтИсп.ПолучитьМассивГруппПользователя(ТекущийПользователь);
	МассивГруппПользователей.Добавить(ТекущийПользователь);
	
	Запрос.Параметры.Вставить("МассивГруппПользователей", МассивГруппПользователей);
	Запрос.Параметры.Вставить("Пользователь", ТекущийПользователь);
	Запрос.Параметры.Вставить("Исполнители", 
		Справочники.ШаблоныТекстов.ПолучитьПользователейДелегирующихПрава(ТекущийПользователь));
		
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		Возврат 0;
	КонецЕсли;
	
	Выборка = Результат.Выбрать();
	Выборка.Следующий();
	
	Возврат Выборка.Количество;
	
КонецФункции

#КонецОбласти
