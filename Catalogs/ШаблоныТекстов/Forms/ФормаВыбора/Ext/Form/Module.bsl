
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ТекущийПользователь = ПользователиКлиентСервер.ТекущийПользователь();
	
	Если Параметры.Свойство("ОбластьПрименения") И ЗначениеЗаполнено(Параметры.ОбластьПрименения) Тогда 
		ОбластьПрименения = Параметры.ОбластьПрименения;
	Иначе 
		ОбластьПрименения = Перечисления.ОбластиПримененияШаблоновТекстов.Почта;
	КонецЕсли;
	
	Если Параметры.Свойство("Заголовок") И ЗначениеЗаполнено(Параметры.Заголовок) Тогда 
		Заголовок = Параметры.Заголовок;
	КонецЕсли;
	
	Если Параметры.Свойство("ЗаголовокФормыСоздания") И ЗначениеЗаполнено(Параметры.ЗаголовокФормыСоздания) Тогда 
		ЗаголовокФормыСоздания = Параметры.ЗаголовокФормыСоздания;
	КонецЕсли;
	
	КонтейнерыПользователя = ПользователиДокументооборот.КонтейнерыПользователя(ТекущийПользователь);
	
	Список.Параметры.УстановитьЗначениеПараметра("КонтейнерыПользователя", КонтейнерыПользователя);
	Список.Параметры.УстановитьЗначениеПараметра("Пользователь", ТекущийПользователь);
	Список.Параметры.УстановитьЗначениеПараметра("ОбластьПрименения", ОбластьПрименения);
	Список.Параметры.УстановитьЗначениеПараметра("Исполнители",
		Справочники.ШаблоныТекстов.ПолучитьПользователейДелегирующихПрава(ТекущийПользователь));
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаКлиенте
Процедура СписокПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Отказ = Истина;
	ПараметрыФормы = Новый Структура;
	
	Если Копирование Тогда 
		ПараметрыФормы.Вставить("ЗначениеКопирования", Элементы.Список.ТекущаяСтрока);
	Иначе 
		ПараметрыФормы.Вставить("ОбластьПрименения", ОбластьПрименения);
	КонецЕсли;
	
	ПараметрыФормы.Вставить("ЗаголовокФормыСоздания", ЗаголовокФормыСоздания);
	
	Открытьформу("Справочник.ШаблоныТекстов.ФормаОбъекта", ПараметрыФормы, 
		Элементы.Список, Новый УникальныйИдентификатор);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПриАктивизацииСтроки(Элемент)
	
	ПодключитьОбработчикОжидания("ОбработчикСписокПриАктивизацииСтроки", 0.5, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработчикСписокПриАктивизацииСтроки()
	
	Шаблон = Элементы.Список.ТекущаяСтрока;
	
	Если ТекущийШаблон = Шаблон Тогда
		Возврат;
	КонецЕсли;
	
	ТекущийШаблон = Шаблон;
	
	РезультатОбработки = ОбработатьАктивизациюСтроки(
		Шаблон,
		УникальныйИдентификатор);
		
	Предпросмотр = РезультатОбработки;	
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ОбработатьАктивизациюСтроки(
	Шаблон,
	УникальныйИдентификаторФормы)
	
	Если Шаблон = Неопределено Тогда
		Возврат "<html><body></body></html>";
	КонецЕсли;	
	
	ТипТекста = Шаблон.ТипТекста;
	Если Не ЗначениеЗаполнено(ТипТекста) Тогда
		ТипТекста = ПредопределенноеЗначение("Перечисление.ТипыТекстовПочтовыхСообщений.ПростойТекст");
	КонецЕсли;
		
	Если ТипТекста = ПредопределенноеЗначение("Перечисление.ТипыТекстовПочтовыхСообщений.ПростойТекст") Тогда
		ТекстПросто = Шаблон.Шаблон;
		ТекстHTML = РаботаС_HTML.ПолучитьHTMLИзТекста(ТекстПросто);
		Возврат ТекстHTML;
	КонецЕсли;	
	
	ТекстHTML = Шаблон.Шаблон;
	ВстроеннаяПочтаСервер.ВставитьКартинкиВТекстHTML(ТекстHTML, Шаблон, УникальныйИдентификаторФормы);
	
	Возврат ТекстHTML;
	
КонецФункции

#КонецОбласти
