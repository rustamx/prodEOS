
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

// Обновление адресной книги
Перем ОбновитьДанныеПроектаВАдреснойКниге;
Перем ОбновитьСоставПроектнойКомандыВАдреснойКниге;
Перем СоставПроектнойКомандыКОбновлениюВАдреснойКниге;
Перем ОбновитьСловаПоискаПоПроектуВАдреснойКниге;
Перем ОбновитьДоступностьВПоискеПоПроекту;

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	Если ЭтоГруппа Тогда 
		Возврат;
	КонецЕсли;
	
	Если ЭтоНовый() Тогда 
		Руководитель = ПользователиКлиентСервер.ТекущийПользователь();
		Состояние = Перечисления.СостоянияПроектов.Инициирован;
		ЕдиницаТрудозатратЗадач = Константы.ОсновнаяЕдиницаТрудозатрат.Получить();
		ЕдиницаДлительностиЗадач = Константы.ОсновнаяЕдиницаДлительности.Получить();
		СписыватьЗатратыНаПроект = Истина;
		СпособПланирования = Перечисления.СпособыПланированияПроекта.ОтДатыНачалаПроекта;
		АвтоматическиРассчитыватьПланПроекта = Истина;
		
		Если Константы.ИспользоватьГрафикиРаботы.Получить() Тогда 
			ГрафикРаботы = Константы.ОсновнойГрафикРаботы.Получить();
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(ВидПроекта) Тогда 
			ВидПроекта = РаботаСПроектами.ПолучитьВидПроектаПоУмолчанию();
		КонецЕсли;	
		
		Если Не ЗначениеЗаполнено(Организация) Тогда
			Организация = РаботаСОрганизациями.ПолучитьОрганизациюПоУмолчанию();
		КонецЕсли;
		
		Если ПолучитьФункциональнуюОпцию("ИспользоватьГрифыДоступа") Тогда
			ГрифДоступа = Константы.ГрифДоступаПоУмолчанию.Получить();
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Руководитель) Тогда
			Подразделение = РаботаСПользователями.ПолучитьПодразделение(Руководитель); 
		КонецЕсли;
		
	КонецЕсли;	
	
	Если ВстроеннаяПочтаКлиентСервер.ЭтоПисьмо(ДанныеЗаполнения) Тогда
		Наименование = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДанныеЗаполнения, "Тема");
	КонецЕсли;	
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ЭтоГруппа Тогда 
		Возврат;
	КонецЕсли;
	
	РеквизитыПроектаПоСсылке = Неопределено;
	
	Если Не ЗначениеЗаполнено(Ссылка) Тогда
		ДополнительныеСвойства.Вставить("ЭтоНовый", Истина);
	Иначе
		РеквизитыПроектаПоСсылке = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Ссылка,
			"ПометкаУдаления, Руководитель, Папка, ПроектнаяКоманда, Наименование");
	КонецЕсли;
	
	ПредыдущаяПометкаУдаления = Ложь;
	Если Не Ссылка.Пустая() Тогда
		ПредыдущаяПометкаУдаления = РеквизитыПроектаПоСсылке.ПометкаУдаления;
	КонецЕсли;
	
	Если ПометкаУдаления <> ПредыдущаяПометкаУдаления Тогда 
		Если ПометкаУдаления Тогда
			ДополнительныеСвойства.Вставить("НужноПометитьНаУдалениеБизнесСобытия", Истина);
		КонецЕсли;
	КонецЕсли;
	
	ПредыдущийТекущийПланНачало = ТекущийПланНачало;
	ПредыдущийТекущийПланОкончание = ТекущийПланОкончание;
	ПредыдущийСпособПланирования = СпособПланирования;
	ПредыдущийАвтоматическиРассчитыватьПланПроекта = АвтоматическиРассчитыватьПланПроекта;
	
	Если Не Ссылка.Пустая() Тогда
		ПредыдущиеРеквизиты = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Ссылка, 
			"ТекущийПланНачало,ТекущийПланОкончание,СпособПланирования,АвтоматическиРассчитыватьПланПроекта");
		ПредыдущийТекущийПланНачало = ПредыдущиеРеквизиты.ТекущийПланНачало;
		ПредыдущийТекущийПланОкончание = ПредыдущиеРеквизиты.ТекущийПланОкончание;
		ПредыдущийСпособПланирования = ПредыдущиеРеквизиты.СпособПланирования;
		ПредыдущийАвтоматическиРассчитыватьПланПроекта = ПредыдущиеРеквизиты.АвтоматическиРассчитыватьПланПроекта;
	КонецЕсли;
	
	Если (ПредыдущийТекущийПланНачало <> ТекущийПланНачало Или ПредыдущийСпособПланирования <> СпособПланирования) 
		И СпособПланирования = Перечисления.СпособыПланированияПроекта.ОтДатыНачалаПроекта Тогда
		ДополнительныеСвойства.Вставить("НужноОбновитьНачалоПроектныхЗадач", Истина);
	КонецЕсли;
	
	Если (ПредыдущийТекущийПланОкончание <> ТекущийПланОкончание Или ПредыдущийСпособПланирования <> СпособПланирования)
		И СпособПланирования = Перечисления.СпособыПланированияПроекта.ОтДатыОкончанияПроекта Тогда
		ДополнительныеСвойства.Вставить("НужноОбновитьОкончаниеПроектныхЗадач", Истина);
	КонецЕсли;
	
	Если ПредыдущийАвтоматическиРассчитыватьПланПроекта = Ложь И АвтоматическиРассчитыватьПланПроекта = Истина Тогда 
		ДополнительныеСвойства.Вставить("НужноОбновитьПланПроекта", Истина);
	КонецЕсли;
	
	Если Не Ссылка.Пустая() Тогда 
		Если Руководитель <> РеквизитыПроектаПоСсылке.Руководитель Тогда 
			ДополнительныеСвойства.Вставить("ИзменилсяРуководитель", Истина);
		КонецЕсли;
	КонецЕсли;
	
	// Обработка рабочей группы
	СсылкаОбъекта = Ссылка;
	// Установка ссылки нового
	Если Не ЗначениеЗаполнено(СсылкаОбъекта) Тогда
		СсылкаОбъекта = ПолучитьСсылкуНового();
		Если Не ЗначениеЗаполнено(СсылкаОбъекта) Тогда
			СсылкаНового = Справочники.Проекты.ПолучитьСсылку();
			УстановитьСсылкуНового(СсылкаНового);
			СсылкаОбъекта = СсылкаНового;
		КонецЕсли;
	КонецЕсли;
	
	// Определение дескрипторов для проверки прав при записи рабочей группы.
	Если ДокументооборотПраваДоступаПовтИсп.ВключеноИспользованиеПравДоступа() Тогда
		ДокументооборотПраваДоступа.ОпределитьДескрипторыОбъекта(ЭтотОбъект);
	КонецЕсли;
	
	// Подготовка рабочей группы
	РабочаяГруппа = РегистрыСведений.РабочиеГруппы.ПолучитьУчастниковПоОбъекту(СсылкаОбъекта);
	
	// Добавление автоматических участников из самого объекта
	Если РаботаСРабочимиГруппами.ПоОбъектуВедетсяАвтоматическоеЗаполнениеРабочейГруппы(ЭтотОбъект) Тогда
		
		ДобавитьУчастниковРабочейГруппыВНабор(РабочаяГруппа);
		
	КонецЕсли;
	
	// Добавление участников, переданных "снаружи", например из формы объекта
	Если ДополнительныеСвойства.Свойство("РабочаяГруппаДобавить") Тогда
		
		Для Каждого Эл Из ДополнительныеСвойства.РабочаяГруппаДобавить Цикл
			
			// Добавление участника в итоговую рабочую группу
			Строка = РабочаяГруппа.Добавить();
			Строка.Участник = Эл.Участник;
			Строка.Изменение = Эл.Изменение;
			
		КонецЦикла;	
			
	КонецЕсли;		
	
	// Удаление участников, переданных "снаружи", например из формы объекта
	Если ДополнительныеСвойства.Свойство("РабочаяГруппаУдалить") Тогда
		
		Для Каждого Эл Из ДополнительныеСвойства.РабочаяГруппаУдалить Цикл
			
			// Поиск удаляемого участника в итоговой рабочей группе
			Для Каждого Эл2 Из РабочаяГруппа Цикл
				
				Если Эл2.Участник = Эл.Участник 
					И Эл2.Изменение = Эл.Изменение Тогда
					
					// Удаление участника из итоговой рабочей группы
					РабочаяГруппа.Удалить(Эл2);
					Прервать;
					
				КонецЕсли;
				
			КонецЦикла;	
				
		КонецЦикла;	
			
	КонецЕсли;		
	
	// Обработка обязательного заполнения рабочих групп 
	Если РабочаяГруппа.Количество() = 0 Тогда
	
		Если РаботаСРабочимиГруппами.ОбязательноеЗаполнениеРабочихГруппДокументов(ВидПроекта) Тогда
			Строка = РабочаяГруппа.Добавить();
			Строка.Участник = ПользователиКлиентСервер.ТекущийПользователь();
		КонецЕсли;
		
	КонецЕсли;	
	
	// Запись итоговой рабочей группы
	РаботаСРабочимиГруппами.ПерезаписатьРабочуюГруппуОбъекта(
		СсылкаОбъекта,
		РабочаяГруппа,
		Ложь, //ОбновитьПраваДоступа
		Пользователи.ТекущийПользователь());
	
	// Установка необходимости обновления прав доступа
	ДополнительныеСвойства.Вставить("ДополнительныеПравообразующиеЗначенияИзменены");
	
	// Обновление адресной книги
	ОбновитьДанныеПроектаВАдреснойКниге = Ложь;
	ОбновитьСоставПроектнойКомандыВАдреснойКниге = Ложь;
	ОбновитьСловаПоискаПоПроектуВАдреснойКниге = Ложь;
	СоставПроектнойКомандыКОбновлениюВАдреснойКниге = Новый Массив;
	ОбновитьДоступностьВПоискеПоПроекту = Ложь;
	
	НоваяПроектнаяКоманда = ПроектнаяКоманда.Выгрузить();
	НоваяПроектнаяКоманда.Свернуть("Исполнитель");
	НоваяПроектнаяКоманда = НоваяПроектнаяКоманда.ВыгрузитьКолонку("Исполнитель");
	
	Если ЭтоНовый() Тогда
		ОбновитьДанныеПроектаВАдреснойКниге = Истина;
		ОбновитьСоставПроектнойКомандыВАдреснойКниге = Истина;
		ОбновитьСловаПоискаПоПроектуВАдреснойКниге = Истина;
	Иначе
		Если РеквизитыПроектаПоСсылке.Папка <> Папка Тогда
			ОбновитьДанныеПроектаВАдреснойКниге = Истина;
		КонецЕсли;
		
		Если РеквизитыПроектаПоСсылке.ПометкаУдаления <> ПометкаУдаления Тогда
			ОбновитьДоступностьВПоискеПоПроекту = Истина;
			ОбновитьДанныеПроектаВАдреснойКниге = Истина;
		КонецЕсли;
		
		СтараяПроектнаяКоманда = РеквизитыПроектаПоСсылке.ПроектнаяКоманда.Выгрузить();
		СтараяПроектнаяКоманда.Свернуть("Исполнитель");
		СтараяПроектнаяКоманда = СтараяПроектнаяКоманда.ВыгрузитьКолонку("Исполнитель");
		
		Для Каждого УчастникПроекта ИЗ НоваяПроектнаяКоманда Цикл
			ТипУчастника = ТипЗнч(УчастникПроекта);
			Если ТипУчастника <> Тип("СправочникСсылка.Пользователи")
				И ТипУчастника <> Тип("СправочникСсылка.ПолныеРоли") Тогда
				
				Продолжить;
			КонецЕсли;
			Если СтараяПроектнаяКоманда.Найти(УчастникПроекта) = Неопределено Тогда
				ОбновитьСоставПроектнойКомандыВАдреснойКниге = Истина;
				ОбновитьСловаПоискаПоПроектуВАдреснойКниге = Истина;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		Для Каждого УчастникПроекта ИЗ СтараяПроектнаяКоманда Цикл
			ТипУчастника = ТипЗнч(УчастникПроекта);
			Если ТипУчастника <> Тип("СправочникСсылка.Пользователи")
				И ТипУчастника <> Тип("СправочникСсылка.ПолныеРоли") Тогда
				
				Продолжить;
			КонецЕсли;
			Если НоваяПроектнаяКоманда.Найти(УчастникПроекта) = Неопределено Тогда
				ОбновитьСоставПроектнойКомандыВАдреснойКниге = Истина;
				ОбновитьСловаПоискаПоПроектуВАдреснойКниге = Истина;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		Если РеквизитыПроектаПоСсылке.Наименование <> Наименование Тогда
			ОбновитьСловаПоискаПоПроектуВАдреснойКниге = Истина;
			ОбновитьДанныеПроектаВАдреснойКниге = Истина;
		КонецЕсли;
	КонецЕсли;
	Если ОбновитьСоставПроектнойКомандыВАдреснойКниге Тогда
		
		ИндексУчастникаПроекта = НоваяПроектнаяКоманда.Количество() - 1;
		Пока ИндексУчастникаПроекта >= 0 Цикл
			УчастникПроекта = НоваяПроектнаяКоманда[ИндексУчастникаПроекта];
			ТипУчастника = ТипЗнч(УчастникПроекта);
			Если ТипУчастника <> Тип("СправочникСсылка.Пользователи")
				И ТипУчастника <> Тип("СправочникСсылка.ПолныеРоли") Тогда
				
				НоваяПроектнаяКоманда.Удалить(ИндексУчастникаПроекта);
			КонецЕсли;
			ИндексУчастникаПроекта = ИндексУчастникаПроекта - 1;
		КонецЦикла;
		
		СоставПроектнойКомандыКОбновлениюВАдреснойКниге = НоваяПроектнаяКоманда;
	КонецЕсли;
	
	// ТСК Близнюк С.И.; 08.10.2018; task#1380{
	ра_ОбщегоНазначения.ПроверитьУникальностьРеквизита(ЭтотОбъект, "Наименование");
	// ТСК Близнюк С.И.; 08.10.2018; task#1380}

КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка  Тогда
		Возврат;
	КонецЕсли;
	
	Если ЭтоГруппа Тогда 
		Возврат;
	КонецЕсли;
	
	// Возможно, выполнена явная регистрация событий при загрузке объекта.
	Если Не ДополнительныеСвойства.Свойство("НеРегистрироватьБизнесСобытия") Тогда
		Если ДополнительныеСвойства.Свойство("ЭтоНовый") И ДополнительныеСвойства.ЭтоНовый Тогда
			БизнесСобытияВызовСервера.ЗарегистрироватьСобытие(Ссылка, Справочники.ВидыБизнесСобытий.СозданиеПроекта);			
		Иначе
			БизнесСобытияВызовСервера.ЗарегистрироватьСобытие(Ссылка, Справочники.ВидыБизнесСобытий.ИзменениеПроекта);
		КонецЕсли;	
	КонецЕсли;	
	
	Если ДополнительныеСвойства.Свойство("НужноПометитьНаУдалениеБизнесСобытия") Тогда
		БизнесСобытияВызовСервера.ПометитьНаУдалениеСобытияПоИсточнику(Ссылка);
	КонецЕсли;	
	
	Если ДополнительныеСвойства.Свойство("НужноОбновитьНачалоПроектныхЗадач") Тогда
		ОбновитьНачалоПроектныхЗадач();
	КонецЕсли;

	Если ДополнительныеСвойства.Свойство("НужноОбновитьОкончаниеПроектныхЗадач") Тогда
		ОбновитьОкончаниеПроектныхЗадач();
	КонецЕсли;
	
	Если ДополнительныеСвойства.Свойство("НужноОбновитьПланПроекта") Тогда
		РаботаСПроектами.РассчитатьПланВсегоПроекта(Ссылка);
	КонецЕсли;
	
	// Обновление адресной книги
	Если ОбновитьДанныеПроектаВАдреснойКниге Тогда
		Справочники.АдреснаяКнига.ОбновитьДанныеОбъекта(
			Ссылка,
			Папка,
			Справочники.АдреснаяКнига.ПоПроектам,
			Ссылка);
	КонецЕсли;
	Если ОбновитьСоставПроектнойКомандыВАдреснойКниге Тогда
		Справочники.АдреснаяКнига.ОбновитьСписокПодчиненныхОбъектов(
			Ссылка,
			Неопределено,
			СоставПроектнойКомандыКОбновлениюВАдреснойКниге,
			Справочники.АдреснаяКнига.ПоПроектам,
			Ссылка);
	КонецЕсли;
	Если ОбновитьСловаПоискаПоПроектуВАдреснойКниге Тогда
		РегистрыСведений.ПоискВАдреснойКниге.ОбновитьСловаПоискаПоПроекту(ЭтотОбъект);
	КонецЕсли;
	Если ОбновитьДоступностьВПоискеПоПроекту Тогда
		РегистрыСведений.ПоискВАдреснойКниге.ОбновитьДоступностьВПоиске(ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ДобавитьУчастниковРабочейГруппыВНабор(ТаблицаНабора)
	
	Если ЗначениеЗаполнено(Ссылка) Тогда
		
		ИсходныеРеквизиты = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Ссылка, 
			"ВидПроекта, Руководитель, ПроектнаяКоманда");
		Если ИсходныеРеквизиты.ВидПроекта = ВидПроекта Тогда
			ДобавитьТолькоНовыхУчастниковРабочейГруппыВНабор(ТаблицаНабора, ИсходныеРеквизиты);	
		Иначе
			ДобавитьВсехУчастниковРабочейГруппыВНабор(ТаблицаНабора);		
		КонецЕсли;
		
	Иначе	
		
		ДобавитьВсехУчастниковРабочейГруппыВНабор(ТаблицаНабора);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ДобавитьТолькоНовыхУчастниковРабочейГруппыВНабор(ТаблицаНабора, ИсходныеРеквизиты)
	
	// Добавление реквизита Руководитель
	Если ИсходныеРеквизиты.Руководитель <> Руководитель Тогда
		РаботаСРабочимиГруппами.ДобавитьУчастникаВТаблицуНабора(ТаблицаНабора, Руководитель, Истина);
	КонецЕсли;				
		
	// Обработка табличной части ПроектнаяКоманда
	ИсходнаяПроектнаяКоманда = ИсходныеРеквизиты.ПроектнаяКоманда.Выгрузить();
	Для каждого Эл Из ПроектнаяКоманда Цикл
		
		// Поиск в исходной табличной части 
		Найден = Ложь;
		Для каждого Эл2 Из ИсходнаяПроектнаяКоманда Цикл
			Если Эл.Исполнитель = Эл2.Исполнитель Тогда
				Найден = Истина;
				Прервать;
			КонецЕсли;	
		КонецЦикла;	
		
		// Добавление нового участника рабочей группы
		Если Не Найден Тогда  
			РаботаСРабочимиГруппами.ДобавитьУчастникаВТаблицуНабора(
				ТаблицаНабора, 
				Эл.Исполнитель);
		КонецЕсли;
		
	КонецЦикла;
		
КонецПроцедуры	
	
Процедура ДобавитьВсехУчастниковРабочейГруппыВНабор(ТаблицаНабора)
	
	РаботаСРабочимиГруппами.ДобавитьУчастникаВТаблицуНабора(ТаблицаНабора, Руководитель, Истина);
	
	// Добавление табличной части ПроектнаяКоманда
	Для каждого Строка Из ПроектнаяКоманда Цикл
		РаботаСРабочимиГруппами.ДобавитьУчастникаВТаблицуНабора(
			ТаблицаНабора, 
			Строка.Исполнитель);
	КонецЦикла;
		
КонецПроцедуры

Процедура ОбновитьНачалоПроектныхЗадач()
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ПроектныеЗадачи.Ссылка
	|ИЗ
	|	Справочник.ПроектныеЗадачи КАК ПроектныеЗадачи
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ПроектныеЗадачи.Предшественники КАК ПроектныеЗадачиПредшественники
	|		ПО ПроектныеЗадачи.Ссылка = ПроектныеЗадачиПредшественники.Ссылка
	|ГДЕ
	|	ПроектныеЗадачи.Владелец = &Владелец
	|	И НЕ ПроектныеЗадачи.СуммарнаяЗадача
	|	И ПроектныеЗадачиПредшественники.Ссылка ЕСТЬ NULL 
	|	И НЕ ПроектныеЗадачи.ПометкаУдаления";
	Запрос.УстановитьПараметр("Владелец", Ссылка);
	
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		Возврат;
	КонецЕсли;	
		
	Выборка = Результат.Выбрать();
	Пока Выборка.Следующий() Цикл
		
		Предшественники = РаботаСПроектами.ПолучитьВсехПредшественников(Выборка.Ссылка, Ложь);
		Если Предшественники.Количество() > 0 Тогда 
			Продолжить;
		КонецЕсли;	
		
		ДанныеПроектнойЗадачи = РаботаСПроектами.ПолучитьДанныеПроектнойЗадачи(Выборка.Ссылка);
		
		СтруктураДанных = Новый Структура;
		СтруктураДанных.Вставить("ТекущийПланНачало", ТекущийПланНачало);
		СтруктураДанных.Вставить("ТекущийПланОкончание", 
			РаботаСПроектами.РассчитатьОкончаниеПериода(ДанныеПроектнойЗадачи, 
			ТекущийПланНачало, 
			ДанныеПроектнойЗадачи.ТекущийПланДлительность, 
			ДанныеПроектнойЗадачи.ТекущийПланЕдиницаДлительности));
		РаботаСПроектами.ЗаписатьСрокиПроектнойЗадачи(ДанныеПроектнойЗадачи.Ссылка, СтруктураДанных);
		
	КонецЦикла;
	РаботаСПроектами.РассчитатьПланВсегоПроекта(Ссылка);
	
КонецПроцедуры

Процедура ОбновитьОкончаниеПроектныхЗадач()
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ПроектныеЗадачи.Ссылка
	|ИЗ
	|	Справочник.ПроектныеЗадачи КАК ПроектныеЗадачи
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ПроектныеЗадачи.Предшественники КАК ПроектныеЗадачиПредшественники
	|		ПО ПроектныеЗадачи.Ссылка = ПроектныеЗадачиПредшественники.Предшественник
	|ГДЕ
	|	ПроектныеЗадачи.Владелец = &Владелец
	|	И НЕ ПроектныеЗадачи.СуммарнаяЗадача
	|	И ПроектныеЗадачиПредшественники.Ссылка ЕСТЬ NULL 
	|	И НЕ ПроектныеЗадачи.ПометкаУдаления";
	Запрос.УстановитьПараметр("Владелец", Ссылка);
	
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	Выборка = Результат.Выбрать();
	Пока Выборка.Следующий() Цикл
		Последователи = РаботаСПроектами.ПолучитьВсехПоследователей(Выборка.Ссылка, Ложь);
		Если Последователи.Количество() > 0 Тогда 
			Продолжить;
		КонецЕсли;
		
		ДанныеПроектнойЗадачи = РаботаСПроектами.ПолучитьДанныеПроектнойЗадачи(Выборка.Ссылка);
		
		СтруктураДанных = Новый Структура;
		СтруктураДанных.Вставить("ТекущийПланОкончание", ТекущийПланОкончание);
		СтруктураДанных.Вставить("ТекущийПланНачало", 
			РаботаСПроектами.РассчитатьНачалоПериода(ДанныеПроектнойЗадачи, 
			ТекущийПланОкончание, 
			ДанныеПроектнойЗадачи.ТекущийПланДлительность, 
			ДанныеПроектнойЗадачи.ТекущийПланЕдиницаДлительности));
		РаботаСПроектами.ЗаписатьСрокиПроектнойЗадачи(ДанныеПроектнойЗадачи.Ссылка, СтруктураДанных);
		
	КонецЦикла;
	РаботаСПроектами.РассчитатьПланВсегоПроекта(Ссылка);
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Если ЗначениеЗаполнено(ТекущийПланНачало) И ЗначениеЗаполнено(ТекущийПланОкончание) И ТекущийПланНачало > ТекущийПланОкончание Тогда 
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			НСтр("ru = 'Дата окончания проекта меньше, чем дата начала.'; en = 'Project finish date is less than the start date.'"),
			ЭтотОбъект,
			"ТекущийПланОкончание",, 
			Отказ);
	КонецЕсли;
	
	КоличествоИсполнителей = ПроектнаяКоманда.Количество();
	Для Инд1 = 0 По КоличествоИсполнителей - 2 Цикл
		Строка1 = ПроектнаяКоманда[Инд1];
		
		Для Инд2 = Инд1+1 По КоличествоИсполнителей - 1 Цикл
			Строка2 = ПроектнаяКоманда[Инд2];
			
			ТекстСообщения = "";
			Если Строка1.Исполнитель = Строка2.Исполнитель Тогда 
				
				ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Исполнитель ""%1"" указан дважды в списке проектной команды!'; en = 'Performer ""%1"" is specified twice among the members of the project team!'"),
					Строка(Строка1.Исполнитель));
				
			КонецЕсли;
			
			Если ТекстСообщения <> "" Тогда 
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
					ТекстСообщения,
					ЭтотОбъект,
					"ПроектнаяКоманда[" + Формат(Строка2.НомерСтроки-1, "ЧГ=0") + "].Исполнитель",, 
					Отказ);
			КонецЕсли;	
			
		КонецЦикла;	
	КонецЦикла;	
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
