
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ТаблСписок = РеквизитФормыВЗначение("Список");
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ТипыСвязей.Ссылка,
	|	ТипыСвязей.Наименование,
	|	ТипыСвязей.Комментарий
	|ИЗ
	|	Справочник.ТипыСвязей КАК ТипыСвязей
	|ГДЕ
	|	НЕ ТипыСвязей.ПометкаУдаления
	|	%1
	|
	|УПОРЯДОЧИТЬ ПО
	|	ТипыСвязей.Наименование";
	
	Если Параметры.Свойство("ДоступныеТипыСвязей") Тогда 
		Запрос.Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			Запрос.Текст, 
			"И ТипыСвязей.Ссылка В (&ТипыСвязей)");
		Запрос.Параметры.Вставить("ТипыСвязей", Параметры.ДоступныеТипыСвязей);
	Иначе 
		Запрос.Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			Запрос.Текст, "");
	КонецЕсли;
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		НоваяСтрока = ТаблСписок.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
	КонецЦикла;	
	
	Если Параметры.Свойство("ВыбранныеТипыСвязей") Тогда 
		Для Каждого ВыбранныйТипСвязей Из Параметры.ВыбранныеТипыСвязей Цикл
			НайденнаяСтрока = ТаблСписок.Найти(ВыбранныйТипСвязей, "Ссылка");
			Если НайденнаяСтрока <> Неопределено Тогда 
				НайденнаяСтрока.Пометка = Истина;
			КонецЕсли;	
		КонецЦикла;	
	КонецЕсли;	
	
	ЗначениеВРеквизитФормы(ТаблСписок, "Список");
	
КонецПроцедуры

&НаКлиенте
Процедура ОК(Команда)
	
	ВыбранныеТипыСвязей = Новый Массив;
	Для Каждого Строка Из Список Цикл
		Если Строка.Пометка Тогда 
			ВыбранныеТипыСвязей.Добавить(Строка.Ссылка);
		КонецЕсли;	
	КонецЦикла;	
	
	Закрыть(ВыбранныеТипыСвязей);
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	Закрыть(Неопределено);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтметитьВсе(Команда)
	
	Для Каждого Строка Из Список Цикл
		Строка.Пометка = Истина;
	КонецЦикла;	
	
КонецПроцедуры

&НаКлиенте
Процедура СнятьОтметки(Команда)
	
	Для Каждого Строка Из Список Цикл
		Строка.Пометка = Ложь;
	КонецЦикла;	
	
КонецПроцедуры


