
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Список.Параметры.УстановитьЗначениеПараметра("УчетнаяЗапись", Неопределено);
	
	УчетныеЗаписи.Параметры.УстановитьЗначениеПараметра("Пользователь", ПользователиКлиентСервер.ТекущийПользователь());
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	СправочникУчетныеЗаписиЭлектроннойПочты.Ссылка
		|ИЗ
		|	Справочник.УчетныеЗаписиЭлектроннойПочты КАК СправочникУчетныеЗаписиЭлектроннойПочты
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ (ВЫБРАТЬ РАЗЛИЧНЫЕ
		|			МоиУчетныеЗаписи.Ссылка КАК Ссылка
		|		ИЗ
		|			Справочник.УчетныеЗаписиЭлектроннойПочты.ОтветственныеЗаОбработкуПисем КАК МоиУчетныеЗаписи
		|		ГДЕ
		|			МоиУчетныеЗаписи.Пользователь = &Пользователь) КАК МоиУчетныеЗаписи
		|		ПО (МоиУчетныеЗаписи.Ссылка = СправочникУчетныеЗаписиЭлектроннойПочты.Ссылка)
		|ГДЕ
		|	СправочникУчетныеЗаписиЭлектроннойПочты.ВариантИспользования = ЗНАЧЕНИЕ(Перечисление.ВариантыИспользованияПочты.Встроенная)
		|	И СправочникУчетныеЗаписиЭлектроннойПочты.ПометкаУдаления = ЛОЖЬ";
	Запрос.УстановитьПараметр("Пользователь", ПользователиКлиентСервер.ТекущийПользователь());
	РезультатЗапроса = Запрос.Выполнить();
	
	Если Не РезультатЗапроса.Пустой() Тогда
		
		Выборка = РезультатЗапроса.Выбрать();
		
		Если Выборка.Количество() = 1 Тогда
			Выборка.Следующий();
			ТекущаяУчетнаяЗапись = Выборка.Ссылка;
			Список.Параметры.УстановитьЗначениеПараметра("УчетнаяЗапись", ТекущаяУчетнаяЗапись);
			Элементы.УчетныеЗаписи.Видимость = Ложь;
		Иначе	
			
		Если Параметры.Свойство("УчетнаяЗапись") Тогда
			Элементы.УчетныеЗаписи.ТекущаяСтрока = Параметры.УчетнаяЗапись;
			ТекущаяУчетнаяЗапись = Параметры.УчетнаяЗапись;
			Список.Параметры.УстановитьЗначениеПараметра("УчетнаяЗапись", ТекущаяУчетнаяЗапись);
		КонецЕсли;
			
		КонецЕсли;
		
	Иначе
		Отказ = Истина;
		Текст = НСтр("ru = 'Нет ни одной учетной записи, для которой можно показать списки адресов.
			|Обратитесь к аминистратору.';
			|en = 'There is no account to show address lists. 
			|Refer to aministrator.'");
		ВызватьИсключение(Текст);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УчетныеЗаписиПриАктивизацииСтроки(Элемент)
	
	ТекущиеДанные = Элементы.УчетныеЗаписи.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено 
		ИЛИ Элемент.ТекущаяСтрока = ТекущаяУчетнаяЗапись Тогда
		Возврат;
	Конецесли;
	ТекущаяУчетнаяЗапись = Элемент.ТекущаяСтрока;
	Список.Параметры.УстановитьЗначениеПараметра("УчетнаяЗапись", ТекущаяУчетнаяЗапись);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	
	Если Не Копирование Тогда
		Отказ = Истина;
		
		ПараметрыОткрытия = Новый Структура("УчетнаяЗапись", ТекущаяУчетнаяЗапись);
		ОткрытьФорму("Справочник.СпискиАдресовЭлектроннойПочты.ФормаОбъекта", ПараметрыОткрытия);
	КонецЕсли;
	
КонецПроцедуры


