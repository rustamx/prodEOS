
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

// Обновление адресной книги
Перем ОбновитьСловаПоискаПоДолжностиВАдреснойКниге;
Перем ОбновитьДоступностьВПоискеПоДолжности;

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	// Обновление адресной книги
	ОбновитьСловаПоискаПоДолжностиВАдреснойКниге = Ложь;
	ОбновитьДоступностьВПоискеПоДолжности = Ложь;
	Если ЭтоНовый() Тогда
		ОбновитьСловаПоискаПоДолжностиВАдреснойКниге = Истина;
	Иначе
		РеквизитыДолжностиПоСсылке = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Ссылка, "Наименование, ПометкаУдаления");
		Если РеквизитыДолжностиПоСсылке.Наименование <> Наименование Тогда
			ОбновитьСловаПоискаПоДолжностиВАдреснойКниге = Истина;
		КонецЕсли;
		Если РеквизитыДолжностиПоСсылке.ПометкаУдаления <> ПометкаУдаления Тогда
			ОбновитьДоступностьВПоискеПоДолжности = Истина;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	// Обновление адресной книги
	Если ОбновитьСловаПоискаПоДолжностиВАдреснойКниге Тогда
		РегистрыСведений.ПоискВАдреснойКниге.ОбновитьСловаПоискаПоДолжности(ЭтотОбъект);
	КонецЕсли;
	Если ОбновитьДоступностьВПоискеПоДолжности Тогда
		РегистрыСведений.ПоискВАдреснойКниге.ОбновитьДоступностьВПоиске(ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
