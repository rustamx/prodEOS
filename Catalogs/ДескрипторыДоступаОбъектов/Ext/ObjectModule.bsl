
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

// Рассчитывает хэш-сумму объекта
// 
Функция РассчитатьХэш() Экспорт
	
	// Упорядочивание всех табличных частей
	Сравнение = Новый СравнениеЗначений;
	Контрагенты.Сортировать("ГруппаДоступа Возр", Сравнение);
	ФизическиеЛица.Сортировать("ГруппаДоступа Возр", Сравнение);
	НастройкаПрав.Сортировать("Пользователь Возр, Право Возр", Сравнение);
	
	// Сборка хэш-строки
	ХэшСтрока = "";
	ДобавитьЗначениеКХэшСтроке(ХэшСтрока, ВидОбъекта);
	ДобавитьЗначениеКХэшСтроке(ХэшСтрока, ВопросДеятельности);
	ДобавитьЗначениеКХэшСтроке(ХэшСтрока, ГрифДоступа);
	ДобавитьЗначениеКХэшСтроке(ХэшСтрока, Организация);
	ДобавитьЗначениеКХэшСтроке(ХэшСтрока, Папка);
	ДобавитьЗначениеКХэшСтроке(ХэшСтрока, ПапкаПередаетВсеПрава);
	ДобавитьЗначениеКХэшСтроке(ХэшСтрока, Проект);
	ДобавитьЗначениеКХэшСтроке(ХэшСтрока, Подразделение);
	
	ДобавитьЗначениеКХэшСтроке(ХэшСтрока, КонтейнерПользователей);
	ДобавитьЗначениеКХэшСтроке(ХэшСтрока, Изменение);
	ДобавитьЗначениеКХэшСтроке(ХэшСтрока, ОбъектДоступа);
	
	Если ДляЛокальныхАдминистраторов Тогда
		ДобавитьЗначениеКХэшСтроке(ХэшСтрока, ДляЛокальныхАдминистраторов);
	КонецЕсли;
	
	// Добавление ТЧ Контрагенты
	Для Каждого Эл Из Контрагенты Цикл
		ДобавитьЗначениеКХэшСтроке(ХэшСтрока, Эл.ГруппаДоступа);
	КонецЦикла;
	
	// Добавление ТЧ ФизическиеЛица
	Для Каждого Эл Из ФизическиеЛица Цикл
		ДобавитьЗначениеКХэшСтроке(ХэшСтрока, Эл.ГруппаДоступа);
	КонецЦикла;
	
	// Добавление ТЧ НастройкаПрав
	Для Каждого Эл Из НастройкаПрав Цикл
		ДобавитьЗначениеКХэшСтроке(ХэшСтрока, Эл.Пользователь);
		ДобавитьЗначениеКХэшСтроке(ХэшСтрока, Эл.Право);
		ДобавитьЗначениеКХэшСтроке(ХэшСтрока, Эл.ПравоЗапрещено);
	КонецЦикла;
	
	// ТСК Талько Э.Г.; 09.06.2018; Управление доступом {
	ДобавитьЗначениеКХэшСтроке(ХэшСтрока, ра_Организация);
	// ТСК Талько Э.Г.; 09.06.2018; Управление доступом }
	
	// Вычисление хэш-значения
	Хэш = ДокументооборотПраваДоступаПовтИсп.ВычислитьХэшПоСтроке(ХэшСтрока);
	
КонецФункции	

Процедура ДобавитьЗначениеКХэшСтроке(ХэшСтрока, Значение)
	
	Если Не ЗначениеЗаполнено(Значение) Тогда
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(Значение) = Тип("Строка") Тогда
		ХэшСтрока = ХэшСтрока + Значение;
		Возврат;
	ИначеЕсли ТипЗнч(Значение) = Тип("Булево") Тогда
		ХэшСтрока = ХэшСтрока + Строка(Значение);
		Возврат;
	КонецЕсли;
	
	Строка = Строка(Значение.УникальныйИдентификатор());
	ХэшСтрока = ХэшСтрока + Строка + " ";
	
КонецПроцедуры	

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	// Если это очистка устаревших дескрипторов, пересчет прав не нужен
	Если ПометкаУдаления Тогда
		Возврат;
	КонецЕсли;
	
	Справочники.ДескрипторыДоступаОбъектов.РассчитатьПрава(Ссылка, Неопределено);
	
КонецПроцедуры

// Заполняет реквизит ЭтоПустойДескриптор
// 
Функция ЗаполнитьПризнакПустого() Экспорт
	
	ЭтоПустойДескриптор = 
		Не (ЗначениеЗаполнено(ВидОбъекта)
		Или ЗначениеЗаполнено(ВопросДеятельности)
		Или ЗначениеЗаполнено(ГрифДоступа)
		Или ЗначениеЗаполнено(Организация)
		Или ЗначениеЗаполнено(Подразделение)
		Или ЗначениеЗаполнено(Папка)
		Или ЗначениеЗаполнено(Проект)
		Или ЗначениеЗаполнено(КонтейнерПользователей)
		Или ЗначениеЗаполнено(ОбъектДоступа)
		// ТСК Талько Э.Г.; 09.06.2018; Управление доступом {
		Или ЗначениеЗаполнено(ра_Организация)
		// ТСК Талько Э.Г.; 09.06.2018; Управление доступом }
		Или Контрагенты.Количество() > 0
		Или ФизическиеЛица.Количество() > 0
		Или НастройкаПрав.Количество() > 0
		Или Группа = Истина
		Или ИдентификаторОбъектаМетаданных = 
			ОбщегоНазначения.ИдентификаторОбъектаМетаданных("Справочник.ПапкиВнутреннихДокументов")
		Или ИдентификаторОбъектаМетаданных = 
			ОбщегоНазначения.ИдентификаторОбъектаМетаданных("Справочник.ПапкиФайлов")
		Или ИдентификаторОбъектаМетаданных = 
			ОбщегоНазначения.ИдентификаторОбъектаМетаданных("Справочник.ПапкиПисем")
		Или ИдентификаторОбъектаМетаданных = 
			ОбщегоНазначения.ИдентификаторОбъектаМетаданных("Справочник.ПапкиМероприятий")
		Или ИдентификаторОбъектаМетаданных = 
			ОбщегоНазначения.ИдентификаторОбъектаМетаданных("Справочник.ПапкиПроектов")	
		Или ИдентификаторОбъектаМетаданных = 
			ОбщегоНазначения.ИдентификаторОбъектаМетаданных("Справочник.ПапкиФорума"));
		
КонецФункции

#КонецЕсли
