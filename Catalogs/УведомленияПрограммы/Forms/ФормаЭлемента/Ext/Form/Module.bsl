#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	ТолькоПросмотр = Истина;
	ИндексВидУведомления = -1;
	Если Объект.ВидУведомления = Перечисления.ВидыУведомленийПрограммы.Ошибка Тогда
		ИндексВидУведомления = 2;
	ИначеЕсли Объект.ВидУведомления = Перечисления.ВидыУведомленийПрограммы.Предупреждение Тогда
		ИндексВидУведомления = 1;
	ИначеЕсли Объект.ВидУведомления = Перечисления.ВидыУведомленийПрограммы.Информация Тогда
		ИндексВидУведомления = 0;
	КонецЕсли;
	
	Элементы.ПерейтиКПредмету.Видимость = ЗначениеЗаполнено(Объект.Объект);
	
	Если Не Объект.Просмотрено Тогда
		Справочники.УведомленияПрограммы.ОтметитьПросмотр(Объект.Ссылка);
		ВыполненПросмотр = Истина;
	КонецЕсли;
	
	Если Параметры.РежимРаботы = "ПоказУведомлений" Тогда
		РежимОткрытияОкна = РежимОткрытияОкнаФормы.Независимый;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если ВыполненПросмотр Тогда
		ОповеститьОбИзменении(Объект.Ссылка);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если Параметры.РежимРаботы = "ПоказУведомлений" И ИмяСобытия = "ПоказаныУведомленияПрограммы" Тогда
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОписаниеПриНажатии(Элемент, ДанныеСобытия, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	РаботаС_HTMLКлиент.ОткрытьСсылку(Элемент, ДанныеСобытия, СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПерейтиКПредмету(Команда)
	
	ПоказатьЗначение(, Объект.Объект);
	Закрыть();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы_Отправить

&НаКлиенте
Процедура ПроцессИсполнение(Команда)
	
	РаботаСБизнесПроцессамиКлиент.ОткрытьПомощникСозданияОсновныхПроцессов(
		"Исполнение", Объект.Ссылка, ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ПроцессОзнакомление(Команда)
	
	РаботаСБизнесПроцессамиКлиент.ОткрытьПомощникСозданияОсновныхПроцессов(
		"Ознакомление", Объект.Ссылка, ЭтаФорма);
	
КонецПроцедуры

#КонецОбласти
