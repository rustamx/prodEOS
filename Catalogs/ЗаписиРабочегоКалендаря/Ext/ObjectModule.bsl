#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Включает на данную дату как исключение из правила повторения
Процедура ДобавитьИсключениеПовторения(ДатаИсключения, ЗаписьИсключения = Неопределено) Экспорт
	
	Если ТипЗаписиКалендаря <> Перечисления.ТипЗаписиКалендаря.ПовторяющеесяСобытие Тогда
		ВызватьИсключение НСтр("ru = 'Некорректный тип повторяющегося события.'; en = 'Incorrect recurring calendar event type.'");
	КонецЕсли;
	
	ПараметрыОтбора = Новый Структура;
	ПараметрыОтбора.Вставить("ДатаИсключения", ДатаИсключения);
	НайденныеСтроки = ИсключенияПовторения.НайтиСтроки(ПараметрыОтбора);
	
	Если НайденныеСтроки.Количество() <> 0 Тогда
		ВызватьИсключение НСтр("ru = 'На данную дату уже указано исключение повторения.'; en = 'Reccurence exception has already been specified on this date.'");
	КонецЕсли;
	
	НоваяСтрокаИсключенияПовторения = ИсключенияПовторения.Добавить();
	НоваяСтрокаИсключенияПовторения.ЗаписьИсключения = ЗаписьИсключения;
	НоваяСтрокаИсключенияПовторения.ДатаИсключения = ДатаИсключения;
	
	Если ЗначениеЗаполнено(ЗаписьИсключения) Тогда
		ДополнительныеСвойства.Вставить("ИзмененоВхождение");
	Иначе
		ДополнительныеСвойства.Вставить("УдаленоВхождение");
	КонецЕсли;
	
КонецПроцедуры

// Возвращает следующую дату начала повторяющегося события
Функция ПолучитьСледующуюДатуНачала(ДатаОтсчета) Экспорт
	
	Если ЗначениеЗаполнено(ДатаОкончанияПовторения) И ДатаОтсчета >= ДатаОкончанияПовторения Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	СтруктураПравилаПовторения = ПолучитьСтруктуруПравилаПовторения();
	
	НомерИтерации = 0;
	ПроверяемаяДата = НачалоДня(ДатаОтсчета);
	
	Пока НомерИтерации < 10000 Цикл // Ограничение на слишком следующую дату начала
		
		Если РаботаСРабочимКалендаремСервер.ДатаУдовлетворяетПравилуПовторения(
				ПроверяемаяДата, СтруктураПравилаПовторения) Тогда
			
			ВозможнаяДата = ПроверяемаяДата + (ДатаНачала - НачалоДня(ДатаНачала));
			Если ДатаОтсчета < ВозможнаяДата Тогда
				Возврат ВозможнаяДата;
			КонецЕсли;
			
		КонецЕсли;
		
		ПроверяемаяДата = ПроверяемаяДата + 86400; // 86400 - число секунд в сутках
		НомерИтерации = НомерИтерации + 1;
		
	КонецЦикла;
	
	Возврат Неопределено;
	
КонецФункции

// Устаревшее. Следует использовать Реквизиты().
// Возвращает реквизиты записи календаря на дату
Функция ПолучитьРеквизитыЗаписиКалендаря(ДатаЗаписи = Неопределено) Экспорт
	
	РеквизитыЗаписиКалендаря = Справочники.ЗаписиРабочегоКалендаря.ПолучитьСтруктуруРеквизитовЗаписиКалендаря();
	ЗаполнитьЗначенияСвойств(РеквизитыЗаписиКалендаря, ЭтотОбъект);
	
	Если ТипЗаписиКалендаря = Перечисления.ТипЗаписиКалендаря.ПовторяющеесяСобытие Тогда
		
		Если ЗначениеЗаполнено(ДатаЗаписи) Тогда
			ДатаНачалаИОкончания = ПолучитьДатуНачалаИДатуОкончания(ДатаЗаписи);
			Если НЕ ЗначениеЗаполнено(ДатаНачалаИОкончания) Тогда
				Возврат Неопределено;
			КонецЕсли;
			ЗаполнитьЗначенияСвойств(РеквизитыЗаписиКалендаря, ДатаНачалаИОкончания);
		Иначе
			РеквизитыЗаписиКалендаря.Повторять = ПолучитьТекстовоеПредставлениеПовторения();
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат РеквизитыЗаписиКалендаря;
	
КонецФункции

// Возвращает реквизиты записи календаря на указанную дату.
//
// Параметры:
//  Дата - Дата - Дата записи календаря.
// 
// Возвращаемое значение:
//  Структура - Реквизиты записи календаря. См. Справочники.ЗаписиРабочегоКалендаря.РеквизитыЗаписиКалендаря().
//
Функция Реквизиты(Дата = Неопределено) Экспорт
	
	РеквизитыЗаписиКалендаря = Справочники.ЗаписиРабочегоКалендаря.ПолучитьСтруктуруРеквизитовЗаписиКалендаря();
	ЗаполнитьЗначенияСвойств(РеквизитыЗаписиКалендаря, ЭтотОбъект);
	
	Если ТипЗаписиКалендаря = Перечисления.ТипЗаписиКалендаря.ПовторяющеесяСобытие Тогда
		
		Если ЗначениеЗаполнено(Дата) Тогда
			ДатаНачалаИОкончания = ПолучитьДатуНачалаИДатуОкончания(Дата);
			Если Не ЗначениеЗаполнено(ДатаНачалаИОкончания) Тогда
				Возврат Неопределено;
			КонецЕсли;
			ЗаполнитьЗначенияСвойств(РеквизитыЗаписиКалендаря, ДатаНачалаИОкончания);
		Иначе
			РеквизитыЗаписиКалендаря.Повторять = ПолучитьТекстовоеПредставлениеПовторения();
		КонецЕсли;
		
	КонецЕсли;
	
	РеквизитыЗаписиКалендаря.Описание =
		РаботаСРабочимКалендаремКлиентСервер.ВыделитьОписаниеБезПервойСтроки(РеквизитыЗаписиКалендаря.Описание);
	
	Возврат РеквизитыЗаписиКалендаря;
	
КонецФункции

// Определяет номер вхождения.
//
// Параметры:
//  Дата - Дата - Дата.
//
// Возвращаемое значение:
//  Число - Номер вхождения.
//
Функция НомерВхождения(Дата) Экспорт
	
	НомерВхождения = Неопределено;
	ПроверяемаяДата = НачалоДня(ДатаНачалаПовторения);
	ИскомаяДата = НачалоДня(Дата);
	Если ТипЗаписиКалендаря <> Перечисления.ТипЗаписиКалендаря.ПовторяющеесяСобытие
		Или Не ЗначениеЗаполнено(ИскомаяДата)
		Или ИскомаяДата < ПроверяемаяДата Тогда
		Возврат НомерВхождения;
	КонецЕсли;
	
	СтруктураПравилаПовторения = ПолучитьСтруктуруПравилаПовторения();
	
	НомерИтерации = 0;
	НомерВхождения = 0;
	Пока НомерИтерации < 10000 Цикл // Ограничение на слишком большую дату окончания повторения
		
		Если ИскомаяДата < ПроверяемаяДата Тогда
			НомерВхождения = Неопределено;
			Прервать;
		КонецЕсли;
		
		ДатаУдовлетворяетПравилуПовторения = РаботаСРабочимКалендаремСервер.ДатаУдовлетворяетПравилуПовторения(
			ПроверяемаяДата,
			СтруктураПравилаПовторения,
			Ложь);
		Если ДатаУдовлетворяетПравилуПовторения Тогда
			
			НомерВхождения = НомерВхождения + 1;
			Если ПроверяемаяДата = ИскомаяДата Тогда
				Прервать;
			КонецЕсли;
			
		КонецЕсли;
		
		ПроверяемаяДата = ПроверяемаяДата + 86400; // 86400 - число секунд в сутках
		НомерИтерации = НомерИтерации + 1;
		
	КонецЦикла;
	
	Возврат НомерВхождения;
	
КонецФункции

// Возвращает признак того что запись календаря связана с мероприятием.
// 
// Возвращаемое значение:
//  Булево - Запись календаря связана с мероприятием.
//
Функция ЭтоМероприятие() Экспорт
	
	ЭтоМероприятие = Связанная И ТипЗнч(Предмет) = Тип("СправочникСсылка.Мероприятия");
	
	Возврат ЭтоМероприятие;
	
КонецФункции

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	Если ЭтоНовый() Тогда
		Автор = ПользователиКлиентСервер.ТекущийПользователь();
		Пользователь = Автор;
	КонецЕсли;
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда
		
		Если ДанныеЗаполнения.Свойство("Предмет") И ЗначениеЗаполнено(ДанныеЗаполнения.Предмет) Тогда
			
			Если ТипЗнч(ДанныеЗаполнения.Предмет) = Тип("СправочникСсылка.Мероприятия") Тогда
				
				Мероприятие = ДанныеЗаполнения.Предмет;
				
				ОписаниеЗаписиКалендаря = Новый Массив;
				ОписаниеЗаписиКалендаря.Добавить(СокрЛП(Мероприятие.Наименование));
				Если ЗначениеЗаполнено(Мероприятие.МестоПроведения) Тогда
					ОписаниеЗаписиКалендаря.Добавить(Символы.ПС);
					ОписаниеЗаписиКалендаря.Добавить(НСтр("ru = 'Место:'; en = 'Location:'"));
					ОписаниеЗаписиКалендаря.Добавить(" ");
					ОписаниеЗаписиКалендаря.Добавить(Мероприятие.МестоПроведения);
				КонецЕсли;
				Если ЗначениеЗаполнено(Мероприятие.Описание) Тогда
					ОписаниеЗаписиКалендаря.Добавить(Символы.ПС);
					ОписаниеЗаписиКалендаря.Добавить(СокрЛП(Мероприятие.Описание));
				КонецЕсли;
				Описание = СтрСоединить(ОписаниеЗаписиКалендаря);
				
				ДатаНачала = Мероприятие.ДатаНачала;
				ДатаОкончания = Мероприятие.ДатаОкончания;
				ВесьДень = Ложь;
				
			ИначеЕсли ТипЗнч(ДанныеЗаполнения.Предмет) = Тип("ДокументСсылка.ВходящееПисьмо")
				Или ТипЗнч(ДанныеЗаполнения.Предмет) = Тип("ДокументСсылка.ИсходящееПисьмо") Тогда
				
				ДанныеЗаполненияОбъект = ДанныеЗаполнения.Предмет.ПолучитьОбъект();
				ТекстПисьма = ДанныеЗаполненияОбъект.ПолучитьТекстовоеПредставлениеСодержанияПисьма();
				ТекстПисьма = СокрЛП(ДанныеЗаполненияОбъект.Тема) + "." + Символы.ПС + ТекстПисьма;
				
				ТекстСобытия = Новый Массив;
				Событие = Обработки.АнализТекста.Событие(ТекстПисьма, ДанныеЗаполненияОбъект.Дата);
				Если Событие <> Неопределено Тогда
					
					ДатаНачала = Событие.Начало;
					ДатаОкончания = Событие.Конец;
					
					ТекстСобытия.Добавить(Событие.Текст);
					
					// Место
					Если ЗначениеЗаполнено(Событие.Место) Тогда
						ТекстСвойства = СтрШаблон("%1: %2", НСтр("ru = 'Место проведения'; en = 'Location'"), Событие.Место);
						ТекстСобытия.Добавить(ТекстСвойства);
					КонецЕсли;
					
				Иначе
					ТекстСобытия.Добавить(СокрЛП(ДанныеЗаполненияОбъект.Тема));
				КонецЕсли;
				Описание = СтрСоединить(ТекстСобытия, Символы.ПС);
				
			ИначеЕсли ТипЗнч(ДанныеЗаполнения.Предмет) = Тип("СправочникСсылка.ЗаметкиДокументооборота") Тогда
				
				Описание = ДанныеЗаполнения.Предмет.СодержаниеТекст;
				
			ИначеЕсли ТипЗнч(ДанныеЗаполнения.Предмет) = Тип("ЗадачаСсылка.ЗадачаИсполнителя")
				И ТипЗнч(ДанныеЗаполнения.Предмет.БизнесПроцесс) = Тип("БизнесПроцессСсылка.Приглашение") Тогда
				
				Описание = СокрЛП(ДанныеЗаполнения.Предмет.Наименование);
				ДатаНачала = ДанныеЗаполнения.Предмет.БизнесПроцесс.ДатаНачалаМероприятия;
				ДатаОкончания = ДанныеЗаполнения.Предмет.БизнесПроцесс.ДатаОкончанияМероприятия;
				ВесьДень = Ложь;
				
			ИначеЕсли ТипЗнч(ДанныеЗаполнения.Предмет) = Тип("СправочникСсылка.Контроль") Тогда 
				
				Описание = СокрЛП(ДанныеЗаполнения.Предмет.Описание);
				
				Если ЗначениеЗаполнено(ДанныеЗаполнения.Предмет.СрокИсполнения)
					И НачалоДня(ДанныеЗаполнения.Предмет.СрокИсполнения) >= НачалоДня(ТекущаяДатаСеанса()) Тогда 
					
					ГрафикРаботы = ГрафикиРаботы.ПолучитьГрафикРаботыПользователя(
						ПользователиКлиентСервер.ТекущийПользователь());
					
					ДатаОкончания = ГрафикиРаботы.ПолучитьПоследнееРабочееВремяДня(
						ГрафикРаботы, 
						ДанныеЗаполнения.Предмет.СрокИсполнения);
						
					ДатаНачала = ДатаОкончания - 3600;
				КонецЕсли;		
				
			ИначеЕсли ТипЗнч(ДанныеЗаполнения.Предмет) = Тип("ДокументСсылка.Бронь") Тогда 
				
				Описание = БронированиеПомещений.ПолучитьОписаниеБрони(ДанныеЗаполнения.Предмет);
				
			ИначеЕсли ТипЗнч(ДанныеЗаполнения.Предмет) = Тип("СправочникСсылка.КонтрольныеТочки") Тогда 
				
				Описание = СокрЛП(ДанныеЗаполнения.Предмет.Описание);
				
				Если ЗначениеЗаполнено(ДанныеЗаполнения.Предмет.ПлановыйСрок)
					И НачалоДня(ДанныеЗаполнения.Предмет.ПлановыйСрок) >= НачалоДня(ТекущаяДатаСеанса()) Тогда 
					ДатаНачала = ДанныеЗаполнения.Предмет.ПлановыйСрок;
					ДатаОкончания = ДатаНачала + 3600;
				КонецЕсли;
				
			ИначеЕсли ТипЗнч(ДанныеЗаполнения.Предмет) = Тип("ДокументСсылка.Отсутствие") Тогда 
				
				ОписаниеЗаписиКалендаря = Новый Массив;
				ОписаниеЗаписиКалендаря.Добавить(СокрЛП(ДанныеЗаполнения.Предмет.ВидОтсутствия));
				ОписаниеЗаписиКалендаря.Добавить(СокрЛП(ДанныеЗаполнения.Предмет.Комментарий));
				Описание = СтрСоединить(ОписаниеЗаписиКалендаря, Символы.ПС);
				
				ДатаНачала = ДанныеЗаполнения.Предмет.ДатаНачала;
				ДатаОкончания = ДанныеЗаполнения.Предмет.ДатаОкончания;
				ВесьДень = ДанныеЗаполнения.Предмет.ВесьДень;
				
			Иначе
				
				Описание = СокрЛП(ДанныеЗаполнения.Предмет.Наименование);
				
			КонецЕсли;
			
		КонецЕсли;
		
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, ДанныеЗаполнения);
		
		Если ДанныеЗаполнения.Свойство("ПовторяющеесяСобытие") И ЗначениеЗаполнено(ДанныеЗаполнения.ПовторяющеесяСобытие)
			И ДанныеЗаполнения.Свойство("ДатаИсключения") И ЗначениеЗаполнено(ДанныеЗаполнения.ДатаИсключения) Тогда
			
			ЗаполнитьЗначенияСвойств(ЭтотОбъект, ДанныеЗаполнения.ПовторяющеесяСобытие,
				"ВесьДень, ДатаНачала, ДатаОкончания, Описание, Пользователь, Предмет, Состояние");
			
			ТипЗаписиКалендаря = Перечисления.ТипЗаписиКалендаря.ЭлементПовторяющегосяСобытия;
			
			ИсходнаяДатаНачала = ДатаНачала;
			ДатаНачала = НачалоДня(ДанныеЗаполнения.ДатаИсключения) + (ИсходнаяДатаНачала - НачалоДня(ИсходнаяДатаНачала));
			ДатаОкончания = НачалоДня(ДанныеЗаполнения.ДатаИсключения) + (ДатаОкончания - НачалоДня(ИсходнаяДатаНачала));
			
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("СправочникСсылка.ЗаписиРабочегоКалендаря") Тогда
		
		СкопироватьЗаписьКалендаря(ДанныеЗаполнения);
		
	КонецЕсли;
	
	Если ЭтоНовый() Тогда
		
		Если Не ЗначениеЗаполнено(ДатаНачала) И Не ЗначениеЗаполнено(ДатаОкончания) Тогда
			ДатаНачала = РаботаСРабочимКалендаремКлиентСервер.КонецПолучаса(ТекущаяДатаСеанса());
			ДатаОкончания = ДатаНачала + 3600;
		КонецЕсли;
		
		Если Пользователь <> Автор Тогда
			Состояние = Перечисления.СостоянияЗаписейРабочегоКалендаря.ПодВопросом;
		КонецЕсли;
		
	КонецЕсли;
	
	РаботаСРабочимКалендаремКлиентСервер.СкорректироватьДатуНачалаИОкончания(ДатаНачала, ДатаОкончания, ВесьДень);
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Если ДатаНачала >= ДатаОкончания Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			НСтр("ru = 'Дата окончания должна быть больше даты начала.'; en = 'End date must be greater tahn start date.'"), ЭтотОбъект, "ДатаОкончания", , Отказ);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда 
		Возврат;
	КонецЕсли;
	
	Если Связанная И Не ДополнительныеСвойства.Свойство("ИзменениеСвязаннойЗаписи") Тогда
		ТекстОшибки = СтрШаблон(
			НСтр("ru = 'Изменение связанных записей календаря недоступно.
				|Для изменения данной записи календаря необходимо внести изменения в ""%1"" (%2).';
				|en = 'Associated calendar entries can not be changed.
				|For changes to this calendar entries you need to make changes to ""%1"" (%2).'"),
			Предмет,
			ТипЗнч(Предмет));
		ВызватьИсключение ТекстОшибки;
	КонецЕсли;
	
	ДополнительныеСвойства.Вставить("ЭтоНовый", Не ЗначениеЗаполнено(Ссылка));
	
	Если ДатаНачала >= ДатаОкончания Тогда
		Отказ = Истина;
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Дата начала  не может быть меньше даты окончания.'; en = 'Start date can not be less than end date.'"));
		Возврат;
	КонецЕсли;
	
	Если ВесьДень Тогда
		РаботаСРабочимКалендаремКлиентСервер.СкорректироватьДатуНачалаИОкончания(
			ДатаНачала, ДатаОкончания, ВесьДень);
	КонецЕсли;
	
	ЗаполнитьНаименованиеИКраткоеОписание();
	
	// Установка настроек повторения
	Если ДополнительныеСвойства.Свойство("НастройкиПовторения") Тогда
		
		// Отказ в установке повторения для помеченной на удаление записи
		Если ПометкаУдаления = Истина Тогда
			Отказ = Истина;
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				НСтр("ru = 'Невозможно настроить повторение для записи, помеченной на удаление.'; en = 'It is impossible to set up recurrence for marked for deletion calendar entry.'"));
			Возврат;
		КонецЕсли;
		
		// Отказ в установке повторения для записи, уже являющейся исключением повторения
		Если ТипЗаписиКалендаря = Перечисления.ТипЗаписиКалендаря.ЭлементПовторяющегосяСобытия Тогда
			Отказ = Истина;
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				НСтр("ru = 'Невозможно настроить повторение для записи, являющейся исключением повторения.'; en = 'It is impossible to set up recurrence for recurrence exception.'"));
			Возврат;
		КонецЕсли;
		
		ДополнительныеСвойства.Вставить("ИзмененыНастройкиПовторения");
		// При смене частоты повторения очищаются все настройки повторения, в том числе и исключения.
		Если ЧастотаПовторения <> ДополнительныеСвойства.НастройкиПовторения.ЧастотаПовторения Тогда
			ОчиститьРеквизитыПовторения();
		КонецЕсли;
		
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, ДополнительныеСвойства.НастройкиПовторения);
		
		// Проверка заполненности частоты повторения
		Если ЗначениеЗаполнено(ЧастотаПовторения) Тогда
			ТипЗаписиКалендаря = Перечисления.ТипЗаписиКалендаря.ПовторяющеесяСобытие;
		Иначе
			ОтменитьПовторение();
		КонецЕсли;
		
		// Установка настроек и проверка заполненности, особенная для каждой частоты повторения.
		Если ЧастотаПовторения = Перечисления.ЧастотаПовторения.Еженедельно Тогда
			
			ПовторениеПоДням.Очистить();
			
			Для Каждого ДеньПовтора Из ДополнительныеСвойства.НастройкиПовторения.ПовторениеПоДнямНедели Цикл
				Если ДеньПовтора.Значение Тогда
					НоваяСтрока = ПовторениеПоДням.Добавить();
					НоваяСтрока.ДеньНедели = ДеньПовтора.Ключ;
					НоваяСтрока.НомерВхождения = 0;
				КонецЕсли;
			КонецЦикла;
			
			Если ПовторениеПоДням.Количество() = 0 Тогда
				ОтменитьПовторение();
			КонецЕсли;
			
		ИначеЕсли ЧастотаПовторения = Перечисления.ЧастотаПовторения.Ежемесячно Тогда
			
			ПовторениеПоДням.Очистить();
			
			Если ЗначениеЗаполнено(ДополнительныеСвойства.НастройкиПовторения.ПовторениеПоДнямНеделиВМесяце) Тогда
				НоваяСтрока = ПовторениеПоДням.Добавить();
				ЗаполнитьЗначенияСвойств(
					НоваяСтрока,
					ДополнительныеСвойства.НастройкиПовторения.ПовторениеПоДнямНеделиВМесяце);
			КонецЕсли;
			
			Если НЕ ЗначениеЗаполнено(ПовторениеПоДнямМесяца) И ПовторениеПоДням.Количество() = 0 Тогда
				ОтменитьПовторение();
			КонецЕсли;
			
		ИначеЕсли ЧастотаПовторения = Перечисления.ЧастотаПовторения.Ежегодно Тогда
			
			Если НЕ ЗначениеЗаполнено(ПовторениеПоДнямМесяца) ИЛИ НЕ ЗначениеЗаполнено(ПовторениеПоМесяцам) Тогда
				ОтменитьПовторение();
			КонецЕсли;
			
		КонецЕсли;
		
		// Удаление исключений повторения, не подходящих под новое правило повторения
		СтруктураПравилаПовторения = ПолучитьСтруктуруПравилаПовторения();
		ИсключенияПовторенияКоличество = ИсключенияПовторения.Количество();
		Для ИндексИсключения = 1 По ИсключенияПовторенияКоличество Цикл
			
			ИсключениеПовторения = ИсключенияПовторения[ИсключенияПовторенияКоличество - ИндексИсключения];
			Если НЕ РаботаСРабочимКалендаремСервер.ДатаУдовлетворяетПравилуПовторения(
				ИсключениеПовторения.ДатаИсключения, СтруктураПравилаПовторения, Ложь) Тогда
				
				ПометитьНаУдалениеИсключениеПовторения(ИсключениеПовторения);
				ИсключенияПовторения.Удалить(ИсключениеПовторения);
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	// Корректировка даты начала повторения и даты окончания повторения
	Если ТипЗаписиКалендаря = Перечисления.ТипЗаписиКалендаря.Событие
		ИЛИ ТипЗаписиКалендаря = Перечисления.ТипЗаписиКалендаря.ЭлементПовторяющегосяСобытия Тогда
		
		ОчиститьРеквизитыПовторения();
		
	ИначеЕсли ТипЗаписиКалендаря = Перечисления.ТипЗаписиКалендаря.ПовторяющеесяСобытие Тогда
		
		ДатаНачалаПовторения = ДатаНачала;
		
		Если ПравилоОкончанияПовторения = Перечисления.ПравилаОкончанияПовторения.Никогда Тогда
			ДатаОкончанияПовторения = Дата(1,1,1);
		ИначеЕсли ПравилоОкончанияПовторения = Перечисления.ПравилаОкончанияПовторения.ПослеЧислаПовторов Тогда
			РассчитатьДатуОкончанияПовторения();
		ИначеЕсли ПравилоОкончанияПовторения = Перечисления.ПравилаОкончанияПовторения.ДоДаты Тогда
			Если НЕ ЗначениеЗаполнено(ДатаОкончанияПовторения) Тогда
				ПравилоОкончанияПовторения = Перечисления.ПравилаОкончанияПовторения.Никогда;
			ИначеЕсли ДатаОкончанияПовторения < ДатаНачалаПовторения Тогда
				ОтменитьПовторение();
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
	// Очистка настроек повторения при удалении повторяющегося события
	Если ТипЗаписиКалендаря = Перечисления.ТипЗаписиКалендаря.ПовторяющеесяСобытие И ИзмениласьПометкаУдаления() Тогда
		
		Если ТипЗаписиКалендаря = Перечисления.ТипЗаписиКалендаря.ПовторяющеесяСобытие Тогда
			ОтменитьПовторение();
		КонецЕсли;
		
	КонецЕсли;
	
	// Обработка рабочей группы
	СсылкаОбъекта = Ссылка;
	
	// Установка ссылки нового
	Если Не ЗначениеЗаполнено(СсылкаОбъекта) Тогда
		СсылкаОбъекта = ПолучитьСсылкуНового();
		Если Не ЗначениеЗаполнено(СсылкаОбъекта) Тогда
			СсылкаНового = Справочники.ЗаписиРабочегоКалендаря.ПолучитьСсылку();
			УстановитьСсылкуНового(СсылкаНового);
			СсылкаОбъекта = СсылкаНового;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда 
		Возврат;
	КонецЕсли;
	
	// Создание push уведомлений
	УстановитьПривилегированныйРежим(Истина);
	// Если элемент помечен на удаление - удалим все уведомления.
	Если ПометкаУдаления
		И Константы.ИспользоватьМобильныеКлиенты.Получить()
		И Константы.ИспользоватьPushУведомления.Получить() Тогда
		
		Набор = РегистрыСведений.СообщенияPushУведомлений.СоздатьНаборЗаписей();
		Набор.Отбор.ОбъектУведомления.Установить(ЭтотОбъект.Ссылка);
		Набор.Записать(Истина);
		
	КонецЕсли;
	Если НЕ ПометкаУдаления 
		И Константы.ИспользоватьМобильныеКлиенты.Получить()
		И Константы.ИспользоватьPushУведомления.Получить() Тогда
		
		ТекстОписания = ЭтотОбъект.Наименование;
		Если Не ЗначениеЗаполнено(ТекстОписания) Тогда
			ТекстОписания = ЭтотОбъект.Описание;
		КонецЕсли;
		
		// Для новых записей календаря созданными для других пользователей создаем уведомления всегда.
		ЭтоНовый = Неопределено;
		Если ДополнительныеСвойства.Свойство("ЭтоНовый", ЭтоНовый)
			И Не Пользователь = Автор Тогда
			
			Сообщение = СтрШаблон("%1 добавил запись в календаре на %2, %3",
				Строка(Автор), ДатаНачала, ТекстОписания);
			
			ОбменСМобильнымиОтправкаУведомленийСервер.ЗаписатьСообщениеВРегистрPushУведомления(Пользователь, 
				Ссылка, ТекущаяДата() + 10, Сообщение);
			
		КонецЕсли;
		
		// Получаем параметры
		УстановитьНапоминание = Неопределено;
		ИнтервалВремениСтрокой = Неопределено;
		ЭтотОбъект.ДополнительныеСвойства.Свойство("УстановитьНапоминание", УстановитьНапоминание);
		ЭтотОбъект.ДополнительныеСвойства.Свойство("ИнтервалВремениСтрокой", ИнтервалВремениСтрокой);
		
		// Если доп свойства получены, значит запись выполняется из формы.
		// Если не получены - нужно проверить есть ли напоминания в регистре.
		// Если есть напоминания - переустановить на новые значения.
		Если УстановитьНапоминание = Неопределено Тогда
			
			Запрос = Новый Запрос();
			Запрос.УстановитьПараметр("Пользователь", Пользователь);
			Запрос.УстановитьПараметр("Источник", Ссылка);
			Запрос.Текст = 
				"ВЫБРАТЬ
				|	НапоминанияПользователя.СрокНапоминания,
				|	НапоминанияПользователя.ВремяСобытия,
				|	НапоминанияПользователя.СпособУстановкиВремениНапоминания,
				|	НапоминанияПользователя.ИнтервалВремениНапоминания,
				|	НапоминанияПользователя.ИмяРеквизитаИсточника,
				|	НапоминанияПользователя.Расписание,
				|	НапоминанияПользователя.ПредставлениеИсточника
				|ИЗ
				|	РегистрСведений.НапоминанияПользователя КАК НапоминанияПользователя
				|ГДЕ
				|	НапоминанияПользователя.Пользователь = &Пользователь
				|	И НапоминанияПользователя.Источник = &Источник";
				
			Напоминания = Запрос.Выполнить().Выгрузить();
			Если Напоминания.Количество() = 0 Тогда
				УстановитьНапоминание = Ложь;
			Иначе
				
				УстановитьНапоминание = Истина;
				ИнтервалВремениСтрокой = НапоминанияПользователяКлиентСервер.ПредставлениеВремени(
					Напоминания[0].ИнтервалВремениНапоминания);
				
			КонецЕсли;
			
		КонецЕсли;
		
		Если УстановитьНапоминание Тогда
			
			ИнтервалВремениНапоминания = 
				НапоминанияПользователяКлиентСервер.ПолучитьИнтервалВремениИзСтроки(ИнтервалВремениСтрокой);
			
			// Для старой записи удаляем существующие уведомления и записываем новые.
			// Возможно изменился заголовок или параметры напоминания.
			Если Не ЭтоНовый Тогда
				Набор = РегистрыСведений.СообщенияPushУведомлений.СоздатьНаборЗаписей();
				Набор.Отбор.Пользователь.Установить(Пользователь);
				Набор.Отбор.ОбъектУведомления.Установить(Ссылка);
				Набор.Записать(Истина);
			КонецЕсли;
			
			Если ИнтервалВремениНапоминания = 0 Тогда
				Сообщение = СтрШаблон("%1 начинается сейчас",
					ТекстОписания);
			Иначе
				Сообщение = СтрШаблон("%1 начнется в %2 (через %3 мин)",
					ТекстОписания, Формат(ДатаНачала, "ДФ=HH:mm"), 
					Окр(ИнтервалВремениНапоминания/60, 0));
			КонецЕсли;
			
			ОбменСМобильнымиОтправкаУведомленийСервер.ЗаписатьСообщениеВРегистрPushУведомления(Пользователь,
				Ссылка, ДатаНачала - ИнтервалВремениНапоминания, Сообщение);
			
		Иначе
			
			Набор = РегистрыСведений.СообщенияPushУведомлений.СоздатьНаборЗаписей();
			Набор.Отбор.ОбъектУведомления.Установить(Ссылка);
			Набор.Записать(Истина);
			
		КонецЕсли;
	
	КонецЕсли;
	УстановитьПривилегированныйРежим(Ложь);
	
	// Обработка события создания записи календаря.
	Если Не ДополнительныеСвойства.Свойство("НеРегистрироватьСобытиеУведомления")
		И ДополнительныеСвойства.ЭтоНовый Тогда
		РаботаСУведомлениями.ОбработатьСобытиеУведомления(
			Ссылка,
			Перечисления.СобытияУведомлений.СозданиеЗаписиКалендаря);
	КонецЕсли;
	
	Если ДополнительныеСвойства.Свойство("ПовторяющеесяСобытие") И ЗначениеЗаполнено(ДополнительныеСвойства.ПовторяющеесяСобытие)
		И ДополнительныеСвойства.Свойство("ДатаИсключения") И ЗначениеЗаполнено(ДополнительныеСвойства.ДатаИсключения) Тогда
		
		ПовторяющеесяСобытиеОбъект = ДополнительныеСвойства.ПовторяющеесяСобытие.ПолучитьОбъект();
		ПовторяющеесяСобытиеОбъект.Заблокировать();
		ПовторяющеесяСобытиеОбъект.ДобавитьИсключениеПовторения(ДополнительныеСвойства.ДатаИсключения, Ссылка);
		ПовторяющеесяСобытиеОбъект.Записать();
		
	КонецЕсли;
	
	Если ДополнительныеСвойства.Свойство("Цвет") Тогда
		
		РаботаСРабочимКалендаремСервер.УстановитьЦветЗаписиКалендаря(Ссылка, ДополнительныеСвойства.Цвет);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	Автор = ПользователиКлиентСервер.ТекущийПользователь();
	Пользователь = ПользователиКлиентСервер.ТекущийПользователь();
	
	СкопироватьЗаписьКалендаря(ОбъектКопирования);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура СкопироватьЗаписьКалендаря(ЗаписьКалендаря)
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, ЗаписьКалендаря,
		"ВесьДень, ДатаНачала, ДатаОкончания, Описание, Состояние, Предмет");
	
	ТипЗаписиКалендаря = Перечисления.ТипЗаписиКалендаря.Событие;
	Связанная = Ложь;
	
	ДатаНачалаПовторения = Неопределено;
	ДатаОкончанияПовторения = Неопределено;
	ИнтервалПовторения = Неопределено;
	КоличествоПовторов = Неопределено;
	ПовторениеПоДнямМесяца = Неопределено;
	ПовторениеПоМесяцам = Неопределено;
	ПравилоОкончанияПовторения = Неопределено;
	ЧастотаПовторения = Неопределено;
	
	ИсключенияПовторения.Очистить();
	ПовторениеПоДням.Очистить();
	
КонецПроцедуры

Процедура ЗаполнитьНаименованиеИКраткоеОписание()
	
	Наименование = СтрПолучитьСтроку(СокрЛ(Описание), 1);
	
КонецПроцедуры

Процедура ОчиститьРеквизитыПовторения()
	
	ДатаНачалаПовторения = Дата(1,1,1);
	ДатаОкончанияПовторения = Дата(1,1,1);
	ИнтервалПовторения = 0;
	ПовторениеПоДнямМесяца = 0;
	ПовторениеПоМесяцам = 0;
	ПравилоОкончанияПовторения = Перечисления.ПравилаОкончанияПовторения.ПустаяСсылка();
	ЧастотаПовторения = Перечисления.ЧастотаПовторения.ПустаяСсылка();
	КоличествоПовторов = 0;
	ПовторениеПоДням.Очистить();
	
	ОчиститьИсключенияПовторения();
	
КонецПроцедуры

Процедура ОчиститьИсключенияПовторения()
	
	Для Каждого ИсключениеПовторения Из ИсключенияПовторения Цикл
		
		ПометитьНаУдалениеИсключениеПовторения(ИсключениеПовторения);
		
	КонецЦикла;
	
	ИсключенияПовторения.Очистить();
	
КонецПроцедуры

Процедура ПометитьНаУдалениеИсключениеПовторения(ИсключениеПовторения)
	
	Если ЗначениеЗаполнено(ИсключениеПовторения.ЗаписьИсключения) Тогда
		
		ЗаписьИсключенияОбъект = ИсключениеПовторения.ЗаписьИсключения.ПолучитьОбъект();
		
		Если ЗаписьИсключенияОбъект = Неопределено Тогда
			Возврат;
		КонецЕсли;
		
		Если ЗаписьИсключенияОбъект.ТипЗаписиКалендаря = Перечисления.ТипЗаписиКалендаря.ЭлементПовторяющегосяСобытия Тогда
			ЗаписьИсключенияОбъект.Заблокировать();
			ЗаписьИсключенияОбъект.ПометкаУдаления = Истина;
			ЗаписьИсключенияОбъект.Записать();
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура РассчитатьДатуОкончанияПовторения()
	
	Если ЗначениеЗаполнено(ДатаОкончанияПовторения) Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(КоличествоПовторов) Тогда
		ПравилоОкончанияПовторения = Перечисления.ПравилаОкончанияПовторения.Никогда;
		Возврат;
	КонецЕсли;
	
	СтруктураПравилаПовторения = ПолучитьСтруктуруПравилаПовторения();
	
	НомерИтерации = 0;
	НомерПовтора = 0;
	ПроверяемаяДата = ДатаНачалаПовторения;
	
	Пока НомерИтерации < 10000 Цикл // Ограничение на слишком большую дату окончания повторения
		
		Если РаботаСРабочимКалендаремСервер.ДатаУдовлетворяетПравилуПовторения(
				ПроверяемаяДата, СтруктураПравилаПовторения) Тогда
			
			НомерИтерации = 0;
			НомерПовтора = НомерПовтора + 1;
			
			Если НомерПовтора = КоличествоПовторов Тогда
				ДатаОкончанияПовторения = ПроверяемаяДата;
				Возврат;
			КонецЕсли;
			
		КонецЕсли;
		
		ПроверяемаяДата = ПроверяемаяДата + 86400; // 86400 - число секунд в сутках
		НомерИтерации = НомерИтерации + 1;
		
	КонецЦикла;
	
КонецПроцедуры

Функция ИзмениласьПометкаУдаления()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЗаписиРабочегоКалендаря.ПометкаУдаления
		|ИЗ
		|	Справочник.ЗаписиРабочегоКалендаря КАК ЗаписиРабочегоКалендаря
		|ГДЕ
		|	ЗаписиРабочегоКалендаря.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	Результат = Запрос.Выполнить();
	
	Если Не Результат.Пустой() Тогда
		Выборка = Результат.Выбрать();
		Выборка.Следующий();
		Возврат ПометкаУдаления И НЕ Выборка.ПометкаУдаления;
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

Функция ПолучитьСтруктуруПравилаПовторения()
	
	СтруктураПравилаПовторения = РаботаСРабочимКалендаремСервер.ПолучитьСтруктуруПравилаПовторения();
	ЗаполнитьЗначенияСвойств(СтруктураПравилаПовторения, ЭтотОбъект);
	СтруктураПравилаПовторения.ИсключенияПовторения = ИсключенияПовторения.Выгрузить();
	СтруктураПравилаПовторения.ПовторениеПоДням = ПовторениеПоДням.Выгрузить();
	
	Возврат СтруктураПравилаПовторения;
	
КонецФункции

Процедура ОтменитьПовторение()
	
	ОчиститьРеквизитыПовторения();
	ТипЗаписиКалендаря = Перечисления.ТипЗаписиКалендаря.Событие;
	
КонецПроцедуры

Функция ПолучитьДатуНачалаИДатуОкончания(ДатаЗаписи)
	
	Если ДатаЗаписи < ДатаНачалаПовторения Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ДатаОкончанияПовторения) И ДатаЗаписи > ДатаОкончанияПовторения Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	СтруктураПравилаПовторения = ПолучитьСтруктуруПравилаПовторения();
	ПроверяемаяДата = НачалоДня(ДатаЗаписи);
	
	Если РаботаСРабочимКалендаремСервер.ДатаУдовлетворяетПравилуПовторения(
			ПроверяемаяДата, СтруктураПравилаПовторения) Тогда
		
		ДатаНачалаИОкончания = Новый Структура("ДатаНачала, ДатаОкончания");
		ДатаНачалаИОкончания.ДатаНачала = ПроверяемаяДата + (ДатаНачала - НачалоДня(ДатаНачала));
		ДатаНачалаИОкончания.ДатаОкончания = ПроверяемаяДата + (ДатаОкончания - НачалоДня(ДатаНачала));
		
		Возврат ДатаНачалаИОкончания;
		
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

Функция ПолучитьТекстовоеПредставлениеПовторения()
	
	ТекстовоеПредставлениеПовторения = "";
	
	НастройкиПовторения = ПолучитьНастройкиПовторения();
	ТекстовоеПредставлениеПовторения =
		РаботаСРабочимКалендаремКлиентСервер.ПолучитьТекстовоеПредставлениеПовторения(НастройкиПовторения);
	
	Возврат ТекстовоеПредставлениеПовторения;
	
КонецФункции

Функция ПолучитьНастройкиПовторения()
	
	ПовторениеПоДнямНедели = Новый Соответствие;
	Для ИндексПовторениеПоДням = 1 По 7 Цикл
		
		Если ЧастотаПовторения = Перечисления.ЧастотаПовторения.Еженедельно Тогда
			
			ПараметрыОтбора = Новый Структура;
			ПараметрыОтбора.Вставить("ДеньНедели", ИндексПовторениеПоДням);
			ПараметрыОтбора.Вставить("НомерВхождения", 0);
			НайденныеСтроки = ПовторениеПоДням.НайтиСтроки(ПараметрыОтбора); 
			
			ПовторениеПоДнямНедели.Вставить(ИндексПовторениеПоДням, НайденныеСтроки.Количество() <> 0);
			
		Иначе
			ПовторениеПоДнямНедели.Вставить(ИндексПовторениеПоДням, Ложь);
		КонецЕсли;
		
	КонецЦикла;
	
	ПовторениеПоДнямНеделиВМесяце = Неопределено;
	
	Если ЧастотаПовторения = Перечисления.ЧастотаПовторения.Ежемесячно
		И НЕ ЗначениеЗаполнено(ПовторениеПоДнямМесяца) И ПовторениеПоДням.Количество() = 1 Тогда
		
		ПовторениеПоДнямНеделиВМесяце = Новый Структура("НомерВхождения, ДеньНедели");
		ЗаполнитьЗначенияСвойств(ПовторениеПоДнямНеделиВМесяце, ПовторениеПоДням[0]);
		
	КонецЕсли;
	
	НастройкиПовторения = РаботаСРабочимКалендаремКлиентСервер.ПолучитьСтруктуруНастройкиПовторения(
		ЧастотаПовторения, ИнтервалПовторения, ПравилоОкончанияПовторения,
		КоличествоПовторов, ДатаОкончанияПовторения, ПовторениеПоДнямНедели,
		ПовторениеПоДнямМесяца, ПовторениеПоДнямНеделиВМесяце, ПовторениеПоМесяцам);
	
	Возврат НастройкиПовторения;
	
КонецФункции

#КонецОбласти

#КонецЕсли
