
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ИспользоватьУчетПоОрганизациям = ПолучитьФункциональнуюОпцию("ИспользоватьУчетПоОрганизациям");
	Если ИспользоватьУчетПоОрганизациям Тогда
		Элементы.УчастникиСВДАдресат.АвтоОтметкаНезаполненного = Истина;
		Элементы.ИмяСВДНашейОрганизации.Видимость = Ложь;
	КонецЕсли;	
	
	Элементы.НастройкаДоставки.Видимость =
		Объект.ФорматСообщения <> Справочники.ФорматыСообщенийСВД.ОператорЭДО1СТакском;
	
	Если Не ЗначениеЗаполнено(Объект.НастройкаПриемаОтправки) Тогда
		НастройкаДоставки = НСтр("ru='Выбрать'; en = 'Select'");
	Иначе	
		ЗаполнитьНастройкуДоставки();
	КонецЕсли;	
	
	Если Не ЗначениеЗаполнено(Объект.ФорматСообщения) Тогда
		
		// Заполним список
		Запрос = Новый Запрос;
		Запрос.Текст
			= "ВЫБРАТЬ
			  |	ФорматыСообщенийСВД.Ссылка
			  |ИЗ
			  |	Справочник.ФорматыСообщенийСВД КАК ФорматыСообщенийСВД
			  |ГДЕ
			  |	ФорматыСообщенийСВД.ПометкаУдаления = ЛОЖЬ";
			  
		ТаблицаЗапроса = Запрос.Выполнить().Выгрузить();
		Если ТаблицаЗапроса.Количество() = 1 Тогда
			Объект.ФорматСообщения = ТаблицаЗапроса[0].Ссылка;
		КонецЕсли;	
		
	КонецЕсли;	
	
	ИспользоватьОбменЭД = Константы.ИспользоватьОбменЭД.Получить();
	
	ОбновитьВидимостьСпискаУчастниковИЗаполнитьСписок();
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьВидимостьСпискаУчастниковИЗаполнитьСписок()
	
	Элементы.ГруппаУчастникиСВД.Видимость = Ложь;
	УчастникиСВД.Очистить();
	
	ИспользоватьУчетПоОрганизациям = ПолучитьФункциональнуюОпцию("ИспользоватьУчетПоОрганизациям");
	
	Если ЗначениеЗаполнено(Объект.НастройкаПриемаОтправки) Тогда
		
		МенеджерОбъекта = ОбщегоНазначения.МенеджерОбъектаПоСсылке(Объект.НастройкаПриемаОтправки);
		Если МенеджерОбъекта <> Неопределено Тогда
			
			Если МенеджерОбъекта.ТребуетсяЗаполнитьСписокУчастников() Тогда
				
				Элементы.ГруппаУчастникиСВД.Видимость = Истина;
				
				Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
					
					// Заполним список
					Запрос = Новый Запрос;
					Запрос.Текст
						= "ВЫБРАТЬ
						  |	УчастникиСВД.Адресат,
						  |	УчастникиСВД.ИмяАдресата
						  |ИЗ
						  |	РегистрСведений.УчастникиСВД КАК УчастникиСВД
						  |ГДЕ
						  |	УчастникиСВД.Транспорт = &Транспорт";
						  
					Запрос.УстановитьПараметр("Транспорт", Объект.Ссылка);
					
					Выборка = Запрос.Выполнить().Выбрать();
					Пока Выборка.Следующий() Цикл
						
						ДобавитьЭтуСтроку = Истина;
						Если Выборка.Адресат = Справочники.Организации.ПустаяСсылка() Тогда
							
							ДобавитьЭтуСтроку = Ложь; 
							Если Не ИспользоватьУчетПоОрганизациям Тогда
								ИмяСВДНашейОрганизации = Выборка.ИмяАдресата;
							КонецЕсли;	
							
						КонецЕсли;	
						
						Если ДобавитьЭтуСтроку Тогда
							Строка = УчастникиСВД.Добавить();
							Строка.Адресат = Выборка.Адресат;
							Строка.ИмяАдресата = Выборка.ИмяАдресата;
						КонецЕсли;
						
					КонецЦикла;
				
				КонецЕсли;	
			
			КонецЕсли;	
			
		КонецЕсли;	
		
	КонецЕсли;	
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьНастройкуДоставки()
	
	Представление = Объект.НастройкаПриемаОтправки.Метаданные().ПредставлениеОбъекта;
	Если Не ЗначениеЗаполнено(Представление) Тогда
		Представление = Объект.НастройкаПриемаОтправки.Метаданные().Имя;
	КонецЕсли;	
			
	НастройкаДоставки = Строка(Объект.НастройкаПриемаОтправки) + " (" + Представление + ")";
			
	ОбновитьВидимостьСпискаУчастниковИЗаполнитьСписок();
	
КонецПроцедуры	

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "Запись_НастройкаПриемаОтправки" Тогда
		
		Объект.НастройкаПриемаОтправки = Источник;
		ЗаполнитьНастройкуДоставки();
		
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура НастройкаДоставкиНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если Не ЗначениеЗаполнено(Объект.ФорматСообщения) И Не ЗначениеЗаполнено(Объект.НастройкаПриемаОтправки) Тогда
		ПоказатьПредупреждение(, НСтр("ru='Заполните поле ""Формат сообщения""'; en = 'Fill in the ""Message format"" field'"));
		Возврат;
	КонецЕсли;	
	
	Если ЗначениеЗаполнено(Объект.НастройкаПриемаОтправки) Тогда
		
		ПоказатьЗначение(, Объект.НастройкаПриемаОтправки);
		
	Иначе	
		
		// Переопределяемый модуль - по виду транспорта вернуть имя формы
		
		СписокНастроек = ПолучитьСписокНастроекТранспорта(Объект.ФорматСообщения);
		Если СписокНастроек.Количество() = 0 Тогда
			ПоказатьПредупреждение(, НСтр("ru='Ни один способ доставки не поддерживает данный формат сообщения.'; en = 'There is no delivery method that supports the format of this message.'"));
			Возврат;
		КонецЕсли;	
		
		ОписаниеОповещения = Новый ОписаниеОповещения(
			"НастройкаДоставкиНажатиеПродолжение",
			ЭтотОбъект);
		Если СписокНастроек.Количество() > 1 Тогда
			ПараметрыФормы = Новый Структура("СписокНастроек", СписокНастроек);
			ОткрытьФорму(
				"Справочник.ТранспортыСВД.Форма.ВыборСпособаДоставки", 
				ПараметрыФормы,,,,,
				ОписаниеОповещения,
				РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		Иначе	
			Значение = СписокНастроек[0];
			ВыполнитьОбработкуОповещения(ОписаниеОповещения, Значение); 
		КонецЕсли;	
				
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура НастройкаДоставкиНажатиеПродолжение(Значение, Параметры) Экспорт
	
	Если Значение = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура("Транспорт", Объект.Ссылка);
	ОткрытьФорму(Значение.Значение, ПараметрыФормы,,,,,, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаСервере
Функция ПолучитьСписокНастроекТранспорта(ФорматСообщения)
	
	СписокНастроек = Новый СписокЗначений;
	
	ТипыНастроекТранспорта = Метаданные.Справочники.ТранспортыСВД.Реквизиты.НастройкаПриемаОтправки.Тип.Типы();
	
	Для Каждого ТипНастроек Из ТипыНастроекТранспорта Цикл
		
		МетаданноеНастройки = Метаданные.НайтиПоТипу(ТипНастроек);
		
		ИмяОбъекта = МетаданноеНастройки.Имя;
		МенеджерОбъекта = Справочники[ИмяОбъекта];
		
		ФорматСообщенияВМетаданном = МенеджерОбъекта.ФорматСообщенияСВД();
		
		Если ФорматСообщенияВМетаданном = ФорматСообщения Тогда
			
			//Если ФорматСообщенияВМетаданном = Справочники.ФорматыСообщенийСВД.ОператорЭДО1СТакском Тогда
			//	ИмяФормыОбъекта = "Справочник." + ИмяОбъекта + ".Форма.ФормаВыбора";
			//Иначе
				ИмяФормыОбъекта = "Справочник." + ИмяОбъекта + ".Форма.ФормаЭлемента";
			//КонецЕсли;				
			Представление = МетаданноеНастройки.ПредставлениеОбъекта;
			Если Не ЗначениеЗаполнено(Представление) Тогда
				Представление = ИмяОбъекта;
			КонецЕсли;	
			
			СписокНастроек.Добавить(ИмяФормыОбъекта, Представление);
			
		КонецЕсли;	
		
	КонецЦикла;	
	
	Возврат СписокНастроек;
	
КонецФункции

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	Если Элементы.ГруппаУчастникиСВД.Видимость Тогда
		
		// очистим регистр
			
		НаборЗаписей = РегистрыСведений.УчастникиСВД.СоздатьНаборЗаписей();
		
		НаборЗаписей.Отбор.Транспорт.Установить(Объект.Ссылка); // ставим отбор только по транспорту - чтобы для всех адресатов стереть
		
		// не добавляем записи в набор - чтобы все стереть
		НаборЗаписей.Записать();
		
		
		// Запишем таблицу в регистр
		Для Каждого Строка Из УчастникиСВД Цикл
			
			МенеджерЗаписи = РегистрыСведений.УчастникиСВД.СоздатьМенеджерЗаписи();
			МенеджерЗаписи.Адресат = Строка.Адресат;
			МенеджерЗаписи.Транспорт = Объект.Ссылка;
			МенеджерЗаписи.ИмяАдресата = Строка.ИмяАдресата;
			МенеджерЗаписи.Записать();
			
		КонецЦикла;		
		
		ИспользоватьУчетПоОрганизациям = ПолучитьФункциональнуюОпцию("ИспользоватьУчетПоОрганизациям");
		Если Не ИспользоватьУчетПоОрганизациям Тогда
			
			МенеджерЗаписи = РегистрыСведений.УчастникиСВД.СоздатьМенеджерЗаписи();
			МенеджерЗаписи.Адресат = Справочники.Организации.ПустаяСсылка();
			МенеджерЗаписи.Транспорт = Объект.Ссылка;
			МенеджерЗаписи.ИмяАдресата = ИмяСВДНашейОрганизации;
			МенеджерЗаписи.Записать();
			
		КонецЕсли;	
		
	КонецЕсли;	
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	Если Элементы.ГруппаУчастникиСВД.Видимость И УчастникиСВД.Количество() = 0 Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru='Укажите участников СВД'; en = 'Specify the EDES participants'"), , "УчастникиСВД", , Отказ);
	КонецЕсли;	
	
	Если Элементы.ГруппаУчастникиСВД.Видимость Тогда
		
		ИспользоватьУчетПоОрганизациям = ПолучитьФункциональнуюОпцию("ИспользоватьУчетПоОрганизациям");
		Если Не ИспользоватьУчетПоОрганизациям Тогда
		
			Если Не ЗначениеЗаполнено(ИмяСВДНашейОрганизации) Тогда
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru='Не заполнено поле ""Наше имя в СВД""'; en = 'You need to fill ""Our name in EDES""'"), , 
					"ИмяСВДНашейОрганизации", , Отказ);	
			КонецЕсли;	
				
		КонецЕсли;		
		
		Для Каждого Строка Из УчастникиСВД Цикл
			
			Если Не ЗначениеЗаполнено(Строка.Адресат) Тогда
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru='Не указан участник СВД'; en = 'EDES participant is not specified'"), , 
					"УчастникиСВД", , Отказ);	
			КонецЕсли;	
			
			Если Не ЗначениеЗаполнено(Строка.ИмяАдресата) Тогда
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru='Не указано имя участника СВД'; en = 'EDES participant name is not specified'"), , 
					"УчастникиСВД", , Отказ);	
			КонецЕсли;	
			
		КонецЦикла;		
		
	КонецЕсли;	
	
	Если Объект.ФорматСообщения <> Справочники.ФорматыСообщенийСВД.ОператорЭДО1СТакском
		И Не ЗначениеЗаполнено(Объект.НастройкаПриемаОтправки) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru='Заполните способ доставки'; en = 'Fill in the delivery method'"), , "НастройкаДоставки", , Отказ);
		Индекс = ПроверяемыеРеквизиты.Найти("НастройкаДоставки");
		ПроверяемыеРеквизиты.Удалить(Индекс);
	КонецЕсли;	
		
КонецПроцедуры

&НаКлиенте
Процедура ВидТранспортаОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если ВыбранноеЗначение = ПредопределенноеЗначение("Справочник.ФорматыСообщенийСВД.ОператорЭДО1СТакском")
		И Не ИспользоватьОбменЭД Тогда
		
		ПоказатьПредупреждение(, НСтр("ru = 'Для отправки документов через оператора ЭДО 1С-Такском необходимо в настройках
                             |программы установить флаг ""Использовать обмен электронными документами"".'; en = 'To send a document via the EDI operator 1C-Taxcom select the checkbox ""Use of electronic documents exchange"" in the program settings.'"));
		
		СтандартнаяОбработка = Ложь;
		Возврат;
		
	КонецЕсли;
	
	Элементы.НастройкаДоставки.Видимость = 
		ВыбранноеЗначение <> ПредопределенноеЗначение("Справочник.ФорматыСообщенийСВД.ОператорЭДО1СТакском");
	
КонецПроцедуры


