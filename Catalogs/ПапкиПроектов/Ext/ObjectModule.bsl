
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

// Обновление адресной книги
Перем ОбновитьДанныеПапкиПроектовВАдреснойКниге;

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	Если ЭтоНовый() Тогда 
		ДатаСоздания = ТекущаяДатаСеанса();
		Ответственный = ПользователиКлиентСервер.ТекущийПользователь();
	КонецЕсли;	
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	РеквизитыПапкиПроектовПоСсылке =
			ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Ссылка, "Родитель, ПометкаУдаления, Наименование");
	
	Если Не Ссылка.Пустая() Тогда
		
		ПредыдущаяПометкаУдаления = РеквизитыПапкиПроектовПоСсылке.ПометкаУдаления;
		Если ПометкаУдаления <> ПредыдущаяПометкаУдаления Тогда 
			
			Если Не ДокументооборотПраваДоступа.ПолучитьПраваПоОбъекту(Ссылка).Удаление Тогда
				ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'У вас нет права ""Пометка на удаление"" папки ""%1"".'; en = 'Not enough permissions to delete the folder ""%1"".'"),
					Строка(Ссылка));
			КонецЕсли;	
			
			Запрос = Новый Запрос;
			Запрос.Текст = 
				"ВЫБРАТЬ РАЗРЕШЕННЫЕ
				|	Проекты.Ссылка,
				|	Проекты.ПометкаУдаления
				|ИЗ
				|	Справочник.Проекты КАК Проекты
				|ГДЕ
				|	Проекты.Папка = &Ссылка";
			Запрос.УстановитьПараметр("Ссылка", Ссылка);
			
			Выборка = Запрос.Выполнить().Выбрать();
			Пока Выборка.Следующий() Цикл
				Если ПометкаУдаления <> Выборка.ПометкаУдаления Тогда 
					Объект = Выборка.Ссылка.ПолучитьОбъект();
					Объект.Заблокировать();
					Объект.УстановитьПометкуУдаления(ПометкаУдаления);
				КонецЕсли;
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЕсли;
	
	// Обновление адресной книги
	ОбновитьДанныеПапкиПроектовВАдреснойКниге = Ложь;
	Если ЭтоНовый() Тогда
		ОбновитьДанныеПапкиПроектовВАдреснойКниге = Истина;
	Иначе
		Если РеквизитыПапкиПроектовПоСсылке.Родитель <> Родитель
			Или РеквизитыПапкиПроектовПоСсылке.ПометкаУдаления <> ПометкаУдаления
			Или РеквизитыПапкиПроектовПоСсылке.Наименование <> Наименование Тогда
			
			ОбновитьДанныеПапкиПроектовВАдреснойКниге = Истина;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	// Обновление адресной книги
	Если ОбновитьДанныеПапкиПроектовВАдреснойКниге Тогда
		Справочники.АдреснаяКнига.ОбновитьДанныеОбъекта(
			Ссылка,
			Родитель,
			Справочники.АдреснаяКнига.ПоПроектам,
			Ссылка);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
