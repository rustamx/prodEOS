
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ИмяДействия = Параметры.ИмяДействия;
	
	РезультатВыполнения = Параметры.РезультатВыполнения;
	
	// Заполним список выбора действий.
	Если Параметры.Действия <> Неопределено Тогда
		Для Каждого СтрокаТаблицы Из Параметры.Действия Цикл
			Элементы.ИмяДействия.СписокВыбора.Добавить(СтрокаТаблицы.Имя, СтрокаТаблицы.Описание);
		КонецЦикла;
	КонецЕсли;
	
	ЗаполнитьСписокВыбораРезультатовПоДействию(ЭтаФорма);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ИмяДействияПриИзменении(Элемент)
	
	РезультатВыполнения = Неопределено;
	
	ЗаполнитьСписокВыбораРезультатовПоДействию(ЭтаФорма);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Готово(Команда)
	
	Если Не ЗначениеЗаполнено(ИмяДействия) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			НСтр("ru = 'Не указано действие'; en = 'Action is not specified'"),, "ИмяДействия");
		
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(РезультатВыполнения) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			НСтр("ru = 'Не указан результат выполнения'; en = 'Execution result is not specified'"),, "РезультатВыполнения");
		
		Возврат;
	КонецЕсли;
	
	Результат = РаботаСКомплекснымиБизнесПроцессамиКлиентСервер.
		СтруктураУсловияВыполненияДляЭлементаОтбораКомпоновкиДанных(
			РезультатВыполнения, ИмяДействия);
	
	Закрыть(Результат);
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	Закрыть(Неопределено);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Заполняет список выбора элемента РезультатВыполнения
// возможными значениями с представлениями по параметру форму РезультатыВыполненияДействий.
//
// Параметры:
//  Форма - УправляемаяФорма
//
&НаКлиентеНаСервереБезКонтекста
Процедура ЗаполнитьСписокВыбораРезультатовПоДействию(Форма)
	
	Форма.Элементы.РезультатВыполнения.СписокВыбора.Очистить();
	
	Если ЗначениеЗаполнено(Форма.ИмяДействия) Тогда
		РезультатыВыполненияДействия = Форма.Параметры.РезультатыВыполненияДействий[Форма.ИмяДействия];
		Для Каждого РезультатВыполненияДействия Из РезультатыВыполненияДействия Цикл
			Форма.Элементы.РезультатВыполнения.СписокВыбора.Добавить(
				РезультатВыполненияДействия.Значение,
				РезультатВыполненияДействия.Представление);
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
