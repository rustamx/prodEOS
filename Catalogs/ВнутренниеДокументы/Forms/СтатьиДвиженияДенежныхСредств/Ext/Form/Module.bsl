#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	ТолькоПросмотр = Параметры.ТолькоПросмотр;
	Если ТолькоПросмотр Тогда
		Элементы.ФормаГотово.Видимость = Ложь;
		Элементы.ФормаОтмена.Видимость = Ложь;
		Элементы.ФормаЗакрыть.Видимость = Истина;
	КонецЕсли;
	
	Сумма = Параметры.Сумма;
	СуммаНДС = Параметры.СуммаНДС;
	МассивВыбранныхСтатей = Новый Массив;
	Для Каждого СтрокаПараметра из Параметры.СтатьиДвиженияДенежныхСредств Цикл
		Строка = ВыбранныеСтатьи.Добавить();
		Строка.СтатьяДвиженияДенежныхСредств = СтрокаПараметра.СтатьяДвиженияДенежныхСредств;
		Строка.Сумма = СтрокаПараметра.Сумма;
		Строка.СуммаНДС = СтрокаПараметра.СуммаНДС;
		МассивВыбранныхСтатей.Добавить(Строка.СтатьяДвиженияДенежныхСредств);
	КонецЦикла;
	
	// Установим отбор, исключив выбранные статьи из общего списка.
	ОтборСписка = СтатьиДвиженияДенежныхСредств.Отбор.Элементы;
	ПоляОтбора = СтатьиДвиженияДенежныхСредств.Отбор.ДоступныеПоляОтбора.Элементы;
	ОтборПоСсылке = ОтборСписка.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборПоСсылке.ВидСравнения = ВидСравненияКомпоновкиДанных.НеВСписке;
	ОтборПоСсылке.Использование = Истина;
	ОтборПоСсылке.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Ссылка");
	ОтборПоСсылке.ПравоеЗначение = МассивВыбранныхСтатей;
	
	Элементы.СуммаНДС.Видимость = Параметры.УчитыватьНДС;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ОбновитьПодвал();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПеренестиВправо(Команда)
	
	ТекущиеДанные = Элементы.СтатьиДвиженияДенежныхСредств.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено 
		И НЕ ТекущиеДанные.ЭтоГруппа Тогда
		ДобавитьСтатьюКВыбранным(ТекущиеДанные.Ссылка);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПеренестиВлево(Команда)
	
	ТекущиеДанные = Элементы.ВыбранныеСтатьи.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		УдалитьСтатьюИзВыбранных(ТекущиеДанные.СтатьяДвиженияДенежныхСредств);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Готово(Команда)
	
	Результат = Новый Массив;
	Для каждого Строка из ВыбранныеСтатьи Цикл
		СтрокаРезультата = Новый Структура;
		СтрокаРезультата.Вставить("СтатьяДвиженияДенежныхСредств", 
			Строка.СтатьяДвиженияДенежныхСредств);
		СтрокаРезультата.Вставить("Сумма", Строка.Сумма);
		СтрокаРезультата.Вставить("СуммаНДС", Строка.СуммаНДС);
		Результат.Добавить(СтрокаРезультата);
	КонецЦикла;
	
	Закрыть(Результат);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийТаблицыФормыСтатьиДвиженияДенежныхСредств

&НаКлиенте
Процедура СтатьиДвиженияДенежныхСредствПриАктивизацииСтроки(Элемент)
	
	ТекущиеДанные = Элементы.СтатьиДвиженияДенежныхСредств.ТекущиеДанные;
	ДоступенВыбор = (ТекущиеДанные <> Неопределено) 
		И Не ТекущиеДанные.ЭтоГруппа
		И Не ТолькоПросмотр;
	Элементы.ПеренестиВправо.Доступность = ДоступенВыбор;
	Элементы.КонтекстноеМенюПеренестиВправо.Доступность = ДоступенВыбор;
	
КонецПроцедуры

&НаКлиенте
Процедура СтатьиДвиженияДенежныхСредствВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ТекущиеДанные = Элементы.СтатьиДвиженияДенежныхСредств.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено 
		И Не ТекущиеДанные.ЭтоГруппа 
		И Не ТолькоПросмотр Тогда
		ДобавитьСтатьюКВыбранным(ТекущиеДанные.Ссылка);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СтатьиДвиженияДенежныхСредствНачалоПеретаскивания(Элемент, ПараметрыПеретаскивания, 
	Выполнение)
	
	ТекущиеДанные = Элементы.СтатьиДвиженияДенежныхСредств.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено 
		Или ТекущиеДанные.ЭтоГруппа
		Или ТолькоПросмотр Тогда
		Выполнение = Ложь;
	Иначе
		ПараметрыПеретаскивания.Значение = ТекущиеДанные.Ссылка;
		ПараметрыПеретаскивания.Действие = ДействиеПеретаскивания.Перемещение;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СтатьиДвиженияДенежныхСредствПроверкаПеретаскивания(Элемент, ПараметрыПеретаскивания, 
	СтандартнаяОбработка, Строка, Поле)
	
	СтандартнаяОбработка = Ложь;
	Если ТипЗнч(ПараметрыПеретаскивания.Значение) 
		=  Тип("СправочникСсылка.СтатьиДвиженияДенежныхСредств") Тогда
		ПараметрыПеретаскивания.ДопустимыеДействия = ДопустимыеДействияПеретаскивания.Перемещение;
	Иначе
		ПараметрыПеретаскивания.ДопустимыеДействия = ДопустимыеДействияПеретаскивания.НеОбрабатывать;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СтатьиДвиженияДенежныхСредствПеретаскивание(Элемент, ПараметрыПеретаскивания, 
	СтандартнаяОбработка, Строка, Поле)
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийТаблицыФормыВыбранныеСтатьи

&НаКлиенте
Процедура ВыбранныеСтатьиПриАктивизацииСтроки(Элемент)
	
	ТекущиеДанные = Элементы.ВыбранныеСтатьи.ТекущиеДанные;
	Элементы.ПеренестиВлево.Доступность = 
		Не ТолькоПросмотр
		И (ТекущиеДанные <> Неопределено);
	Элементы.КонтекстноеМенюПеренестиВлево.Доступность = 
		Не ТолькоПросмотр
		И (ТекущиеДанные <> Неопределено);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбранныеСтатьиВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Поле <> Элементы.СтатьиДвиженияДенежныхСредств Тогда
		Возврат;
	КонецЕсли;
	
	СтандартнаяОбработка = Ложь;
	ТекущиеДанные = Элементы.ВыбранныеСтатьи.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено
		И Не ТолькоПросмотр Тогда
		УдалитьСтатьюИзВыбранных(ТекущиеДанные.СтатьяДвиженияДенежныхСредств);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбранныеСтатьиПеретаскивание(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, 
		Строка, Поле)
	
	Если ТипЗнч(ПараметрыПеретаскивания.Значение) 
		= Тип("СправочникСсылка.СтатьиДвиженияДенежныхСредств") Тогда
		ПеренестиВправо(ПараметрыПеретаскивания.Значение);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбранныеСтатьиНачалоПеретаскивания(Элемент, ПараметрыПеретаскивания, Выполнение)
	
	Если ТолькоПросмотр Тогда
		Выполнение = Ложь;
	Иначе
		ТекущиеДанные = Элементы.ВыбранныеСтатьи.ТекущиеДанные;
		ПараметрыПеретаскивания.Значение = ТекущиеДанные.СтатьяДвиженияДенежныхСредств;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбранныеСтатьиПроверкаПеретаскивания(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка,
	Строка, Поле)
	
	СтандартнаяОбработка = Ложь;
	Если ТипЗнч(ПараметрыПеретаскивания.Значение) 
		= Тип("СправочникСсылка.СтатьиДвиженияДенежныхСредств") Тогда
		ПараметрыПеретаскивания.ДопустимыеДействия = ДопустимыеДействияПеретаскивания.Перемещение;
	Иначе
		ПараметрыПеретаскивания.ДопустимыеДействия = ДопустимыеДействияПеретаскивания.НеОбрабатывать;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбранныеСтатьиОкончаниеПеретаскивания(Элемент, ПараметрыПеретаскивания, 
	СтандартнаяОбработка)
	
	Если ТипЗнч(ПараметрыПеретаскивания.Значение) 
		= Тип("СправочникСсылка.СтатьиДвиженияДенежныхСредств") Тогда
		ПеренестиВлево(ПараметрыПеретаскивания.Значение);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СуммаНДСПриИзменении(Элемент)
	
	ОбновитьПодвал();
	
КонецПроцедуры

&НаКлиенте
Процедура СуммаПриИзменении(Элемент)
	
	ОбновитьПодвал();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Добавляет статью в список выбранных, обновляя общий список.
//
// Параметры:
//   Статья - СправочникСсылка.СтатьиДвиженияДенежныхСредств - добавляемая статья.
//
&НаКлиенте
Процедура ДобавитьСтатьюКВыбранным(Статья)
	
	ИтогСумма = 0; ИтогСуммаНДС = 0;
	Для каждого Строка из ВыбранныеСтатьи Цикл
		Если Строка.СтатьяДвиженияДенежныхСредств = Статья Тогда
			Возврат;
		КонецЕсли;
		ИтогСумма = ИтогСумма + Строка.Сумма;
		ИтогСуммаНДС = ИтогСуммаНДС + Строка.СуммаНДС;
	КонецЦикла;
	
	Строка = ВыбранныеСтатьи.Добавить();
	Строка.СтатьяДвиженияДенежныхСредств = Статья;
	Если ИтогСумма < Сумма Тогда
		Строка.Сумма = Сумма - ИтогСумма;
		Если ИтогСуммаНДС < СуммаНДС Тогда
			Строка.СуммаНДС = СуммаНДС - ИтогСуммаНДС;
		КонецЕсли;
	КонецЕсли;
	
	ОбновитьПодвал();
	ОбновитьОтборСписка();
	
КонецПроцедуры

// Удаляет статью из списка выбранных, обновляя общий список.
//
// Параметры:
//   Статья - СправочникСсылка.СтатьиДвиженияДенежныхСредств - удаляемая статья.
//
&НаКлиенте
Процедура УдалитьСтатьюИзВыбранных(Статья)
	
	Для каждого Строка из ВыбранныеСтатьи Цикл
		Если Строка.СтатьяДвиженияДенежныхСредств = Статья Тогда
			ВыбранныеСтатьи.Удалить(Строка);
			ОбновитьПодвал();
			ОбновитьОтборСписка();
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

// Обновляет отбор динамического списка выбранными статьями.
//
&НаКлиенте
Процедура ОбновитьОтборСписка()
	
	#Если Не Вебклиент Тогда
		
	МассивСтатей = Новый Массив;
	Для каждого СтрокаВыбранных из ВыбранныеСтатьи Цикл
		МассивСтатей.Добавить(СтрокаВыбранных.СтатьяДвиженияДенежныхСредств);
	КонецЦикла;
	
	ОтборСписка = СтатьиДвиженияДенежныхСредств.Отбор.Элементы;
	ОтборСписка[0].ПравоеЗначение = МассивСтатей; // единственный отбор
	
	#КонецЕсли
	
КонецПроцедуры

// Обновляет итоги в подвале.
//
&НаКлиенте
Процедура ОбновитьПодвал()
	
	ИтогСумма = 0; ИтогСуммаНДС = 0;
	Для Каждого Строка из ВыбранныеСтатьи Цикл
		ИтогСумма = ИтогСумма + Строка.Сумма;
		ИтогСуммаНДС = ИтогСуммаНДС + Строка.СуммаНДС;
	КонецЦикла;
	Элементы.Сумма.ТекстПодвала = Формат(ИтогСумма, "ЧДЦ=2; ЧГ=3,0");
	Элементы.СуммаНДС.ТекстПодвала = Формат(ИтогСуммаНДС, "ЧДЦ=2; ЧГ=3,0");
	
КонецПроцедуры

#КонецОбласти
