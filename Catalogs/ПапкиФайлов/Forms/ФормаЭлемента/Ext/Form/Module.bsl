
////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ПротоколированиеРаботыПользователей.ЗаписатьОткрытие(Объект.Ссылка);
	
	Если Параметры.Свойство("Родитель") Тогда
		Объект.Родитель = Параметры.Родитель;
	КонецЕсли;
	
	РабочийКаталог = РаботаСФайламиВызовСервера.РабочийКаталогПапки(Объект.Ссылка);
	
	// Свойства
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("Объект", Объект);
	УправлениеСвойствами.ПриСозданииНаСервере(ЭтаФорма, ДополнительныеПараметры);
	// Конец Свойства
	
	УчитыватьПодпапки = Ложь;
	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ОбновитьПолныйПуть();
	КонецЕсли;	
	
	// Установка видимости команды "Права"
	Если Не УправлениеДоступом.ОграничиватьДоступНаУровнеЗаписей() Тогда
		Элементы.ФормаНастройкаПрав.Видимость = Ложь;
	КонецЕсли;	
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		Элементы.ПодсчитатьСУчетомПодпапок.Доступность = Ложь;
		Элементы.ОткрытьСписокПапокИФайлов.Доступность = Ложь;	
		Объект.ДатаСоздания = ТекущаяДатаСеанса();
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьПолныйПуть()
	
	ПапкаРодитель = Объект.Родитель;
	
	ПолныйПуть = "";
	Пока ЗначениеЗаполнено(ПапкаРодитель) Цикл
		
		ПолныйПуть = Строка(ПапкаРодитель) + "\" + ПолныйПуть;
		ПапкаРодитель = ОбщегоНазначенияДокументооборотВызовСервера.ЗначениеРеквизитаОбъекта(ПапкаРодитель, "Родитель");
		Если Не ЗначениеЗаполнено(ПапкаРодитель) Тогда
			Прервать;
		КонецЕсли;	
		
	КонецЦикла;
	
	ПолныйПуть = ПолныйПуть + Строка(Объект.Ссылка);
	
	Если Не ПустаяСтрока(ПолныйПуть) Тогда
		ПолныйПуть = """" + ПолныйПуть + """";
	КонецЕсли;
	
КонецПроцедуры	

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	// СтандартныеПодсистемы.Свойства
	Если УправлениеСвойствамиКлиент.ОбрабатыватьОповещения(ЭтотОбъект, ИмяСобытия, Параметр) Тогда
		ОбновитьЭлементыДополнительныхРеквизитов();
		УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	ПараметрыЗаписи.Вставить("ЭтоНовыйОбъект", НЕ ЗначениеЗаполнено(ТекущийОбъект.Ссылка));
	
	// СтандартныеПодсистемы.Свойства
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.Свойства") Тогда
		МодульУправлениеСвойствами = ОбщегоНазначения.ОбщийМодуль("УправлениеСвойствами");
		МодульУправлениеСвойствами.ПередЗаписьюНаСервере(ЭтотОбъект, ТекущийОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	// СтандартныеПодсистемы.Свойства
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.Свойства") Тогда
		МодульУправлениеСвойствами = ОбщегоНазначения.ОбщийМодуль("УправлениеСвойствами");
		МодульУправлениеСвойствами.ОбработкаПроверкиЗаполнения(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.Свойства

КонецПроцедуры

&НаКлиенте
Процедура Подписаться(Команда)
	
	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ПараметрыФормы = Новый Структура("ОбъектПодписки", Объект.Ссылка);
		ОткрытьФорму("ОбщаяФорма.ПодпискаНаУведомленияПоОбъекту", ПараметрыФормы);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	Элементы.ПодсчитатьСУчетомПодпапок.Доступность = Истина;
	Элементы.ОткрытьСписокПапокИФайлов.Доступность = Истина;

КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ ПОДСИСТЕМЫ СВОЙСТВ

&НаКлиенте
Процедура Подключаемый_РедактироватьСоставСвойств()
	
	Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("СтандартныеПодсистемы.Свойства") Тогда
		МодульУправлениеСвойствамиКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("УправлениеСвойствамиКлиент");
		МодульУправлениеСвойствамиКлиент.РедактироватьСоставСвойств(ЭтотОбъект, Объект.Ссылка);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьЭлементыДополнительныхРеквизитов()
	
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.Свойства") Тогда
		МодульУправлениеСвойствами = ОбщегоНазначения.ОбщийМодуль("УправлениеСвойствами");
		МодульУправлениеСвойствами.ОбновитьЭлементыДополнительныхРеквизитов(ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьЗависимостиДополнительныхРеквизитов()
      УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры
 
&НаКлиенте
Процедура Подключаемый_ПриИзмененииДополнительногоРеквизита(Элемент)
      УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура РабочийКаталогВладельцаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если Объект.Ссылка.Пустая() Тогда
		Если Записать() = Ложь Тогда
			Возврат;
		КонецЕсли;	
	КонецЕсли;	
	
	ОчиститьСообщения();
	
	Каталог = "";
	Режим = РежимДиалогаВыбораФайла.ВыборКаталога;
	
	ДиалогОткрытияФайла = Новый ДиалогВыбораФайла(Режим);
	ДиалогОткрытияФайла.Каталог = РабочийКаталог;
	ДиалогОткрытияФайла.ПолноеИмяФайла = "";
	Фильтр = НСтр("ru = 'Все файлы(*.*)|*.*'; en = 'All files (*. *) | *. *'");
	ДиалогОткрытияФайла.Фильтр = Фильтр;
	ДиалогОткрытияФайла.МножественныйВыбор = Ложь;
	ДиалогОткрытияФайла.Заголовок = НСтр("ru = 'Выберите каталог'; en = 'Select a directory'");
	Если ДиалогОткрытияФайла.Выбрать() Тогда
		
		ИмяКаталога = ДиалогОткрытияФайла.Каталог;
		ИмяКаталога = ОбщегоНазначенияКлиентСервер.ДобавитьКонечныйРазделительПути(ИмяКаталога, 
			ОбщегоНазначенияКлиентПовтИсп.ТипПлатформыКлиента());
		
		// Создать каталог для файлов
		Попытка
			СоздатьКаталог(ИмяКаталога);
			ИмяКаталогаТестовое = ИмяКаталога + "ПроверкаДоступа\";
			СоздатьКаталог(ИмяКаталогаТестовое);
			УдалитьФайлы(ИмяКаталогаТестовое);
		Исключение
			// нет прав на создание каталога, или такой путь отсутствует
			
			ТекстОшибки 
				= СтрШаблон(НСтр("ru = 'Неверный путь или отсутствуют права на запись в каталог ""%1""'; en = 'Invalid path or you do not have write access to the directory ""%1""'"),
				ИмяКаталога);
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки, , "РабочийКаталог");
			Возврат;
		КонецПопытки;
		
		РабочийКаталог = ИмяКаталога;
		РаботаСФайламиВызовСервера.СохранитьРабочийКаталог(Объект.Ссылка, РабочийКаталог);
		
	КонецЕсли;
	
	ОбновитьОтображениеДанных();
	
КонецПроцедуры

&НаКлиенте
Процедура РабочийКаталогВладельцаОчистка(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	РодительСсылка = ОбщегоНазначенияДокументооборотВызовСервера.ЗначениеРеквизитаОбъекта(Объект.Ссылка, "Родитель");
	РабочийКаталогРодителя = РаботаСФайламиВызовСервера.РабочийКаталогПапки(РодительСсылка);	
	
	РабочийКаталогЭтойПапки = РаботаСФайламиВызовСервера.РабочийКаталогПапки(Объект.Ссылка);
	РабочийКаталогУнаследованный = РабочийКаталогРодителя + Объект.Наименование 
		+ ФайловыеФункцииКлиентСервер.ПолучитьСлеш(ОбщегоНазначенияКлиентПовтИсп.ТипПлатформыКлиента());
	
	Если ПустаяСтрока(РабочийКаталогРодителя) Тогда
		РабочийКаталог = "";
		РаботаСФайламиВызовСервера.ОчиститьРабочийКаталог(Объект.Ссылка);
	ИначеЕсли РабочийКаталогУнаследованный <> РабочийКаталогЭтойПапки Тогда
		РабочийКаталог = РабочийКаталогУнаследованный;
		РаботаСФайламиВызовСервера.СохранитьРабочийКаталог(Объект.Ссылка, РабочийКаталог);
	КонецЕсли;
	
	ОбновитьОтображениеДанных();
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	РабочийКаталог = РаботаСФайламиВызовСервера.РабочийКаталогПапки(Объект.Ссылка);
	ОбновитьПолныйПуть();
	
	ПротоколированиеРаботыПользователей.ЗаписатьСоздание(Объект.Ссылка, ПараметрыЗаписи.ЭтоНовыйОбъект);
	ПротоколированиеРаботыПользователей.ЗаписатьИзменение(Объект.Ссылка);
	
КонецПроцедуры


&НаКлиенте
Процедура РодительПриИзменении(Элемент)
	
	ОбновитьПолныйПуть();
	
КонецПроцедуры


&НаКлиенте
Процедура ОткрытьСписокПапокИФайлов(Команда)
	
	ПараметрыФормы = Новый Структура("Папка", Объект.Ссылка);
	
	ОткрытьФорму("Справочник.Файлы.Форма.ХранилищеФайлов", ПараметрыФормы, ,Объект.Ссылка);
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	ЗаполнитьИнформациюОСодержимом();
	
	// СтандартныеПодсистемы.Свойства
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.Свойства") Тогда
		МодульУправлениеСвойствами = ОбщегоНазначения.ОбщийМодуль("УправлениеСвойствами");
		МодульУправлениеСвойствами.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьИнформациюОСодержимом()
	
	ЧислоВерсий = 0;
	ЧислоФайлов = 0;
	ЧислоПапок = 0;
	РазмерВерсий = 0;
	РазмерФайлов = 0;
	
	Запрос = Новый Запрос;
	
	Если УчитыватьПодпапки Тогда
		
		Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
		               |	ПапкиФайлов.Ссылка КАК Ссылка
		               |ПОМЕСТИТЬ ВременнаяТаблицаПапок
		               |ИЗ
		               |	Справочник.ПапкиФайлов КАК ПапкиФайлов
		               |ГДЕ
		               |	ПапкиФайлов.Ссылка В ИЕРАРХИИ(&ПапкаКорневая)
		               |
		               |ИНДЕКСИРОВАТЬ ПО
		               |	Ссылка
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ РАЗРЕШЕННЫЕ
		               |	КОЛИЧЕСТВО(ВременнаяТаблицаПапок.Ссылка) КАК ЧислоПапок
		               |ИЗ
		               |	ВременнаяТаблицаПапок КАК ВременнаяТаблицаПапок
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ РАЗРЕШЕННЫЕ
		               |	КОЛИЧЕСТВО(Файлы.Ссылка) КАК ЧислоФайлов,
		               |	ЕСТЬNULL(СУММА(Файлы.ТекущаяВерсияРазмер), 0) КАК РазмерФайлов
		               |ИЗ
		               |	Справочник.Файлы КАК Файлы
		               |ГДЕ
		               |	Файлы.ВладелецФайла В
		               |			(ВЫБРАТЬ
		               |				ВременнаяТаблицаПапок.Ссылка
		               |			ИЗ
		               |				ВременнаяТаблицаПапок)
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ РАЗРЕШЕННЫЕ
		               |	КОЛИЧЕСТВО(ВерсииФайлов.Ссылка) КАК ЧислоВерсий,
		               |	ЕСТЬNULL(СУММА(ВерсииФайлов.Размер), 0) КАК РазмерВерсий
		               |ИЗ
		               |	Справочник.Файлы КАК Файлы
		               |		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВерсииФайлов КАК ВерсииФайлов
		               |		ПО (ВерсииФайлов.Владелец = Файлы.Ссылка)
		               |ГДЕ
		               |	Файлы.ВладелецФайла В
		               |			(ВЫБРАТЬ
		               |				ВременнаяТаблицаПапок.Ссылка
		               |			ИЗ
		               |				ВременнаяТаблицаПапок)
		               |	И ВерсииФайлов.ФайлУдален = ЛОЖЬ";
		
	Иначе // без подпапок
		
		Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
		               |	&ПапкаКорневая КАК Ссылка
		               |ПОМЕСТИТЬ ВременнаяТаблицаПапок
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ РАЗРЕШЕННЫЕ
		               |	КОЛИЧЕСТВО(ПапкиФайлов.Ссылка) КАК ЧислоПапок
		               |ИЗ
		               |	Справочник.ПапкиФайлов КАК ПапкиФайлов
		               |ГДЕ
		               |	ПапкиФайлов.Родитель = &ПапкаКорневая
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ РАЗРЕШЕННЫЕ
		               |	КОЛИЧЕСТВО(Файлы.Ссылка) КАК ЧислоФайлов,
		               |	ЕСТЬNULL(СУММА(Файлы.ТекущаяВерсияРазмер), 0) КАК РазмерФайлов
		               |ИЗ
		               |	Справочник.Файлы КАК Файлы
		               |ГДЕ
		               |	Файлы.ВладелецФайла В
		               |			(ВЫБРАТЬ
		               |				ВременнаяТаблицаПапок.Ссылка
		               |			ИЗ
		               |				ВременнаяТаблицаПапок)
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ РАЗРЕШЕННЫЕ
		               |	КОЛИЧЕСТВО(ВерсииФайлов.Ссылка) КАК ЧислоВерсий,
		               |	ЕСТЬNULL(СУММА(ВерсииФайлов.Размер), 0) КАК РазмерВерсий
		               |ИЗ
		               |	Справочник.Файлы КАК Файлы
		               |		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВерсииФайлов КАК ВерсииФайлов
		               |		ПО (ВерсииФайлов.Владелец = Файлы.Ссылка)
		               |ГДЕ
		               |	Файлы.ВладелецФайла В
		               |			(ВЫБРАТЬ
		               |				ВременнаяТаблицаПапок.Ссылка
		               |			ИЗ
		               |				ВременнаяТаблицаПапок)
		               |	И ВерсииФайлов.ФайлУдален = ЛОЖЬ";

	КонецЕсли;
				   
	Запрос.УстановитьПараметр("ПапкаКорневая", Объект.Ссылка);
	МассивРезультатов = Запрос.ВыполнитьПакет();
	
	Выборка = МассивРезультатов[1].Выгрузить();
	ЧислоПапок = Выборка[0].ЧислоПапок;
	Если УчитыватьПодпапки Тогда
		ЧислоПапок = ЧислоПапок - 1;
	КонецЕсли;
	
	Выборка = МассивРезультатов[2].Выгрузить();
	ЧислоФайлов  = Выборка[0].ЧислоФайлов;
	РазмерФайлов = Выборка[0].РазмерФайлов;
	
	Выборка = МассивРезультатов[3].Выгрузить();
	ЧислоВерсий  = Выборка[0].ЧислоВерсий;
	РазмерВерсий = Выборка[0].РазмерВерсий;

	КоличествоВерсий = ?(ЧислоВерсий > 0, СтрШаблон(НСтр("ru='%1 (%2)'; en = '%1 (%2)'"), 
		ЧислоВерсий, ФайловыеФункцииКлиентСервер.ПолучитьСтрокуСРазмеромФайлаДляКарточкиПапки(РазмерВерсий)), "-");
	КоличествоПодпапок = ?(ЧислоПапок > 0, Строка(ЧислоПапок), "-");
	КоличествоФайлов = ?(ЧислоФайлов > 0, СтрШаблон(НСтр("ru='%1 (%2)'; en = '%1 (%2)'"), 
		ЧислоФайлов, ФайловыеФункцииКлиентСервер.ПолучитьСтрокуСРазмеромФайлаДляКарточкиПапки(РазмерФайлов)), "-");

КонецПроцедуры	

&НаКлиенте
Процедура ПодсчитатьСУчетомПодпапок(Команда)
	
	ОписаниеОповещения = Новый ОписаниеОповещения(
		"ПодсчитатьСУчетомПодпапокПродолжение",
		ЭтотОбъект);
	ПоказатьВопрос(
		ОписаниеОповещения,
		НСтр("ru = 'Сейчас будет выполнен подсчет содержимого папки с учетом подпапок.
			|Эта операция может занять продолжительное время. Продолжить?'; en = 'Calculation of folder (with subfolders) content is about to start. This may take several minutes. Do you want to continue?'"),
		РежимДиалогаВопрос.ДаНет,,
		КодВозвратаДиалога.Да);
	
КонецПроцедуры

&НаКлиенте
Процедура ПодсчитатьСУчетомПодпапокПродолжение(Результат, Параметры) Экспорт
	
	Если Результат <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;	
	
	Состояние(НСтр("ru='Выполняется подсчет содержимого папки с учетом подпапок. Пожалуйста, подождите...'; en = 'Calculating content of folder with subfolders. Please wait ...'"));
	
	УчитыватьПодпапки = Истина;
	ЗаполнитьИнформациюОСодержимом();
	Элементы.ГруппаИнформация.Заголовок = НСтр("ru='Папка содержит (с учетом подпапок)'; en = 'Folder contains (taking into account subfolders)'");
	Элементы.ПодсчитатьСУчетомПодпапок.Доступность = Ложь;
	
	Состояние();	
	
КонецПроцедуры

&НаКлиенте
Процедура НастройкаПрав(Команда)
	
	ИмяОсновногоРеквизита = "Объект";
	ИмяПодчиненнойФормы = "ОбщаяФорма.НастройкиПравПапок";
	
	ТекстВопроса = НСтр("ru = 'Данные еще не записаны.
		|Выполнение команды ""Настройка прав"" возможно только после записи данных.
		|Данные будут записаны.';
		|en = 'The data have not yet been saved. 
		|Performing the command ""Permissions setting"" is possible only after the data is saved. 
		|Data will be saved.'");
	
	ДокументооборотПраваДоступаКлиент.ЗаписатьОбъектЕслиНовыйИОткрытьПодчиненнуюФорму(
		ЭтаФорма, ИмяОсновногоРеквизита, ТекстВопроса, ИмяПодчиненнойФормы);
	
КонецПроцедуры

&НаКлиенте
Процедура ПраваДоступа(Команда)
	
	ДокументооборотПраваДоступаКлиент.ОткрытьФормуПравДоступа(ЭтаФорма);
	
КонецПроцедуры


