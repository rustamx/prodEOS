////////////////////////////////////////////////////////////////////////////////
// МОДУЛЬ ДЛЯ РАБОТЫ С РЕЗОЛЮЦИЯМИ (КЛИЕНТ)
//  
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

Процедура ДобавитьРезолюцию(ЭтаФорма) Экспорт
	
	ДокументСсылка = ЭтаФорма.Объект.Ссылка;
	
	Если ДокументСсылка.Пустая() Тогда 
		Если Не ЭтаФорма.Записать() Тогда 
			Возврат;
		КонецЕсли;
		
		ДокументСсылка = ЭтаФорма.Объект.Ссылка;
		
		ПоказатьОповещениеПользователя(
			"Создание:", 
			ПолучитьНавигационнуюСсылку(ДокументСсылка),
			Строка(ДокументСсылка),
			БиблиотекаКартинок.Информация32);
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Документ", ДокументСсылка);
	ОткрытьФорму("Справочник.Резолюции.Форма.ФормаЭлемента", ПараметрыФормы, ЭтаФорма);
	
КонецПроцедуры

Процедура ИзменитьРезолюцию(ЭтаФорма) Экспорт
	
	Резолюция = Неопределено;
	
	Если ЭтаФорма.Резолюции.Количество() = 1 Тогда
		Резолюция = ЭтаФорма.Резолюции[0].Ссылка;
	ИначеЕсли ЭтаФорма.Резолюции.Количество() > 1 Тогда
		
		ПараметрыОбработчика = Новый Структура;
		ПараметрыОбработчика.Вставить("ЭтаФорма", ЭтаФорма);
		ОписаниеОповещения = Новый ОписаниеОповещения(
			"ИзменитьРезолюциюПослеВыбора",
			ЭтотОбъект,
			ПараметрыОбработчика);
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("Документ", ЭтаФорма.Объект.Ссылка);
		ПараметрыФормы.Вставить("ПоказыватьУдаленныеРезолюции", ЭтаФорма.ПоказыватьУдаленныеРезолюции);
		ПараметрыФормы.Вставить("ЗаголовокФормы", НСтр("ru = 'Выберите резолюцию для открытия'; en = 'Select resolution to open'"));
		ОткрытьФорму(
			"Справочник.Резолюции.ФормаВыбора",
			ПараметрыФормы,
			ЭтаФорма,,,,
			ОписаниеОповещения,
			РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		
		Возврат;
		
	КонецЕсли;
	
	Параметры = Новый Структура;
	Параметры.Вставить("ЭтаФорма", ЭтаФорма);
	ИзменитьРезолюциюПослеВыбора(Резолюция, Параметры);
	
КонецПроцедуры

Процедура ИзменитьРезолюциюПослеВыбора(ВыбраннаяРезолюция, Параметры) Экспорт
	
	Если Не ЗначениеЗаполнено(ВыбраннаяРезолюция) Тогда 
		Возврат;
	КонецЕсли;
	
	ЭтаФорма = Параметры.ЭтаФорма;
	
	РеквизитыРезолюции = ОбщегоНазначенияДокументооборотВызовСервера.ЗначенияРеквизитовОбъекта(ВыбраннаяРезолюция,
		"Документ, Источник");
	Документ = РеквизитыРезолюции.Документ;
	Источник = РеквизитыРезолюции.Источник;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Ключ", ВыбраннаяРезолюция);
	ПараметрыФормы.Вставить("ОбъектСсылка", Документ);
	
	Если Не РаботаСРезолюциями.ЭтоАвторРезолюции(ВыбраннаяРезолюция) Тогда
		ПараметрыФормы.Вставить("ТекстПредупреждения",
			НСтр("ru = 'Редактировать резолюцию может только автор или пользователь, который ее внес.'; en = 'Only the author and the user who entered it can edit the resloution.'"));
		ПараметрыФормы.Вставить("ТолькоПросмотр", Истина);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Источник) Тогда
		ПараметрыФормы.Вставить("ТекстПредупреждения",
			НСтр("ru = 'Нельзя редактировать резолюцию, добавленную при выполнении процесса ""Рассмотрение"".'; en = 'It is not possible to edit resoultions that were added in course of execution of process ""Reviewal"".'"));
		ПараметрыФормы.Вставить("ТолькоПросмотр", Истина);
	КонецЕсли;
	
	ОткрытьФорму("Справочник.Резолюции.Форма.ФормаЭлемента", ПараметрыФормы, ЭтаФорма);
	
КонецПроцедуры

Процедура УдалитьРезолюцию(ЭтаФорма, ОписаниеОповещенияВозврата) Экспорт
	
	Резолюция = Неопределено;
	
	Если ЭтаФорма.Резолюции.Количество() = 1 Тогда
		Резолюция = ЭтаФорма.Резолюции[0].Ссылка;
	ИначеЕсли ЭтаФорма.Резолюции.Количество() > 1 Тогда
		
		ПараметрыОбработчика = Новый Структура;
		ПараметрыОбработчика.Вставить("ЭтаФорма", ЭтаФорма);
		ПараметрыОбработчика.Вставить("ОписаниеОповещенияВозврата", ОписаниеОповещенияВозврата);
		ОписаниеОповещения = Новый ОписаниеОповещения(
			"УдалитьРезолюцияПослеВыбора",
			ЭтотОбъект,
			ПараметрыОбработчика);
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("Документ", ЭтаФорма.Объект.Ссылка);
		ПараметрыФормы.Вставить("ПоказыватьУдаленныеРезолюции", ЭтаФорма.ПоказыватьУдаленныеРезолюции);
		ПараметрыФормы.Вставить("ЗаголовокФормы", НСтр("ru = 'Выберите резолюцию для удаления'; en = 'Select resolution to delete'"));
		ОткрытьФорму(
			"Справочник.Резолюции.ФормаВыбора",
			ПараметрыФормы,
			ЭтаФорма,,,,
			ОписаниеОповещения,
			РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		
		Возврат;
		
	КонецЕсли;
	
	Параметры = Новый Структура;
	Параметры.Вставить("ЭтаФорма", ЭтаФорма);
	Параметры.Вставить("ОписаниеОповещенияВозврата", ОписаниеОповещенияВозврата);
	УдалитьРезолюцияПослеВыбора(Резолюция, Параметры);
	
КонецПроцедуры

Процедура УдалитьРезолюцияПослеВыбора(ВыбраннаяРезолюция, Параметры) Экспорт
	
	Если Не ЗначениеЗаполнено(ВыбраннаяРезолюция) Тогда 
		Возврат;
	КонецЕсли;
	
	ЭтаФорма = Параметры.ЭтаФорма;
	ОписаниеОповещения = Параметры.ОписаниеОповещенияВозврата;
	
	РеквизитыРезолюции = ОбщегоНазначенияДокументооборотВызовСервера.ЗначенияРеквизитовОбъекта(ВыбраннаяРезолюция,
		"Документ, Источник, ПометкаУдаления");
	Документ = РеквизитыРезолюции.Документ;
	Источник = РеквизитыРезолюции.Источник;
	ТекущаяПометкаУдаления = РеквизитыРезолюции.ПометкаУдаления;
	
	Если Не РаботаСРезолюциями.ЭтоАвторРезолюции(ВыбраннаяРезолюция) Тогда
		ТекстПредупреждения = НСтр("ru = 'Удалить резолюцию может только автор или пользователь, который ее внес.'; en = 'Only the author and the user who entered it can delete the resloution.'");
		ПоказатьПредупреждение(Неопределено, ТекстПредупреждения);
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Источник) Тогда 
		ТекстПредупреждения = НСтр("ru = 'Нельзя удалить резолюцию, добавленную при выполнении процесса ""Рассмотрение"".'; en = 'It is not possible to delete resoultions that were added in course of execution of process ""Reviewal"".'");
		ПоказатьПредупреждение(Неопределено, ТекстПредупреждения);
		Возврат;
	КонецЕсли;
	
	Если ТекущаяПометкаУдаления Тогда
		ТекстВопроса = НСтр("ru = 'Снять с резолюции пометку на удаление?'; en = 'Remove deletion mark from the resolution?'");
	Иначе
		ТекстВопроса = НСтр("ru = 'Пометить резолюцию на удаление?'; en = 'Mark the resolution for deletion?'");
	КонецЕсли;
	
	ПараметрыОбработчика = Новый Структура;
	ПараметрыОбработчика.Вставить("ОписаниеОповещения", ОписаниеОповещения);
	ПараметрыОбработчика.Вставить("Резолюция", ВыбраннаяРезолюция);
	ПараметрыОбработчика.Вставить("ТекущаяПометкаУдаления", ТекущаяПометкаУдаления);
	ПараметрыОбработчика.Вставить("Документ", Документ);
	ОписаниеОповещенияОтветаНаВопрос = Новый ОписаниеОповещения(
		"УдалитьРезолюциюПродолжение",
		ЭтотОбъект,
		ПараметрыОбработчика);
	ПоказатьВопрос(ОписаниеОповещенияОтветаНаВопрос, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
	
КонецПроцедуры

Процедура УдалитьРезолюциюПродолжение(Результат, Параметры) Экспорт
	
	Если Результат <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	ПометкаИзменена = РаботаСРезолюциями.УстановитьПометкуНаУдаление(
		Параметры.Резолюция,
		НЕ Параметры.ТекущаяПометкаУдаления);
	
	Если ПометкаИзменена Тогда
		Если Параметры.ТекущаяПометкаУдаления Тогда
			Состояние(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Пометка на удаление с резолюции к документу ""%1"" успешно снята.'; en = 'Deletiom mark for resolution of the document ""%1"" removed successfully.'"),
				Параметры.Документ));
		Иначе
			Состояние(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Пометка на удаление успешно установлена для резолюции к документу ""%1"".'; en = 'Deletiom mark for resolution of the document ""%1"" set successfully.'"),
				Параметры.Документ));
		КонецЕсли;
		ВыполнитьОбработкуОповещения(Параметры.ОписаниеОповещения);	
	Иначе
		Если Параметры.ТекущаяПометкаУдаления Тогда
			ТекстПредупреждения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Не удалось снять пометку на удаление с резолюции к документу ""%1"".'; en = 'Could not remove the deletion mark for a resolution of document ""%1"".'"),
				Параметры.Документ);
		Иначе
			ТекстПредупреждения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Не удалось установить пометку на удаление для резолюции к документу ""%1"".'; en = 'Could not set the deletion mark for a resolution of document ""%1"".'"),
				Параметры.Документ);
		КонецЕсли;
		ПоказатьПредупреждение(Неопределено, ТекстПредупреждения);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриНажатииСтатусПроверкиЭП(ЭтаФорма) Экспорт
	
	Резолюция = ПолучитьТекущуюРезолюцию(ЭтаФорма);
	РаботаСЭПКлиент.ОткрытьПодпись(Резолюция, ЭтаФорма.УникальныйИдентификатор);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПолучитьТекущуюРезолюцию(ЭтаФорма)
	
	Если ЭтаФорма.Резолюции.Количество() = 0 Тогда
		Возврат Неопределено;
	ИначеЕсли ЭтаФорма.Резолюции.Количество() = 1 Тогда
		Возврат ЭтаФорма.Резолюции[0];
	Иначе
		Возврат ЭтаФорма.Элементы.Резолюции.ТекущиеДанные;
	КонецЕсли;
	
КонецФункции

#КонецОбласти
