
// Добавляет переданный документ в регистр "Мои документы".
//
// Параметры:
//   МассивДокументов - массив документов - содержит ссылки на добавляемые документы.
//
Процедура ДобавитьВМоиДокументы(МассивДокументов) Экспорт 
	
	КоличествоДокументов = МассивДокументов.Количество();
	Если КоличествоДокументов = 0 Тогда 
		Возврат;
	КонецЕсли;
	
	ТекстСообщения = "";
	
	Для Каждого Документ Из МассивДокументов Цикл 
		Если Не ДелопроизводствоКлиентСервер.ЭтоДокумент(Документ) Тогда 
			Продолжить;
		КонецЕсли;
		
		МоиДокументы.ДобавитьЗаписьВМоиДокументы(Документ, 
			ПредопределенноеЗначение("Перечисление.ПричиныДобавленияВМоиДокументы.ДобавленВручную"),
			ПользователиКлиентСервер.ТекущийПользователь());
		
	КонецЦикла;
	
	Если КоличествоДокументов = 1 Тогда
		ТекстСообщения = СтрШаблон(НСтр("ru = 'Документ ""%1"" добавлен в список ""Мои документы"".'; en = 'Document ""%1"" added to the list ""My documents"".'"),
			Строка(МассивДокументов[0]));
		НавигационнаяСсылка = ПолучитьНавигационнуюСсылку(МассивДокументов[0]);
	Иначе 
		ТекстСообщения = СтрШаблон(НСтр("ru = 'Документы (%1) добавлены в список ""Мои документы"".'; en = 'Documents (%1) added to the list ""My documents"".'"),
			Строка(КоличествоДокументов));
		НавигационнаяСсылка = "";
	КонецЕсли;
	
	Оповестить("ОбновитьСписокМоиДокументы",,);
	ПоказатьОповещениеПользователя(
		НСтр("ru = 'Список ""Мои документы"" обновлен'; en = '""My documents"" list updated'"),
		НавигационнаяСсылка,
		ТекстСообщения,
		БиблиотекаКартинок.Информация32);
	
КонецПроцедуры


