// проверяет применимость условия маршрутизации к объекту
//
//Параметры:
//	Предмет - ссылка на документ
//	СсылкаНаУсловие - ссылка на условие маршрутизации
//Возвращает:
//	Истина, если условие применимо
//	Ложь, если условие неприменимо или указаны пустые ссылки
Функция ПроверитьПрименимостьУсловияМаршрутизацииКОбъекту(Знач Предмет, СсылкаНаУсловие) Экспорт
	
	Результат = Ложь;
	
	//Условия нет - возвращается Истина
	Если НЕ ЗначениеЗаполнено(СсылкаНаУсловие) Тогда
		Возврат Истина;
	КонецЕсли;
	
	ИндексЗначенияПеречисления = Перечисления.ТипыОбъектов.Индекс(СсылкаНаУсловие.ТипОбъекта);
	ИмяЗначенияПеречисления = Метаданные.Перечисления.ТипыОбъектов.ЗначенияПеречисления[ИндексЗначенияПеречисления].Имя;
	
	//Если Предмет не указан, в условия маршрутизации передается пустая ссылка того типа, который записан в условии маршрутизации 
	Если НЕ ЗначениеЗаполнено(Предмет) Тогда
		Предмет = Справочники[ИмяЗначенияПеречисления].ПустаяСсылка();
	КонецЕсли;
	
	
	//Если Предмет имеет другой тип по сравнению с тем, который указан в условии
	Если ИмяЗначенияПеречисления <> Предмет.Метаданные().Имя
		// ТСК Корнюшенков А.Ю. Искать текст "МаршрутыСогласованияЕОСК" 01.08.2018 {
		// добавлено условие на документы качества
		И НЕ ра_ОбщегоНазначенияКлиентСервер.ЭтоДокументКачества(Предмет.Метаданные())
		// ТСК Корнюшенков А.Ю. Искать текст "МаршрутыСогласованияЕОСК" 01.08.2018 } 
		Тогда
		Возврат Ложь;
	КонецЕсли;
		
	СпособЗаданияУсловия = СсылкаНаУсловие.СпособЗаданияУсловия;
	Если СпособЗаданияУсловия = Перечисления.СпособыЗаданияУсловия.НаВстроенномЯзыке Тогда
		
		Результат = ПроверитьУсловиеНаВстроенномЯзыке(Предмет, СсылкаНаУсловие);
		
	ИначеЕсли СпособЗаданияУсловия = Перечисления.СпособыЗаданияУсловия.ВРежимеКонструктора Тогда
				
		Результат = ПроверитьУсловиеВРежимеКонструктора(Предмет, 
			СсылкаНаУсловие.НастройкаУсловия.Получить(),
			ИмяЗначенияПеречисления);
	
	ИначеЕсли СпособЗаданияУсловия = Перечисления.СпособыЗаданияУсловия.КомбинацияИзДругихУсловий Тогда
		
		Настройки = СсылкаНаУсловие.НастройкаКомбинацииУсловий.Получить();
		ЗаполнитьКомбинациюПравил(Предмет, Настройки.Отбор.Элементы);
		Результат = ПроверитьУсловиеВРежимеКонструктора(Предмет, 
			Настройки,
			"Условия",
			Ложь);
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция ПроверитьУсловиеНаВстроенномЯзыке(Предмет, СсылкаНаУсловие)
	
	Результат = Истина;
	
	Попытка
		УстановитьБезопасныйРежим(Истина);
		Выполнить(СсылкаНаУсловие.ВыражениеУсловия);
		УстановитьБезопасныйРежим(Ложь);
	Исключение
		Инфо = ИнформацияОбОшибке();
		ЗаписьЖурналаРегистрации(
			НСтр("ru = 'Проверка условия маршрутизации'; en = 'Testing routing rule'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка()),
			УровеньЖурналаРегистрации.Ошибка,
			СсылкаНаУсловие.Метаданные(),
			СсылкаНаУсловие,
			ПодробноеПредставлениеОшибки(Инфо));
		ТекстИсключения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'При проверке условия маршрутизации ""%1"" возникла ошибка:
				|%2
				|Обратитесь к администратору.';
				|en = 'Error occured while testing routing rule ""%1"": 
				|%2 
				|Contact the administrator.'"),
			СсылкаНаУсловие.Наименование, 
			Инфо.Описание);
		ВызватьИсключение ТекстИсключения;
	КонецПопытки;
	
	Возврат Результат;
	
КонецФункции

Функция ПроверитьУсловиеВРежимеКонструктора(Предмет, Настройки, ТипМакета, ЕстьПараметры = Истина)
	
	СхемаКомпоновкиДанных = Справочники.УсловияМаршрутизации.ПолучитьМакет(ТипМакета);
	
	Компоновщик = Новый КомпоновщикНастроекКомпоновкиДанных;
	URLСхемы = ПоместитьВоВременноеХранилище(СхемаКомпоновкиДанных, Новый УникальныйИдентификатор());
	ИсточникНастроек = Новый ИсточникДоступныхНастроекКомпоновкиДанных(URLСхемы);
	Компоновщик.Инициализировать(ИсточникНастроек);
	Компоновщик.ЗагрузитьНастройки(Настройки);
	
	Если ЕстьПараметры Тогда
		ПараметрПредмет = Компоновщик.Настройки.ПараметрыДанных.Элементы[0];
		ПараметрПредмет.Значение = Предмет;
		ПараметрПредмет.Использование = Истина;
	КонецЕсли;
	
	//дополнительная обработка групп пользователей - разворачивание их в пользователей
	РазвернутьГруппыВУсловияхОтбора(Компоновщик.Настройки.Отбор.Элементы);
	
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	
	МакетКомпоновки = КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных, 
		Компоновщик.ПолучитьНастройки()
		,
		,
		, Тип("ГенераторМакетаКомпоновкиДанныхДляКоллекцииЗначений"));
    ПроцессорКомпоновки = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновки.Инициализировать(МакетКомпоновки);
    ТаблицаРезультата = Новый ТаблицаЗначений;
    ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВКоллекциюЗначений;
    ПроцессорВывода.УстановитьОбъект(ТаблицаРезультата);
	ПроцессорВывода.Вывести(ПроцессорКомпоновки);
	
	Результат = ТаблицаРезультата.Количество() > 0;
	
	Возврат Результат;
		
КонецФункции

Процедура ЗаполнитьКомбинациюПравил(Предмет, ЭлементыКомбинации)
	
	Для Каждого ЭлементОтбора Из ЭлементыКомбинации Цикл
		Если ТипЗнч(ЭлементОтбора) = Тип("ГруппаЭлементовОтбораКомпоновкиДанных") Тогда
			ЗаполнитьКомбинациюПравил(Предмет, ЭлементОтбора.Элементы);		
		Иначе
			СпособЗаданияУсловия = ЭлементОтбора.ПравоеЗначение.СпособЗаданияУсловия;
			Если СпособЗаданияУсловия = Перечисления.СпособыЗаданияУсловия.НаВстроенномЯзыке Тогда
				Результат = ПроверитьУсловиеНаВстроенномЯзыке(Предмет, ЭлементОтбора.ПравоеЗначение);
			ИначеЕсли СпособЗаданияУсловия = Перечисления.СпособыЗаданияУсловия.ВРежимеКонструктора Тогда
				ИндексЗначенияПеречисления = Перечисления.ТипыОбъектов.Индекс(ЭлементОтбора.ПравоеЗначение.ТипОбъекта);
				ИмяЗначенияПеречисления = Метаданные.Перечисления.ТипыОбъектов.ЗначенияПеречисления[ИндексЗначенияПеречисления].Имя;
				Результат = ПроверитьУсловиеВРежимеКонструктора(Предмет, 
					ЭлементОтбора.ПравоеЗначение.НастройкаУсловия.Получить(),
					ИмяЗначенияПеречисления);
			ИначеЕсли СпособЗаданияУсловия = Перечисления.СпособыЗаданияУсловия.КомбинацияИзДругихУсловий Тогда
				НастройкиВложенногоПравила = ЭлементОтбора.ПравоеЗначение.НастройкаКомбинацииУсловий.Получить();
				ЗаполнитьКомбинациюПравил(Предмет, НастройкиВложенногоПравила.Отбор.Элементы);
				Результат = ПроверитьУсловиеВРежимеКонструктора(Предмет, 
					НастройкиВложенногоПравила,
					"Условия",
					Ложь);
			КонецЕсли;
			
			ПолеОтбора = Новый ПолеКомпоновкиДанных("ПолеДляПроверки");
    		ЭлементОтбора.ЛевоеЗначение = ПолеОтбора;
    		ЭлементОтбора.Использование = Истина;
    		ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
			ЭлементОтбора.ПравоеЗначение = Результат;
		КонецЕсли;	
	КонецЦикла;
	
КонецПроцедуры

Процедура РазвернутьГруппыВУсловияхОтбора(ЭлементыОдногоУровня)
	
	Для Каждого ЭлементОтбора Из ЭлементыОдногоУровня Цикл
		Если ТипЗнч(ЭлементОтбора) = Тип("ЭлементОтбораКомпоновкиДанных") Тогда
			Если ТипЗнч(ЭлементОтбора.ПравоеЗначение) = Тип("СправочникСсылка.РабочиеГруппы") Тогда
				ПользователиГруппы = РаботаСПользователями.ПолучитьПользователейРабочейГруппы(ЭлементОтбора.ПравоеЗначение);
				ЭлементОтбора.ПравоеЗначение = Новый СписокЗначений();
				ЭлементОтбора.ПравоеЗначение.ТипЗначения = Новый ОписаниеТипов("СправочникСсылка.Пользователи");
				ЭлементОтбора.ПравоеЗначение.ЗагрузитьЗначения(ПользователиГруппы);
				Если ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.ВИерархии Тогда
					ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.ВСписке;				
				ИначеЕсли ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.НеВИерархии Тогда
					ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.НеВСписке;					
				КонецЕсли;
			ИначеЕсли ТипЗнч(ЭлементОтбора.ПравоеЗначение) = Тип("СписокЗначений") 
				И ЭлементОтбора.ПравоеЗначение.Количество() > 0
				И ТипЗнч(ЭлементОтбора.ПравоеЗначение[0].Значение) = Тип("СправочникСсылка.РабочиеГруппы") Тогда
				СписокПользователейГрупп = Новый Массив;
				Для Каждого РабочаяГруппа Из ЭлементОтбора.ПравоеЗначение Цикл
					ПользователиГруппы = РаботаСПользователями.ПолучитьПользователейРабочейГруппы(РабочаяГруппа.Значение);
					ЭлементОтбора.ПравоеЗначение = Новый СписокЗначений();
					ЭлементОтбора.ПравоеЗначение.ТипЗначения = Новый ОписаниеТипов("СправочникСсылка.Пользователи");
					Для Каждого Пользователь Из ПользователиГруппы Цикл
						СписокПользователейГрупп.Добавить(Пользователь);
					КонецЦикла;
				КонецЦикла;
				ЭлементОтбора.ПравоеЗначение = Новый СписокЗначений();
				ЭлементОтбора.ПравоеЗначение.ТипЗначения = Новый ОписаниеТипов("СправочникСсылка.Пользователи");
				ЭлементОтбора.ПравоеЗначение.ЗагрузитьЗначения(ПользователиГруппы);
                Если ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.ВСпискеПоИерархии Тогда
					ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.ВСписке;				
				ИначеЕсли ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.НеВСпискеПоИерархии Тогда
					ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.НеВСписке;					
				КонецЕсли;
			КонецЕсли;
		ИначеЕсли ТипЗнч(ЭлементОтбора) = Тип("ГруппаЭлементовОтбораКомпоновкиДанных") Тогда
			РазвернутьГруппыВУсловияхОтбора(ЭлементОтбора.Элементы);
		КонецЕсли;
	КонецЦикла;	
	
КонецПроцедуры


