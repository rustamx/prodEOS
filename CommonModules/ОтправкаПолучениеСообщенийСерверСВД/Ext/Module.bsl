
Процедура ПриемСообщенийСерверомСВД() Экспорт
	
	ОбщегоНазначения.ПриНачалеВыполненияРегламентногоЗадания(Метаданные.РегламентныеЗадания.ПриемСообщенийСерверомСВД);
	
	УстановитьПривилегированныйРежим(Истина);
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ТранспортыПриемаСообщенийСВД.Ссылка
		|ИЗ
		|	Справочник.ТранспортыПриемаСообщенийСВД КАК ТранспортыПриемаСообщенийСВД
		|ГДЕ
		|	ТранспортыПриемаСообщенийСВД.Активен = ИСТИНА";
	РезультатЗапроса = Запрос.Выполнить();
	Если Не РезультатЗапроса.Пустой() Тогда
		Выборка = РезультатЗапроса.Выбрать();
		Пока Выборка.Следующий() Цикл
			ТранспортСсылка = Выборка.Ссылка;
			МенеджерТранспорта = ОбщегоНазначения.МенеджерОбъектаПоСсылке(ТранспортСсылка);
			МенеджерТранспорта.Получитьсообщения(ТранспортСсылка);
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры


Процедура ПересылкаСообщенийСерверомСВД() Экспорт
	
	ОбщегоНазначения.ПриНачалеВыполненияРегламентногоЗадания(Метаданные.РегламентныеЗадания.ПересылкаСообщенийСерверомСВД);
	
	УстановитьПривилегированныйРежим(Истина);
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	СерверныеСообщенияСВД.Ссылка,
		|	ПравилаПересылкиСообщений.ТранспортОтправки
		|ИЗ
		|	Справочник.СерверныеСообщенияСВД КАК СерверныеСообщенияСВД
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ПравилаПересылкиСообщений КАК ПравилаПересылкиСообщений
		|		ПО СерверныеСообщенияСВД.Кому = ПравилаПересылкиСообщений.Пользователь
		|ГДЕ
		|	СерверныеСообщенияСВД.Отправлено = ЛОЖЬ
		|	И СерверныеСообщенияСВД.ПометкаУдаления = ЛОЖЬ";
	РезультатЗапроса = Запрос.Выполнить();
	Если Не РезультатЗапроса.Пустой() Тогда
		Выборка = РезультатЗапроса.Выбрать();
		Пока Выборка.Следующий() Цикл
			МенеджерТранспорта = ОбщегоНазначения.МенеджерОбъектаПоСсылке(Выборка.ТранспортОтправки);
			МенеджерТранспорта.ОтправитьСообщение(Выборка.ТранспортОтправки, Выборка.ссылка);
			
			СообщениеОбъект = Выборка.Ссылка.ПолучитьОбъект();
			СообщениеОбъект.Отправлено = Истина;
			СообщениеОбъект.ДатаОтправкиПолучателю = ТекущаяДатаСеанса();
			СообщениеОбъект.Записать();
						
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры



