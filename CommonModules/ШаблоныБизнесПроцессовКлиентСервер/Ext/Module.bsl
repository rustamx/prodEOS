
////////////////////////////////////////////////////////////////////////////////
// <Шаблоны бизнес процессов клиент сервер>
//  
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Определяет, является ли ссылка на объект шаблоном процесса.
//
// Параметры:
//  СсылкаНаОбъект - ЛюбаяСсылка - ссылка на любой объект.
//
// Возвращаемое значение:
//  Булево - возвращает истину, если объект является шаблоном процесса.
//
Функция ЭтоШаблонПроцесса(СсылкаНаОбъект) Экспорт
	
	ТипОбъекта = ТипЗнч(СсылкаНаОбъект);
	
	ТипыШаблоновПроцессов = Новый Массив;
	ТипыШаблоновПроцессов.Добавить(Тип("СправочникСсылка.ШаблоныИсполнения"));
	ТипыШаблоновПроцессов.Добавить(Тип("СправочникСсылка.ШаблоныКомплексныхБизнесПроцессов"));
	ТипыШаблоновПроцессов.Добавить(Тип("СправочникСсылка.ШаблоныОзнакомления"));
	ТипыШаблоновПроцессов.Добавить(Тип("СправочникСсылка.ШаблоныПоручения"));
	ТипыШаблоновПроцессов.Добавить(Тип("СправочникСсылка.ШаблоныПриглашения"));
	ТипыШаблоновПроцессов.Добавить(Тип("СправочникСсылка.ШаблоныРассмотрения"));
	ТипыШаблоновПроцессов.Добавить(Тип("СправочникСсылка.ШаблоныРегистрации"));
	ТипыШаблоновПроцессов.Добавить(Тип("СправочникСсылка.ШаблоныСогласования"));
	ТипыШаблоновПроцессов.Добавить(Тип("СправочникСсылка.ШаблоныСоставныхБизнесПроцессов"));
	ТипыШаблоновПроцессов.Добавить(Тип("СправочникСсылка.ШаблоныУтверждения"));
	
	Возврат ТипыШаблоновПроцессов.Найти(ТипОбъекта) <> Неопределено;
	
КонецФункции

#КонецОбласти

#Область ПрограммныйИнтерфейс_ДоступностьШаблоновПроцессов

// Обновляет видимость группы доступности в карточке шаблона.
//
// Параметры:
//  Форма - Управляемаяформа - карточка шаблона процесса.
//
Процедура ОбновитьОтображениеГруппыДоступности(Форма) Экспорт
	
	Форма.Элементы.ГруппаДоступностьШаблона.Видимость = 
		ЗначениеЗаполнено(Форма.Объект.Ссылка) И Не Форма.ЭтоДействиеКомплексногоПроцесса;
	
КонецПроцедуры

// Обновляет отображение признака доступности шаблона процесса в карточке.
//
// Параметры:
//  Форма - УправляемаяФорма - карточка шаблона процесса.
//
Процедура ОбновитьОтображениеДоступностиШаблона(Форма) Экспорт
	
	Если Не Форма.ДоступенРучнойЗапускПоШаблону И Не Форма.ДоступенАвтоматическийЗапускПоШаблону Тогда
		Форма.Элементы.ГруппаДоступностьШаблонаСтраницы.ТекущаяСтраница = 
			Форма.Элементы.ГруппаШаблонНедоступенЗапуска;
	ИначеЕсли Форма.ДоступенРучнойЗапускПоШаблону И Не Форма.ДоступенАвтоматическийЗапускПоШаблону Тогда
		Форма.Элементы.ГруппаДоступностьШаблонаСтраницы.ТекущаяСтраница = 
			Форма.Элементы.ГруппаРучнойЗапуск;
	Иначе
		Форма.Элементы.ГруппаДоступностьШаблонаСтраницы.ТекущаяСтраница = 
			Форма.Элементы.ГруппаРучнойИАвтоматическийЗапуск;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
