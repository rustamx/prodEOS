
// Функция ВозможныеПраваДляНастройкиПравОбъектов возвращает описание возможных прав
// для настройки прав на объекты указанной таблицы.
//  Подробнее о возможных правах см. в комментарии к функции
// УправлениеДоступомПовтИсп.ВозможныеПраваДляНастройкиПравОбъектов()
//
// Параметры:
//  ОписаниеТаблицы - Неопределено - возвращается таблица всех описаний
//                  - Строка - полное имя таблицы объектов
//                  - Ссылка - ссылка таблицы объектов
//                  - Тип - тип данных таблицы объектов
//
// Возвращаемое значение:
//  ТаблицаЗначений с полями,
//                 описание которых см. в комментарии к функции
//                 УправлениеДоступомПовтИсп.ВозможныеПраваДляНастройкиПравОбъектов().
//                 Если задан параметр Таблица, возвращаются только её описания.
//
Функция ВозможныеПраваДляНастройкиПравОбъектов(Знач ОписаниеТаблицы = Неопределено) Экспорт
	
	ВозможныеПрава = УправлениеДоступомСлужебныйПовтИсп.Параметры().ВозможныеПраваДляНастройкиПравОбъектов;
	
	Если ОписаниеТаблицы = Неопределено Тогда
		Строки = Неопределено;
	Иначе
		Если ТипЗнч(ОписаниеТаблицы) <> Тип("Строка") Тогда
			Если ТипЗнч(ОписаниеТаблицы) <> Тип("Тип") Тогда
				ОписаниеТаблицы = ТипЗнч(ОписаниеТаблицы);
			КонецЕсли;
			МетаданныеТаблицы = Метаданные.НайтиПоТипу(ОписаниеТаблицы);
			Если Метаданные = Неопределено Тогда
				ОписаниеТаблицы = "";
			Иначе
				ОписаниеТаблицы = МетаданныеТаблицы.ПолноеИмя();
			КонецЕсли;
		
		КонецЕсли;
		Строки = ВозможныеПрава.НайтиСтроки(Новый Структура("ВладелецПрав", ОписаниеТаблицы));
	КонецЕсли;
	
	Возврат ВозможныеПрава.Скопировать(Строки);
	
КонецФункции

// Функция ЗаголовокПодчиненнойФормы параметризирует заголовок подчиненной формы,
// например, НСтр("ru = 'Права доступа: %1 (%2)'" -> "Права доступа: Иванов (Пользователь)"
//
// Параметры:
//  СсылкаНаВладельца - ссылка на объект-владелец.
//
Функция ЗаголовокПодчиненнойФормы(СтрокаЗаголовкаСПараметрами, СсылкаНаВладельца) Экспорт
	
	МетаданныеВладельца    = СсылкаНаВладельца.Метаданные();
	ПредставлениеВладельца = Строка(СсылкаНаВладельца);
	
	Если ЗначениеЗаполнено(МетаданныеВладельца.ПредставлениеОбъекта) Тогда
		ПредставлениеТипаВладельца = МетаданныеВладельца.ПредставлениеОбъекта;
		
	ИначеЕсли ЗначениеЗаполнено(МетаданныеВладельца.Синоним) Тогда
		ПредставлениеТипаВладельца = МетаданныеВладельца.Синоним;
	Иначе
		ПредставлениеТипаВладельца = МетаданныеВладельца.Имя;
	КонецЕсли;
	
	Возврат СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		СтрокаЗаголовкаСПараметрами,
		ПредставлениеВладельца,
		ПредставлениеТипаВладельца);
	
КонецФункции

// Функция ПолучитьСсылкуОбъекта возвращает ссылку
// старого объекта или ссылку нового объекта
//
Функция ПолучитьСсылкуОбъекта(Знач Объект, ЭтоНовый = Неопределено) Экспорт
	
	Ссылка = Объект.Ссылка;
	ЭтоНовый = НЕ ЗначениеЗаполнено(Ссылка);
	
	Если ЭтоНовый Тогда
		Ссылка = Объект.ПолучитьСсылкуНового();
		
		Если НЕ ЗначениеЗаполнено(Ссылка) Тогда
			
			Менеджер = ОбщегоНазначения.МенеджерОбъектаПоСсылке(Объект.Ссылка);
			Ссылка = Менеджер.ПолучитьСсылку();
			Объект.УстановитьСсылкуНового(Ссылка);
		КонецЕсли;
	КонецЕсли;
	
	Возврат Ссылка;
	
КонецФункции

// Обновляет возможные права для настройки прав объектов и сохраняет состав последних изменений.
// 
// Параметры:
//  ЕстьИзменения - Булево (возвращаемое значение) - если изменения найдены,
//                  устанавливается Истина, иначе не изменяется.
//
Процедура ОбновитьВозможныеПраваДляНастройкиПравОбъектов(ЕстьИзменения = Неопределено, ТолькоПроверка = Ложь) Экспорт
	
	Если ТолькоПроверка ИЛИ МонопольныйРежим() Тогда
		СнятьМонопольныйРежим = Ложь;
	Иначе
		СнятьМонопольныйРежим = Истина;
		УстановитьМонопольныйРежим(Истина);
	КонецЕсли;
	
	ВозможныеПрава = ВозможныеПрава();
	
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("Константа.ПараметрыОграниченияДоступа");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	
	НачатьТранзакцию();
	Попытка
		Блокировка.Заблокировать();
		
		Параметры = СтандартныеПодсистемыСервер.ПараметрыРаботыПрограммы(
			"ПараметрыОграниченияДоступа");
		
		Сохраненные = Неопределено;
		
		Если Параметры.Свойство("ВозможныеПраваДляНастройкиПравОбъектов") Тогда
			Сохраненные = Параметры.ВозможныеПраваДляНастройкиПравОбъектов;
			
			Если НЕ ОбщегоНазначения.ДанныеСовпадают(ВозможныеПрава, Сохраненные) Тогда
				Сохраненные = Неопределено;
			КонецЕсли;
		КонецЕсли;
		
		УстановитьПривилегированныйРежим(Истина);
		
		Если Сохраненные = Неопределено Тогда
			ЕстьИзменения = Истина;
			Если ТолькоПроверка Тогда
				ЗафиксироватьТранзакцию();
				Возврат;
			КонецЕсли;
			СтандартныеПодсистемыСервер.УстановитьПараметрРаботыПрограммы(
				"ПараметрыОграниченияДоступа",
				"ВозможныеПраваДляНастройкиПравОбъектов",
				ВозможныеПрава);
		КонецЕсли;
		
		Если НЕ ТолькоПроверка Тогда
			СтандартныеПодсистемыСервер.ДобавитьИзмененияПараметраРаботыПрограммы(
				"ПараметрыОграниченияДоступа",
				"ВозможныеПраваДляНастройкиПравОбъектов",
				?(Сохраненные = Неопределено,
				  Новый ФиксированнаяСтруктура("ЕстьИзменения", Истина),
				  Новый ФиксированнаяСтруктура()) );
		КонецЕсли;
		
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		Если СнятьМонопольныйРежим Тогда
			УстановитьМонопольныйРежим(Ложь);
		КонецЕсли;
		ВызватьИсключение;
	КонецПопытки;
	
	Если СнятьМонопольныйРежим Тогда
		УстановитьМонопольныйРежим(Ложь);
	КонецЕсли;
	
КонецПроцедуры

// Вызывает процедуру подготовленную прикладным разработчиком в
// УправлениеДоступомПереопределяемый.ЗаполнитьВозможныеПраваДляНастройкиПравОбъектов(),
// в которой прикладному разработчику нужно добавить возможные права
// в виде строк таблицы значений, переданной в качестве параметра ВозможныеПрава
// со структурой колонок:
//
//   ВладелецПрав      - Строка - полное имя таблицы объектов, для которой выполняется настройка прав
//
//   Имя               - Строка - идентификатор права, например, ИзменениеПапок
//                       Право с имененем "УправлениеПравами" должно быть обязательно определено
//                       Право "УправлениеПравами" проверяется при открытии ОбщаяФорма.НастройкиПравОбъектов
//
//   Синоним           - Строка - полное наименование права, например, "Изменение папок файлов"
//
//   Сокращение        - Строка - краткое наименование права среди прав одного владельца,
//                       например, "Изменение
//                                 |папок" - это наименование используется в заголовке колонки стандартной формы настройки прав
//
//   НачальноеЗначение - Булево - начальное значение флажка права при добавлении новой строки в ОбщаяФорма.НастройкиПравОбъектов
//
//   ТребуемыеПрава    - Массив строк - имена прав, требуемых для этого права,
//                       например, право "ИзменениеФайлов" требует право "Чтение",
//                       а право "ПометкаУдаления" требует одно из прав "ИзменениеПапок|ИзменениеФайлов"
//                       При настройке прав пользователем, автоматически устанавливаются требуемые права
//
// Возвращаемое значение:
//   Таблица значений описанная выше с дополнительной колонкой:
//   ИдентификаторВладелецаПрав - СправочникСсылка.ИдентификаторыОбъектовМетаданных
//
Функция ВозможныеПрава()
	
	ВозможныеПрава = Новый ТаблицаЗначений();
	ВозможныеПрава.Колонки.Добавить("ВладелецПрав",      Новый ОписаниеТипов("Строка",      , Новый КвалификаторыСтроки(350)));
	ВозможныеПрава.Колонки.Добавить("Имя",               Новый ОписаниеТипов("Строка",      , Новый КвалификаторыСтроки(60)));
	ВозможныеПрава.Колонки.Добавить("Синоним",           Новый ОписаниеТипов("Строка",      , Новый КвалификаторыСтроки(80)));
	ВозможныеПрава.Колонки.Добавить("Сокращение",        Новый ОписаниеТипов("Строка",      , Новый КвалификаторыСтроки(50)));
	ВозможныеПрава.Колонки.Добавить("НачальноеЗначение", Новый ОписаниеТипов("Булево, Число", Новый КвалификаторыЧисла(1, 0, ДопустимыйЗнак.Неотрицательный)));
	ВозможныеПрава.Колонки.Добавить("ТребуемыеПрава",    Новый ОписаниеТипов("Массив"));
	
	ЗаполнитьВозможныеПраваДляНастройкиПравОбъектов(ВозможныеПрава);
	
	ВозможныеПрава.Колонки.Добавить("ИдентификаторВладелецаПрав", Новый ОписаниеТипов("СправочникСсылка.ИдентификаторыОбъектовМетаданных"));
	
	Для каждого ВозможноеПраво Из ВозможныеПрава Цикл
		ОбъектМетаданных = Метаданные.НайтиПоПолномуИмени(ВозможноеПраво.ВладелецПрав);
		Если ОбъектМетаданных = Неопределено Тогда
			ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Ошибка при подготовке возможных прав для настройки прав объектов.
				           |Не найден объект метаданных заданного владельца прав:
				           |""%1""';
				           |en = 'Error occurred while preparing the possible rights to objects permissions settings.
				           |Not found the metadata object for a given rights owner:
				           |""%1""'"),
				ВозможноеПраво.ВладелецПрав);
		КонецЕсли;
		ВозможноеПраво.ИдентификаторВладелецаПрав = ОбщегоНазначения.ИдентификаторОбъектаМетаданных(ОбъектМетаданных);
		// Преобразования списков требуемых прав в массивы
		Разделитель = "|";
		Для Индекс = 0 По ВозможноеПраво.ТребуемыеПрава.Количество()-1 Цикл
			Если Найти(ВозможноеПраво.ТребуемыеПрава[Индекс], Разделитель) > 0 Тогда
				ВозможноеПраво.ТребуемыеПрава[Индекс] =
					СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(
						ВозможноеПраво.ТребуемыеПрава[Индекс],
						Разделитель);
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	Возврат ВозможныеПрава;
	
КонецФункции

Процедура ЗаполнитьВозможныеПраваДляНастройкиПравОбъектов(ВозможныеПрава) Экспорт
	
	ЗаполнитьВозможныеПраваДляПапокФайлов(ВозможныеПрава);
	ЗаполнитьВозможныеПраваДляПапокВнутреннихДокументов(ВозможныеПрава);
	ЗаполнитьВозможныеПраваДляПапокПисем(ВозможныеПрава);
	ЗаполнитьВозможныеПраваДляПапокМероприятий(ВозможныеПрава);
	ЗаполнитьВозможныеПраваДляПапокФорума(ВозможныеПрава);
	ЗаполнитьВозможныеПраваДляПапокПроектов(ВозможныеПрава);
	
КонецПроцедуры

Процедура ЗаполнитьВозможныеПраваДляПапокФайлов(ВозможныеПрава)
	
	// Право "Чтение папок и файлов"
	Право = ВозможныеПрава.Добавить();
	Право.ВладелецПрав  = "Справочник.ПапкиФайлов";
	Право.Имя           = "ЧтениеПапокИФайлов";
	Право.Синоним       = НСтр("ru = 'Чтение папок и файлов'; en = 'Read folders and files'");
	Право.Сокращение    = НСтр("ru = 'Чтение'; en = 'Read'");
	
	// Право "Добавление папок и файлов" - ограничивает добавление файлов в папке
	Право = ВозможныеПрава.Добавить();
	Право.ВладелецПрав  = "Справочник.ПапкиФайлов";
	Право.Имя           = "ДобавлениеПапокИФайлов";
	Право.Синоним       = НСтр("ru = 'Добавление файлов'; en = 'Create files'");
	Право.Сокращение    = НСтр("ru = 'Добавление
	                                 |файлов';
	                                 |en = 'Create
	                                 |files'");
	// Права, требуемые для этого права.
	Право.ТребуемыеПрава.Добавить("ИзменениеПапокИФайлов");
	
	// Право "Изменение файлов" - ограничивает изменение файлов в папке
	Право = ВозможныеПрава.Добавить();
	Право.ВладелецПрав  = "Справочник.ПапкиФайлов";
	Право.Имя           = "ИзменениеПапокИФайлов";
	Право.Синоним       = НСтр("ru = 'Изменение файлов'; en = 'Change files'");
	Право.Сокращение    = НСтр("ru = 'Изменение
	                                 |файлов';
	                                 |en = 'Change  
	                                 |files'");
	// Требуемая роль (одна из указанных).
	Право.ТребуемыеПрава.Добавить("ЧтениеПапокИФайлов");
	
	// Право "Изменение папок файлов" - ограничивает изменение структуры 
	// папок, добавление, изменение, пометку удаления
	Право = ВозможныеПрава.Добавить();
	Право.ВладелецПрав  = "Справочник.ПапкиФайлов";
	Право.Имя           = "ИзменениеПапок";
	Право.Синоним       = НСтр("ru = 'Изменение папок файлов'; en = 'Change files folders'");
	Право.Сокращение    = НСтр("ru = 'Изменение
	                                 |папок';
	                                 |en = 'Change 
	                                 |folders'");
	// Права, требуемые для этого права.
	Право.ТребуемыеПрава.Добавить("ЧтениеПапокИФайлов");
	
	// Право "Пометка удаления папок и файлов" - ограничивает пометку удаления 
	// файлов в папке
	Право = ВозможныеПрава.Добавить();
	Право.ВладелецПрав  = "Справочник.ПапкиФайлов";
	Право.Имя           = "ПометкаУдаленияПапокИФайлов";
	Право.Синоним       = НСтр("ru = 'Пометка удаления файлов'; en = 'Marking files for deletion'");
	Право.Сокращение    = НСтр("ru = 'Пометка
	                                 |удаления';
	                                 |en = 'Marking
	                                 |for deletion'");
	// Права, требуемые для этого права.
	Право.ТребуемыеПрава.Добавить("ИзменениеПапокИФайлов");
	
	// Право "Управление правами папок файлов"
	Право = ВозможныеПрава.Добавить();
	Право.ВладелецПрав  = "Справочник.ПапкиФайлов";
	Право.Имя           = "УправлениеПравами";
	Право.Синоним       = НСтр("ru = 'Управление правами папок файлов'; en = 'Rights management for file folders'");
	Право.Сокращение    = НСтр("ru = 'Управление
	                                 |правами';
	                                 |en = 'Rights
	                                 |management'");
	// Права, требуемые для этого права.
	Право.ТребуемыеПрава.Добавить("ЧтениеПапокИФайлов");
	
КонецПроцедуры

Процедура ЗаполнитьВозможныеПраваДляПапокВнутреннихДокументов(ВозможныеПрава)

	// Право "Чтение"
	Право = ВозможныеПрава.Добавить();
	Право.ВладелецПрав  = "Справочник.ПапкиВнутреннихДокументов";
	Право.Имя           = "ЧтениеПапокИВнутреннихДокументов";
	Право.Синоним       = НСтр("ru = 'Чтение папок и внутренних документов'; en = 'Read folders and internal documents'");
	Право.Сокращение    = НСтр("ru = 'Чтение'; en = 'Read'");
	
	// Право "Добавление"
	Право = ВозможныеПрава.Добавить();
	Право.ВладелецПрав  = "Справочник.ПапкиВнутреннихДокументов";
	Право.Имя           = "ДобавлениеПапокИВнутреннихДокументов";
	Право.Синоним       = НСтр("ru = 'Добавление внутренних документов'; en = 'Create internal documents'");
	Право.Сокращение    = НСтр("ru = 'Добавление
	                                 |документов';
	                                 |en = 'Create
	                                 |documents'");
	// Права, требуемые для этого права.
	Право.ТребуемыеПрава.Добавить("ИзменениеПапокИВнутреннихДокументов");
	
	// Право "Изменение"
	Право = ВозможныеПрава.Добавить();
	Право.ВладелецПрав  = "Справочник.ПапкиВнутреннихДокументов";
	Право.Имя           = "ИзменениеПапокИВнутреннихДокументов";
	Право.Синоним       = НСтр("ru = 'Изменение внутренних документов'; en = 'Change internal documents'");
	Право.Сокращение    = НСтр("ru = 'Изменение
	                                 |документов';
	                                 |en = 'Change 
	                                 |documents'");
	// Права, требуемые для этого права.
	Право.ТребуемыеПрава.Добавить("ЧтениеПапокИВнутреннихДокументов");
	
	// Право "Изменение папок"
	Право = ВозможныеПрава.Добавить();
	Право.ВладелецПрав  = "Справочник.ПапкиВнутреннихДокументов";
	Право.Имя           = "ИзменениеПапокВнутреннихДокументов";
	Право.Синоним       = НСтр("ru = 'Изменение папок внутренних документов'; en = 'Change internal document folders'");
	Право.Сокращение    = НСтр("ru = 'Изменение
	                                 |папок';
	                                 |en = 'Change 
	                                 |folders'");
	// Права, требуемые для этого права.
	Право.ТребуемыеПрава.Добавить("ЧтениеПапокИВнутреннихДокументов");
	
	// Право "Пометка удаления"
	Право = ВозможныеПрава.Добавить();
	Право.ВладелецПрав  = "Справочник.ПапкиВнутреннихДокументов";
	Право.Имя           = "ПометкаУдаленияПапокИВнутреннихДокументов";
	Право.Синоним       = НСтр("ru = 'Пометка удаления внутренних документов'; en = 'Marking internal documents for deletion'");
	Право.Сокращение    = НСтр("ru = 'Пометка
	                                 |удаления';
	                                 |en = 'Marking
	                                 |for deletion'");
	// Права, требуемые для этого права.
	Право.ТребуемыеПрава.Добавить("ИзменениеПапокИВнутреннихДокументов");
	
	// Право "Управление правами"
	Право = ВозможныеПрава.Добавить();
	Право.ВладелецПрав  = "Справочник.ПапкиВнутреннихДокументов";
	Право.Имя           = "УправлениеПравами";
	Право.Синоним       = НСтр("ru = 'Управление правами папок внутренних документов'; en = 'Rights management for internal document folders'");
	Право.Сокращение    = НСтр("ru = 'Управление
	                                 |правами';
	                                 |en = 'Rights
	                                 |management'");
	// Права, требуемые для этого права.
	Право.ТребуемыеПрава.Добавить("ЧтениеПапокИВнутреннихДокументов");

КонецПроцедуры

Процедура ЗаполнитьВозможныеПраваДляПапокПисем(ВозможныеПрава)
	
	// Право "Чтение папок и писем"
	Право = ВозможныеПрава.Добавить();
	Право.ВладелецПрав  = "Справочник.ПапкиПисем";
	Право.Имя           = "ЧтениеПапокИПисем";
	Право.Синоним       = НСтр("ru = 'Чтение папок и писем'; en = 'Read folders and emails'");
	Право.Сокращение    = НСтр("ru = 'Чтение'; en = 'Read'");
	
	// Право "Добавление папок и писем" - ограничивает добавление писем в папке
	Право = ВозможныеПрава.Добавить();
	Право.ВладелецПрав  = "Справочник.ПапкиПисем";
	Право.Имя           = "ДобавлениеПапокИПисем";
	Право.Синоним       = НСтр("ru = 'Добавление писем'; en = 'Create emails'");
	Право.Сокращение    = НСтр("ru = 'Добавление
	                                 |писем';
	                                 |en = 'Create
	                                 |emails'");
	// Права, требуемые для этого права.
	Право.ТребуемыеПрава.Добавить("ИзменениеПапокИПисем");
	
	// Право "Изменение писем" - ограничивает изменение писем в папке
	Право = ВозможныеПрава.Добавить();
	Право.ВладелецПрав  = "Справочник.ПапкиПисем";
	Право.Имя           = "ИзменениеПапокИПисем";
	Право.Синоним       = НСтр("ru = 'Изменение писем'; en = 'Change emails'");
	Право.Сокращение    = НСтр("ru = 'Изменение
	                                 |писем';
	                                 |en = 'Change 
	                                 |emails'");
	// Права, требуемые для этого права.
	Право.ТребуемыеПрава.Добавить("ЧтениеПапокИПисем");
	
	// Право "Изменение папок писем" - ограничивает изменение структуры 
	// папок, добавление, изменение, пометку удаления
	Право = ВозможныеПрава.Добавить();
	Право.ВладелецПрав  = "Справочник.ПапкиПисем";
	Право.Имя           = "ИзменениеПапок";
	Право.Синоним       = НСтр("ru = 'Изменение папок писем'; en = 'Change email folders'");
	Право.Сокращение    = НСтр("ru = 'Изменение
	                                 |папок';
	                                 |en = 'Change 
	                                 |folders'");
	// Права, требуемые для этого права.
	Право.ТребуемыеПрава.Добавить("ЧтениеПапокИписем");
	
	// Право "Пометка удаления папок и писем" - ограничивает пометку удаления 
	// писем в папке
	Право = ВозможныеПрава.Добавить();
	Право.ВладелецПрав  = "Справочник.ПапкиПисем";
	Право.Имя           = "ПометкаУдаленияПапокИПисем";
	Право.Синоним       = НСтр("ru = 'Пометка удаления писем'; en = 'Marking emails for deletion'");
	Право.Сокращение    = НСтр("ru = 'Пометка
	                                 |удаления';
	                                 |en = 'Marking
	                                 |for deletion'");
	// Права, требуемые для этого права.
	Право.ТребуемыеПрава.Добавить("ИзменениеПапокИПисем");
	
	// Право "Управление правами папок писем"
	Право = ВозможныеПрава.Добавить();
	Право.ВладелецПрав  = "Справочник.ПапкиПисем";
	Право.Имя           = "УправлениеПравами";
	Право.Синоним       = НСтр("ru = 'Управление правами папок писем'; en = 'Rights management for email folders'");
	Право.Сокращение    = НСтр("ru = 'Управление
	                                 |правами';
	                                 |en = 'Rights
	                                 |management'");
	// Права, требуемые для этого права.
	Право.ТребуемыеПрава.Добавить("ЧтениеПапокИПисем");
	
КонецПроцедуры

Процедура ЗаполнитьВозможныеПраваДляПапокМероприятий(ВозможныеПрава)
	
	// Право "Чтение"
	Право = ВозможныеПрава.Добавить();
	Право.ВладелецПрав  = "Справочник.ПапкиМероприятий";
	Право.Имя           = "ЧтениеПапокИМероприятий";
	Право.Синоним       = НСтр("ru = 'Чтение папок и мероприятий'; en = 'Read folders and events'");
	Право.Сокращение    = НСтр("ru = 'Чтение'; en = 'Read'");
	
	// Право "Добавление"
	Право = ВозможныеПрава.Добавить();
	Право.ВладелецПрав  = "Справочник.ПапкиМероприятий";
	Право.Имя           = "ДобавлениеПапокИМероприятий";
	Право.Синоним       = НСтр("ru = 'Добавление мероприятий'; en = 'Create events'");
	Право.Сокращение    = НСтр("ru = 'Добавление
	                                 |мероприятий';
	                                 |en = 'Create
	                                 |events'");
	// Права, требуемые для этого права.
	Право.ТребуемыеПрава.Добавить("ИзменениеПапокИМероприятий");
	
	// Право "Изменение"
	Право = ВозможныеПрава.Добавить();
	Право.ВладелецПрав  = "Справочник.ПапкиМероприятий";
	Право.Имя           = "ИзменениеПапокИМероприятий";
	Право.Синоним       = НСтр("ru = 'Изменение мероприятий'; en = 'Change events'");
	Право.Сокращение    = НСтр("ru = 'Изменение
	                                 |мероприятий';
	                                 |en = 'Change 
	                                 |events'");
	// Права, требуемые для этого права.
	Право.ТребуемыеПрава.Добавить("ЧтениеПапокИМероприятий");
	
	// Право "Изменение папок"
	Право = ВозможныеПрава.Добавить();
	Право.ВладелецПрав  = "Справочник.ПапкиМероприятий";
	Право.Имя           = "ИзменениеПапокМероприятий";
	Право.Синоним       = НСтр("ru = 'Изменение папок мероприятий'; en = 'Change events folders'");
	Право.Сокращение    = НСтр("ru = 'Изменение
	                                 |папок';
	                                 |en = 'Change 
	                                 |folders'");
	// Права, требуемые для этого права.
	Право.ТребуемыеПрава.Добавить("ЧтениеПапокИМероприятий");
	
	// Право "Пометка удаления"
	Право = ВозможныеПрава.Добавить();
	Право.ВладелецПрав  = "Справочник.ПапкиМероприятий";
	Право.Имя           = "ПометкаУдаленияПапокИМероприятий";
	Право.Синоним       = НСтр("ru = 'Пометка удаления мероприятий'; en = 'Marking events for deletion'");
	Право.Сокращение    = НСтр("ru = 'Пометка
	                                 |мероприятий';
	                                 |en = 'Marking
	                                 |events'");
	// Права, требуемые для этого права.
	Право.ТребуемыеПрава.Добавить("ИзменениеПапокИМероприятий");
	
	// Право "Управление правами"
	Право = ВозможныеПрава.Добавить();
	Право.ВладелецПрав  = "Справочник.ПапкиМероприятий";
	Право.Имя           = "УправлениеПравами";
	Право.Синоним       = НСтр("ru = 'Управление правами папок мероприятий'; en = 'Rights management for event folders'");
	Право.Сокращение    = НСтр("ru = 'Управление
	                                 |правами';
	                                 |en = 'Rights
	                                 |management'");
	// Права, требуемые для этого права.
	Право.ТребуемыеПрава.Добавить("ЧтениеПапокИМероприятий");
	
КонецПроцедуры

Процедура ЗаполнитьВозможныеПраваДляПапокФорума(ВозможныеПрава)
	
	// Право "Чтение"
	Право = ВозможныеПрава.Добавить();
	Право.ВладелецПрав  = "Справочник.ПапкиФорума";
	Право.Имя           = "ЧтениеПапокИТем";
	Право.Синоним       = НСтр("ru = 'Чтение разделов и тем'; en = 'Read sub-forums and topics'");
	Право.Сокращение    = НСтр("ru = 'Чтение'; en = 'Read'");
	
	// Право "Добавление"
	Право = ВозможныеПрава.Добавить();
	Право.ВладелецПрав  = "Справочник.ПапкиФорума";
	Право.Имя           = "ДобавлениеТемИСообщений";
	Право.Синоним       = НСтр("ru = 'Добавление тем и сообщений'; en = 'Create topics and messages'");
	Право.Сокращение    = НСтр("ru = 'Добавление'; en = 'Create'");
	// Права, требуемые для этого права.
	Право.ТребуемыеПрава.Добавить("ЧтениеПапокИТем");
	
	// Право "Изменение"
	Право = ВозможныеПрава.Добавить();
	Право.ВладелецПрав  = "Справочник.ПапкиФорума";
	Право.Имя           = "ИзменениеТемИСообщений";
	Право.Синоним       = НСтр("ru = 'Изменение тем и сообщений'; en = 'Change topics and messages'");
	Право.Сокращение    = НСтр("ru = 'Изменение'; en = 'Change'");
	// Права, требуемые для этого права.
	Право.ТребуемыеПрава.Добавить("ЧтениеПапокИТем");
	
	// Право "Изменение папок"
	Право = ВозможныеПрава.Добавить();
	Право.ВладелецПрав  = "Справочник.ПапкиФорума";
	Право.Имя           = "ИзменениеПапок";
	Право.Синоним       = НСтр("ru = 'Изменение разделов'; en = 'Change sub-forums'");
	Право.Сокращение    = НСтр("ru = 'Изменение
	                                 |разделов';
	                                 |en = 'Change 
	                                 |sub-forums'");
	// Права, требуемые для этого права.
	Право.ТребуемыеПрава.Добавить("ЧтениеПапокИТем");
	
	// Право "Пометка удаления"
	Право = ВозможныеПрава.Добавить();
	Право.ВладелецПрав  = "Справочник.ПапкиФорума";
	Право.Имя           = "ПометкаУдаленияТемИСообщений";
	Право.Синоним       = НСтр("ru = 'Удаление тем и сообщений'; en = 'Delete topics and messages'");
	Право.Сокращение    = НСтр("ru = 'Удаление'; en = 'Deletion'");
	// Права, требуемые для этого права.
	Право.ТребуемыеПрава.Добавить("ИзменениеТемИСообщений");
	
	// Право "Управление правами"
	Право = ВозможныеПрава.Добавить();
	Право.ВладелецПрав  = "Справочник.ПапкиФорума";
	Право.Имя           = "УправлениеПравами";
	Право.Синоним       = НСтр("ru = 'Управление правами разделов'; en = 'Rights management for sub-forum'");
	Право.Сокращение    = НСтр("ru = 'Управление
	                                 |правами';
	                                 |en = 'Rights
	                                 |management'");
	// Права, требуемые для этого права.
	Право.ТребуемыеПрава.Добавить("ЧтениеПапокИТем");

КонецПроцедуры

Процедура ЗаполнитьВозможныеПраваДляПапокПроектов(ВозможныеПрава)
	
	// Право "Чтение"
	Право = ВозможныеПрава.Добавить();
	Право.ВладелецПрав  = "Справочник.ПапкиПроектов";
	Право.Имя           = "ЧтениеПапокИПроектов";
	Право.Синоним       = НСтр("ru = 'Чтение папок и проектов'; en = 'Read folders and projects'");
	Право.Сокращение    = НСтр("ru = 'Чтение'; en = 'Read'");
	
	// Право "Добавление"
	Право = ВозможныеПрава.Добавить();
	Право.ВладелецПрав  = "Справочник.ПапкиПроектов";
	Право.Имя           = "ДобавлениеПапокИПроектов";
	Право.Синоним       = НСтр("ru = 'Добавление проектов'; en = 'Create projects'");
	Право.Сокращение    = НСтр("ru = 'Добавление
	                                 |проектов';
	                                 |en = 'Create
	                                 |projects'");
	// Права, требуемые для этого права.
	Право.ТребуемыеПрава.Добавить("ИзменениеПапокИПроектов");
	
	// Право "Изменение"
	Право = ВозможныеПрава.Добавить();
	Право.ВладелецПрав  = "Справочник.ПапкиПроектов";
	Право.Имя           = "ИзменениеПапокИПроектов";
	Право.Синоним       = НСтр("ru = 'Изменение проектов'; en = 'Change projects'");
	Право.Сокращение    = НСтр("ru = 'Изменение
	                                 |проектов';
	                                 |en = 'Change
	                                 |projects'");
	// Права, требуемые для этого права.
	Право.ТребуемыеПрава.Добавить("ЧтениеПапокИПроектов");
	
	// Право "Изменение папок"
	Право = ВозможныеПрава.Добавить();
	Право.ВладелецПрав  = "Справочник.ПапкиПроектов";
	Право.Имя           = "ИзменениеПапокПроектов";
	Право.Синоним       = НСтр("ru = 'Изменение папок проектов'; en = 'Change project folders'");
	Право.Сокращение    = НСтр("ru = 'Изменение
	                                 |папок';
	                                 |en = 'Change 
	                                 |folders'");
	// Права, требуемые для этого права.
	Право.ТребуемыеПрава.Добавить("ЧтениеПапокИПроектов");
	
	// Право "Пометка удаления"
	Право = ВозможныеПрава.Добавить();
	Право.ВладелецПрав  = "Справочник.ПапкиПроектов";
	Право.Имя           = "ПометкаУдаленияПапокИПроектов";
	Право.Синоним       = НСтр("ru = 'Пометка удаления проектов'; en = 'Marking for deletion of projects'");
	Право.Сокращение    = НСтр("ru = 'Пометка
	                                 |проектов';
	                                 |en = 'Marking
	                                 |projects'");
	// Права, требуемые для этого права.
	Право.ТребуемыеПрава.Добавить("ИзменениеПапокИПроектов");
	
	// Право "Управление правами"
	Право = ВозможныеПрава.Добавить();
	Право.ВладелецПрав  = "Справочник.ПапкиПроектов";
	Право.Имя           = "УправлениеПравами";
	Право.Синоним       = НСтр("ru = 'Управление правами папок проектов'; en = 'Rights management for project folders'");
	Право.Сокращение    = НСтр("ru = 'Управление
	                                 |правами';
	                                 |en = 'Rights
	                                 |management'");
	// Права, требуемые для этого права.
	Право.ТребуемыеПрава.Добавить("ЧтениеПапокИПроектов");
	
КонецПроцедуры


