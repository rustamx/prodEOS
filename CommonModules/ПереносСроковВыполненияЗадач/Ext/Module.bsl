////////////////////////////////////////////////////////////////////////////////
// Перенос сроков выполнения задач
//  
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Инициализирует реквизиты формы для учета переносов сроков.
//
// Параметры:
//  Форма - УправляемаяФорма - форма процесса.
//  Отказ - Булево
//
Процедура ПриСозданииНаСервере(Форма, Отказ, СтандартнаяОбработка) Экспорт
	
	Форма.ВестиУчетПереносаСроков = ПолучитьФункциональнуюОпцию("ВестиУчетПереносовСроковЗадач");
	
	Если Форма.Параметры.Свойство("ЗаявкаНаПереносСрока") Тогда
		Форма.ЗаявкаНаПереносСрока = Форма.Параметры.ЗаявкаНаПереносСрока;
	КонецЕсли;
	
КонецПроцедуры

// Передает Причину переноса срока в доп. параметры объекта из структуры параметров.
//
// Параметры:
//  Объект - БизнесПроцессОбъект, ЗадачаОбъект
//  ПараметрыЗаписи - Структура
//
Процедура ПередатьПричинуИЗаявкуНаПереносаСрока(Объект, Параметры) Экспорт
	
	Если Параметры.Свойство("ПричинаПереносаСрока") Тогда
		Объект.ДополнительныеСвойства.Вставить("ПричинаПереносаСрока", Параметры.ПричинаПереносаСрока);
	КонецЕсли;
	
	Если Параметры.Свойство("БизнесПроцессПереноса") Тогда
		Объект.ДополнительныеСвойства.Вставить("БизнесПроцессПереноса", Параметры.БизнесПроцессПереноса);
	КонецЕсли;
	
КонецПроцедуры

// Переносит срок исполнения задачи по заявке.
//
// Параметры:
//  НовыйСрок - Дата - новый срок задачи.
//  ПричинаПереносаСрока - Строка - причина, по которой переносится срок задачи.
//  ЗаявкаНаПереносСрока - БизнесПроцессСсылка.РешениеВопросовВыполненияЗадач - заявка на перенос срока.
//
Процедура ПеренестиСрокЗадачиПоЗаявке(НовыйСрок, ПричинаПереносаСрока, ЗаявкаНаПереносСрока) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	РеквизитыЗаявки = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
		ЗаявкаНаПереносСрока, "ПредметРассмотрения, ПредметРассмотрения.БизнесПроцесс");
	
	Задача = РеквизитыЗаявки.ПредметРассмотрения;
	Процесс = РеквизитыЗаявки.ПредметРассмотренияБизнесПроцесс;
	
	МенеджерПроцесса = ОбщегоНазначения.МенеджерОбъектаПоСсылке(Процесс);
	
	МенеджерПроцесса.ИзменитьСрокИсполненияЗадачи(
		НовыйСрок, Задача, Процесс, ПричинаПереносаСрока, ЗаявкаНаПереносСрока);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Обработчик подписки ПереносСроковИсполненияПередЗаписьюЗадач
//
Процедура ПереносСроковИсполненияПередЗаписьюЗадач(Источник, Отказ) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если Не ПолучитьФункциональнуюОпцию("ВестиУчетПереносовСроковЗадач")
		Или Не Источник.ДополнительныеСвойства.Свойство("ПричинаПереносаСрока") Тогда
		
		Возврат;
	КонецЕсли;
	
	СрокИсполнения = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Источник.Ссылка, "СрокИсполнения");
		
	Источник.ДополнительныеСвойства.Вставить("СтарыйСрокИсполнения", СрокИсполнения)
	
КонецПроцедуры

// Обработчик подписки ПереносСроковИсполненияПриЗаписиЗадач
//
Процедура ПереносСроковИсполненияПриЗаписиЗадач(Источник, Отказ) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если Не ПолучитьФункциональнуюОпцию("ВестиУчетПереносовСроковЗадач")
		Или Не Источник.ДополнительныеСвойства.Свойство("ПричинаПереносаСрока")
		Или Источник.ДополнительныеСвойства.СтарыйСрокИсполнения = Источник.СрокИсполнения Тогда
		
		Возврат;
	КонецЕсли;
	
	ТекущийУзелОбмена = РаботаСБизнесПроцессами.ЭтотУзелОбменаДляОбработкиПроцессов();
	УзелПроцесса = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Источник.БизнесПроцесс, "УзелОбмена");
	
	Если УзелПроцесса <> ТекущийУзелОбмена Тогда
		Возврат;
	КонецЕсли;
	
	МенеджерЗаписи = РегистрыСведений.ПереносыСроковВыполненияПроцессовИЗадач.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.ДатаПереносаСрока = ТекущаяДатаСеанса();
	МенеджерЗаписи.Предмет = Источник.Ссылка;
	МенеджерЗаписи.СтарыйСрок = Источник.ДополнительныеСвойства.СтарыйСрокИсполнения;
	МенеджерЗаписи.НовыйСрок = Источник.СрокИсполнения;
	
	Если Источник.ДополнительныеСвойства.Свойство("ПричинаПереносаСрока") Тогда
		МенеджерЗаписи.КомментарийАвтора = Источник.ДополнительныеСвойства.ПричинаПереносаСрока;
	КонецЕсли;
	
	МенеджерЗаписи.Пользователь = ПользователиКлиентСервер.ТекущийПользователь();
	
	Если Источник.ДополнительныеСвойства.Свойство("БизнесПроцессПереноса") Тогда
		МенеджерЗаписи.БизнесПроцессПереноса = Источник.ДополнительныеСвойства.БизнесПроцессПереноса;
		МенеджерЗаписи.Итерация = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(
			МенеджерЗаписи.БизнесПроцессПереноса, "Итерация");
	КонецЕсли;
	
	МенеджерЗаписи.Записать();
	
КонецПроцедуры

// Обработчик подписки ПереносСроковИсполненияПередЗаписьюПроцесса
//
Процедура ПереносСроковИсполненияПередЗаписьюПроцесса(Источник, Отказ) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если Не ПолучитьФункциональнуюОпцию("ВестиУчетПереносовСроковЗадач")
		Или Не Источник.ДополнительныеСвойства.Свойство("ПричинаПереносаСрока") Тогда
		
		Возврат;
	КонецЕсли;
	
	СрокИсполненияПроцесса = 
		ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Источник.Ссылка, "СрокИсполненияПроцесса");
		
	Источник.ДополнительныеСвойства.Вставить("СтарыйСрокИсполненияПроцесса", СрокИсполненияПроцесса)
	
КонецПроцедуры

// Обработчик подписки ПереносСроковИсполненияПриЗаписиПроцесса
//
Процедура ПереносСроковИсполненияПриЗаписиПроцесса(Источник, Отказ) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если Не ПолучитьФункциональнуюОпцию("ВестиУчетПереносовСроковЗадач")
		Или Не Источник.ДополнительныеСвойства.Свойство("ПричинаПереносаСрока")
		Или Источник.ДополнительныеСвойства.СтарыйСрокИсполненияПроцесса = Источник.СрокИсполненияПроцесса Тогда
		
		Возврат;
	КонецЕсли;
	
	ТекущийУзелОбмена = РаботаСБизнесПроцессами.ЭтотУзелОбменаДляОбработкиПроцессов();
	
	Если Источник.УзелОбмена <> ТекущийУзелОбмена Тогда
		Возврат;
	КонецЕсли;
	
	МенеджерЗаписи = РегистрыСведений.ПереносыСроковВыполненияПроцессовИЗадач.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.ДатаПереносаСрока = ТекущаяДатаСеанса();
	МенеджерЗаписи.Предмет = Источник.Ссылка;
	МенеджерЗаписи.СтарыйСрок = Источник.ДополнительныеСвойства.СтарыйСрокИсполненияПроцесса;
	МенеджерЗаписи.НовыйСрок = Источник.СрокИсполненияПроцесса;
	
	Если Источник.ДополнительныеСвойства.Свойство("ПричинаПереносаСрока") Тогда
		МенеджерЗаписи.КомментарийАвтора = Источник.ДополнительныеСвойства.ПричинаПереносаСрока;
	КонецЕсли;
	
	МенеджерЗаписи.Пользователь = ПользователиКлиентСервер.ТекущийПользователь();
	
	Если Источник.ДополнительныеСвойства.Свойство("БизнесПроцессПереноса") Тогда
		МенеджерЗаписи.БизнесПроцессПереноса = Источник.ДополнительныеСвойства.БизнесПроцессПереноса;
		МенеджерЗаписи.Итерация = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(
			МенеджерЗаписи.БизнесПроцессПереноса, "Итерация");
	КонецЕсли;
	
	МенеджерЗаписи.Записать();
	
КонецПроцедуры

#КонецОбласти




