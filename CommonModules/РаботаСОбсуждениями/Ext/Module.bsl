////////////////////////////////////////////////////////////////////////////////
// Модуль для работы с форумом.
//
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Возвращает очередной номер для нового сообщения в теме,
// т.е. номер, следующий за максимальным уже присвоенным номером в теме.
// Если в теме нет сообщений, возвращает 1.
Функция ПосчитатьНомерНовогоСообщения(Тема) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	МАКСИМУМ(СообщенияОбсуждений.НомерСообщения) КАК НомерСообщения
		|ИЗ
		|	Справочник.СообщенияОбсуждений КАК СообщенияОбсуждений
		|ГДЕ
		|	СообщенияОбсуждений.ВладелецСообщения = &Тема
		|
		|СГРУППИРОВАТЬ ПО
		|	СообщенияОбсуждений.ВладелецСообщения";			   
	Запрос.УстановитьПараметр("Тема", Тема);			   		   
	Результат = Запрос.Выполнить();
	
	Если Результат.Пустой() Тогда 
		Возврат 1;
	КонецЕсли;
	
	Выборка = Результат.Выбрать();
	Выборка.Следующий();
	Возврат Выборка.НомерСообщения + 1;
			
КонецФункции

// Возвращает количество сообщений в теме, не считая удаленные сообщения.
Функция ПосчитатьКоличествоСообщений(Тема) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЕстьNULL(КОЛИЧЕСТВО(РАЗЛИЧНЫЕ СообщенияОбсуждений.Ссылка),0) КАК КоличествоСообщений
		|ИЗ
		|	Справочник.СообщенияОбсуждений КАК СообщенияОбсуждений
		|ГДЕ
		|	СообщенияОбсуждений.ВладелецСообщения = &Тема
		|	И СообщенияОбсуждений.ПометкаУдаления = ЛОЖЬ";
				   
	Запрос.УстановитьПараметр("Тема", Тема);			   	   
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	Выборка.Следующий();
	Возврат Выборка.КоличествоСообщений;
			
КонецФункции

// Возвращает первое сообщение темы. Если первого сообщения нет - возвращает пустую ссылку.
Функция НайтиПервоеСообщениеТемы(Тема) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	СообщенияОбсуждений.Ссылка
		|ИЗ
		|	Справочник.СообщенияОбсуждений КАК СообщенияОбсуждений
		|ГДЕ
		|	СообщенияОбсуждений.ВладелецСообщения = &Тема
		|	И СообщенияОбсуждений.ПервоеСообщениеТемы = ИСТИНА
		|
		|СГРУППИРОВАТЬ ПО
		|	СообщенияОбсуждений.ВладелецСообщения,
		|	СообщенияОбсуждений.Ссылка";
	Запрос.УстановитьПараметр("Тема", Тема);
	Результат = Запрос.Выполнить();
	
	Если Результат.Пустой() Тогда 
		Возврат Справочники.ТемыОбсуждений.ПустаяСсылка();
	КонецЕсли;
	
	Выборка = Результат.Выбрать();
	Выборка.Следующий();
	Возврат Выборка.Ссылка;
	
КонецФункции

// Возвращает список сообщений темы. Если сообщений нет - вернет Неопределено.
Функция ПолучитьСписокСообщенийТемы(Тема) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	СообщенияОбсуждений.Ссылка
		|ИЗ
		|	Справочник.СообщенияОбсуждений КАК СообщенияОбсуждений
		|ГДЕ
		|	СообщенияОбсуждений.ВладелецСообщения = &Тема";
	Запрос.УстановитьПараметр("Тема", Тема);
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	СообщенияТемы = Новый Массив;
	Пока Выборка.Следующий() Цикл
		СообщенияТемы.Добавить(Выборка.Ссылка);
	КонецЦикла;
	
	Возврат СообщенияТемы;
	
КонецФункции

// Устанавливает нового родителя у массива сообщений.
// Если у какого-либо сообщения тема не совпадает 
// с темой нового владельца, то перенос сообщений не производится.
// Если элемент массива имеет тип не сообщения, то перенос не производится.
// Тема родителя необходима в связи с тем, что в НовомРодители может оказаться 
// значение Неопределено, а в данном случае все равно необходимо проверять перенос 
// в другие темы.
Функция ПереносСообщения(МассивСообщений, НовыйРодитель, ТемаРодителя) Экспорт
	
	Для каждого Сообщение Из МассивСообщений Цикл
		Если ТипЗнч(Сообщение) <> Тип("СправочникСсылка.СообщенияОбсуждений") Тогда
			Возврат 1;	
		КонецЕсли;
		
		Если Сообщение.ВладелецСообщения <> ТемаРодителя Тогда
			Возврат 2;
		КонецЕсли;
		
		Если Сообщение.ПервоеСообщениеТемы = Истина Тогда
			Возврат 3;	
		КонецЕсли
	КонецЦикла;
	
	Для каждого Сообщение Из МассивСообщений Цикл
		СообщениеОбъект = Сообщение.ПолучитьОбъект();
		СообщениеОбъект.Родитель = НовыйРодитель;
		СообщениеОбъект.Записать();
	КонецЦикла;
	
КонецФункции

// Устанавливает нового родителя у массива тем.
// Если элемент массива имеет тип не сообщения, то перенос 
// не производится и возвращает значение Ложь.
// Если перенос прошел успешно возвращает значение Истина.
Функция ПереносТемы(МассивТем, НовыйРодитель) Экспорт
	
	Для каждого Тема Из МассивТем Цикл
		Если ТипЗнч(Тема) <> Тип("СправочникСсылка.ТемыОбсуждений") Тогда
			Возврат Ложь;	
		КонецЕсли;
	КонецЦикла;
	
	Для каждого Тема Из МассивТем Цикл
		ТемаОбъект = Тема.ПолучитьОбъект();
		ТемаОбъект.Родитель = НовыйРодитель;
		ТемаОбъект.Записать();
	КонецЦикла;
	
	Возврат Истина;
	
КонецФункции

// Обновляет информацию о теме сообщения в регистре сведений.
Процедура ОбновитьИнформациюОТеме(Тема) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	СообщенияОбсуждений.Автор КАК АвторПоследнегоСообщения,
		|	ВЫБОР
		|		КОГДА СообщенияОбсуждений.ДатаСоздания > СообщенияОбсуждений.ДатаИзменения
		|			ТОГДА СообщенияОбсуждений.ДатаСоздания
		|		ИНАЧЕ СообщенияОбсуждений.ДатаИзменения
		|	КОНЕЦ КАК ДатаПоследнегоСообщения
		|ИЗ
		|	Справочник.СообщенияОбсуждений КАК СообщенияОбсуждений
		|ГДЕ
		|	СообщенияОбсуждений.ВладелецСообщения = &Тема
		|	И СообщенияОбсуждений.ПометкаУдаления = ЛОЖЬ
		|
		|УПОРЯДОЧИТЬ ПО
		|	ДатаПоследнегоСообщения УБЫВ";
	
	Запрос.УстановитьПараметр("Тема", Тема);
	
	Результат = Запрос.Выполнить();
	
	Если Результат.Пустой() Тогда 
		АвторПоследнегоСообщения = Неопределено;
		ДатаПоследнегоСообщения = Неопределено;
		КоличествоСообщений = 0;
	Иначе
		Выборка = Результат.Выбрать();
		Выборка.Следующий();
		АвторПоследнегоСообщения = Выборка.АвторПоследнегоСообщения;
		ДатаПоследнегоСообщения = Выборка.ДатаПоследнегоСообщения;
		КоличествоСообщений = ПосчитатьКоличествоСообщений(Тема)
	КонецЕсли;
	
	РегистрыСведений.СведенияОТемахОбсуждений.УстановитьСведенияОТеме(
		Тема, 
		АвторПоследнегоСообщения, 
		ДатаПоследнегоСообщения, 
		КоличествоСообщений);
	
КонецПроцедуры

// Возвращает тему по предмету
Функция НайтиТемуПоПредмету(Предмет) Экспорт
	
	Если Предмет = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ТемыОбсуждений.Ссылка КАК Тема
		|ИЗ
		|	Справочник.ТемыОбсуждений КАК ТемыОбсуждений
		|ГДЕ
		|	ТемыОбсуждений.Документ = &Документ";
	Запрос.УстановитьПараметр("Документ", Предмет);
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		Возврат Неопределено;
	Иначе
		Выборка = Результат.Выбрать();
		Выборка.Следующий();
		Возврат Выборка.Тема;
	КонецЕсли;
	
КонецФункции

// Возвращает запись регистра сведений СведенияОТемахОбсуждений,
// соответствующую переданной Теме. Если переданная тема не заполнено,
// возвращает Неопределено.
Функция ПолучитьСведенияОТеме(Тема) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если ЗначениеЗаполнено(Тема) Тогда
		СведенияОТеме = РегистрыСведений.СведенияОТемахОбсуждений.ПолучитьСведенияОТеме(Тема);
		Возврат СведенияОТеме;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

// Удаляет все записи в регистре СведенияОГолосовании, соответствующие сообщению. 
Процедура УдалитьГолосование(Сообщение) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	РегистрыСведений.СведенияОГолосовании.УбратьСведенияОГолосовании(Сообщение);
	
КонецПроцедуры

// Голосует за выбранный вариант от имени текущего пользователя.
//
// Параметры:
//  Сообщение - СправочникСсылка.СообщенияОбсуждений - Сообщение, по которому открыто голосование.
//  ВариантОтвета - Число - Вариант ответа.
//
Процедура Проголосовать(Сообщение, ВариантОтвета) Экспорт
	
	Если Не ЗначениеЗаполнено(Сообщение) Тогда
		ОписаниеОшибки = НСтр("ru = 'Невозможно проголосовать: не указано сообщение для голосования.'; en = 'You cannot vote: poll message is not specified.'");
		ВызватьИсключение ОписаниеОшибки;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ВариантОтвета) Тогда
		ОписаниеОшибки = НСтр("ru = 'Невозможно проголосовать: не указан вариант голосования.'; en = 'You cannot vote: vote option is not specified.'");
		ВызватьИсключение ОписаниеОшибки;
	КонецЕсли;
	
	РеквизитыСообщения = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Сообщение,
		"ВладелецСообщения, ДобавленоГолосование");
	Если Не РеквизитыСообщения.ДобавленоГолосование Тогда
		ОписаниеОшибки = НСтр("ru = 'Невозможно проголосовать: не найдено голосование.'; en = 'You cannot vote: the poll is not found.'");
		ВызватьИсключение ОписаниеОшибки;
	КонецЕсли;
	
	ТемаЗакрыта = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(
		РеквизитыСообщения.ВладелецСообщения, "Закрытая");
	Если ТемаЗакрыта Тогда
		ОписаниеОшибки = НСтр("ru = 'Невозможно проголосовать: тема закрыта.'; en = 'You cannot vote: the thread is closed.'");
		ВызватьИсключение ОписаниеОшибки;
	КонецЕсли;
	
	ВариантОтветаЗначениеВРегистре = РегистрыСведений.СведенияОГолосовании.ПолучитьВариантОтветаПользователя(Сообщение);
	Если ВариантОтвета <> ВариантОтветаЗначениеВРегистре Тогда
		РегистрыСведений.СведенияОГолосовании.УстановитьВариантОтветаПользователя(Сообщение, ВариантОтвета);
	КонецЕсли;
	
КонецПроцедуры

// Удаляет запись в регистре сведений СведенияОГолосовании, соответствующую голосу 
// текущего пользователя в голосовании переданного сообщения.
Процедура ОтменитьГолос(Сообщение) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ТемаЗакрыта = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Сообщение.ВладелецСообщения, "Закрытая");
	Если ТемаЗакрыта Тогда
		Текст = НСтр("ru = 'Нельзя отменить голос, т.к. тема закрыта автором.'; en = 'You cannot cancel your vote because the thread has been closed.'");
		ВызватьИсключение Текст;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Сообщение) Тогда
		РегистрыСведений.СведенияОГолосовании.ОтменитьГолосПользователя(Сообщение);
	КонецЕсли;
	
КонецПроцедуры

// Функция возвращает вариант ответа текущего пользователя на вопрос голосования сообщения.
Функция ПолучитьРезультатГолосованияТекущегоПользователя(Сообщение) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если ЗначениеЗаполнено(Сообщение) Тогда
		Возврат РегистрыСведений.СведенияОГолосовании.ПолучитьВариантОтветаПользователя(Сообщение);
	КонецЕсли;
	
КонецФункции

// Функция возвращает список значений, содержащий результат голосования переданного сообщения.
// Значение соответствует варианту ответа голосования, представление соответствует строке 
// с результатом голосования за данный вариант.
Функция ПолучитьРезультатГолосования(Сообщение) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ СведенияОГолосовании.Пользователь) КАК КоличествоГолосов,
		|	СведенияОГолосовании.ВариантОтвета
		|ИЗ
		|	РегистрСведений.СведенияОГолосовании КАК СведенияОГолосовании
		|ГДЕ
		|	СведенияОГолосовании.Сообщение = &Сообщение
		|
		|СГРУППИРОВАТЬ ПО
		|	СведенияОГолосовании.ВариантОтвета";

	Запрос.УстановитьПараметр("Сообщение", Сообщение);

	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();

	СписокРезультатГолосования = Новый СписокЗначений();
	ВсегоГолосов = 0;
	Пока Выборка.Следующий() Цикл
		
		СписокРезультатГолосования.Добавить(Выборка.ВариантОтвета, Выборка.КоличествоГолосов);
		ВсегоГолосов = ВсегоГолосов + Выборка.КоличествоГолосов; 

	КонецЦикла;
	
	Если Не ЗначениеЗаполнено(ВсегоГолосов) Тогда
		ВсегоГолосов = 0;
	КонецЕсли;
	
	Голосование = Новый СписокЗначений();
	Для каждого ВариантГолосования Из Сообщение.ВариантыГолосования Цикл
		ГолосовЗаВариантЭлементСписка = СписокРезультатГолосования.НайтиПоЗначению(ВариантГолосования.НомерСтроки);
		Если ГолосовЗаВариантЭлементСписка = Неопределено Тогда
			ГолосовЗаВариант = 0;
		Иначе
			ГолосовЗаВариант = Число(ГолосовЗаВариантЭлементСписка.Представление);
		КонецЕсли;
		
		Если ВсегоГолосов = 0 Тогда
			ПроцентГолосовЗаВариант = 0;
		Иначе
			ПроцентГолосовЗаВариант = Окр(ГолосовЗаВариант/ВсегоГолосов*100);
		КонецЕсли;
		
		РезультатГолосования = 
			"" + ВариантГолосования.НомерСтроки 
			+ ". " + ВариантГолосования.ТекстВарианта 
			+ " " + ГолосовЗаВариант 
			+ " (" + ПроцентГолосовЗаВариант + "%);";
		
		Голосование.Добавить(ВариантГолосования.НомерСтроки, РезультатГолосования);
	КонецЦикла;
	
	Голосование.Добавить(0, НСтр("ru = 'Всего голосов'; en = 'Votes total'") + ": " + ВсегоГолосов + ".");
	Возврат Голосование; 
	
КонецФункции

// Функция возвращает строкой представление результата голоса текущего пользователя
// по голосованию сообщения.
Функция ПолучитьПредставлениеГолосаПользователя(Сообщение) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ПредставлениеГолосаПользователя = "";
	Если ЗначениеЗаполнено(Сообщение) Тогда
		ВариантГолосования = ПолучитьРезультатГолосованияТекущегоПользователя(Сообщение);
		НайденнаяСтрока = Сообщение.ВариантыГолосования.Найти(ВариантГолосования, "НомерСтроки");
		Если НайденнаяСтрока <> Неопределено Тогда
			ПредставлениеГолосаПользователя = "" + НайденнаяСтрока.НомерСтроки + ". " + НайденнаяСтрока.ТекстВарианта;
		КонецЕсли;
	КонецЕсли;
	
	Возврат ПредставлениеГолосаПользователя;
	
КонецФункции

// Формирует текстовое представление темы.
Функция СформироватьТекстовоеПредставлениеТемы(Тема, ПервоеСообщениеТемы = Неопределено) Экспорт
	
	Если Не ЗначениеЗаполнено(Тема) Тогда
		Возврат "";
	КонецЕсли;
	
	Если ПервоеСообщениеТемы = Неопределено Тогда
		
		ПервоеСообщениеТемы = НайтиПервоеСообщениеТемы(Тема);
		
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ПервоеСообщениеТемы) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстовоеПредставлениеТемы = СформироватьТекстовоеПредставлениеСообщения(ПервоеСообщениеТемы);
	
	Возврат ТекстовоеПредставлениеТемы;
	
КонецФункции

// Формирует текстовое представление сообщения.
Функция СформироватьТекстовоеПредставлениеСообщения(Сообщение) Экспорт
	
	Если Не ЗначениеЗаполнено(Сообщение) Тогда
		Возврат "";
	КонецЕсли;
	
	ДатаСообщения = Макс(Сообщение.ДатаСоздания, Сообщение.ДатаИзменения);
	
	ТекстовоеПредставлениеСообщения =
		РаботаСОбсуждениямиКлиентСервер.СформироватьТекстовоеПредставлениеСообщения(
			Сообщение.ТекстСообщения,
			Сообщение.Автор,
			ДатаСообщения);
	
	Возврат ТекстовоеПредставлениеСообщения;
	
КонецФункции

// Возвращает таблицу значений с информацией о вложениях сообщения.
Функция ПолучитьФайлыСообщения(
	Сообщение,
	ФормироватьПредставлениеРазмера = Ложь,
	ВключатьПомеченныеНаУдаление = Ложь) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	Файлы.Ссылка КАК Ссылка,
		|	ПРЕДСТАВЛЕНИЕ(Файлы.Ссылка) КАК Представление,
		|	Файлы.ИндексКартинки КАК ИндексКартинки,
		|	Файлы.ТекущаяВерсияРазмер КАК Размер,
		|	ВЫБОР
		|		КОГДА Файлы.ТекущаяВерсияРасширение = """"
		|			ТОГДА Файлы.Наименование
		|		ИНАЧЕ Файлы.Наименование + ""."" + Файлы.ТекущаяВерсияРасширение
		|	КОНЕЦ КАК ИмяФайла,
		|	ВЫРАЗИТЬ("""" КАК СТРОКА(20)) КАК РазмерПредставление,
		|	Файлы.ПометкаУдаления КАК ПометкаУдаления,
		|	Файлы.Редактирует КАК Редактирует,
		|	ВЫБОР
		|		КОГДА Файлы.Редактирует = &ТекущийПользователь
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК РедактируетТекущийПользователь,
		|	Файлы.ТекущаяВерсия КАК ТекущаяВерсия,
		|	Файлы.ТекущаяВерсияРасширение КАК ТекущаяВерсияРасширение,
		|	Файлы.ТекущаяВерсияДатаМодификацииФайла КАК ТекущаяВерсияДатаМодификацииФайла
		|ИЗ
		|	Справочник.Файлы КАК Файлы
		|ГДЕ
		|	Файлы.ВладелецФайла = &Сообщение
		|	И (&ВключатьПомеченныеНаУдаление
		|			ИЛИ НЕ Файлы.ПометкаУдаления)");
	Запрос.УстановитьПараметр("Сообщение", Сообщение);
	Запрос.УстановитьПараметр("ВключатьПомеченныеНаУдаление", ВключатьПомеченныеНаУдаление);
	Запрос.УстановитьПараметр("ТекущийПользователь", Пользователи.ТекущийПользователь());
	Результат = Запрос.Выполнить().Выгрузить();
	
	Если ФормироватьПредставлениеРазмера Тогда
		Для каждого СтрокаТаблицы Из Результат Цикл
			СтрокаТаблицы.РазмерПредставление = РаботаСоСтроками.ПолучитьРазмерСтрокой(СтрокаТаблицы.Размер);
		КонецЦикла;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Возвращает персональную настройку форума текущего пользователя.
// Доступные настройки: ШрифтОбластиЧтения, ВыбранныеРазделы, ДоступныеРазделы, ОтображатьВсеРазделы.
//
// Параметры:
//  Настройка - Строка - Имя настройки.
//
// Возвращаемое значение:
//  Произвольное - Значение настройки.
//
Функция ПолучитьПерсональнуюНастройку(Настройка) Экспорт
	
	Если Настройка = "ШрифтОбластиЧтения" Тогда
		
		Значение = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
			"НастройкиФорума",
			"ШрифтОбластиЧтения",
			Новый Шрифт(ШрифтыСтиля.ШрифтТекста, , 12));
		
	ИначеЕсли Настройка = "ВыбранныеРазделы" Тогда
		
		Значение = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
			"НастройкиФорума",
			"ВыбранныеРазделы",
			Новый СписокЗначений);
		
	ИначеЕсли Настройка = "ДоступныеРазделы" Тогда
		
		Значение = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
			"НастройкиФорума",
			"ДоступныеРазделы",
			Неопределено);
		
	ИначеЕсли Настройка = "ОтображатьВсеРазделы" Тогда
		
		Значение = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
			"НастройкиФорума",
			"ОтображатьВсеРазделы",
			Ложь);
		
	Иначе
		
		ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Некорректная настройка форума: ""%1""'; en = 'Incorrect forum settings: ""%1""'"),
			Настройка);
		
	КонецЕсли;
	
	Возврат Значение;
	
КонецФункции

// Возвращает персональную настройку форума текущего пользователя.
// Доступные настройки: ШрифтОбластиЧтения, ВыбранныеРазделы, ДоступныеРазделы, ОтображатьВсеРазделы.
//
// Параметры:
//  Настройка - Строка - Имя настройки.
//  Настройка - Произвольное - Значение настройки.
//
Процедура УстановитьПерсональнуюНастройку(Настройка, Значение) Экспорт
	
	Если Настройка = "ШрифтОбластиЧтения" Тогда
		
		Если ТипЗнч(Значение) <> Тип("Шрифт") Тогда
			
			ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Некорректное значение настройки форума: ""%1""'; en = 'Incorrect forum settings value: ""%1""'"),
				Настройка);
			
		КонецЕсли;
		
		ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(
			"НастройкиФорума",
			"ШрифтОбластиЧтения",
			Значение);
		
	ИначеЕсли Настройка = "ВыбранныеРазделы" Тогда
		
		Если ТипЗнч(Значение) <> Тип("СписокЗначений") Тогда
			
			ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Некорректное значение настройки форума: ""%1""'; en = 'Incorrect forum settings value: ""%1""'"),
				Настройка);
			
		КонецЕсли;
		
		ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(
			"НастройкиФорума",
			"ВыбранныеРазделы",
			Значение);
		
	ИначеЕсли Настройка = "ДоступныеРазделы" Тогда
		
		Если ТипЗнч(Значение) <> Тип("СписокЗначений") Тогда
			
			ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Некорректное значение настройки форума: ""%1""'; en = 'Incorrect forum settings value: ""%1""'"),
				Настройка);
			
		КонецЕсли;
		
		ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(
			"НастройкиФорума",
			"ДоступныеРазделы",
			Значение);
		
	ИначеЕсли Настройка = "ОтображатьВсеРазделы" Тогда
		
		Если ТипЗнч(Значение) <> Тип("Булево") Тогда
			
			ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Некорректное значение настройки форума: ""%1""'; en = 'Incorrect forum settings value: ""%1""'"),
				Настройка);
			
		КонецЕсли;
		
		ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(
			"НастройкиФорума",
			"ОтображатьВсеРазделы",
			Значение);
		
	Иначе
		
		ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Некорректная настройка форума: ""%1""'; en = 'Incorrect forum settings: ""%1""'"),
			Настройка);
		
	КонецЕсли;
	
КонецПроцедуры

// Формирует небольшой заголовок по переданному тексту.
//
// Параметры:
//  ОсновнойТекст - Строка - Текст, из которого формируется заголовок.
//
// Возвращаемое значение:
//  Строка - Текст заголовка.
//
Функция ПолучитьЗаголовок(ОсновнойТекст) Экспорт
	
	ДлинаЗаголовка = Мин(40, СтрДлина(ОсновнойТекст));
	ТекстЗаголовка = Лев(ОсновнойТекст, ДлинаЗаголовка);
	Если СтрДлина(ОсновнойТекст) > ДлинаЗаголовка Тогда
		ТекстЗаголовка = ТекстЗаголовка + "...";
	КонецЕсли;
	
	Возврат ТекстЗаголовка;
	
КонецФункции

// Возвращает представление предмета.
Функция ПолучитьПредставлениеПредмета(Предмет) Экспорт
	
	ПредставлениеПредмета = "";
	Если Не ЗначениеЗаполнено(Предмет) Тогда
		Возврат ПредставлениеПредмета;
	КонецЕсли;
	
	ПраваПоПредмету = ДокументооборотПраваДоступа.ПолучитьПраваПоОбъекту(
		Предмет,
		ПользователиКлиентСервер.ТекущийПользователь());
	Если Не ПраваПоПредмету.Чтение Тогда
		ПредставлениеПредмета = Строка(Предмет);
		Возврат ПредставлениеПредмета;
	КонецЕсли;
	
	Если ТипЗнч(Предмет) = Тип("ЗадачаСсылка.ЗадачаИсполнителя") Тогда
		Наименование = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Предмет, "Наименование");
	Иначе
		Наименование = Строка(Предмет)
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Предмет.Метаданные().ПредставлениеОбъекта) Тогда 
		ПредставлениеОбъекта = Предмет.Метаданные().ПредставлениеОбъекта; 
	Иначе 	
		ПредставлениеОбъекта = Предмет.Метаданные().Синоним;
	КонецЕсли;
	
	ПредставлениеПредмета = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("%1 (%2)",
		Наименование, ПредставлениеОбъекта);
	
	Возврат ПредставлениеПредмета;
	
КонецФункции

// Меняет раздел для массива тем форума на новый.
//
// Параметры:
//  МассивТем - Массив - Тема форума, у которой необходимо изменить раздел.
//  НовыйРаздел - СправочникСсылка.ПапкиФорума - Новый раздел темы форума.
//
// Возвращаемое значение:
//  Булево - Успешное изменение раздела форума.
//
Функция ИзменитьРазделТем(МассивТем, НовыйРаздел) Экспорт
	
	// Не указан новый раздел
	Если Не ЗначениеЗаполнено(НовыйРаздел) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	// Нет элементов в массиве
	Если МассивТем.Количество() = 0 Тогда
		Возврат Ложь;
	КонецЕсли;
	
	// Раздел не изменился
	Если МассивТем[0].Папка = НовыйРаздел Тогда
		Возврат Ложь;
	КонецЕсли;
	
	// Проверка прав тем форума
	Для Каждого ТемаСсылка Из МассивТем Цикл
		Если Не ДокументооборотПраваДоступа.ПолучитьПраваПоОбъекту(ТемаСсылка).Изменение Тогда 
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'У вас нет права на изменение мероприятия ""%1"".'; en = 'Not enough permissions to change event ""%1"".'"),
				Строка(ТемаСсылка));
			ВызватьИсключение ТекстОшибки;
		КонецЕсли;
	КонецЦикла;
	
	НачатьТранзакцию();
	Попытка
		Для Каждого ТемаСсылка Из МассивТем Цикл
			ЗаблокироватьДанныеДляРедактирования(ТемаСсылка);
			ТемаОбъект = ТемаСсылка.ПолучитьОбъект();
			ТемаОбъект.Папка = НовыйРаздел;
			ТемаОбъект.Записать();
		КонецЦикла;
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		ВызватьИсключение;
	КонецПопытки;
	
	Возврат Истина;
	
КонецФункции

// Получает структуру разделов отображения.
//
// Возвращаемое значение:
//  Структура - Структура разделов отображения.
//   * ВыбранныеРазделы - СписокЗначений - Выбранные пользователем разделы отображения.
//   * ДоступныеРазделы - СписокЗначений - Доступные пользователю по правам разделы.
//   * ОтображатьВсеРазделы - Булево - Признак отображения всех разделов.
//
Функция ПолучитьРазделыОтображения() Экспорт
	
	// Доступные разделы
	ДоступныеРазделыСейчас = ПолучитьДоступныеРазделы();
	ДоступныеРазделы = Новый СписокЗначений;
	ДоступныеРазделы.ЗагрузитьЗначения(ДоступныеРазделыСейчас);
	
	// Отображать все разделы
	ОтображатьВсеРазделы = ПолучитьПерсональнуюНастройку("ОтображатьВсеРазделы");
	
	РазделыОтображения = Новый Структура("ВыбранныеРазделы, ДоступныеРазделы, ОтображатьВсеРазделы");
	РазделыОтображения.ВыбранныеРазделы = Новый СписокЗначений;
	РазделыОтображения.ДоступныеРазделы = ДоступныеРазделы;
	РазделыОтображения.ОтображатьВсеРазделы = ОтображатьВсеРазделы;
	
	// Если отображаем все разделы то вести список выбранных разделов не нужно.
	Если ОтображатьВсеРазделы Тогда
		Возврат РазделыОтображения;
	КонецЕсли;
	
	// Если ранее не было доступных разделов - значит первый запуск. Не производим коррекцию.
	ДоступныеРазделыРанее = ПолучитьПерсональнуюНастройку("ДоступныеРазделы");
	Если ДоступныеРазделыРанее = Неопределено Тогда
		РазделыОтображения.ОтображатьВсеРазделы = Истина;
		Возврат РазделыОтображения;
	КонецЕсли;
	
	// Список выбранных разделов корректируем вновь ставшими доступными разделами.
	ВыбранныеРазделы = ПолучитьПерсональнуюНастройку("ВыбранныеРазделы");
	Для Каждого ДоступныйРаздел Из ДоступныеРазделыСейчас Цикл
		
		// Проверка был ли раздел выбран ранее.
		Если ВыбранныеРазделы.НайтиПоЗначению(ДоступныйРаздел) <> Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		// Проверка был ли доступен раздел ранее.
		Если ДоступныеРазделыРанее.НайтиПоЗначению(ДоступныйРаздел) <> Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		// Новый доступный раздел. Требуется скорректировать его выбор.
		КорректироватьРаздел = Истина;
		Родитель = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДоступныйРаздел, "Родитель");
		Пока КорректироватьРаздел Цикл
			
			КорректироватьРаздел = Ложь;
			
			// Корневой раздел следует включить в выбранные разделы.
			Если Не ЗначениеЗаполнено(Родитель) Тогда
				ВыбранныеРазделы.Добавить(ДоступныйРаздел);
				Прервать;
			КонецЕсли;
			
			// Если родитель был выбран - следует включать в выбранные разделы.
			Если ВыбранныеРазделы.НайтиПоЗначению(Родитель) <> Неопределено Тогда
				ВыбранныеРазделы.Добавить(ДоступныйРаздел);
				Прервать;
			КонецЕсли;
			
			// Если родитель был не был доступен - следует проверить родителя уровнем выше.
			Если ДоступныеРазделыРанее.НайтиПоЗначению(Родитель) = Неопределено Тогда
				Родитель = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Родитель, "Родитель");
				КорректироватьРаздел = Истина;
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;
	РазделыОтображения.ВыбранныеРазделы = ВыбранныеРазделы;
	
	Возврат РазделыОтображения;
	
КонецФункции

// Помечает раздел форума на удаление.
//
// Параметры:
//  ПапкаФорума		 - СправочникСсылка.ПапкиФорума	 - ПапкаФорума, которую помечаем на удаление.
//  ПометкаУдаления	 - Булево						 - Текущее значение пометки удаления раздела.
//
Процедура УстановитьПометкуУдаленияПапкиФорума(Знач ПапкаФорума, Знач ПометкаУдаления) Экспорт
	
	ПапкаФорумаОбъект = ПапкаФорума.ПолучитьОбъект();
	ПапкаФорумаОбъект.Заблокировать();
	ПапкаФорумаОбъект.УстановитьПометкуУдаления(ПометкаУдаления);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Формирует массив доступных по правам разделов.
//
// Возвращаемое значение:
//  Массив - Доступные разделы.
//
Функция ПолучитьДоступныеРазделы()
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ПапкиФорума.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.ПапкиФорума КАК ПапкиФорума
		|ГДЕ
		|	ПапкиФорума.ПометкаУдаления = ЛОЖЬ";
	
	ДоступныеРазделы = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
	
	Возврат ДоступныеРазделы;
	
КонецФункции

#КонецОбласти
