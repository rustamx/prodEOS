////////////////////////////////////////////////////////////////////////////////
// Подсистема "Центр Мониторинга"
//
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Выполняется при запуске регламентного задания.
//
Процедура ПриСбореПоказателейСтатистикиКонфигурации() Экспорт
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	ЗамерыМетрик.Показатель,
		|	МАКСИМУМ(ЗамерыМетрик.Дата) КАК Дата
		|ПОМЕСТИТЬ ДатыПоследнихЗамеров
		|ИЗ
		|	РегистрСведений.ЗамерыМетрик КАК ЗамерыМетрик
		|ГДЕ
		|	ЗамерыМетрик.Показатель.Предопределенный
		|
		|СГРУППИРОВАТЬ ПО
		|	ЗамерыМетрик.Показатель
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Метрики.ИмяПредопределенныхДанных КАК Метрика,
		|	ЗамерыМетрик.Дата,
		|	ЗамерыМетрик.Результат
		|ИЗ
		|	ДатыПоследнихЗамеров КАК ДатыПоследнихЗамеров
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ЗамерыМетрик КАК ЗамерыМетрик
		|		ПО ДатыПоследнихЗамеров.Показатель = ЗамерыМетрик.Показатель
		|			И ДатыПоследнихЗамеров.Дата = ЗамерыМетрик.Дата
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Метрики КАК Метрики
		|		ПО ДатыПоследнихЗамеров.Показатель = Метрики.Ссылка");
		
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ЦентрМониторинга.ЗаписатьСтатистикуОбъектаКонфигурации(
			"Справочник.Метрики." + Выборка.Метрика, Выборка.Результат);
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти
