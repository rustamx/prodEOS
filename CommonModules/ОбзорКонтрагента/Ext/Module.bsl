
Процедура ДобавитьЗначениеСКартинкой(HTMLТекст, Поле, Картинка, МассивЗначений, СсылкаНаРесурс = "")
	
	HTMLТекст = HTMLТекст + "<table cellpadding=""1"" style=""border-collapse: collapse;"">";
	HTMLТекст = HTMLТекст + "<tr align=""left"">";
	HTMLТекст = HTMLТекст + "<td width=""20"">";
	ДобавитьКартинку(HTMLТекст, Картинка,, Поле);
	HTMLТекст = HTMLТекст + "</td>";
	HTMLТекст = HTMLТекст + "<td>";
	
	Если ЗначениеЗаполнено(МассивЗначений) И ЗначениеЗаполнено(СсылкаНаРесурс) Тогда
		
		Количество = МассивЗначений.Количество();
		Для Ит = 0 По Количество - 1 Цикл 
			Значение = МассивЗначений.Получить(Ит);
			РаботаС_HTML.ЗаменитьСпецСимволыHTML(Значение);
			
			Если Ит = Количество - 1 Тогда 
				HTMLТекст = HTMLТекст + СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					"<A href=v8doc:%1>%2</A><BR>",
					СсылкаНаРесурс + Значение, Значение);
			Иначе 
				HTMLТекст = HTMLТекст + СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					"<A href=v8doc:%1>%2</A>",
					СсылкаНаРесурс + Значение, Значение);
				HTMLТекст = HTMLТекст + ", ";
			КонецЕсли;
		КонецЦикла;
		
	ИначеЕсли ЗначениеЗаполнено(МассивЗначений) Тогда 
		Количество = МассивЗначений.Количество();
		Для Ит = 0 По Количество - 1 Цикл 
			Значение = МассивЗначений.Получить(Ит);
			
			Если Ит = Количество - 1 Тогда 
				ОбзорОбъектовКлиентСервер.ДобавитьЗначение(HTMLТекст, Значение, "");
			Иначе 
				ОбзорОбъектовКлиентСервер.ДобавитьЗначение(HTMLТекст, Значение, "");
				HTMLТекст = HTMLТекст + ", ";
			КонецЕсли;
		КонецЦикла;
		
	Иначе 
		ОбзорОбъектовКлиентСервер.ДобавитьЗначение(HTMLТекст, НСтр("ru = 'не указан'; en = 'not specified'"), "696969");
		
	КонецЕсли;
	
	HTMLТекст = HTMLТекст + "</td>";
	HTMLТекст = HTMLТекст + "</tr>";
	HTMLТекст = HTMLТекст + "</table>";
	
КонецПроцедуры

Процедура ДобавитьКартинку(HTMLТекст, Картинка, Ссылка = "", Подсказка = "") 
	
	ДвоичныеДанныеКартинки = Картинка.ПолучитьДвоичныеДанные();
	Base64ДанныеКартинки = Base64Строка(ДвоичныеДанныеКартинки);
	
	Если ЗначениеЗаполнено(Ссылка) Тогда
		HTMLТекст = HTMLТекст + "<a href=v8doc:"+Ссылка+">";
	КонецЕсли;
	
	HTMLТекст = HTMLТекст
		+ "<img src='data:image/"
		+ Картинка.Формат()
		+ ";base64,"
		+ Base64ДанныеКартинки
		+ "' title='"
		+ Подсказка
		+ "' align='absmiddle' style='margin-right:5px' border='0' >";
		
	Если ЗначениеЗаполнено(Ссылка) Тогда
		HTMLТекст = HTMLТекст + "</a>";
	КонецЕсли;
	
КонецПроцедуры

// Формирует обзор переданного объекта в виде HTML текста.
//
// Параметры:
//    Контрагент       - СправочникСсылка.Контрагенты - обзор которого формируем
//    ПоказыватьУдаленные - Булево - Истина, если требуется показывать удаленные контактные лица
//
// Возвращаемое значение:
//    HTMLТекст - Строка - обзор переданного объекта в виде HTML текста
//
Функция ПолучитьОбзорКонтрагента(Контрагент, ПоказыватьУдаленные) Экспорт 
	
	Если Не ЗначениеЗаполнено(Контрагент) Тогда 
		Возврат "";
	КонецЕсли;
	
	HTMLТекст = ОбзорКонтрагентаПереопределяемый.ПолучитьОбзорКонтрагента(Контрагент);
	Если ЗначениеЗаполнено(HTMLТекст) Тогда 
		Возврат HTMLТекст;
	КонецЕсли;
	
	РеквизитыОбъекта = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Контрагент,
		"НаименованиеПолное, Наименование, ЭтоГруппа, Комментарий, ЮрФизЛицо, ФизЛицо");
		
	НеУказан = НСтр("ru = 'не указан'; en = 'not specified'");
	
	HTMLТекст = "<html>
		|<head>
		|<style type=""text/css"">
		|	body {
		|		overflow:    auto;
		|		margin-top:  12px; 		 
		|		margin-left: 10px; 
		|		font-family: Arial; }
		|		font-size:   10pt;}
		|	table {
		|		width:       100%; 
		|		font-family: Arial; 
		|		font-size:   10pt;
		|		border-collapse: collapse;
		|		table-layout: fixed;}
		|	td {vertical-align: top;}
		| 	a:link {
		|		color: #006699; text-decoration: none;}
		|	a:visited {
		|		color: #006699; text-decoration: none;}
		|	a:hover {
		|		color: #006699; text-decoration: underline;}
		|	p {
		|		margin-top: 8px;}
		|</style>
		|<body>";
	
	HTMLТекст = HTMLТекст + "<table><col width=""100px""/><col/><col/>";
	HTMLТекст = HTMLТекст + "<tr align=""left"">";
	HTMLТекст = HTMLТекст + "<td colspan=""3"" height=""50"">";
	
	Если ЗначениеЗаполнено(РеквизитыОбъекта.НаименованиеПолное) Тогда 
		Наименование = РеквизитыОбъекта.НаименованиеПолное;
	Иначе 
		Наименование = РеквизитыОбъекта.Наименование;
	КонецЕсли;
	
	HTMLТекст = HTMLТекст + СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		"<div style= font-size:""11pt""; font-family:Arial; text-align:""left"";><B>%1</B></font>", 
		РаботаС_HTML.ЗаменитьСпецСимволыHTML(Наименование));
	HTMLТекст = HTMLТекст + "</td>";
	HTMLТекст = HTMLТекст + "</tr>";
	HTMLТекст = HTMLТекст + "<p>";
	
	// Для групповых элементов выводим только наименование.
	Если РеквизитыОбъекта.ЭтоГруппа Тогда 
		Если ЗначениеЗаполнено(РеквизитыОбъекта.Комментарий) Тогда 
			HTMLТекст = HTMLТекст + "<tr align=""left"">";
			HTMLТекст = HTMLТекст + "<td colspan=""3"">";
			ОбзорОбъектовКлиентСервер.ДобавитьРеквизит(HTMLТекст,, РеквизитыОбъекта.Комментарий);
			HTMLТекст = HTMLТекст + "</td>";
			HTMLТекст = HTMLТекст + "</tr>";
		КонецЕсли;
		
		HTMLТекст = HTMLТекст + "</table>";
		HTMLТекст = HTMLТекст + "</body></html>";
		ОбзорОбъектовКлиентСервер.УдалитьВредоносныйКодИзТекста(HTMLТекст);
		Возврат HTMLТекст;
	КонецЕсли;
	
	Если РеквизитыОбъекта.ЮрФизЛицо = Перечисления.ЮрФизЛицо.ФизЛицо 
		И ЗначениеЗаполнено(РеквизитыОбъекта.ФизЛицо) Тогда 
		КонтактнаяИнформация = РеквизитыОбъекта.ФизЛицо.КонтактнаяИнформация;
	Иначе 
		КонтактнаяИнформация = Контрагент.КонтактнаяИнформация;
	КонецЕсли;
	
	Телефон = Новый Массив; Факс = Новый Массив;
	Емайл = Новый Массив; Сайт = Новый Массив;
	Адрес = "";
	Для Каждого КИ Из КонтактнаяИнформация Цикл 
		Если КИ.Вид = Справочники.ВидыКонтактнойИнформации.СайтКонтрагента Тогда 
			Сайт.Добавить(КИ.Представление);
			
		ИначеЕсли КИ.Вид = Справочники.ВидыКонтактнойИнформации.EmailКонтрагента 
			Или КИ.Вид = Справочники.ВидыКонтактнойИнформации.EmailФизическогоЛица Тогда 
			Емайл.Добавить(КИ.Представление);
			
		ИначеЕсли КИ.Вид = Справочники.ВидыКонтактнойИнформации.ТелефонКонтрагента
			Или КИ.Вид = Справочники.ВидыКонтактнойИнформации.МобильныйТелефонФизическогоЛица
			Или КИ.Вид = Справочники.ВидыКонтактнойИнформации.ДомашнийТелефонФизическогоЛица Тогда 
			Телефон.Добавить(КИ.Представление);
			
		ИначеЕсли КИ.Вид = Справочники.ВидыКонтактнойИнформации.ФаксКонтрагента Тогда 
			Факс.Добавить(КИ.Представление);
			
		ИначеЕсли КИ.Вид = Справочники.ВидыКонтактнойИнформации.ПочтовыйАдресКонтрагента
			Или КИ.Вид = Справочники.ВидыКонтактнойИнформации.ЮридическийАдресКонтрагента
			Или КИ.Вид = Справочники.ВидыКонтактнойИнформации.ФактическийАдресКонтрагента
			Или КИ.Вид = Справочники.ВидыКонтактнойИнформации.ДомашнийАдресФизическогоЛица Тогда 
			
			Если  Найти(Адрес, КИ.Представление) = 0 Тогда 
				Представление = КИ.Представление;
				Адрес = Адрес + "<br>" + РаботаС_HTML.ЗаменитьСпецСимволыHTML(Представление);
				Сокращение = "";
				
				Если КИ.Вид = Справочники.ВидыКонтактнойИнформации.ЮридическийАдресКонтрагента Тогда 
					Сокращение = Лев(КИ.Вид, 2);
				ИначеЕсли КИ.Вид = Справочники.ВидыКонтактнойИнформации.ДомашнийАдресФизическогоЛица Тогда 
					Сокращение = Лев(КИ.Вид, 3);
				Иначе 
					Сокращение = Лев(КИ.Вид, 4);
				КонецЕсли;
				
				Адрес = Адрес + " (" + Сокращение + ".)";
			Иначе 
				Сокращение = "";
				
				Если КИ.Вид = Справочники.ВидыКонтактнойИнформации.ЮридическийАдресКонтрагента Тогда 
					Сокращение = Лев(КИ.Вид, 2);
				ИначеЕсли КИ.Вид = Справочники.ВидыКонтактнойИнформации.ДомашнийАдресФизическогоЛица Тогда 
					Сокращение = Лев(КИ.Вид, 3);
				Иначе 
					Сокращение = Лев(КИ.Вид, 4);
				КонецЕсли;
								
				ПозицияНачалаСледующегоАдреса = СтрНайти(Адрес, "<br>",, СтрНайти(Адрес, КИ.Представление));
				Если ПозицияНачалаСледующегоАдреса > 0 Тогда
					Адрес = СтрШаблон("%1, (%2.)%3",
									Лев(Адрес, ПозицияНачалаСледующегоАдреса - 1),
									Сокращение,
									Сред(Адрес,ПозицияНачалаСледующегоАдреса));
				Иначе
					Адрес = Адрес + ", (" + Сокращение + ".)";	
				КонецЕсли;	
				
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Если ЗначениеЗаполнено(Адрес) Тогда 
		Адрес = Прав(Адрес, СтрДлина(Адрес) - 4);
	КонецЕсли;
	
	HTMLТекст = HTMLТекст + "<p>";
	
	// Контактная информация.
	HTMLТекст = HTMLТекст + "<tr align=""left"">";
	HTMLТекст = HTMLТекст + "<td colspan=""3"">";
	
	ДобавитьЗначениеСКартинкой(HTMLТекст, НСтр("ru = 'E-mail'; en = 'Email'"),
		БиблиотекаКартинок.НепрочтенноеПисьмо, Емайл, "message");
		
	ДобавитьЗначениеСКартинкой(HTMLТекст, НСтр("ru = 'Телефон'; en = 'Phone'"),
		БиблиотекаКартинок.Телефон16, Телефон);
		
	Если ЗначениеЗаполнено(Факс) Тогда 
		ДобавитьЗначениеСКартинкой(HTMLТекст, НСтр("ru = 'Факс'; en = 'Fax'"),
			БиблиотекаКартинок.Факс16, Факс);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Сайт) Тогда 
		ДобавитьЗначениеСКартинкой(HTMLТекст, НСтр("ru = 'Сайт'; en = 'Site'"),
			БиблиотекаКартинок.Сайт16, Сайт, "site");
	КонецЕсли;
	
	HTMLТекст = HTMLТекст + "<p>";
	
	// Адрес без заголовка.
	HTMLТекст = HTMLТекст + "<table cellpadding=""1"" style=""border-collapse: collapse;"">";
	HTMLТекст = HTMLТекст + "<tr align=""left"">";
	HTMLТекст = HTMLТекст + "<td width=""100%"">";
	Если ЗначениеЗаполнено(Адрес) Тогда 
		HTMLТекст = HTMLТекст + Адрес;
		HTMLТекст = HTMLТекст + "<br>";
	Иначе 
		ОбзорОбъектовКлиентСервер.ДобавитьРеквизит(HTMLТекст, "", НСтр("ru = 'Адрес не указан'; en = 'Address not specified'"), "696969");
	КонецЕсли;
	HTMLТекст = HTMLТекст + "</td>";
	HTMLТекст = HTMLТекст + "</tr>";
	HTMLТекст = HTMLТекст + "</table>";
	
	HTMLТекст = HTMLТекст + "</td>";
	HTMLТекст = HTMLТекст + "</tr>";
	HTMLТекст = HTMLТекст + "<p>";
	
	// Отступ.
	HTMLТекст = HTMLТекст + "<tr>";
	HTMLТекст = HTMLТекст + "<td>";
	HTMLТекст = HTMLТекст + "<p>&nbsp;</p>";
	HTMLТекст = HTMLТекст + "</td>";
	HTMLТекст = HTMLТекст + "</tr>";
	
	Если Не РеквизитыОбъекта.ЮрФизЛицо = Перечисления.ЮрФизЛицо.ФизЛицо Тогда 
		// Контактные лица.
		HTMLТекст = HTMLТекст + "<tr align=""left"">";
		HTMLТекст = HTMLТекст + "<td colspan=""3"">";
		
		HTMLТекст = HTMLТекст + СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			"<B>%1</B>", НСтр("ru = 'Контактные лица'; en = 'Contacts'"));
		
		HTMLТекст = HTMLТекст + "</td>";
		HTMLТекст = HTMLТекст + "</tr>";
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	КонтактныеЛица.Ссылка,
			|	КонтактныеЛица.Наименование,
			|	КонтактныеЛица.Должность,
			|	КонтактныеЛица.ПометкаУдаления КАК ПометкаУдаления,
			|	КонтактныеЛица.КонтактнаяИнформация.(
			|		Тип,
			|		Вид,
			|		Представление,
			|		ЗначенияПолей
			|	) КАК КонтактнаяИнформация
			|ИЗ
			|	Справочник.КонтактныеЛица КАК КонтактныеЛица
			|ГДЕ
			|	КонтактныеЛица.Владелец = &Контрагент";
		  
		Если Не ПоказыватьУдаленные Тогда  
			Запрос.Текст = Запрос.Текст + "   
				|	И НЕ КонтактныеЛица.ПометкаУдаления";
		КонецЕсли;
			
		Запрос.Текст = Запрос.Текст + "
			|УПОРЯДОЧИТЬ ПО
			|	ПометкаУдаления";
		
		Запрос.Параметры.Вставить("Контрагент", Контрагент);
		КонтактныеЛица = Запрос.Выполнить().Выгрузить();
		КоличествоКонтактныхЛиц = КонтактныеЛица.Количество();
		
		Если КоличествоКонтактныхЛиц = 0 Тогда 
			HTMLТекст = HTMLТекст + "<tr align=""left"">";
			HTMLТекст = HTMLТекст + "<td colspan=""3"">";
			ОбзорОбъектовКлиентСервер.ДобавитьРеквизит(HTMLТекст, "", НСтр("ru = 'не указаны'; en = 'not specified'"), "696969");
			HTMLТекст = HTMLТекст + "</td>";
			HTMLТекст = HTMLТекст + "</tr>";
		КонецЕсли;
		
		Выводим = 1;
		Для Каждого КонтактноеЛицо Из КонтактныеЛица Цикл
			
			Если Выводим > 2 Тогда 
				Прервать;
			КонецЕсли;
			
			HTMLТекст = HTMLТекст + "<tr align=""left"">";
			HTMLТекст = HTMLТекст + "<td colspan=""3"">";
			
			Если ЗначениеЗаполнено(КонтактноеЛицо.Наименование) И ЗначениеЗаполнено(КонтактноеЛицо.Должность) Тогда
				ДолжностьКонтактногоЛица = ", " + КонтактноеЛицо.Должность;
			Иначе 
				ДолжностьКонтактногоЛица = "";
			КонецЕсли;
			
			Если КонтактноеЛицо.ПометкаУдаления Тогда 
				ДолжностьКонтактногоЛица = ДолжностьКонтактногоЛица + " " + НСтр("ru = '(помечен на удаление)'; en = '(marked for deletion)'");
			КонецЕсли;
			
			Если ЗначениеЗаполнено(ДолжностьКонтактногоЛица) Тогда 
				HTMLТекст = HTMLТекст + СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					"<A href=v8doc:%1>%2</A>%3<BR>",
					ПолучитьНавигационнуюСсылку(КонтактноеЛицо.Ссылка), 
					РаботаС_HTML.ЗаменитьСпецСимволыHTML(КонтактноеЛицо.Наименование),
					РаботаС_HTML.ЗаменитьСпецСимволыHTML(ДолжностьКонтактногоЛица));
			Иначе 
				HTMLТекст = HTMLТекст + СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					"<A href=v8doc:%1>%2</A><BR>",
					ПолучитьНавигационнуюСсылку(КонтактноеЛицо.Ссылка), 
					РаботаС_HTML.ЗаменитьСпецСимволыHTML(КонтактноеЛицо.Наименование));
			КонецЕсли;
			
			КонтактнаяИнформация = КонтактноеЛицо.КонтактнаяИнформация;
			Телефон = Новый Массив; Емайл = Новый Массив; 
			Для Каждого КИ Из КонтактнаяИнформация Цикл 
				Если КИ.Вид = Справочники.ВидыКонтактнойИнформации.EmailКонтактногоЛица Тогда 
					Емайл.Добавить(КИ.Представление);
				ИначеЕсли КИ.Вид = Справочники.ВидыКонтактнойИнформации.ТелефонКонтактногоЛица Тогда 
					Телефон.Добавить(КИ.Представление);
				КонецЕсли;
			КонецЦикла;
			
			ДобавитьЗначениеСКартинкой(HTMLТекст, НСтр("ru = 'E-mail'; en = 'Email'"),
				БиблиотекаКартинок.НепрочтенноеПисьмо, Емайл, "message");
			
			ДобавитьЗначениеСКартинкой(HTMLТекст, НСтр("ru = 'Телефон'; en = 'Phone'"),
				БиблиотекаКартинок.Телефон16, Телефон);
			
			HTMLТекст = HTMLТекст + "</td>";
			HTMLТекст = HTMLТекст + "</tr>";
			
			// Отступ.
			HTMLТекст = HTMLТекст + "<tr>";
			HTMLТекст = HTMLТекст + "<td>";
			HTMLТекст = HTMLТекст + "<BR>";
			HTMLТекст = HTMLТекст + "</td>";
			HTMLТекст = HTMLТекст + "</tr>";
			
			Выводим = Выводим + 1;
		КонецЦикла;
		
		Если КоличествоКонтактныхЛиц > 2 Тогда 
			HTMLТекст = HTMLТекст + "<tr align=""left"">";
			HTMLТекст = HTMLТекст + "<td colspan=""3"">";
			HTMLТекст = HTMLТекст + СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				"<A href=v8doc:contactpersons>%1 (%2)</A><BR>",
				НСтр("ru = 'Показать всех'; en = 'Show all'"), КоличествоКонтактныхЛиц);
			HTMLТекст = HTMLТекст + "</td>";
			HTMLТекст = HTMLТекст + "</tr>";
		КонецЕсли;
	КонецЕсли;
	
	HTMLТекст = HTMLТекст + "</table>";
	HTMLТекст = HTMLТекст + "</body></html>";
	
	ОбзорОбъектовКлиентСервер.УдалитьВредоносныйКодИзТекста(HTMLТекст);
	
	Возврат HTMLТекст;
	
КонецФункции


