
// Выполняет добавление работы в отчет
Процедура ДобавитьВОтчетКлиент(
	ДатаОтчета, 
	ВключенХронометраж, 
	ДатаНачалаХронометража, 
	ДатаКонцаХронометража, 
	ВидыРабот, 
	Объект, 
	СпособУказанияВремени,
	ЭлементПереключитьХронометраж, 
	Выполнена,
	ФормаОбъекта) Экспорт
	
	НуженДиалог = Ложь;
	ПараметрыОповещения = Неопределено;
	
	Если ВключенХронометраж Тогда // сейчас будем выключать хронометраж и делать запись отчета
		Если ДатаНачалаХронометража <> '00010101' Тогда
			СегодняшнийДень = НачалоДня(ТекущаяДата());
			ДеньНачала = НачалоДня(ДатаНачалаХронометража);
			
			Если ДеньНачала <> СегодняшнийДень Тогда
				НуженДиалог = Истина;
			КонецЕсли;
		Иначе
			
		КонецЕсли;
		
		Если ВидыРабот.Количество() <> 1 Тогда
			НуженДиалог = Истина;
		КонецЕсли;
	Иначе
		НуженДиалог = Истина;
	КонецЕсли;
	
	Если НуженДиалог = Ложь Тогда
		ВидРаботы = ВидыРабот[0].Значение;
		
		КонецХронометража = ТекущаяДата();
		ДлительностьРаботы = КонецХронометража - ДатаНачалаХронометража;
		
		ПараметрыОтчета = Новый Структура();
		ПараметрыОтчета.Вставить("ДатаОтчета", ДатаОтчета);
		ПараметрыОтчета.Вставить("ВидРаботы", ВидРаботы);
		ПараметрыОтчета.Вставить("ОписаниеРаботы", Строка(Объект));
		ПараметрыОтчета.Вставить("ДлительностьРаботы", ДлительностьРаботы);
		ПараметрыОтчета.Вставить("НачалоРаботы", ДатаНачалаХронометража);
		ПараметрыОтчета.Вставить("ОкончаниеРаботы", КонецХронометража);
		ПараметрыОтчета.Вставить("Объект", Объект);
		
		УчетВремени.ДобавитьВОтчет(ПараметрыОтчета, ПараметрыОповещения);
		
		ВключенХронометраж = Ложь;
		ЭлементПереключитьХронометраж.Пометка = Ложь;
		
		Если ПараметрыОповещения <> Неопределено Тогда
			ПоказатьОповещение(ПараметрыОповещения, ВключенХронометраж, Объект);
		КонецЕсли;
	Иначе
		ДлительностьРаботы = 0;
		Если ВключенХронометраж И ДатаНачалаХронометража <> '00010101' Тогда
			СегодняшнийДень = НачалоДня(ТекущаяДата());
			ДеньНачала = НачалоДня(ДатаНачалаХронометража);
			
			Если ДеньНачала = СегодняшнийДень Тогда
				КонецХронометража = ТекущаяДата();
				ДлительностьРаботы = КонецХронометража - ДатаНачалаХронометража;
			КонецЕсли;
		КонецЕсли;
		
		ПараметрыФормы = Новый Структура();
		ПараметрыФормы.Вставить("ДатаОтчета", ДатаОтчета);
		ПараметрыФормы.Вставить("ВидыРабот", ВидыРабот);
		ПараметрыФормы.Вставить("ОписаниеРаботы", Строка(Объект));
		ПараметрыФормы.Вставить("ДлительностьРаботы", ДлительностьРаботы);
		ПараметрыФормы.Вставить("НачалоРаботы", ДатаНачалаХронометража);
		ПараметрыФормы.Вставить("ОкончаниеРаботы", КонецХронометража);
		ПараметрыФормы.Вставить("Объект", Объект);
		ПараметрыФормы.Вставить("СпособУказанияВремени", СпособУказанияВремени);
		
		ПараметрыОписания = Новый Структура();
		ПараметрыОписания.Вставить("ФормаОбъекта", ФормаОбъекта);
		ПараметрыОписания.Вставить("ТипДействия", "ДобавитьВОтчетКлиент");
		ПараметрыОписания.Вставить("ЭлементПереключитьХронометраж", ЭлементПереключитьХронометраж);
		
		ОписаниеОповещения = Новый ОписаниеОповещения(
			"ЗавершениеФормыДобавленияРаботы", ЭтотОбъект, ПараметрыОписания);
		
		ОткрытьФорму(
			"РегистрСведений.ФактическиеТрудозатраты.Форма.ФормаДобавленияРаботы",
			ПараметрыФормы,,,,,
			ОписаниеОповещения,
			РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	КонецЕсли;
	
КонецПроцедуры	

// Возвращает Истина, если нужен диалог для ввода параметров работы
Функция НуженДиалогДляХронометража(ВключенХронометраж, ДатаНачалаХронометража, ВидыРабот) Экспорт
	
	НуженДиалог = Ложь;
	
	Если ВключенХронометраж Тогда // сейчас будем выключать хронометраж и делать запись отчета
		Если ДатаНачалаХронометража <> '00010101000000' Тогда
			СегодняшнийДень = НачалоДня(ТекущаяДата());
			ДеньНачала = НачалоДня(ДатаНачалаХронометража);
			
			Если ДеньНачала <> СегодняшнийДень Тогда
				НуженДиалог = Истина;
			КонецЕсли;
		Иначе
			
		КонецЕсли;
		
		Если ВидыРабот.Количество() <> 1 Тогда
			НуженДиалог = Истина;
		КонецЕсли;
	КонецЕсли;

	Если НуженДиалог Тогда
		
		ДлительностьРаботы = ПолучитьДлительностьРаботы(ДатаНачалаХронометража);
		Если ДлительностьРаботы < 60 Тогда // меньше 1 минуты
			НуженДиалог = Ложь;
		КонецЕсли;	
		
	КонецЕсли;
	
	Возврат НуженДиалог;
	
КонецФункции	

// Показать оповещение после записи в отчет
Процедура ПоказатьОповещение(ПараметрыОповещения, ВключенХронометраж, Объект) Экспорт
	
	// покажем оповещение
	Если ПараметрыОповещения <> Неопределено Тогда 
	
		Если ПараметрыОповещения.Свойство("КлючЗаписи") Тогда
			ТекстОповещения = НСтр("ru = 'Создание:'; en = 'Added:'");
			НавигационнаяСсылка = ПолучитьНавигационнуюСсылку(ПараметрыОповещения.КлючЗаписи);
			ПоказатьОповещениеПользователя(ТекстОповещения, НавигационнаяСсылка, ПараметрыОповещения.Представление, БиблиотекаКартинок.Информация32);
		КонецЕсли;
		
		Если ПараметрыОповещения.Свойство("СсылкаНаОтчет") Тогда
			ТекстОповещения = "";
			Если ПараметрыОповещения.СозданОтчет Тогда
				ТекстОповещения = НСтр("ru = 'Создание:'; en = 'Added:'");
			Иначе
				ТекстОповещения = НСтр("ru = 'Изменение:'; en = 'Changed:'");
			КонецЕсли;			
			НавигационнаяСсылка = ПолучитьНавигационнуюСсылку(ПараметрыОповещения.СсылкаНаОтчет);
			ПоказатьОповещениеПользователя(ТекстОповещения, НавигационнаяСсылка, ПараметрыОповещения.ПредставлениеСсылки, БиблиотекаКартинок.Информация32);
		КонецЕсли;	
		
		ПараметрОповещения = Неопределено;
		Если ПараметрыОповещения.Свойство("Изменение_ФактическиеТрудозатратыЗадачи", ПараметрОповещения) Тогда
			Оповестить("Изменение_ФактическиеТрудозатратыЗадачи", ПараметрОповещения);
		КонецЕсли;	
		
		ПараметрОповещения = Неопределено;
		Если ПараметрыОповещения.Свойство("Изменение_ФактическиеТрудозатратыПроектнойЗадачи", ПараметрОповещения) Тогда
			Оповестить("Изменение_ФактическиеТрудозатратыПроектнойЗадачи", ПараметрОповещения);
		КонецЕсли;
		
	ИначеЕсли ВключенХронометраж Тогда
		Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Включен хронометраж по ""%1""'; en = 'Time tracking for ""%1"" is enabled'"), Строка(Объект));
		Состояние(Текст);
		
	КонецЕсли;	
	
КонецПроцедуры

// Вычисляет ДлительностьРаботы 
Функция ПолучитьДлительностьРаботы(ДатаНачалаХронометража) Экспорт
	
	ДлительностьРаботы = 0;
	
	Если ДатаНачалаХронометража <> '00010101' Тогда
		СегодняшнийДень = НачалоДня(ТекущаяДата());
		ДеньНачала = НачалоДня(ДатаНачалаХронометража);
		
		Если ДеньНачала = СегодняшнийДень Тогда
			КонецХронометража = ТекущаяДата();
			ДлительностьРаботы = КонецХронометража - ДатаНачалаХронометража;
		КонецЕсли;
	КонецЕсли;
	
	Возврат ДлительностьРаботы;
	
КонецФункции

// На клиенте открывает диалог (если нужно) и заполняет параметры работы для последующего добавления в отчет
Процедура ПодготовитьРаботуДляДобавкиВОтчет(ДатаОтчета, ВключенХронометраж, 
	ДатаНачалаХронометража, ДатаКонцаХронометража, ВидыРабот, Объект,
	СпособУказанияВремени, ОписаниеОповещенияДобавления) Экспорт

	ПараметрыОтчета = Неопределено;
	НуженДиалог = Ложь;
	
	Если ВключенХронометраж Тогда // сейчас будем выключать хронометраж и делать запись отчета
		Если ДатаНачалаХронометража <> '00010101' Тогда
			СегодняшнийДень = НачалоДня(ТекущаяДата());
			ДеньНачала = НачалоДня(ДатаНачалаХронометража);
			
			Если ДеньНачала <> СегодняшнийДень Тогда
				НуженДиалог = Истина;
			КонецЕсли;
		КонецЕсли;
		
		Если ВидыРабот.Количество() <> 1 Тогда
			НуженДиалог = Истина;
		КонецЕсли;
	Иначе
		НуженДиалог = Истина;
	КонецЕсли;
	
	Если НуженДиалог = Ложь Тогда
		ВидРаботы = ВидыРабот[0].Значение;
		
		ДатаОкончания = ТекущаяДата();
		ДлительностьРаботы = ДатаОкончания - ДатаНачалаХронометража;
		
		Если ДлительностьРаботы < 60 Тогда // меньше 1 минуты
			ВыполнитьОбработкуОповещения(ОписаниеОповещенияДобавления, Неопределено);
			Возврат;
		КонецЕсли;	
		
		ПараметрыОтчета = Новый Структура();
		ПараметрыОтчета.Вставить("ДатаОтчета", ДатаОтчета);
		ПараметрыОтчета.Вставить("ВидРаботы", ВидРаботы);
		ПараметрыОтчета.Вставить("ОписаниеРаботы", Строка(Объект));
		ПараметрыОтчета.Вставить("ДлительностьРаботы", ДлительностьРаботы);
		ПараметрыОтчета.Вставить("НачалоРаботы", ДатаНачалаХронометража);
		ПараметрыОтчета.Вставить("ОкончаниеРаботы", ДатаОкончания);
		ПараметрыОтчета.Вставить("Объект", Объект);
		
		ВыполнитьОбработкуОповещения(ОписаниеОповещенияДобавления, Неопределено);
	Иначе
		ДлительностьРаботы = 0;
		
		Если ВключенХронометраж И ДатаНачалаХронометража <> '00010101000000' Тогда
			
			СегодняшнийДень = НачалоДня(ТекущаяДата());
			ДеньНачала = НачалоДня(ДатаНачалаХронометража);
			
			Если ДеньНачала = СегодняшнийДень Тогда
				
				КонецХронометража = ТекущаяДата();
				ДлительностьРаботы = КонецХронометража - ДатаНачалаХронометража;
				
				Если ДлительностьРаботы < 60 Тогда // меньше 1 минуты
					ВыполнитьОбработкуОповещения(ОписаниеОповещенияДобавления, Неопределено);
					Возврат;
				КонецЕсли;
				
			КонецЕсли;
		КонецЕсли;
		
		ПараметрыФормы = Новый Структура();
		ПараметрыФормы.Вставить("ДатаОтчета", ДатаОтчета);
		ПараметрыФормы.Вставить("ВидыРабот", ВидыРабот);
		ПараметрыФормы.Вставить("ОписаниеРаботы", Строка(Объект));
		ПараметрыФормы.Вставить("ДлительностьРаботы", ДлительностьРаботы);
		ПараметрыФормы.Вставить("НачалоРаботы", ДатаНачалаХронометража);
		ПараметрыФормы.Вставить("ОкончаниеРаботы", КонецХронометража);
		ПараметрыФормы.Вставить("Объект", Объект);
		ПараметрыФормы.Вставить("СпособУказанияВремени", СпособУказанияВремени);
		
		ОткрытьФорму(
			"РегистрСведений.ФактическиеТрудозатраты.Форма.ФормаДобавленияРаботы",
			ПараметрыФормы,,,,,
			ОписаниеОповещенияДобавления,
			РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		
	КонецЕсли;
	
КонецПроцедуры // ПодготовитьРаботуДляДобавкиВОтчет()

// Добавить в отчет после выполнения задачи
Процедура ДобавитьВОтчетПослеВыполненияЗадачи(ОпцияВестиУчетТрудозатрат,
	ДатаИсполнения, Объект, ВключенХронометраж, 
	ДатаНачалаХронометража, ДатаКонцаХронометража,
	ВидыРабот, СпособУказанияВремени, ОписаниеОповещения) Экспорт
	
	ДобавлятьВОтчет = СтандартныеПодсистемыКлиентПовтИсп.ПараметрыРаботыКлиента().ПерсональныеНастройкиУчетаВремени.ДобавлятьРаботуВЕжедневныйОтчетПриВыполненииЗадачи;
	ПараметрыОтчета = Неопределено;
	ПараметрыОповещения = Неопределено;
	
	Если ОпцияВестиУчетТрудозатрат 
		И (ДобавлятьВОтчет 
		ИЛИ ВключенХронометраж
		И ДатаНачалаХронометража <> '00010101') Тогда
		
		ПараметрыОбработчика = Новый Структура;
		ПараметрыОбработчика.Вставить("ОписаниеОповещения", ОписаниеОповещения);
		ПараметрыОбработчика.Вставить("ПараметрыОповещения", ПараметрыОповещения);
		ПараметрыОбработчика.Вставить("Объект", Объект);
		ПараметрыОбработчика.Вставить("ВключенХронометраж", ВключенХронометраж);
		ОписаниеОповещенияДобавления = Новый ОписаниеОповещения(
			"ПодготовитьРаботуДляДобавкиВОтчетПродолжение",
			ЭтотОбъект,
			ПараметрыОбработчика);
		ДатаОтчета = ДатаИсполнения;
		ПодготовитьРаботуДляДобавкиВОтчет(ДатаОтчета, 
			ВключенХронометраж, ДатаНачалаХронометража, 
			ДатаКонцаХронометража, ВидыРабот, Объект, СпособУказанияВремени, ОписаниеОповещенияДобавления);
		Возврат;
	КонецЕсли;
	
	ВыполнитьОбработкуОповещения(ОписаниеОповещения);
	
КонецПроцедуры

Процедура ПодготовитьРаботуДляДобавкиВОтчетПродолжение(ПараметрыОтчета, Параметры) Экспорт
	
	Если ПараметрыОтчета <> Неопределено Тогда
		УчетВремени.ДобавитьВОтчет(ПараметрыОтчета, Параметры.ПараметрыОповещения);
	Иначе
		УчетВремени.ОчиститьХронометраж(Параметры.Объект);	
	КонецЕсли;	
	
	ПоказатьОповещение(Параметры.ПараметрыОповещения, Параметры.ВключенХронометраж, Параметры.Объект);
	ВыполнитьОбработкуОповещения(Параметры.ОписаниеОповещения);
	
КонецПроцедуры

Процедура ЗавершениеФормыДобавленияРаботы(ПараметрыОтчета, Параметры) Экспорт
	
	ФормаОбъекта = Параметры.ФормаОбъекта;
	ПараметрыОповещения = Неопределено;
	
	Если Параметры.ТипДействия = "ДобавитьВОтчетКлиент" Тогда
		Если ПараметрыОтчета <> Неопределено Тогда
			УчетВремени.ДобавитьВОтчет(ПараметрыОтчета, ПараметрыОповещения);
			ФормаОбъекта.ВключенХронометраж = Ложь;
			Параметры.ЭлементПереключитьХронометраж.Пометка = Ложь;
		КонецЕсли;
		Если ПараметрыОповещения <> Неопределено Тогда
			ПоказатьОповещение(
				ПараметрыОповещения, 
				ФормаОбъекта.ВключенХронометраж,
				ФормаОбъекта.Объект);
		КонецЕсли;
	ИначеЕсли Параметры.ТипДействия = "ПереключитьХронометраж" Тогда
		Если ПараметрыОтчета <> Неопределено Тогда
			
			ФормаОбъекта.ДобавитьВОтчетИОбновитьФорму(
				ПараметрыОтчета,
				ПараметрыОповещения);
			
			ПоказатьОповещение(
				ПараметрыОповещения, 
				ФормаОбъекта.ВключенХронометраж,
				ФормаОбъекта.Объект.Ссылка);
		Иначе
			ФормаОбъекта.ОтключитьХронометражСервер();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры


