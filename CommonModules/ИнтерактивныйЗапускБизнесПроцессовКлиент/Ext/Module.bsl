
// Выполнить интерактивный запуск бизнес-процесса
Процедура ВыполнитьИнтерактивныйЗапускБизнесПроцесса(ШаблоныДляАвтоЗапуска, Предмет, ВидСобытияСтрока, ФормаДокумента, Отказ, ЗакрытьФорму) Экспорт
	
	Если Не ЗначениеЗаполнено(Предмет) Тогда
		Возврат;
	КонецЕсли;	
	
	ПараметрыОбработчикаОповещения = Новый Структура;
		ПараметрыОбработчикаОповещения.Вставить("ФормаДокумента", ФормаДокумента);
		ПараметрыОбработчикаОповещения.Вставить("Предмет", Предмет);
		ПараметрыОбработчикаОповещения.Вставить("ЗакрытьФорму", ЗакрытьФорму);
		
	ОписаниеОповещения = Новый ОписаниеОповещения(
		"ЗавершениеИнтерактивногоЗапускаПроцесса",
		ЭтотОбъект,
		ПараметрыОбработчикаОповещения);
			
	Если ШаблоныДляАвтоЗапуска.Количество() <> 0 Тогда
		
		ПараметрыФормы = Новый Структура("ВидСобытияСтрока, СписокШаблонов", ВидСобытияСтрока, ШаблоныДляАвтоЗапуска);
		Шаблон = Неопределено;
		
		Если ШаблоныДляАвтоЗапуска.Количество() = 1 Тогда
			 ОткрытьФорму(
			 	"ОбщаяФорма.ОповещениеПриИнтерактивномАвтоЗапускеБизнесПроцессов",
				ПараметрыФормы,,,,,
			 	ОписаниеОповещения,
			 	РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		Иначе	
			 ОткрытьФорму(
			 	"ОбщаяФорма.ВыборПриИнтерактивномАвтоЗапускеБизнесПроцессов",
				ПараметрыФормы,,,,,
			 	ОписаниеОповещения,
			 	РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		КонецЕсли;	
		
		Отказ = Истина;
		
	КонецЕсли;	
	
КонецПроцедуры

// Продолжение интерактивного запуска
Процедура ЗавершениеИнтерактивногоЗапускаПроцесса(Шаблон, Параметры) Экспорт
	
	Параметры.ФормаДокумента.БылПоказанДиалогИнтерактивногоЗапускаПроцесса = Истина;
	Если Шаблон <> Неопределено И Шаблон <> КодВозвратаДиалога.Отмена Тогда
		
		Если Параметры.ЗакрытьФорму = Истина И Параметры.ФормаДокумента.Открыта() Тогда
			Параметры.ФормаДокумента.Закрыть();
		КонецЕсли;
		
		ИмяФормы = "";
		ИмяФормы = БизнесПроцессыИЗадачиВызовСервера.ПолучитьИмяФормыПроцессаПоШаблону(Шаблон);
	
		Если Не ПустаяСтрока(ИмяФормы) Тогда
			
			РежимОткрытияКарточкиПроцесса = Неопределено;
			Если Параметры.ФормаДокумента.ВладелецФормы <> Неопределено Тогда
				РежимОткрытияКарточкиПроцесса = РежимОткрытияОкнаФормы.БлокироватьОкноВладельца;
			КонецЕсли;
			
			Основание = Новый Структура("Шаблон, Предметы", Шаблон, Параметры.Предмет);
			ПараметрыФормы = Новый Структура("Основание", Основание);
			ОткрытьФорму(
				ИмяФормы, 
				ПараметрыФормы, 
				Параметры.ФормаДокумента.ВладелецФормы,,,,,
				РежимОткрытияКарточкиПроцесса);
			
		КонецЕсли;
		
		Возврат;
		
	КонецЕсли;
	
	Если Параметры.ЗакрытьФорму = Истина И Параметры.ФормаДокумента.Открыта() Тогда
		Параметры.ФормаДокумента.Закрыть();
	КонецЕсли;
	
КонецПроцедуры


