////////////////////////////////////////////////////////////////////////////////////////////////////
// Обработка постановки предмета на контроль
//  
////////////////////////////////////////////////////////////////////////////////////////////////////
#Область ПрограммныйИнтерфейс

// Выполняет обработку при нажатии на кнопку Контроль
//
// Параметры
// 	  Предмет - ссылка на предмет справочника Контроль -  
//	      ссылка на предмет, из которого вызывается команда
//	  Остальные параметры - соответствуют параметрам процедуры ОткрытьФорму
//
Процедура ОбработкаКомандыКонтроль(Предмет,
	Владелец,
	Уникальность = Неопределено,
	Окно = Неопределено,
	НавигационнаяСсылка = Неопределено) Экспорт
	
	ИмяФормы = "";
	ПараметрыФормы = "";
	
	Контроль.ОбработкаКомандыКонтроль(Предмет, ИмяФормы, ПараметрыФормы);
	
	Если ИмяФормы = "Справочник.Контроль.Форма.ФормаНового" И ПараметрыФормы.Свойство("Основание") Тогда 
		ОткрытьФорму(ИмяФормы, 
			ПараметрыФормы,
			Владелец, 
			ПараметрыФормы.Основание,
			Окно, 
			НавигационнаяСсылка);
	Иначе	
		ОткрытьФорму(ИмяФормы, 
			ПараметрыФормы,
			Владелец, 
			Уникальность, 
			Окно, 
			НавигационнаяСсылка);
	КонецЕсли;		
	
КонецПроцедуры	

// Выполняет оповещения при записи карточки контроля
//
// Параметры
//    КонтрольОбъект - СправочникОбъект.Контроль - записываемый объект
//
Процедура ОповеститьОЗаписиКонтроля(КонтрольОбъект) Экспорт 
	
	Параметр = Новый Структура;
	Параметр.Вставить("Предмет", КонтрольОбъект.Предмет);
	Параметр.Вставить("Ссылка", КонтрольОбъект.Ссылка);
	Параметр.Вставить("Контролер", КонтрольОбъект.Контролер);
	
	Оповестить("ЗаписьКонтроля", Параметр);
	
	Если ТипЗнч(КонтрольОбъект.Предмет) = Тип("БизнесПроцессСсылка.Исполнение")
		Или ТипЗнч(КонтрольОбъект.Предмет) = Тип("БизнесПроцессСсылка.КомплексныйПроцесс")
		Или ТипЗнч(КонтрольОбъект.Предмет) = Тип("БизнесПроцессСсылка.ОбработкаВнутреннегоДокумента")
		Или ТипЗнч(КонтрольОбъект.Предмет) = Тип("БизнесПроцессСсылка.ОбработкаВходящегоДокумента")
		Или ТипЗнч(КонтрольОбъект.Предмет) = Тип("БизнесПроцессСсылка.ОбработкаИсходящегоДокумента")
		Или ТипЗнч(КонтрольОбъект.Предмет) = Тип("БизнесПроцессСсылка.Ознакомление")
		Или ТипЗнч(КонтрольОбъект.Предмет) = Тип("БизнесПроцессСсылка.Поручение")
		Или ТипЗнч(КонтрольОбъект.Предмет) = Тип("БизнесПроцессСсылка.Приглашение")
		Или ТипЗнч(КонтрольОбъект.Предмет) = Тип("БизнесПроцессСсылка.Рассмотрение")
		Или ТипЗнч(КонтрольОбъект.Предмет) = Тип("БизнесПроцессСсылка.Регистрация")
		Или ТипЗнч(КонтрольОбъект.Предмет) = Тип("БизнесПроцессСсылка.РешениеВопросовВыполненияЗадач")
		Или ТипЗнч(КонтрольОбъект.Предмет) = Тип("БизнесПроцессСсылка.Согласование")
		Или ТипЗнч(КонтрольОбъект.Предмет) = Тип("БизнесПроцессСсылка.Утверждение") Тогда 
		
		ОсновныеПредметы = МультипредметностьКлиентСервер.ПолучитьМассивПредметовОбъекта(КонтрольОбъект.Предмет, , Истина);
		Для Каждого Предмет Из ОсновныеПредметы Цикл
			Параметр = Новый Структура;
			Параметр.Вставить("Предмет", Предмет);
			Параметр.Вставить("Ссылка", КонтрольОбъект.Ссылка);
			Параметр.Вставить("Контролер", КонтрольОбъект.Контролер);
	
			Оповестить("ЗаписьКонтроля", Параметр);
		КонецЦикла;
	КонецЕсли;	
		
КонецПроцедуры	

#КонецОбласти
