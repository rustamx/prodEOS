
// Переопределяемая часть функции ПолучитьПраваПоОбъекту
//
Функция ПолучитьЗапросНаПраваПоОбъекту(
			ОбъектыДоступа,
			ТипОбъекта,
			МетаданныеОбъекта,
			ЗаменяемыеПодстроки,
			Пользователи = Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	
	Если ШаблоныБизнесПроцессовКлиентСервер.ЭтоШаблонПроцесса(ОбъектыДоступа[0]) Тогда
		
		Запрос.Текст =
			"ВЫБРАТЬ
			|	ВложенныйЗапрос.ОбъектДоступа,
			|	ВложенныйЗапрос.Пользователь,
			|	МАКСИМУМ(ВложенныйЗапрос.Чтение) КАК Чтение,
			|	МАКСИМУМ(ВложенныйЗапрос.Изменение) КАК Изменение,
			|	МАКСИМУМ(ВложенныйЗапрос.Добавление) КАК Добавление,
			|	МАКСИМУМ(ВложенныйЗапрос.Удаление) КАК Удаление,
			|	МАКСИМУМ(ВложенныйЗапрос.УправлениеПравами) КАК УправлениеПравами
			|ИЗ
			|	(ВЫБРАТЬ
			|		ТаблицаШаблона.Ссылка КАК ОбъектДоступа,
			|		СоставСубъектов.Пользователь КАК Пользователь,
			|		ИСТИНА КАК Чтение,
			|		ЛОЖЬ КАК Изменение,
			|		ЛОЖЬ КАК Добавление,
			|		ЛОЖЬ КАК Удаление,
			|		ЛОЖЬ КАК УправлениеПравами
			|	ИЗ
			|		%ТаблицаШаблона% КАК ТаблицаШаблона
			|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.УчастникиПроцессов КАК УчастникиПроцессов
			|				ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СоставСубъектовПравДоступа КАК СоставСубъектов
			|				ПО УчастникиПроцессов.Участник = СоставСубъектов.Субъект
			|					И СоставСубъектов.ИмяОбластиДелегирования В (&ОбластиДелегирования)
			|					%ОтборПоПользователюСоставаСубъектов%
			|			ПО ТаблицаШаблона.КомплексныйПроцесс = УчастникиПроцессов.Процесс
			|	
			|	ГДЕ
			|		ТаблицаШаблона.Ссылка В(&ОбъектыДоступа)
			|	
			|	ОБЪЕДИНИТЬ ВСЕ
			|	
			|	ВЫБРАТЬ
			|		ТаблицаШаблона.Ссылка,
			|		СоставСубъектов.Пользователь,
			|		ИСТИНА,
			|		ИСТИНА,
			|		ИСТИНА,
			|		ИСТИНА,
			|		ЛОЖЬ
			|	ИЗ
			|		%ТаблицаШаблона% КАК ТаблицаШаблона
			|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ БизнесПроцесс.КомплексныйПроцесс КАК КомплексныйПроцессБП
			|				ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СоставСубъектовПравДоступа КАК СоставСубъектов
			|				ПО КомплексныйПроцессБП.Автор = СоставСубъектов.Субъект
			|					И СоставСубъектов.ИмяОбластиДелегирования В (&ОбластиДелегирования)
			|					%ОтборПоПользователюСоставаСубъектов%
			|			ПО ТаблицаШаблона.КомплексныйПроцесс = КомплексныйПроцессБП.Ссылка
			|	
			|	ГДЕ
			|		ТаблицаШаблона.Ссылка В(&ОбъектыДоступа)
			|	
			|	ОБЪЕДИНИТЬ ВСЕ
			|	
			|	ВЫБРАТЬ
			|		ТаблицаШаблона.Ссылка,
			|		ПраваПоДескрипторамДоступаОбъектов.Пользователь,
			|		ПраваПоДескрипторамДоступаОбъектов.Чтение,
			|		ПраваПоДескрипторамДоступаОбъектов.Изменение,
			|		ПраваПоДескрипторамДоступаОбъектов.Добавление,
			|		ПраваПоДескрипторамДоступаОбъектов.Удаление,
			|		ПраваПоДескрипторамДоступаОбъектов.УправлениеПравами
			|	ИЗ
			|		%ТаблицаШаблона% КАК ТаблицаШаблона
			|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ДескрипторыДляОбъектов КАК ДескрипторыДляОбъектов
			|				ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ПраваПоДескрипторамДоступаОбъектов КАК ПраваПоДескрипторамДоступаОбъектов
			|				ПО ДескрипторыДляОбъектов.Дескриптор = ПраваПоДескрипторамДоступаОбъектов.Дескриптор
			|			ПО ТаблицаШаблона.Ссылка = ДескрипторыДляОбъектов.Объект
			|		
			|	ГДЕ
			|		ДескрипторыДляОбъектов.Объект В(&ОбъектыДоступа)
			|		И НЕ ДескрипторыДляОбъектов.Отключен
			|		%ОтборПоПользователю%) КАК ВложенныйЗапрос
			|
			|СГРУППИРОВАТЬ ПО
			|	ВложенныйЗапрос.ОбъектДоступа,
			|	ВложенныйЗапрос.Пользователь";
			
		ЗаменяемыеПодстроки.Вставить("%ТаблицаШаблона%", ОбъектыДоступа[0].Метаданные().ПолноеИмя());
		ЗаменяемыеПодстроки.Вставить("%ОтборПоПользователюСоставаСубъектов%",
			?(Пользователи = Неопределено, "", "И СоставСубъектов.Пользователь В (&Пользователи)"));
		
	ИначеЕсли ТипОбъекта = Тип("СправочникСсылка.Мероприятия")
		Или ТипОбъекта = Тип("СправочникСсылка.Проекты") Тогда	
		
		Запрос.Текст =
			"ВЫБРАТЬ
			|	ДескрипторыДляОбъектов.Объект КАК ОбъектДоступа,
			|	ПраваПоДескрипторамДоступаОбъектов.Пользователь КАК Пользователь,
			|	ИСТИНА КАК Чтение,
			|	МАКСИМУМ(ПраваПоДескрипторамДоступаОбъектов.Добавление
			|			И ДескрипторыДляОбъектов.ТипДескриптора <> 0) КАК Добавление,
			|	МАКСИМУМ(ПраваПоДескрипторамДоступаОбъектов.Изменение
			|			И ДескрипторыДляОбъектов.ТипДескриптора <> 0) КАК Изменение,
			|	МАКСИМУМ(ПраваПоДескрипторамДоступаОбъектов.Удаление
			|			И ДескрипторыДляОбъектов.ТипДескриптора <> 0) КАК Удаление,
			|	ЛОЖЬ КАК УправлениеПравами,
			|	МАКСИМУМ(ПраваПоДескрипторамДоступаОбъектов.Изменение
			|			И ДескрипторыДляОбъектов.ТипДескриптора В (1, 4)) КАК ЕстьПравоИзмененияБезОграничения
			|ПОМЕСТИТЬ ПраваБезУчетаПолитикИПапок
			|ИЗ
			|	РегистрСведений.ДескрипторыДляОбъектов КАК ДескрипторыДляОбъектов
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ПраваПоДескрипторамДоступаОбъектов КАК ПраваПоДескрипторамДоступаОбъектов
			|		ПО ДескрипторыДляОбъектов.Дескриптор = ПраваПоДескрипторамДоступаОбъектов.Дескриптор
			|ГДЕ
			|	ДескрипторыДляОбъектов.Объект В(&ОбъектыДоступа)
			|	И НЕ ДескрипторыДляОбъектов.Отключен
			|	%ОтборПоПользователю%
			|
			|СГРУППИРОВАТЬ ПО
			|	ДескрипторыДляОбъектов.Объект,
			|	ПраваПоДескрипторамДоступаОбъектов.Пользователь,
			|	ДескрипторыДляОбъектов.ТипДескриптора В (1, 4)
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ДескрипторыДляОбъектов.Объект КАК ОбъектДоступа,
			|	ПраваПоДескрипторамДоступаОбъектов.Пользователь КАК Пользователь,
			|	МАКСИМУМ(ПраваПоДескрипторамДоступаОбъектов.Добавление) КАК Добавление,
			|	МАКСИМУМ(ПраваПоДескрипторамДоступаОбъектов.Изменение) КАК Изменение,
			|	МАКСИМУМ(ПраваПоДескрипторамДоступаОбъектов.Удаление) КАК Удаление
			|ПОМЕСТИТЬ ПраваПоПолитикамИПапкам
			|ИЗ
			|	РегистрСведений.ДескрипторыДляОбъектов КАК ДескрипторыДляОбъектов
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ПраваПоДескрипторамДоступаОбъектов КАК ПраваПоДескрипторамДоступаОбъектов
			|		ПО ДескрипторыДляОбъектов.Дескриптор = ПраваПоДескрипторамДоступаОбъектов.Дескриптор
			|ГДЕ
			|	ДескрипторыДляОбъектов.Объект В(&ОбъектыДоступа)
			|	И ДескрипторыДляОбъектов.ТипДескриптора = 0
			|	И НЕ ДескрипторыДляОбъектов.Отключен
			|	%ОтборПоПользователю%
			|
			|СГРУППИРОВАТЬ ПО
			|	ДескрипторыДляОбъектов.Объект,
			|	ПраваПоДескрипторамДоступаОбъектов.Пользователь
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ПраваБезУчетаПолитикИПапок.ОбъектДоступа,
			|	ПраваБезУчетаПолитикИПапок.Пользователь,
			|	ПраваБезУчетаПолитикИПапок.Чтение,
			|	ПраваБезУчетаПолитикИПапок.ЕстьПравоИзмененияБезОграничения
			|		ИЛИ ПраваБезУчетаПолитикИПапок.Добавление
			|			И ЕСТЬNULL(ПраваПоПолитикамИПапкам.Добавление, ЛОЖЬ) КАК Добавление,
			|	ПраваБезУчетаПолитикИПапок.ЕстьПравоИзмененияБезОграничения
			|		ИЛИ ПраваБезУчетаПолитикИПапок.Изменение
			|			И ЕСТЬNULL(ПраваПоПолитикамИПапкам.Изменение, ЛОЖЬ) КАК Изменение,
			|	ПраваБезУчетаПолитикИПапок.ЕстьПравоИзмененияБезОграничения
			|		ИЛИ ПраваБезУчетаПолитикИПапок.Удаление
			|			И ЕСТЬNULL(ПраваПоПолитикамИПапкам.Удаление, ЛОЖЬ) КАК Удаление,
			|	ПраваБезУчетаПолитикИПапок.УправлениеПравами
			|ИЗ
			|	ПраваБезУчетаПолитикИПапок КАК ПраваБезУчетаПолитикИПапок
			|		ЛЕВОЕ СОЕДИНЕНИЕ ПраваПоПолитикамИПапкам КАК ПраваПоПолитикамИПапкам
			|		ПО ПраваБезУчетаПолитикИПапок.ОбъектДоступа = ПраваПоПолитикамИПапкам.ОбъектДоступа
			|			И ПраваБезУчетаПолитикИПапок.Пользователь = ПраваПоПолитикамИПапкам.Пользователь";
			
	ИначеЕсли ТипОбъекта = Тип("СправочникСсылка.ПротоколыМероприятий") Тогда
		
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	ПротоколыМероприятий.Ссылка КАК ОбъектДоступа,
			|	ПраваПоДескрипторамДоступаОбъектов.Пользователь,
			|	ИСТИНА КАК Чтение,
			|	МАКСИМУМ(ПраваПоДескрипторамДоступаОбъектов.Добавление
			|			И ДескрипторыДляОбъектов.ТипДескриптора <> 0) КАК Добавление,
			|	МАКСИМУМ(ПраваПоДескрипторамДоступаОбъектов.Изменение
			|			И ДескрипторыДляОбъектов.ТипДескриптора <> 0) КАК Изменение,
			|	МАКСИМУМ(ПраваПоДескрипторамДоступаОбъектов.Удаление
			|			И ДескрипторыДляОбъектов.ТипДескриптора <> 0) КАК Удаление,
			|	ЛОЖЬ КАК УправлениеПравами
			|ИЗ
			|	Справочник.ПротоколыМероприятий КАК ПротоколыМероприятий
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ДескрипторыДляОбъектов КАК ДескрипторыДляОбъектов
			|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ПраваПоДескрипторамДоступаОбъектов КАК ПраваПоДескрипторамДоступаОбъектов
			|			ПО ДескрипторыДляОбъектов.Дескриптор = ПраваПоДескрипторамДоступаОбъектов.Дескриптор
			|		ПО ПротоколыМероприятий.Владелец = ДескрипторыДляОбъектов.Объект
			|ГДЕ
			|	ПротоколыМероприятий.Ссылка В(&ОбъектыДоступа)
			|	И НЕ ДескрипторыДляОбъектов.Отключен
			|	%ОтборПоПользователю%
			|
			|СГРУППИРОВАТЬ ПО
			|	ПротоколыМероприятий.Ссылка,
			|	ПраваПоДескрипторамДоступаОбъектов.Пользователь";
		
	ИначеЕсли ТипОбъекта = Тип("СправочникСсылка.ПроектныеЗадачи") Тогда
		
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	ПроектныеЗадачи.Ссылка КАК ОбъектДоступа,
			|	ПраваПоДескрипторамДоступаОбъектов.Пользователь,
			|	ИСТИНА КАК Чтение,
			|	МАКСИМУМ(ПраваПоДескрипторамДоступаОбъектов.Добавление
			|			И ДескрипторыДляОбъектов.ТипДескриптора <> 0) КАК Добавление,
			|	МАКСИМУМ(ПраваПоДескрипторамДоступаОбъектов.Изменение
			|			И ДескрипторыДляОбъектов.ТипДескриптора <> 0) КАК Изменение,
			|	МАКСИМУМ(ПраваПоДескрипторамДоступаОбъектов.Удаление
			|			И ДескрипторыДляОбъектов.ТипДескриптора <> 0) КАК Удаление,
			|	ЛОЖЬ КАК УправлениеПравами
			|ИЗ
			|	Справочник.ПроектныеЗадачи КАК ПроектныеЗадачи
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ДескрипторыДляОбъектов КАК ДескрипторыДляОбъектов
			|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ПраваПоДескрипторамДоступаОбъектов КАК ПраваПоДескрипторамДоступаОбъектов
			|			ПО ДескрипторыДляОбъектов.Дескриптор = ПраваПоДескрипторамДоступаОбъектов.Дескриптор
			|		ПО ПроектныеЗадачи.Владелец = ДескрипторыДляОбъектов.Объект
			|ГДЕ
			|	ПроектныеЗадачи.Ссылка В(&ОбъектыДоступа)
			|	И НЕ ДескрипторыДляОбъектов.Отключен
			|	%ОтборПоПользователю%
			|
			|СГРУППИРОВАТЬ ПО
			|	ПроектныеЗадачи.Ссылка,
			|	ПраваПоДескрипторамДоступаОбъектов.Пользователь";
		
	ИначеЕсли ТипОбъекта = Тип("СправочникСсылка.Контроль") Тогда	
		
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	Контроль.Ссылка КАК ОбъектДоступа,
			|	СоставСубъектов.Пользователь,
			|	ИСТИНА КАК Чтение,
			|	Контроль.Автор = СоставСубъектов.Пользователь КАК Добавление,
			|	ИСТИНА КАК Изменение,
			|	ИСТИНА КАК Удаление,
			|	ЛОЖЬ КАК УправлениеПравами
			|ИЗ
			|	Справочник.Контроль КАК Контроль
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СоставСубъектовПравДоступа КАК СоставСубъектов
			|		ПО (Контроль.Контролер = СоставСубъектов.Субъект
			|				ИЛИ Контроль.Автор = СоставСубъектов.Субъект)
			|			И СоставСубъектов.ИмяОбластиДелегирования В ("""", ""Контроль"")
			|ГДЕ
			|	Контроль.Ссылка В (&ОбъектыДоступа)
			|	%ОтборПоПользователю%";
			
		ЗаменяемыеПодстроки.Вставить("%ОтборПоПользователю%",
			?(Пользователи = Неопределено, "", "И СоставСубъектов.Пользователь В (&Пользователи)"));
		
	ИначеЕсли ТипОбъекта = Тип("ДокументСсылка.Отсутствие") Тогда
		
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	Отсутствие.Ссылка КАК ОбъектДоступа,
			|	СоставСубъектов.Пользователь,
			|	ИСТИНА КАК Чтение,
			|	ВЫБОР
			|		КОГДА Отсутствие.Автор = СоставСубъектов.Пользователь
			|			ТОГДА ИСТИНА
			|		ИНАЧЕ ЛОЖЬ
			|	КОНЕЦ КАК Добавление,
			|	ВЫБОР
			|		КОГДА СоставСубъектов.Субъект ЕСТЬ NULL 
			|			ТОГДА ЛОЖЬ
			|		ИНАЧЕ ИСТИНА
			|	КОНЕЦ КАК Изменение,
			|	ЛОЖЬ КАК Удаление,
			|	ЛОЖЬ КАК УправлениеПравами
			|ИЗ
			|	Документ.Отсутствие КАК Отсутствие
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СоставСубъектовПравДоступа КАК СоставСубъектов
			|		ПО (Отсутствие.Сотрудник = СоставСубъектов.Субъект
			|				ИЛИ Отсутствие.Автор = СоставСубъектов.Субъект)
			|			И СоставСубъектов.ИмяОбластиДелегирования В(&ОбластиДелегирования)
			|			%ОтборПоПользователю%
			|ГДЕ
			|	Отсутствие.Ссылка В (&ОбъектыДоступа)";
		
		ЗаменяемыеПодстроки.Вставить("%ОтборПоПользователю%",
			?(Пользователи = Неопределено, "", "И СоставСубъектов.Пользователь В (&Пользователи)"));
		
	ИначеЕсли ТипОбъекта = Тип("СправочникСсылка.ТерриторииИПомещения") Тогда
		
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	ТерриторииИПомещения.Ссылка КАК ОбъектДоступа,
			|	СоставСубъектов.Пользователь,
			|	ИСТИНА КАК Чтение,
			|	ЛОЖЬ КАК Добавление,
			|	ВЫБОР
			|		КОГДА СоставСубъектов.Субъект ЕСТЬ NULL 
			|			ТОГДА ЛОЖЬ
			|		ИНАЧЕ ИСТИНА
			|	КОНЕЦ КАК Изменение,
			|	ЛОЖЬ КАК Удаление,
			|	ЛОЖЬ КАК УправлениеПравами
			|ИЗ
			|	Справочник.ТерриторииИПомещения КАК ТерриторииИПомещения
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СоставСубъектовПравДоступа КАК СоставСубъектов
			|		ПО ТерриторииИПомещения.Ответственный = СоставСубъектов.Субъект
			|			И СоставСубъектов.ИмяОбластиДелегирования В(&ОбластиДелегирования)
			|			%ОтборПоПользователю%
			|ГДЕ
			|	ТерриторииИПомещения.Ссылка В (&ОбъектыДоступа)";
			
		ЗаменяемыеПодстроки.Вставить("%ОтборПоПользователю%",
			?(Пользователи = Неопределено, "", "И СоставСубъектов.Пользователь В (&Пользователи)"));
		
	ИначеЕсли ТипОбъекта = Тип("ДокументСсылка.Бронь") Тогда
		
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	Бронь.Ссылка КАК ОбъектДоступа,
			|	СоставСубъектов.Пользователь,
			|	ИСТИНА КАК Чтение,
			|	ВЫБОР
			|		КОГДА Бронь.Автор = СоставСубъектов.Пользователь
			|			ТОГДА ИСТИНА
			|		ИНАЧЕ ЛОЖЬ
			|	КОНЕЦ КАК Добавление,
			|	ВЫБОР
			|		КОГДА СоставСубъектов.Субъект ЕСТЬ NULL 
			|			ТОГДА ЛОЖЬ
			|		ИНАЧЕ ИСТИНА
			|	КОНЕЦ КАК Изменение,
			|	ЛОЖЬ КАК Удаление,
			|	ЛОЖЬ КАК УправлениеПравами
			|ИЗ
			|	Документ.Бронь КАК Бронь
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СоставСубъектовПравДоступа КАК СоставСубъектов
			|		ПО Бронь.Автор = СоставСубъектов.Субъект
			|			И СоставСубъектов.ИмяОбластиДелегирования В(&ОбластиДелегирования)
			|			%ОтборПоПользователю%
			|ГДЕ
			|	Бронь.Ссылка В (&ОбъектыДоступа)";
		
		ЗаменяемыеПодстроки.Вставить("%ОтборПоПользователю%",
			?(Пользователи = Неопределено, "", "И СоставСубъектов.Пользователь В (&Пользователи)"));
		
	ИначеЕсли ТипОбъекта = Тип("СправочникСсылка.ПоказателиПроцессов") Тогда
		
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	ПоказателиПроцессов.Ссылка КАК ОбъектДоступа,
			|	СоставСубъектов.Пользователь КАК Пользователь,
			|	ИСТИНА КАК Чтение,
			|	ВЫБОР
			|		КОГДА ПоказателиПроцессов.Автор = СоставСубъектов.Пользователь
			|			ТОГДА ИСТИНА
			|		ИНАЧЕ ЛОЖЬ
			|	КОНЕЦ КАК Добавление,
			|	ВЫБОР
			|		КОГДА СоставСубъектов.Субъект ЕСТЬ NULL 
			|			ТОГДА ЛОЖЬ
			|		ИНАЧЕ ИСТИНА
			|	КОНЕЦ КАК Изменение,
			|	ЛОЖЬ КАК Удаление,
			|	ЛОЖЬ КАК УправлениеПравами
			|ИЗ
			|	Справочник.ПоказателиПроцессов КАК ПоказателиПроцессов
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СоставСубъектовПравДоступа КАК СоставСубъектов
			|		ПО ПоказателиПроцессов.Автор = СоставСубъектов.Субъект
			|			И СоставСубъектов.ИмяОбластиДелегирования В(&ОбластиДелегирования)
			|			%ОтборПоПользователю%
			|ГДЕ
			|	ПоказателиПроцессов.Ссылка В (&ОбъектыДоступа)";
		
		ЗаменяемыеПодстроки.Вставить("%ОтборПоПользователю%",
			?(Пользователи = Неопределено, "", "И СоставСубъектов.Пользователь В (&Пользователи)"));
			
	ИначеЕсли ТипОбъекта = Тип("СправочникСсылка.МестаХраненияДел") Тогда
		
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	ВложенныйЗапрос.ОбъектДоступа,
			|	ВложенныйЗапрос.Пользователь,
			|	МАКСИМУМ(ВложенныйЗапрос.Чтение) КАК Чтение,
			|	МАКСИМУМ(ВложенныйЗапрос.Добавление) КАК Добавление,
			|	МАКСИМУМ(ВложенныйЗапрос.Изменение) КАК Изменение,
			|	МАКСИМУМ(ВложенныйЗапрос.Удаление) КАК Удаление,
			|	МАКСИМУМ(ВложенныйЗапрос.УправлениеПравами) КАК УправлениеПравами
			|ИЗ
			|	(ВЫБРАТЬ
			|		МестаХраненияДел.Ссылка КАК ОбъектДоступа,
			|		ПраваПоДескрипторамДоступаОбъектов.Пользователь КАК Пользователь,
			|		ПраваПоДескрипторамДоступаОбъектов.Чтение КАК Чтение,
			|		ПраваПоДескрипторамДоступаОбъектов.Добавление КАК Добавление,
			|		ПраваПоДескрипторамДоступаОбъектов.Изменение КАК Изменение,
			|		ПраваПоДескрипторамДоступаОбъектов.Удаление КАК Удаление,
			|		ПраваПоДескрипторамДоступаОбъектов.УправлениеПравами КАК УправлениеПравами
			|	ИЗ
			|		Справочник.МестаХраненияДел КАК МестаХраненияДел
			|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ДескрипторыДляОбъектов КАК ДескрипторыДляОбъектов
			|				ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ПраваПоДескрипторамДоступаОбъектов КАК ПраваПоДескрипторамДоступаОбъектов
			|				ПО ДескрипторыДляОбъектов.Дескриптор = ПраваПоДескрипторамДоступаОбъектов.Дескриптор
			|			ПО МестаХраненияДел.Организация = ДескрипторыДляОбъектов.Объект
			|	ГДЕ
			|		МестаХраненияДел.Ссылка В(&ОбъектыДоступа)
			|		И НЕ ДескрипторыДляОбъектов.Отключен
			|		%ОтборПоПользователю%
			|	
			|	ОБЪЕДИНИТЬ ВСЕ
			|	
			|	ВЫБРАТЬ
			|		МестаХраненияДел.Ссылка,
			|		ПользователиВКонтейнерах.Пользователь,
			|		ИСТИНА,
			|		ПраваРолей.Добавление,
			|		ПраваРолей.Изменение,
			|		ПраваРолей.Удаление,
			|		ЛОЖЬ
			|	ИЗ
			|		Справочник.МестаХраненияДел КАК МестаХраненияДел,
			|		РегистрСведений.ПолномочияПользователей КАК ПолномочияПользователей
			|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ПрофилиГруппДоступа.Роли КАК ПрофилиГруппДоступаРоли
			|				ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ПраваРолей КАК ПраваРолей
			|				ПО ПрофилиГруппДоступаРоли.Роль = ПраваРолей.Роль
			|			ПО ПолномочияПользователей.Полномочия = ПрофилиГруппДоступаРоли.Ссылка
			|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ПользователиВКонтейнерах КАК ПользователиВКонтейнерах
			|			ПО ПолномочияПользователей.Владелец = ПользователиВКонтейнерах.Контейнер
			|	ГДЕ
			|		ПраваРолей.ОбъектМетаданных = &ИдентификаторОМ
			|		И (МестаХраненияДел.Организация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
			|				ИЛИ ЛОЖЬ В
			|					(ВЫБРАТЬ
			|						ИспользоватьУчетПоОрганизациям.Значение
			|					ИЗ
			|						Константа.ИспользоватьУчетПоОрганизациям КАК ИспользоватьУчетПоОрганизациям))
			|		%ОтборПоПользователюКонтейнера%) КАК ВложенныйЗапрос
			|
			|СГРУППИРОВАТЬ ПО
			|	ВложенныйЗапрос.ОбъектДоступа,
			|	ВложенныйЗапрос.Пользователь";
			
		ЗаменяемыеПодстроки.Вставить("%ОтборПоПользователюКонтейнера%",
			?(Пользователи = Неопределено, "", "И ПользователиВКонтейнерах.Пользователь В (&Пользователи)"));
		
	КонецЕсли;
	
	Возврат Запрос;
	
КонецФункции

// Переопределяемая часть обработчика подписки ДокументооборотПередЗаписьюПравообразующихОбъектов
Функция ДокументооборотПередЗаписьюПравообразующихОбъектов(Источник, Отказ) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	// Проверка на то, что изменились правообразующие реквизиты
	Если ТипЗнч(Источник) = Тип("СправочникОбъект.Проекты") Тогда
		
		Если ПолучитьФункциональнуюОпцию("ВестиУчетПоПроектам") Тогда
			
			Если Источник.ЭтоНовый() Тогда
				Источник.ДополнительныеСвойства.Вставить("ЗначениеИзменено", Истина);
			Иначе
				СтарыйРуководитель = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Источник.Ссылка, "Руководитель");
				Если Источник.Руководитель <> СтарыйРуководитель Тогда
					Источник.ДополнительныеСвойства.Вставить("ЗначениеИзменено", Истина);
				КонецЕсли;
			КонецЕсли;
			
		КонецЕсли;	
		
		Возврат Истина;
		
	ИначеЕсли ТипЗнч(Источник) = Тип("СправочникОбъект.ДелегированиеПрав") Тогда
		
		Если Источник.ЭтоНовый() Тогда
			Источник.ДополнительныеСвойства.Вставить("ЗначениеИзменено", Истина);
		Иначе
			
			РеквизитыСсылки = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
				Источник.Ссылка, "Кому, ОтКого, ПометкаУдаления, Действует, ВариантДелегирования");
			
			СтарыеОбластиДелегирования = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(
				Источник.Ссылка, "ОбластиДелегирования").Выгрузить();
			
			Если Источник.Кому <> РеквизитыСсылки.Кому
			 Или Источник.ОтКого <> РеквизитыСсылки.ОтКого
			 Или Источник.ПометкаУдаления <> РеквизитыСсылки.ПометкаУдаления 
			 Или Источник.Действует <> РеквизитыСсылки.Действует 
			 Или Источник.ВариантДелегирования <> РеквизитыСсылки.ВариантДелегирования 
			 Или ОбщегоНазначенияДокументооборотКлиентСервер.ЕстьОтличияВТаблицах(
				 Источник.ОбластиДелегирования, СтарыеОбластиДелегирования, "ОбластьДелегирования") Тогда 
				
				Источник.ДополнительныеСвойства.Вставить("ЗначениеИзменено", Истина);
				
				Если Источник.ОтКого <> РеквизитыСсылки.ОтКого Тогда
					Источник.ДополнительныеСвойства.Вставить("СтарыйОтКого", РеквизитыСсылки.ОтКого);
				КонецЕсли;
				
			КонецЕсли;
		КонецЕсли;
		
		Возврат Истина;
		
	ИначеЕсли ТипЗнч(Источник) = Тип("СправочникОбъект.ОбластиДелегированияПрав") Тогда
		
		Если Источник.ЭтоНовый()
		 Или Источник.ПометкаУдаления <> Источник.Ссылка.ПометкаУдаления Тогда 
		 
		 	Источник.ДополнительныеСвойства.Вставить("ЗначениеИзменено", Истина);
			
		Иначе	
			
			Если Источник.Ссылка.Состав.Количество() <> Источник.Состав.Количество() Тогда 
				Источник.ДополнительныеСвойства.Вставить("ЗначениеИзменено", Истина);
			Иначе	
				Для Каждого Строка Из Источник.Ссылка.Состав Цикл
					СтруктураОтбора = Новый Структура("ОбъектМетаданных", Строка.ОбъектМетаданных);
					НайденныеСтроки = Источник.Состав.НайтиСтроки(СтруктураОтбора);
					Если НайденныеСтроки.Количество() = 0 Тогда 
						Источник.ДополнительныеСвойства.Вставить("ЗначениеИзменено", Истина);
						Прервать;
					КонецЕсли;
				КонецЦикла;	
			КонецЕсли;	
			
		КонецЕсли;
		
		Возврат Истина;
		
	КонецЕсли;
	
	Возврат Ложь; // Не обработано
	
КонецФункции

// Переопределяемая часть обработчика подписки ДокументооборотПередЗаписьюПравообразующихОбъектов
Функция ДокументооборотПриЗаписиПравообразующихОбъектов(Источник, Отказ) Экспорт
	
	Если ТипЗнч(Источник) = Тип("СправочникОбъект.Проекты") Тогда
		
		Запрос = Новый Запрос(
			"ВЫБРАТЬ
			|	ДескрипторыДоступаРегистров.Ссылка
			|ИЗ
			|	Справочник.ДескрипторыДоступаРегистров КАК ДескрипторыДоступаРегистров
			|ГДЕ
			|	ДескрипторыДоступаРегистров.ОбъектДоступа1 = &Проект");
			
		Запрос.УстановитьПараметр("Проект", Источник.Ссылка);
		
		ТекущийПриоритетОчередиОбновленияПрав = ПараметрыСеанса.ПриоритетОчередиОбновленияПрав;
		ПараметрыСеанса.ПриоритетОчередиОбновленияПрав = 2; // Долгая очередь
		
  		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			Справочники.ДескрипторыДоступаРегистров.ОбновитьПрава(Выборка.Ссылка);
	  	КонецЦикла;
		
		ПараметрыСеанса.ПриоритетОчередиОбновленияПрав = ТекущийПриоритетОчередиОбновленияПрав;
		
		Возврат Истина;
		
	КонецЕсли;
	
	Возврат Ложь; // Не обработано
	
КонецФункции

Функция ОбновитьПромежуточныеДанныеДляРасчетаПрав(Источник, ТипИсточника) Экспорт
	
	Если ТипИсточника = Тип("СправочникОбъект.Проекты") Тогда
		
		РегистрыСведений.СоставСубъектовПравДоступа.ЗаписьПроекта(Источник);
		
		Возврат Истина;
		
	ИначеЕсли ТипИсточника = Тип("СправочникОбъект.ОбластиДелегированияПрав") Тогда
		
		РегистрыСведений.СоставСубъектовПравДоступа.ЗаписьОбластиДелегирования(Источник);
		
		Возврат Истина;
		
	КонецЕсли;
	
	Возврат Ложь; // Не обработано
	
КонецФункции

// Переопределяемая часть обработчика подписки ДокументооборотПраваДоступаПриЗаписиПравообразующихКонстант
Функция ДокументооборотПраваДоступаПриЗаписиПравообразующихКонстант(Источник, Отказ) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если ТипЗнч(Источник) = Тип("КонстантаМенеджерЗначения.ВестиУчетПоПроектам") Тогда
		
		// Обработка всех дескрипторов, у которых установлен реквизит Проект
		Запрос = Новый Запрос;
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	ДескрипторыДоступаОбъектов.Ссылка
			|ИЗ
			|	Справочник.ДескрипторыДоступаОбъектов КАК ДескрипторыДоступаОбъектов
			|ГДЕ
			|	ДескрипторыДоступаОбъектов.Проект <> &ПустойПроект";
		
		Запрос.УстановитьПараметр("ПустойПроект", Справочники.Проекты.ПустаяСсылка());
		
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			Дескриптор = Выборка.Ссылка;
			Справочники.ДескрипторыДоступаОбъектов.ОбновитьПрава(Дескриптор);
		КонецЦикла;
		
		Возврат Истина;
		
	КонецЕсли;	
	
	Возврат Ложь;
	
КонецФункции

// Переопределяемая часть метода  ДокументооборотПраваДоступа.ОбновитьПраваСвязанныхДескрипторовПоОбъекту
Процедура ОбновитьПраваСвязанныхДескрипторовПоОбъекту(Объект) Экспорт
	
КонецПроцедуры

Функция ДокументооборотПриЗаписиПравообразующихРегистров(Источник, Отказ) Экспорт 
	
	Если ТипЗнч(Источник) = Тип("РегистрСведенийНаборЗаписей.УчастникиМероприятия") Тогда
		
		Мероприятие = Источник.Отбор.Мероприятие.Значение;
		Если ЗначениеЗаполнено(Мероприятие) Тогда
			МероприятиеОбъект = Мероприятие.ПолучитьОбъект();
			Если МероприятиеОбъект <> Неопределено Тогда // Проверка на удаление мероприятия
				ДокументооборотПраваДоступа.ОпределитьДескрипторыОбъекта(МероприятиеОбъект);
			КонецЕсли;
		КонецЕсли;
		
		Возврат Истина;
		
	// ТСК Талько Э.Г.; 09.06.2018; Управление доступом {
	ИначеЕсли ТипЗнч(Источник) = Тип("РегистрСведенийНаборЗаписей.ra_KomandyNesootvetstvij") Тогда
		
		ОтборПоНесоответствию = Источник.Отбор.Nesootvetstvie;
		
		Если Не ОтборПоНесоответствию.Использование Тогда
			Возврат Истина;
		КонецЕсли;
		
		ЗначениеСсылки = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ОтборПоНесоответствию.Значение, "Ссылка");
		Если ЗначениеСсылки = Неопределено Тогда
			Если Не Источник.ОбменДанными.Загрузка Или Источник.Количество() = 0 Тогда
				// Это удаление элемента, ставить его в очередь не нужно.
				Возврат Истина;
			КонецЕсли;
		КонецЕсли;
		
		ОбновитьПраваДоступа = ?(Источник.ДополнительныеСвойства.Свойство("ОбновитьПраваДоступа"),
			Источник.ДополнительныеСвойства.ОбновитьПраваДоступа, Ложь);
		
		//Документы.ra_Nesootvetstvie.ПерезаписатьРабочиеГруппыОбъектов(ОтборПоНесоответствию.Значение, ОбновитьПраваДоступа);
		Документы.ra_Nesootvetstvie.ПерезаписатьРабочиеГруппыОбъектов(ОтборПоНесоответствию.Значение, Истина);
		
		Возврат Истина;
		
	ИначеЕсли ТипЗнч(Источник) = Тип("РегистрСведенийНаборЗаписей.ra_KomandyPoObmenuLuchshimiPraktikami") Тогда
		
		ОтборПоНесоответствию = Источник.Отбор.Nesootvetstvie;
		
		Если Не ОтборПоНесоответствию.Использование Тогда
			Возврат Истина;
		КонецЕсли;
		
		ЗначениеСсылки = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ОтборПоНесоответствию.Значение, "Ссылка");
		Если ЗначениеСсылки = Неопределено Тогда
			Если Не Источник.ОбменДанными.Загрузка Или Источник.Количество() = 0 Тогда
				// Это удаление элемента, ставить его в очередь не нужно.
				Возврат Истина;
			КонецЕсли;
		КонецЕсли;
		
		ОбновитьПраваДоступа = ?(Источник.ДополнительныеСвойства.Свойство("ОбновитьПраваДоступа"),
			Источник.ДополнительныеСвойства.ОбновитьПраваДоступа, Ложь);
		
		//Документы.ra_Nesootvetstvie.ПерезаписатьРабочиеГруппыОбъектов(ОтборПоНесоответствию.Значение, ОбновитьПраваДоступа);
		Документы.ra_Nesootvetstvie.ПерезаписатьРабочиеГруппыОбъектов(ОтборПоНесоответствию.Значение, Истина);
		
		Возврат Истина;
		
	// ТСК Талько Э.Г.; 09.06.2018; Управление доступом }
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

// Возвращает список имен объектов метаданных, чьи права зависят от прав других объектов
// при обновлении прав объекты с этими именами должны обрабатываться в последнюю очередь
Функция ОбъектыСЗависимымиПравами() Экспорт
	
	// Объекты в папках.
	ЗависимыеОбъекты = Новый Массив;
	ЗависимыеОбъекты.Добавить(ОбщегоНазначения.ИдентификаторОбъектаМетаданных(Метаданные.Справочники.ВнутренниеДокументы));
	ЗависимыеОбъекты.Добавить(ОбщегоНазначения.ИдентификаторОбъектаМетаданных(Метаданные.Справочники.ШаблоныВнутреннихДокументов));
	ЗависимыеОбъекты.Добавить(ОбщегоНазначения.ИдентификаторОбъектаМетаданных(Метаданные.Справочники.Проекты));
	ЗависимыеОбъекты.Добавить(ОбщегоНазначения.ИдентификаторОбъектаМетаданных(Метаданные.Справочники.Мероприятия));
	ЗависимыеОбъекты.Добавить(ОбщегоНазначения.ИдентификаторОбъектаМетаданных(Метаданные.Справочники.ТемыОбсуждений));
	ЗависимыеОбъекты.Добавить(ОбщегоНазначения.ИдентификаторОбъектаМетаданных(Метаданные.Справочники.СообщенияОбсуждений));
	ЗависимыеОбъекты.Добавить(ОбщегоНазначения.ИдентификаторОбъектаМетаданных(Метаданные.Документы.ВходящееПисьмо));
	ЗависимыеОбъекты.Добавить(ОбщегоНазначения.ИдентификаторОбъектаМетаданных(Метаданные.Документы.ИсходящееПисьмо));
	
	Возврат ЗависимыеОбъекты;
	
КонецФункции

// Возвращает Истина, если пользователю делегированы права доступа
// Параметры:
// Пользователь - пользователь, чьи права нужно проверить
// УчитыватьСрокДелегирования - Булево - если Истина, то проверять, что дата окончания делегирования еще не наступила
//
Функция ЭтоДелегат(Пользователь, УчитыватьСрокДелегирования = Истина) Экспорт
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ИСТИНА КАК ЕстьЗаписи
		|ИЗ
		|	Справочник.ДелегированиеПрав КАК ДелегированиеПрав
		|ГДЕ
		|	ДелегированиеПрав.Кому = &Пользователь
		|	И (ДелегированиеПрав.ДатаОкончанияДействия = ДАТАВРЕМЯ(1, 1, 1)
		|			ИЛИ НЕ &УчитыватьСрокДелегирования
		|			ИЛИ ДелегированиеПрав.ДатаОкончанияДействия >= &ТекущаяДата)");
	
	Запрос.УстановитьПараметр("Пользователь", Пользователь);
	Запрос.УстановитьПараметр("УчитыватьСрокДелегирования", УчитыватьСрокДелегирования);
	Запрос.УстановитьПараметр("ТекущаяДата", ТекущаяДата());
	
	ЕстьДелегированныеПрава = Не Запрос.Выполнить().Пустой();
	
	Возврат ЕстьДелегированныеПрава;
	
КонецФункции

// Переопределяемая часть функции ДокументооборотПраваДоступаПовтИсп.ТипыПапокИСодержимого
//
Процедура ДополнитьТипыПапокИСодержимого(Результат) Экспорт
	
	// Проекты
	МассивМетаданныхСодержимого = Новый Массив;
	МассивМетаданныхСодержимого.Добавить(Метаданные.Справочники.Проекты);
	Результат.Вставить(Тип("СправочникСсылка.ПапкиПроектов"), МассивМетаданныхСодержимого);
	
	// Мероприятия
	МассивМетаданныхСодержимого = Новый Массив;
	МассивМетаданныхСодержимого.Добавить(Метаданные.Справочники.Мероприятия);
	Результат.Вставить(Тип("СправочникСсылка.ПапкиМероприятий"), МассивМетаданныхСодержимого);
	
	// Письма
	МассивМетаданныхСодержимого = Новый Массив;
	МассивМетаданныхСодержимого.Добавить(Метаданные.Документы.ВходящееПисьмо);
	МассивМетаданныхСодержимого.Добавить(Метаданные.Документы.ИсходящееПисьмо);
	Результат.Вставить(Тип("СправочникСсылка.ПапкиПисем"), МассивМетаданныхСодержимого);
	
	// Темы обсуждений
	МассивМетаданныхСодержимого = Новый Массив;
	МассивМетаданныхСодержимого.Добавить(Метаданные.Справочники.ТемыОбсуждений);
	Результат.Вставить(Тип("СправочникСсылка.ПапкиФорума"), МассивМетаданныхСодержимого);
	
КонецПроцедуры

// Переопределяемая часть метода
//
Процедура ОбновитьПраваЗависящиеОтСоставаУчастниковПроцесса(МассивПроцессов) Экспорт
	
КонецПроцедуры

// Обновляет права объектов, которые зависят от прав на файл.
// 
// Параметры:
// 	ФайлСсылка - СправочникСсылка - ссылка на файл.
// 
Процедура ОбновитьПраваЗависящиеОтФайла(ФайлСсылка) Экспорт
	
	// Права на темы форума, созданные на основании файла.
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	ТемыОбсуждений.Ссылка
		|ИЗ
		|	Справочник.ТемыОбсуждений КАК ТемыОбсуждений
		|ГДЕ
		|	ТемыОбсуждений.Документ = &Файл");
		
	Запрос.УстановитьПараметр("Файл", ФайлСсылка);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ДокументооборотПраваДоступа.ОпределитьДескрипторыОбъекта(Выборка.Ссылка);
	КонецЦикла;
	
КонецПроцедуры

// Заполняет структуру настроек, которые будут использоваться при получении прав объекта
//
// Параметры:
//	ДопНастройки - Структура - структура настроек
//  ТипОбъекта - Тип - тип объекта доступа
//  ОбъектДоступа - Ссылка - объект доступа
//  
Процедура ЗаполнитьДопНастройкиДляПолученияПравПоОбъекту(ДопНастройки, ТипОбъекта) Экспорт
	
	Если ТипОбъекта = Тип("СправочникСсылка.ГруппыКонтактовПользователей") Тогда
		ДопНастройки.ДобавлятьРуководителейИДелегатов = Ложь;
	КонецЕсли;
	
КонецПроцедуры

// Для внутреннего использования
//
Процедура ДополнитьОписанияТаблицСоСсылкамиНаКонтрагентов(ОписанияТаблиц) Экспорт
	
	ОписанияТаблиц.Удалить("Справочник.ВнутренниеДокументы");
	ОписанияТаблиц.Удалить("Справочник.ШаблоныВнутреннихДокументов");
	
	ОписанияТаблиц.Вставить("Справочник.ВнутренниеДокументы.Контрагенты", "Контрагент");
	ОписанияТаблиц.Вставить("Справочник.ШаблоныВнутреннихДокументов.Контрагенты", "Контрагент");
	
КонецПроцедуры

// Возвращает имена предопределенных областей делегирования,
//  в состав которых входит переданный идентификатор объекта метаданных
// 
// Параметры: 
// 	ИОМ - ссылка на справочник "Идентификаторы объектов метаданных"
// 
// Возвращаемое значение: массив имен предопределенных областей
// 
Функция ПолучитьОбластиДелегированияПоИОМ(ИдентификаторОМ) Экспорт
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ОбластиДелегированияПрав.ИмяПредопределенныхДанных
		|ИЗ
		|	Справочник.ОбластиДелегированияПрав КАК ОбластиДелегированияПрав
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ОбластиДелегированияПрав.Состав КАК ОбластиДелегированияПравСостав
		|		ПО ОбластиДелегированияПрав.Ссылка = ОбластиДелегированияПравСостав.Ссылка
		|ГДЕ
		|	ОбластиДелегированияПравСостав.ОбъектМетаданных = &ИдентификаторОМ");
	
	Запрос.УстановитьПараметр("ИдентификаторОМ", ИдентификаторОМ);
	ОбластиДелегирования = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("ИмяПредопределенныхДанных");
	ОбластиДелегирования.Добавить("");
	
	Возврат ОбластиДелегирования;
	
КонецФункции

// Возвращает таблицу видов доступа, в соответствии с текущими настройками.
// 
// Параметры:
//  ТолькоАктуальные - Булево - если Истина, то в результат будут включены только те виды доступа,
//   которые используются для расчета прав и не отключены по функциональным опциям.
// 
// Возвращаемое значение: Таблица значений.
// 
Функция ТаблицаРазрезовДоступа(ТолькоАктуальные = Истина) Экспорт
	
	ТаблицаРазрезовДоступа = Новый ТаблицаЗначений;
	ТаблицаРазрезовДоступа.Колонки.Добавить("Имя");
	ТаблицаРазрезовДоступа.Колонки.Добавить("ВидДоступа");
	ТаблицаРазрезовДоступа.Колонки.Добавить("ИмяТаблицыЗначенийДоступа");
	ТаблицаРазрезовДоступа.Колонки.Добавить("ИмяТЧДескриптора");
	ТаблицаРазрезовДоступа.Колонки.Добавить("ИмяПоляТаблицыДескриптора");
	
	// ТСК Талько Э.Г.; 09.06.2018; Управление доступом {
	Стр = ТаблицаРазрезовДоступа.Добавить();
	Стр.Имя = "ра_Организации";
	Стр.ВидДоступа = ПланыВидовХарактеристик.ВидыДоступа.ра_Организации;
	Стр.ИмяТаблицыЗначенийДоступа = Метаданные.Справочники.Контрагенты.ПолноеИмя();
	// ТСК Талько Э.Г.; 09.06.2018; Управление доступом }
	
	Стр = ТаблицаРазрезовДоступа.Добавить();
	Стр.Имя = "ВидыВнутреннихДокументов";
	Стр.ВидДоступа = ПланыВидовХарактеристик.ВидыДоступа.ВидыВнутреннихДокументов;
	Стр.ИмяТаблицыЗначенийДоступа = Метаданные.Справочники.ВидыВнутреннихДокументов.ПолноеИмя();
	
	Стр = ТаблицаРазрезовДоступа.Добавить();
	Стр.Имя = "ВидыВходящихДокументов";
	Стр.ВидДоступа = ПланыВидовХарактеристик.ВидыДоступа.ВидыВходящихДокументов;
	Стр.ИмяТаблицыЗначенийДоступа = Метаданные.Справочники.ВидыВходящихДокументов.ПолноеИмя();
	
	Стр = ТаблицаРазрезовДоступа.Добавить();
	Стр.Имя = "ВидыИсходящихДокументов";
	Стр.ВидДоступа = ПланыВидовХарактеристик.ВидыДоступа.ВидыИсходящихДокументов;
	Стр.ИмяТаблицыЗначенийДоступа = Метаданные.Справочники.ВидыИсходящихДокументов.ПолноеИмя();
	
	Стр = ТаблицаРазрезовДоступа.Добавить();
	Стр.Имя = "ВидыМероприятий";
	Стр.ВидДоступа = ПланыВидовХарактеристик.ВидыДоступа.ВидыМероприятий;
	Стр.ИмяТаблицыЗначенийДоступа = Метаданные.Справочники.ВидыМероприятий.ПолноеИмя();
	
	Стр = ТаблицаРазрезовДоступа.Добавить();
	Стр.Имя = "ВопросыДеятельности";
	Стр.ВидДоступа = ПланыВидовХарактеристик.ВидыДоступа.ВопросыДеятельности;
	Стр.ИмяТаблицыЗначенийДоступа = Метаданные.Справочники.ВопросыДеятельности.ПолноеИмя();
	
	Стр = ТаблицаРазрезовДоступа.Добавить();
	Стр.Имя = "ГрифыДоступа";
	Стр.ВидДоступа = ПланыВидовХарактеристик.ВидыДоступа.ГрифыДоступа;
	Стр.ИмяТаблицыЗначенийДоступа = Метаданные.Справочники.ГрифыДоступа.ПолноеИмя();
	
	Стр = ТаблицаРазрезовДоступа.Добавить();
	Стр.Имя = "Организации";
	Стр.ВидДоступа = ПланыВидовХарактеристик.ВидыДоступа.Организации;
	Стр.ИмяТаблицыЗначенийДоступа = Метаданные.Справочники.Организации.ПолноеИмя();
	
	Стр = ТаблицаРазрезовДоступа.Добавить();
	Стр.Имя = "Подразделения";
	Стр.ВидДоступа = ПланыВидовХарактеристик.ВидыДоступа.Подразделения;
	Стр.ИмяТаблицыЗначенийДоступа = Метаданные.Справочники.СтруктураПредприятия.ПолноеИмя();
	
	Стр = ТаблицаРазрезовДоступа.Добавить();
	Стр.Имя = "ГруппыДоступаКонтрагентов";
	Стр.ВидДоступа = ПланыВидовХарактеристик.ВидыДоступа.ГруппыДоступаКонтрагентов;
	Стр.ИмяТаблицыЗначенийДоступа = Метаданные.Справочники.ГруппыДоступаКонтрагентов.ПолноеИмя();
	
	Стр = ТаблицаРазрезовДоступа.Добавить();
	Стр.Имя = "ГруппыДоступаФизическихЛиц";
	Стр.ВидДоступа = ПланыВидовХарактеристик.ВидыДоступа.ГруппыДоступаФизическихЛиц;
	Стр.ИмяТаблицыЗначенийДоступа = Метаданные.Справочники.ГруппыДоступаФизическихЛиц.ПолноеИмя();
	
	Если ТолькоАктуальные Тогда
		ОтключенныеРазрезы = ДокументооборотПраваДоступаПовтИсп.ОтключенныеРазрезыДоступа();
		Для Каждого Разрез Из ОтключенныеРазрезы Цикл
			Стр = ТаблицаРазрезовДоступа.Найти(Разрез, "ВидДоступа");
			Если Стр <> Неопределено Тогда
				ТаблицаРазрезовДоступа.Удалить(Стр);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	ИменаРеквизитов = ДокументооборотПраваДоступаПовтИсп.ИменаРеквизитовДескрпитораПоРазрезамДоступа();
	Для Каждого Стр Из ТаблицаРазрезовДоступа Цикл
		ИмяРеквизитаДескриптора = ИменаРеквизитов.Получить(Стр.ВидДоступа);
 		НайденнаяПозиция = СтрНайти(ИмяРеквизитаДескриптора, ".");
		Если НайденнаяПозиция = 0 Тогда
			Стр.ИмяПоляТаблицыДескриптора = ИмяРеквизитаДескриптора;
		Иначе
			Стр.ИмяТЧДескриптора = Лев(ИмяРеквизитаДескриптора, НайденнаяПозиция - 1);
			Стр.ИмяПоляТаблицыДескриптора = Сред(ИмяРеквизитаДескриптора, НайденнаяПозиция + 1);
		КонецЕсли;
 	КонецЦикла;
	
	Возврат ТаблицаРазрезовДоступа;
	
КонецФункции

// Возвращает соответствие разрезов доступа и имен реквизитов Дескриптора.
//
Функция ИменаРеквизитовДескрпитораПоРазрезамДоступа() Экспорт
	
	Результат = Новый Соответствие;
	МенеджерВидовДоступа = ПланыВидовХарактеристик.ВидыДоступа;
	
	Результат.Вставить(МенеджерВидовДоступа.ВидыВнутреннихДокументов, "ВидОбъекта");
	Результат.Вставить(МенеджерВидовДоступа.ВидыВходящихДокументов, "ВидОбъекта");
	Результат.Вставить(МенеджерВидовДоступа.ВидыИсходящихДокументов, "ВидОбъекта");
	Результат.Вставить(МенеджерВидовДоступа.ВидыМероприятий, "ВидОбъекта");
	Результат.Вставить(МенеджерВидовДоступа.ВопросыДеятельности, "ВопросДеятельности");
	Результат.Вставить(МенеджерВидовДоступа.ГрифыДоступа, "ГрифДоступа");
	Результат.Вставить(МенеджерВидовДоступа.Организации, "Организация");
	Результат.Вставить(МенеджерВидовДоступа.Подразделения, "Подразделение");
	// ТСК Талько Э.Г.; 09.06.2018; Управление доступом {
	Результат.Вставить(МенеджерВидовДоступа.ра_Организации, "ра_Организация");
	// ТСК Талько Э.Г.; 09.06.2018; Управление доступом }
	
	Результат.Вставить(МенеджерВидовДоступа.ГруппыДоступаКонтрагентов, "Контрагенты.ГруппаДоступа");
	Результат.Вставить(МенеджерВидовДоступа.ГруппыДоступаФизическихЛиц, "ФизическиеЛица.ГруппаДоступа");
	
	Возврат Результат;
	
КонецФункции

// Возвращает массив неиспользуемых реквизитов дескриптора.
// 
// Параметры:
//  ИдентификаторОбъектаМетаданных - Ссылка на ИОМ - значение одноименного реквизита дескриптора.
// 
// Возвращаемое значение:
//  Массив - список имен неиспользуемых реквизитов.
// 
Функция НеиспользуемыеРеквизитыДескриптора(ИдентификаторОбъектаМетаданных) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Результат = Новый Массив;
	
	ОтключенныеРазрезы = ДокументооборотПраваДоступаПовтИсп.ОтключенныеРазрезыДоступа(Ложь);
	ИменаРеквизитов = ДокументооборотПраваДоступаПовтИсп.ИменаРеквизитовДескрпитораПоРазрезамДоступа();
	
	Для Каждого Разрез Из ОтключенныеРазрезы Цикл
		
		ИмяРеквизитаДескриптора = ИменаРеквизитов.Получить(Разрез);
		
		Если ИмяРеквизитаДескриптора = "ВидОбъекта" Тогда
			
			// Проверяется, что виды отключены именно для текущего объекта.
			
			ИдентификаторыОбъектовСОтключеннымВидом = Новый Массив;
			
			Если Разрез = ПланыВидовХарактеристик.ВидыДоступа.ВидыВнутреннихДокументов Тогда
				
				ИдентификаторыОбъектовСОтключеннымВидом.Добавить(
					ОбщегоНазначения.ИдентификаторОбъектаМетаданных(
						Метаданные.Справочники.ВнутренниеДокументы));
				ИдентификаторыОбъектовСОтключеннымВидом.Добавить(
					ОбщегоНазначения.ИдентификаторОбъектаМетаданных(
						Метаданные.Справочники.ВидыВнутреннихДокументов));
				ИдентификаторыОбъектовСОтключеннымВидом.Добавить(
					ОбщегоНазначения.ИдентификаторОбъектаМетаданных(
						Метаданные.Справочники.ШаблоныВнутреннихДокументов));
					
			ИначеЕсли Разрез = ПланыВидовХарактеристик.ВидыДоступа.ВидыВходящихДокументов Тогда
				
				ИдентификаторыОбъектовСОтключеннымВидом.Добавить(
					ОбщегоНазначения.ИдентификаторОбъектаМетаданных(
						Метаданные.Справочники.ВходящиеДокументы));
				ИдентификаторыОбъектовСОтключеннымВидом.Добавить(
					ОбщегоНазначения.ИдентификаторОбъектаМетаданных(
						Метаданные.Справочники.ВидыВходящихДокументов));
				ИдентификаторыОбъектовСОтключеннымВидом.Добавить(
					ОбщегоНазначения.ИдентификаторОбъектаМетаданных(
						Метаданные.Справочники.ШаблоныВходящихДокументов));
					
			ИначеЕсли Разрез = ПланыВидовХарактеристик.ВидыДоступа.ВидыИсходящихДокументов Тогда
				
				ИдентификаторыОбъектовСОтключеннымВидом.Добавить(
					ОбщегоНазначения.ИдентификаторОбъектаМетаданных(
						Метаданные.Справочники.ИсходящиеДокументы));
				ИдентификаторыОбъектовСОтключеннымВидом.Добавить(
					ОбщегоНазначения.ИдентификаторОбъектаМетаданных(
						Метаданные.Справочники.ВидыИсходящихДокументов));
				ИдентификаторыОбъектовСОтключеннымВидом.Добавить(
					ОбщегоНазначения.ИдентификаторОбъектаМетаданных(
						Метаданные.Справочники.ШаблоныИсходящихДокументов));
					
			ИначеЕсли Разрез = ПланыВидовХарактеристик.ВидыДоступа.ВидыМероприятий Тогда
				
				ИдентификаторыОбъектовСОтключеннымВидом.Добавить(
					ОбщегоНазначения.ИдентификаторОбъектаМетаданных(
						Метаданные.Справочники.Мероприятия));
				ИдентификаторыОбъектовСОтключеннымВидом.Добавить(
					ОбщегоНазначения.ИдентификаторОбъектаМетаданных(
						Метаданные.Справочники.ВидыМероприятий));
					
			КонецЕсли;
			
			Если ИдентификаторыОбъектовСОтключеннымВидом.Найти(ИдентификаторОбъектаМетаданных) = Неопределено Тогда
				Продолжить;
			КонецЕсли;
			
		КонецЕсли;
		
		// Если получен реквизит ТЧ, добавляется только имя ТЧ.
		НайденнаяПозиция = СтрНайти(ИмяРеквизитаДескриптора, ".");
		Если НайденнаяПозиция > 0 Тогда
			ИмяРеквизитаДескриптора = Лев(ИмяРеквизитаДескриптора, НайденнаяПозиция - 1);
		КонецЕсли;
		
		Результат.Добавить(ИмяРеквизитаДескриптора);
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

// Возвращает таблицу объектов, которые могут иметь отдельные разрешения доступа.
// 
// Возвращаемое значение: ТаблицаЗначений
//   * ОбъектМетаданных - СправочникСсылка.ИдентификаторыОбъектовМетаданных - идентификатор объекта.
//   * ВидыДоступа - СписокЗначений - виды доступа, которые влияют на объект.
//   * НеЯвляетсяСамостоятельнымПредметомДоступа - Булево - признак того, что 
//     для объекта нет смысла использовать исключения из общих разрешений доступа.
//
Функция ПредметыДоступаСРазрезами() Экспорт
	
	ТаблицаСвойств = Новый ТаблицаЗначений;
	ТаблицаСвойств.Колонки.Добавить("ОбъектМетаданных", Новый ОписаниеТипов("СправочникСсылка.ИдентификаторыОбъектовМетаданных"));
	ТаблицаСвойств.Колонки.Добавить("ВидыДоступа", Новый ОписаниеТипов("СписокЗначений"));
	ТаблицаСвойств.Колонки.Добавить("НеЯвляетсяСамостоятельнымПредметомДоступа", Новый ОписаниеТипов("Булево"));
	
	// ТСК Талько Э.Г.; 09.06.2018; Управление доступом {
	// Документы качества
	Для каждого ДокументКачества Из ра_ОбщегоНазначенияКлиентСервер.ДокументыКачества() Цикл
		Стр = ТаблицаСвойств.Добавить();
		Стр.ОбъектМетаданных = ОбщегоНазначения.ИдентификаторОбъектаМетаданных(Метаданные.Документы[ДокументКачества]);
		Стр.НеЯвляетсяСамостоятельнымПредметомДоступа = Истина;
		Стр.ВидыДоступа.Добавить(ПланыВидовХарактеристик.ВидыДоступа.ра_Организации);
		Стр.ВидыДоступа.Добавить(ПланыВидовХарактеристик.ВидыДоступа.Подразделения);
	КонецЦикла;
	// ТСК Талько Э.Г.; 09.06.2018; Управление доступом }
	
	// Внутренние документы
	Стр = ТаблицаСвойств.Добавить();
	Стр.ОбъектМетаданных = ОбщегоНазначения.ИдентификаторОбъектаМетаданных(
		Метаданные.Справочники.ВнутренниеДокументы);
	Стр.ВидыДоступа.Добавить(ПланыВидовХарактеристик.ВидыДоступа.ВидыВнутреннихДокументов);
	Стр.ВидыДоступа.Добавить(ПланыВидовХарактеристик.ВидыДоступа.ВопросыДеятельности);
	Стр.ВидыДоступа.Добавить(ПланыВидовХарактеристик.ВидыДоступа.ГрифыДоступа);
	Стр.ВидыДоступа.Добавить(ПланыВидовХарактеристик.ВидыДоступа.ГруппыДоступаКонтрагентов);
	Стр.ВидыДоступа.Добавить(ПланыВидовХарактеристик.ВидыДоступа.Организации);
	Стр.ВидыДоступа.Добавить(ПланыВидовХарактеристик.ВидыДоступа.Подразделения);
	// ТСК Талько Э.Г.; 09.06.2018; Управление доступом {
	Стр.ВидыДоступа.Добавить(ПланыВидовХарактеристик.ВидыДоступа.ра_Организации);
	// ТСК Талько Э.Г.; 09.06.2018; Управление доступом }
	
	// Входящие документы
	Стр = ТаблицаСвойств.Добавить();
	Стр.ОбъектМетаданных = ОбщегоНазначения.ИдентификаторОбъектаМетаданных(
		Метаданные.Справочники.ВходящиеДокументы);
	Стр.ВидыДоступа.Добавить(ПланыВидовХарактеристик.ВидыДоступа.ВидыВходящихДокументов);
	Стр.ВидыДоступа.Добавить(ПланыВидовХарактеристик.ВидыДоступа.ВопросыДеятельности);
	Стр.ВидыДоступа.Добавить(ПланыВидовХарактеристик.ВидыДоступа.ГрифыДоступа);
	Стр.ВидыДоступа.Добавить(ПланыВидовХарактеристик.ВидыДоступа.ГруппыДоступаКонтрагентов);
	Стр.ВидыДоступа.Добавить(ПланыВидовХарактеристик.ВидыДоступа.Организации);
	Стр.ВидыДоступа.Добавить(ПланыВидовХарактеристик.ВидыДоступа.Подразделения);
	
	// Исходящие документы
	Стр = ТаблицаСвойств.Добавить();
	Стр.ОбъектМетаданных = ОбщегоНазначения.ИдентификаторОбъектаМетаданных(
		Метаданные.Справочники.ИсходящиеДокументы);
	Стр.ВидыДоступа.Добавить(ПланыВидовХарактеристик.ВидыДоступа.ВидыИсходящихДокументов);
	Стр.ВидыДоступа.Добавить(ПланыВидовХарактеристик.ВидыДоступа.ВопросыДеятельности);
	Стр.ВидыДоступа.Добавить(ПланыВидовХарактеристик.ВидыДоступа.ГрифыДоступа);
	Стр.ВидыДоступа.Добавить(ПланыВидовХарактеристик.ВидыДоступа.ГруппыДоступаКонтрагентов);
	Стр.ВидыДоступа.Добавить(ПланыВидовХарактеристик.ВидыДоступа.Организации);
	Стр.ВидыДоступа.Добавить(ПланыВидовХарактеристик.ВидыДоступа.Подразделения);
	
	// Контрагенты
	Стр = ТаблицаСвойств.Добавить();
	Стр.ОбъектМетаданных = ОбщегоНазначения.ИдентификаторОбъектаМетаданных(
		Метаданные.Справочники.Контрагенты);
	Стр.ВидыДоступа.Добавить(ПланыВидовХарактеристик.ВидыДоступа.ГруппыДоступаКонтрагентов);
	Стр.НеЯвляетсяСамостоятельнымПредметомДоступа = Истина;
	
	// Мероприятия
	Стр = ТаблицаСвойств.Добавить();
	Стр.ОбъектМетаданных = ОбщегоНазначения.ИдентификаторОбъектаМетаданных(
		Метаданные.Справочники.Мероприятия);
	Стр.ВидыДоступа.Добавить(ПланыВидовХарактеристик.ВидыДоступа.ВидыМероприятий);
	Стр.ВидыДоступа.Добавить(ПланыВидовХарактеристик.ВидыДоступа.ГрифыДоступа);
	Стр.ВидыДоступа.Добавить(ПланыВидовХарактеристик.ВидыДоступа.Организации);
	Стр.ВидыДоступа.Добавить(ПланыВидовХарактеристик.ВидыДоступа.Подразделения);
	
	// Проекты
	Стр = ТаблицаСвойств.Добавить();
	Стр.ОбъектМетаданных = ОбщегоНазначения.ИдентификаторОбъектаМетаданных(
		Метаданные.Справочники.Проекты);
	Стр.ВидыДоступа.Добавить(ПланыВидовХарактеристик.ВидыДоступа.ГрифыДоступа);
	Стр.ВидыДоступа.Добавить(ПланыВидовХарактеристик.ВидыДоступа.Организации);
	Стр.ВидыДоступа.Добавить(ПланыВидовХарактеристик.ВидыДоступа.Подразделения);
	
	// Физические лица
	Стр = ТаблицаСвойств.Добавить();
	Стр.ОбъектМетаданных = ОбщегоНазначения.ИдентификаторОбъектаМетаданных(
		Метаданные.Справочники.ФизическиеЛица);
	Стр.ВидыДоступа.Добавить(ПланыВидовХарактеристик.ВидыДоступа.ГруппыДоступаФизическихЛиц);
	Стр.НеЯвляетсяСамостоятельнымПредметомДоступа = Истина;
	
	// Шаблоны внутренних документов
	Стр = ТаблицаСвойств.Добавить();
	Стр.ОбъектМетаданных = ОбщегоНазначения.ИдентификаторОбъектаМетаданных(
		Метаданные.Справочники.ШаблоныВнутреннихДокументов);
	Стр.ВидыДоступа.Добавить(ПланыВидовХарактеристик.ВидыДоступа.ВидыВнутреннихДокументов);
	Стр.ВидыДоступа.Добавить(ПланыВидовХарактеристик.ВидыДоступа.ВопросыДеятельности);
	Стр.ВидыДоступа.Добавить(ПланыВидовХарактеристик.ВидыДоступа.ГрифыДоступа);
	Стр.ВидыДоступа.Добавить(ПланыВидовХарактеристик.ВидыДоступа.ГруппыДоступаКонтрагентов);
	Стр.ВидыДоступа.Добавить(ПланыВидовХарактеристик.ВидыДоступа.Организации);
	Стр.ВидыДоступа.Добавить(ПланыВидовХарактеристик.ВидыДоступа.Подразделения);
	Стр.НеЯвляетсяСамостоятельнымПредметомДоступа = Истина;
	
	// Шаблоны входящих документов
	Стр = ТаблицаСвойств.Добавить();
	Стр.ОбъектМетаданных = ОбщегоНазначения.ИдентификаторОбъектаМетаданных(
		Метаданные.Справочники.ШаблоныВходящихДокументов);
	Стр.ВидыДоступа.Добавить(ПланыВидовХарактеристик.ВидыДоступа.ВидыВходящихДокументов);
	Стр.ВидыДоступа.Добавить(ПланыВидовХарактеристик.ВидыДоступа.ВопросыДеятельности);
	Стр.ВидыДоступа.Добавить(ПланыВидовХарактеристик.ВидыДоступа.ГрифыДоступа);
	Стр.ВидыДоступа.Добавить(ПланыВидовХарактеристик.ВидыДоступа.ГруппыДоступаКонтрагентов);
	Стр.ВидыДоступа.Добавить(ПланыВидовХарактеристик.ВидыДоступа.Организации);
	Стр.ВидыДоступа.Добавить(ПланыВидовХарактеристик.ВидыДоступа.Подразделения);
	Стр.НеЯвляетсяСамостоятельнымПредметомДоступа = Истина;
	
	// Шаблоны исходящих документов
	Стр = ТаблицаСвойств.Добавить();
	Стр.ОбъектМетаданных = ОбщегоНазначения.ИдентификаторОбъектаМетаданных(
		Метаданные.Справочники.ШаблоныИсходящихДокументов);
	Стр.ВидыДоступа.Добавить(ПланыВидовХарактеристик.ВидыДоступа.ВидыИсходящихДокументов);
	Стр.ВидыДоступа.Добавить(ПланыВидовХарактеристик.ВидыДоступа.ВопросыДеятельности);
	Стр.ВидыДоступа.Добавить(ПланыВидовХарактеристик.ВидыДоступа.ГрифыДоступа);
	Стр.ВидыДоступа.Добавить(ПланыВидовХарактеристик.ВидыДоступа.ГруппыДоступаКонтрагентов);
	Стр.ВидыДоступа.Добавить(ПланыВидовХарактеристик.ВидыДоступа.Организации);
	Стр.ВидыДоступа.Добавить(ПланыВидовХарактеристик.ВидыДоступа.Подразделения);
	Стр.НеЯвляетсяСамостоятельнымПредметомДоступа = Истина;
	
	Возврат ТаблицаСвойств;
	
КонецФункции

// Возвращает идентификатор объекта,
// по которому нужно искать исключительные разрешения при расчете прав переданного объекта.
// 
Функция ИдентификаторДляПроверкиПолитикДоступа(ИдОбъекта, МетаданныеОбъекта) Экспорт
	
	Результат = ИдОбъекта;
	
	// Исключения
	Если МетаданныеОбъекта = Метаданные.Справочники.ШаблоныВнутреннихДокументов Тогда
		Результат = ОбщегоНазначения.ИдентификаторОбъектаМетаданных(Метаданные.Справочники.ВнутренниеДокументы);
		
	ИначеЕсли МетаданныеОбъекта = Метаданные.Справочники.ШаблоныВходящихДокументов Тогда
		Результат = ОбщегоНазначения.ИдентификаторОбъектаМетаданных(Метаданные.Справочники.ВходящиеДокументы);
		
	ИначеЕсли МетаданныеОбъекта = Метаданные.Справочники.ШаблоныИсходящихДокументов Тогда
		Результат = ОбщегоНазначения.ИдентификаторОбъектаМетаданных(Метаданные.Справочники.ИсходящиеДокументы);
		
	// ТСК Талько Э.Г.; 09.06.2018; Управление доступом {
	ИначеЕсли ра_ОбщегоНазначенияКлиентСервер.ЭтоДокументКачества(МетаданныеОбъекта) Тогда
		Результат = ОбщегоНазначения.ИдентификаторОбъектаМетаданных(Метаданные.Справочники.ВнутренниеДокументы);
	
	// ТСК Талько Э.Г.; 09.06.2018; Управление доступом }
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Возвращает массив неиспользуемых разрезов доступа.
//
// Параметры:
//  ОтключенныеПоФункциональнымОпциям - Булево - включать разрезы, отключенные по функциональным опциям.
//  ОтключенныеВручную - Булево - включать разрезы, отключенные вручную.
//
// Возвращаемое значение:
//  Массив - массив отключенных разрезов доступа.
//
Функция ОтключенныеРазрезыДоступа(ОтключенныеПоФункциональнымОпциям, ОтключенныеВручную) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Результат = Новый Массив;
	МенеджерРазрезов = ПланыВидовХарактеристик.ВидыДоступа;
	СписокРазрезов = Константы.ИспользованиеРазрезовДоступа.Получить().Получить();
	
	Если СписокРазрезов = Неопределено Тогда
		МенеджерЗначения = Константы.ИспользованиеРазрезовДоступа.СоздатьМенеджерЗначения();
		МенеджерЗначения.НачальноеЗаполнение();
		СписокРазрезов = Константы.ИспользованиеРазрезовДоступа.Получить().Получить();
	КонецЕсли;
	
	Для Каждого Разрез Из СписокРазрезов Цикл
		
		Если ОтключенныеВручную Тогда
			Если Не Разрез.Пометка Тогда
				Результат.Добавить(Разрез.Значение);
				Продолжить;
			КонецЕсли;
		КонецЕсли;
		
		Если ОтключенныеПоФункциональнымОпциям Тогда
			
			Если Разрез.Значение = МенеджерРазрезов.ВидыВнутреннихДокументов
					И Не ПолучитьФункциональнуюОпцию("ИспользоватьВидыВнутреннихДокументов")
				Или Разрез.Значение = МенеджерРазрезов.ВидыВходящихДокументов
					И Не ПолучитьФункциональнуюОпцию("ИспользоватьВидыВходящихДокументов")
				Или Разрез.Значение = МенеджерРазрезов.ВидыИсходящихДокументов
					И Не ПолучитьФункциональнуюОпцию("ИспользоватьВидыИсходящихДокументов")
				Или Разрез.Значение = МенеджерРазрезов.ВопросыДеятельности
					И Не ПолучитьФункциональнуюОпцию("ИспользоватьВопросыДеятельности")
				Или Разрез.Значение = МенеджерРазрезов.ГрифыДоступа
					И Не ПолучитьФункциональнуюОпцию("ИспользоватьГрифыДоступа")
				Или Разрез.Значение = МенеджерРазрезов.Организации
					И Не ПолучитьФункциональнуюОпцию("ИспользоватьУчетПоОрганизациям")
				Или Разрез.Значение = МенеджерРазрезов.ВидыМероприятий
					И Не ПолучитьФункциональнуюОпцию("ИспользоватьУправлениеМероприятиями") Тогда
				
				Результат.Добавить(Разрез.Значение);
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции


