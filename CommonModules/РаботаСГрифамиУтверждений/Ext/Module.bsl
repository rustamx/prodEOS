// Возвращает массив утверждающих ответственных лиц по указанному документу
//
// Параметры:
//  ГрифыУтверждения - табличная часть ГрифыУтверждения справочник ВнутренниеДокументы
//
Функция АвторыУтвержденийДокумента(ГрифыУтверждения) Экспорт
	
	Результат = Новый Массив;
	
	Если ТипЗнч(ГрифыУтверждения) = Тип("РезультатЗапроса") Тогда
		ГрифыУтверждения = ГрифыУтверждения.Выгрузить();
	КонецЕсли;
	
	Для Каждого ГрифУтверждения Из ГрифыУтверждения Цикл
		Если ТипЗнч(ГрифУтверждения.АвторУтверждения) = Тип("СправочникСсылка.Пользователи")
			И ЗначениеЗаполнено(ГрифУтверждения.АвторУтверждения) Тогда
				Результат.Добавить(ГрифУтверждения.АвторУтверждения);
		КонецЕсли;
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

// Выполняет обработку грифов утверждений при начале нового процесса утверждения
//
// Параметры:
//  БизнесПроцесс - Ссылка на бизнес-процесс
//
Процедура ОбработатьГрифыПриНачалеПроцесса(БизнесПроцесс) Экспорт

	Если Не БизнесПроцесс.ВидПроцесса = Перечисления.ВидыБизнесПроцессаУтверждение.Утверждение Тогда
		Возврат;
	КонецЕсли;
	
	ТипыДокументов = Новый Массив;
	ТипыДокументов.Добавить(Тип("СправочникСсылка.ВнутренниеДокументы"));
	
	Предметы = МультипредметностьКлиентСервер.ПолучитьМассивПредметовОбъекта(
		БизнесПроцесс, ТипыДокументов, Истина);

	Для Каждого Документ Из Предметы Цикл
		ДобавитьГрифыУтвержденийПоБизнесПроцессу(Документ, БизнесПроцесс);
		
	КонецЦикла;

КонецПроцедуры

// Выполняет обработку грифов утверждений при выполнении задачи утверждения процесса утверждение
//
// Параметры:
//  БизнесПроцесс - Ссылка на бизнес-процесс
//  Документ - Ссылка на внутренний документ
//  Задача - Задача процесса
//
Процедура ОбработатьГрифыПриВыполненииЗадачи(БизнесПроцесс, Документ, Задача, РезультатУтверждения) Экспорт

	Если Не БизнесПроцесс.ВидПроцесса = Перечисления.ВидыБизнесПроцессаУтверждение.Утверждение Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ДелопроизводствоКлиентСервер.ЭтоВнутреннийДокумент(Документ) Тогда
		Возврат;
	КонецЕсли;
	
	ЗаблокироватьДанныеДляРедактирования(Документ);
	ДокументОбъект = Документ.ПолучитьОбъект();
	РаботаСРабочимиГруппами.ОтключитьПерезаписьРабочейГруппыПредметаПроцесса(ДокументОбъект);
	
	АвторГрифаНайден = Ложь;
	Для Каждого ГрифУтверждения Из ДокументОбъект.ГрифыУтверждения Цикл
		Если ГрифУтверждения.АвторУтверждения = Задача.Исполнитель Или 
			ГрифУтверждения.АвторУтверждения = Задача.РольИсполнителя Тогда
			АвторГрифаНайден = Истина;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Если Не АвторГрифаНайден Тогда
		ГрифУтверждения = ДокументОбъект.ГрифыУтверждения.Добавить();
	КонецЕсли;
	
	ГрифУтверждения.АвторУтверждения = Задача.Исполнитель;
	ГрифУтверждения.ДатаУтверждения = ТекущаяДатаСеанса();
	ГрифУтверждения.Результат = РезультатУтверждения;
	ГрифУтверждения.Комментарий = Задача.РезультатВыполнения;
	
	ДокументОбъект.Записать();
	
КонецПроцедуры

// Добавляет новые грифов утверждений по данным процесса
//
// Параметры:
//  Документ - Ссылка на внутренний документ
//  БизнесПроцесс - Ссылка на бизнес-процесс
//
Процедура ДобавитьГрифыУтвержденийПоБизнесПроцессу(Документ, БизнесПроцесс) Экспорт
	
	Если Не ЗначениеЗаполнено(Документ) 
		Или Не ДелопроизводствоКлиентСервер.ЭтоВнутреннийДокумент(Документ) Тогда
			Возврат;
	КонецЕсли;
	
	ГрифУтверждения = ПолучитьГрифУтвержденияДокументаПоИсполнителю(Документ, БизнесПроцесс.Исполнитель);
	
	ДокументОбъект = Документ.ПолучитьОбъект();
	
	Если ГрифУтверждения = Неопределено Тогда 
		
		НовыйГриф = ДокументОбъект.ГрифыУтверждения.Добавить();
		НовыйГриф.АвторУтверждения = БизнесПроцесс.Исполнитель;
		НовыйГриф.УстановилГриф = БизнесПроцесс.Автор;
		НовыйГриф.ДатаУстановкиГрифа = ТекущаяДатаСеанса();
		
	Иначе
		НайденнаяСтрока = ДокументОбъект.ГрифыУтверждения.Найти(БизнесПроцесс.Исполнитель, "АвторУтверждения");
		НайденнаяСтрока.УстановилГриф = БизнесПроцесс.Автор;
		НайденнаяСтрока.ДатаУстановкиГрифа = ТекущаяДатаСеанса();
		
	КонецЕсли;
	
	ДокументОбъект.Записать();
	
КонецПроцедуры

// Получает грифов утверждения документа по исполнителю
//
// Параметры:
//  Документ - Ссылка на внутренний документ
//  Исполнитель - Ссылка на Пользователи
//
Функция ПолучитьГрифУтвержденияДокументаПоИсполнителю(Документ, Исполнитель) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ГрифыУтверждения.Ссылка
	|ИЗ
	|	Справочник.ВнутренниеДокументы.ГрифыУтверждения КАК ГрифыУтверждения
	|ГДЕ
	|	ГрифыУтверждения.Ссылка = &Документ
	|	И ГрифыУтверждения.АвторУтверждения = &Исполнитель";
	
	Запрос.УстановитьПараметр("Документ", Документ);
	Запрос.УстановитьПараметр("Исполнитель", Исполнитель);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Не Выборка.Следующий() ИЛИ Выборка.Ссылка.Пустая() Тогда 
		Возврат Неопределено;
	КонецЕсли;
	
	Возврат Выборка.Ссылка;
	
КонецФункции

