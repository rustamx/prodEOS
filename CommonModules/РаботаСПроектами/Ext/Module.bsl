
// Заполняет коды СДР всех задач проекта 
Процедура ЗаполнитьКодыСДРПроектныхЗадач(Проект) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	// Коды СДР обычных задач
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПроектныеЗадачи.Ссылка,
	|	ПроектныеЗадачи.НомерЗадачиВУровне,
	|	ПроектныеЗадачи.ПометкаУдаления,
	|	ПроектныеЗадачи.КодСДР
	|ИЗ
	|	Справочник.ПроектныеЗадачи КАК ПроектныеЗадачи
	|ГДЕ
	|	ПроектныеЗадачи.Владелец = &Проект
	|	И ПроектныеЗадачи.Родитель = ЗНАЧЕНИЕ(Справочник.ПроектныеЗадачи.ПустаяСсылка)
	|
	|УПОРЯДОЧИТЬ ПО
	|	ПроектныеЗадачи.НомерЗадачиВУровне,
	|	ПроектныеЗадачи.Наименование";
	Запрос.УстановитьПараметр("Проект", Проект);
	
	Результат = Запрос.Выполнить();	
	Если Результат.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	Счетчик = 1;
	Выборка = Результат.Выбрать();
	Пока Выборка.Следующий() Цикл
		
		Если Выборка.ПометкаУдаления Тогда
			КодСДР = "";
			НомерЗадачиВУровне = 9999;
		Иначе
			КодСДР = Строка(Счетчик);
			НомерЗадачиВУровне = Счетчик;
			
			Счетчик = Счетчик + 1;
		КонецЕсли;
		
		Если (Выборка.НомерЗадачиВУровне <> НомерЗадачиВУровне) Или (Выборка.КодСДР <> КодСДР) Тогда 
			ЗаблокироватьДанныеДляРедактирования(Выборка.Ссылка);
			ЗадачаОбъект = Выборка.Ссылка.ПолучитьОбъект();
			
			ЗадачаОбъект.НомерЗадачиВУровне = НомерЗадачиВУровне;
			ЗадачаОбъект.КодСДР = КодСДР;
			
			ЗадачаОбъект.Записать();
		КонецЕсли; 
		
		ЗаполнитьКодыСДРПодчиненныхЗадач(Выборка.Ссылка);
	КонецЦикла;
	
	ЗаполнитьПорядокПроектныхЗадач(Проект);
	
КонецПроцедуры

// Заполняет коды СДР подчиненных задач 
Процедура ЗаполнитьКодыСДРПодчиненныхЗадач(Родитель, ИзмененныеЗадачи = Неопределено) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если ИзмененныеЗадачи = Неопределено Тогда 
		ИзмененныеЗадачи = Новый Массив;
	КонецЕсли;	
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПроектныеЗадачи.Ссылка,
	|	ПроектныеЗадачи.НомерЗадачиВУровне,
	|	ПроектныеЗадачи.ПометкаУдаления,
	|	ПроектныеЗадачи.КодСДР
	|ИЗ
	|	Справочник.ПроектныеЗадачи КАК ПроектныеЗадачи
	|ГДЕ
	|	ПроектныеЗадачи.Родитель = &Родитель
	|
	|УПОРЯДОЧИТЬ ПО
	|	ПроектныеЗадачи.НомерЗадачиВУровне,
	|	ПроектныеЗадачи.Наименование";
	
	Запрос.УстановитьПараметр("Родитель", Родитель);
	
	Результат = Запрос.Выполнить();	
	Если Результат.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	КодРодителя = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Родитель, "КодСДР");
	
	Счетчик = 1;
	Выборка = Результат.Выбрать();
	Пока Выборка.Следующий() Цикл
		
		Если Выборка.ПометкаУдаления Тогда
			КодСДР = "";
			НомерЗадачиВУровне = 9999;
		Иначе	
			Если Не ЗначениеЗаполнено(КодРодителя) Тогда
				КодСДР = Строка(Счетчик);
			Иначе
				КодСДР = КодРодителя + "." + Строка(Счетчик);
			КонецЕсли;
			НомерЗадачиВУровне = Счетчик;
			
			Счетчик = Счетчик + 1;
		КонецЕсли;
		
		Если (Выборка.НомерЗадачиВУровне <> НомерЗадачиВУровне) Или (Выборка.КодСДР <> КодСДР) Тогда 
		 	ЗаблокироватьДанныеДляРедактирования(Выборка.Ссылка);
			ЗадачаОбъект = Выборка.Ссылка.ПолучитьОбъект();
			
			ЗадачаОбъект.НомерЗадачиВУровне = НомерЗадачиВУровне;
			ЗадачаОбъект.КодСДР = КодСДР; 
		
			ЗадачаОбъект.Записать();
			ИзмененныеЗадачи.Добавить(ЗадачаОбъект.Ссылка);
		КонецЕсли;
		
		ЗаполнитьКодыСДРПодчиненныхЗадач(Выборка.Ссылка, ИзмененныеЗадачи);
	КонецЦикла;	
	
КонецПроцедуры

// Возвращает максимальный номер проектной задачи в пределах родителя 
Функция ПолучитьМаксимальныйНомерЗадачиУровня(Проект, Родитель) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ЕСТЬNULL(МАКСИМУМ(ПроектныеЗадачи.НомерЗадачиВУровне), 0) КАК НомерЗадачиВУровне
	|ИЗ
	|	Справочник.ПроектныеЗадачи КАК ПроектныеЗадачи
	|ГДЕ
	|	ПроектныеЗадачи.Владелец = &Проект
	|	И ПроектныеЗадачи.Родитель = &Родитель
	|	И НЕ ПроектныеЗадачи.ПометкаУдаления";
		
	Запрос.УстановитьПараметр("Родитель", Родитель);	
	Запрос.УстановитьПараметр("Проект", Проект);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка.НомерЗадачиВУровне;
	КонецЕсли;
	
	Возврат 0;
	
КонецФункции

// Возвращает массив проектных задач одного уровня с переданной 
Функция ПолучитьМассивЗадачОдногоУровняСУказанной(Проект, Задача) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПроектныеЗадачи.Ссылка
	|ИЗ
	|	Справочник.ПроектныеЗадачи КАК ПроектныеЗадачи
	|ГДЕ
	|	ПроектныеЗадачи.Владелец = &Проект
	|	И ПроектныеЗадачи.Родитель = &Родитель
	|	И НЕ ПроектныеЗадачи.ПометкаУдаления";
	
	Родитель = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Задача, "Родитель");
	Запрос.УстановитьПараметр("Родитель", Родитель);
	Запрос.УстановитьПараметр("Проект", Проект);	
	
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
	
КонецФункции

// Возвращает Код СДР и номер проектной задачи
Функция ПолучитьКодСДРИНомерЗадачиВУровне(Проект, Родитель) Экспорт 
	
	МаксимальныйНомер = РаботаСПроектами.ПолучитьМаксимальныйНомерЗадачиУровня(Проект, Родитель);
	
	КодСДРСтрока = "";
	Если ЗначениеЗаполнено(Родитель) Тогда
		КодСДРСтрока = Родитель.КодСДР;	
	КонецЕсли;
	
	НомерЗадачиВУровне = МаксимальныйНомер + 1;
	КодСДР = КодСДРСтрока
		+ ?(ЗначениеЗаполнено(КодСДРСтрока), ".", "")
		+ Строка(НомерЗадачиВУровне);
		
	ДанныеВозврата = Новый Структура;
	ДанныеВозврата.Вставить("КодСДР", КодСДР);
	ДанныеВозврата.Вставить("НомерЗадачиВУровне", НомерЗадачиВУровне);
	
	Возврат ДанныеВозврата;
	
КонецФункции

// Пересчитывает трудозатраты из одной единицы измерения в другую
Функция ПересчитатьТрудозатраты(Знач Трудозатраты, ИзЕдиницы, ВЕдиницу, ГрафикРаботы) Экспорт 
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьГрафикиРаботы") Тогда 
		КоличествоРабочихЧасовВДне = ГрафикРаботы.КоличествоРабочихЧасовВДне;
		КоличествоРабочихЧасовВНеделе = ГрафикРаботы.КоличествоРабочихЧасовВНеделе;
		КоличествоРабочихДнейВМесяце = ГрафикРаботы.КоличествоРабочихДнейВМесяце;
	Иначе
		КоличествоРабочихЧасовВДне = 24;
		КоличествоРабочихЧасовВНеделе = 168;
		КоличествоРабочихДнейВМесяце = 30;
	КонецЕсли;	
	
	ТрудозатратыСек = 0;
	
	Если ИзЕдиницы = Перечисления.ЕдиницыТрудозатрат.ЧеловекоМинута Тогда 
		ТрудозатратыСек = Трудозатраты * 60;
	ИначеЕсли ИзЕдиницы = Перечисления.ЕдиницыТрудозатрат.ЧеловекоЧас Тогда 
		ТрудозатратыСек = Трудозатраты * 3600;
	ИначеЕсли ИзЕдиницы = Перечисления.ЕдиницыТрудозатрат.ЧеловекоДень Тогда 
		ТрудозатратыСек = Трудозатраты * (3600 * КоличествоРабочихЧасовВДне);
	ИначеЕсли ИзЕдиницы = Перечисления.ЕдиницыТрудозатрат.ЧеловекоНеделя Тогда 	
		ТрудозатратыСек = Трудозатраты * (3600 * КоличествоРабочихЧасовВНеделе);
	ИначеЕсли ИзЕдиницы = Перечисления.ЕдиницыТрудозатрат.ЧеловекоМесяц Тогда 	
		ТрудозатратыСек = Трудозатраты * (3600 * КоличествоРабочихЧасовВДне * КоличествоРабочихДнейВМесяце);	
	КонецЕсли;
	
	Если ВЕдиницу = Перечисления.ЕдиницыТрудозатрат.ЧеловекоМинута Тогда 
		Трудозатраты = ТрудозатратыСек / 60;
	ИначеЕсли ВЕдиницу = Перечисления.ЕдиницыТрудозатрат.ЧеловекоЧас Тогда 
		Трудозатраты = ТрудозатратыСек / 3600;
	ИначеЕсли ВЕдиницу = Перечисления.ЕдиницыТрудозатрат.ЧеловекоДень Тогда 
		Трудозатраты = ТрудозатратыСек / (3600 * КоличествоРабочихЧасовВДне);
	ИначеЕсли ВЕдиницу = Перечисления.ЕдиницыТрудозатрат.ЧеловекоНеделя Тогда 	
		Трудозатраты = ТрудозатратыСек / (3600 * КоличествоРабочихЧасовВНеделе);
	ИначеЕсли ВЕдиницу = Перечисления.ЕдиницыТрудозатрат.ЧеловекоМесяц Тогда 	
		Трудозатраты = ТрудозатратыСек / (3600 * КоличествоРабочихЧасовВДне * КоличествоРабочихДнейВМесяце);	
	КонецЕсли;
		
	Возврат Трудозатраты;	
		
КонецФункции

// Пересчитывает длительность из одной единицы измерения в другую
Функция ПересчитатьДлительность(Знач Длительность, ИзЕдиницы, ВЕдиницу, ГрафикРаботы) Экспорт 
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьГрафикиРаботы") Тогда 
		КоличествоРабочихЧасовВДне = ГрафикРаботы.КоличествоРабочихЧасовВДне;
		КоличествоРабочихЧасовВНеделе = ГрафикРаботы.КоличествоРабочихЧасовВНеделе;
		КоличествоРабочихДнейВМесяце = ГрафикРаботы.КоличествоРабочихДнейВМесяце;
	Иначе
		КоличествоРабочихЧасовВДне = 24;
		КоличествоРабочихЧасовВНеделе = 168;
		КоличествоРабочихДнейВМесяце = 30;
	КонецЕсли;
	
	ДлительностьСек = 0;
	
	Если ИзЕдиницы = Перечисления.ЕдиницыДлительности.Минута Тогда 
		ДлительностьСек = Длительность * 60;
	ИначеЕсли ИзЕдиницы = Перечисления.ЕдиницыДлительности.Час Тогда 
		ДлительностьСек = Длительность * 3600;
	ИначеЕсли ИзЕдиницы = Перечисления.ЕдиницыДлительности.День Тогда 
		ДлительностьСек = Длительность * (3600 * КоличествоРабочихЧасовВДне);
	ИначеЕсли ИзЕдиницы = Перечисления.ЕдиницыДлительности.Неделя Тогда 	
		ДлительностьСек = Длительность * (3600 * КоличествоРабочихЧасовВНеделе);
	ИначеЕсли ИзЕдиницы = Перечисления.ЕдиницыДлительности.Месяц Тогда 	
		ДлительностьСек = Длительность * (3600 * КоличествоРабочихЧасовВДне * КоличествоРабочихДнейВМесяце);	
	КонецЕсли;
	
	Если ВЕдиницу = Перечисления.ЕдиницыДлительности.Минута Тогда 
		Длительность = ДлительностьСек / 60;
	ИначеЕсли ВЕдиницу = Перечисления.ЕдиницыДлительности.Час Тогда 
		Длительность = ДлительностьСек / 3600;
	ИначеЕсли ВЕдиницу = Перечисления.ЕдиницыДлительности.День Тогда 
		Длительность = ДлительностьСек / (3600 * КоличествоРабочихЧасовВДне);
	ИначеЕсли ВЕдиницу = Перечисления.ЕдиницыДлительности.Неделя Тогда 	
		Длительность = ДлительностьСек / (3600 * КоличествоРабочихЧасовВНеделе);
	ИначеЕсли ВЕдиницу = Перечисления.ЕдиницыДлительности.Месяц Тогда 	
		Длительность = ДлительностьСек / (3600 * КоличествоРабочихЧасовВДне * КоличествоРабочихДнейВМесяце);
	КонецЕсли;
		
	Возврат Длительность;
	
КонецФункции

// Возвращает единицу фактических трудозатрат для задачи бизнес-процесса
Функция ПолучитьЕдиницуФактическихТрудозатратЗадачиИсполнителя(Задача) Экспорт 
	
	Если ЗначениеЗаполнено(Задача.ПроектнаяЗадача) Тогда 
		Возврат Задача.ПроектнаяЗадача.ЕдиницаТрудозатратФакт;
	ИначеЕсли ЗначениеЗаполнено(Задача.Проект) Тогда 
		Возврат Задача.ПроектнаяЗадача.ЕдиницаТрудозатратЗадач;
	Иначе
		Возврат Константы.ОсновнаяЕдиницаТрудозатрат.Получить();
	КонецЕсли;	
	
КонецФункции	

// Возвращает график работы проектной задачи
Функция ПолучитьГрафикРаботыПроектнойЗадачи(ПроектнаяЗадача) Экспорт 
	
	ГрафикЗадачи = ПроектнаяЗадача.ГрафикРаботы;
	Если ЗначениеЗаполнено(ГрафикЗадачи) Тогда 
		Возврат ГрафикЗадачи;
	КонецЕсли;
		
	ГрафикПроекта = ПолучитьГрафикРаботыПроекта(ПроектнаяЗадача.Владелец);
	Если ЗначениеЗаполнено(ГрафикПроекта) Тогда 
		Возврат ГрафикПроекта;
	КонецЕсли;	
		
	Возврат Константы.ОсновнойГрафикРаботы.Получить();
	
КонецФункции

// Возвращает график работы проекта
Функция ПолучитьГрафикРаботыПроекта(Проект) Экспорт 
	
	ГрафикРаботы = Проект.ГрафикРаботы;
	Если ЗначениеЗаполнено(ГрафикРаботы) Тогда 
		Возврат ГрафикРаботы;
	КонецЕсли;
	
	Возврат Константы.ОсновнойГрафикРаботы.Получить();
	
КонецФункции	

// Рассчитывает окончание проектной задачи по дате начала и длительности
Функция РассчитатьОкончаниеПериода(Знач ПроектнаяЗадача, ДатаНачала, Длительность, ЕдиницаДлительности) Экспорт 
	
	Если Не ЗначениеЗаполнено(ЕдиницаДлительности) Тогда 
		ЕдиницаДлительности = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ПроектнаяЗадача.Владелец, "ЕдиницаДлительностиЗадач");
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Длительность) Тогда 
		Возврат ДатаНачала;
	КонецЕсли;	
	
	ГрафикРаботы = ПолучитьГрафикРаботыПроектнойЗадачи(ПроектнаяЗадача);
	ДлительностьЧас = ПересчитатьДлительность(Длительность, ЕдиницаДлительности, Перечисления.ЕдиницыДлительности.Час, ГрафикРаботы);
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьГрафикиРаботы") Тогда 
		ДатаОкончания = ГрафикиРаботы.ПолучитьДатуОкончанияСек(ГрафикРаботы, ДатаНачала, ДлительностьЧас * 3600); 
	Иначе
		ДатаОкончания = ДатаНачала + ДлительностьЧас * 3600;
	КонецЕсли;	
	
	Возврат ДатаОкончания;
	
КонецФункции

// Рассчитывает длительность проектной задачи по дате начала и дате окончания
Функция РассчитатьДлительностьПериода(Знач ПроектнаяЗадача, ДатаНачала, ДатаОкончания, ЕдиницаДлительности) Экспорт 

	Если Не ЗначениеЗаполнено(ЕдиницаДлительности) Тогда 
		ЕдиницаДлительности = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ПроектнаяЗадача.Владелец, "ЕдиницаДлительностиЗадач");
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ДатаНачала) Или Не ЗначениеЗаполнено(ДатаОкончания) Тогда 
		Возврат 0;
	КонецЕсли;
	
	Если ДатаНачала > ДатаОкончания Тогда 
		Возврат 0;
	КонецЕсли;	
	
	ГрафикРаботы = ПолучитьГрафикРаботыПроектнойЗадачи(ПроектнаяЗадача);
	Если ПолучитьФункциональнуюОпцию("ИспользоватьГрафикиРаботы") Тогда 
		ДлительностьСек = ГрафикиРаботы.ПолучитьДлительностьПериодаСек(ГрафикРаботы, ДатаНачала, ДатаОкончания); 
	Иначе
		ДлительностьСек = ДатаОкончания - ДатаНачала;
	КонецЕсли;
	
	ДлительностьЧас = ДлительностьСек / 3600;
	
	Длительность = ПересчитатьДлительность(ДлительностьЧас, Перечисления.ЕдиницыДлительности.Час, ЕдиницаДлительности, ГрафикРаботы);
	
	Возврат Длительность;
	
КонецФункции	

// Рассчитывает начало проектной задачи по дате окончания и длительности
Функция РассчитатьНачалоПериода(Знач ПроектнаяЗадача, ДатаОкончания, Длительность, ЕдиницаДлительности) Экспорт 
	
	Если Не ЗначениеЗаполнено(ЕдиницаДлительности) Тогда 
		ЕдиницаДлительности = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ПроектнаяЗадача, "ЕдиницаДлительностиЗадач");
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Длительность) Тогда 
		Возврат ДатаОкончания;
	КонецЕсли;	
	
	ГрафикРаботы = ПолучитьГрафикРаботыПроектнойЗадачи(ПроектнаяЗадача);
	ДлительностьЧас = ПересчитатьДлительность(Длительность, ЕдиницаДлительности, Перечисления.ЕдиницыДлительности.Час, ГрафикРаботы);
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьГрафикиРаботы") Тогда 
		ДатаНачала = ГрафикиРаботы.ПолучитьДатуНачалаПериодаСек(ГрафикРаботы, ДатаОкончания, ДлительностьЧас * 3600); 
	Иначе
		ДатаНачала = ДатаОкончания - ДлительностьЧас * 3600;
	КонецЕсли;	
	
	Возврат ДатаНачала;
	
КонецФункции	

// Возвращает текущий план трудозатрат, вычисленный из проектных задач
Функция ВычислитьПлановыеТрудозатратыПроекта(Проект) Экспорт 
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВЫБОР
		|		КОГДА ПроектныеЗадачиИсполнители.ТекущийПланТрудозатраты ЕСТЬ NULL 
		|			ТОГДА ПроектныеЗадачи.ТекущийПланТрудозатраты
		|		ИНАЧЕ ПроектныеЗадачиИсполнители.ТекущийПланТрудозатраты
		|	КОНЕЦ КАК ТекущийПланТрудозатраты,
		|	ПроектныеЗадачи.ТекущийПланЕдиницаТрудозатрат,
		|	ВЫБОР
		|		КОГДА ПроектныеЗадачи.ГрафикРаботы <> ЗНАЧЕНИЕ(Справочник.ГрафикиРаботы.ПустаяСсылка)
		|			ТОГДА ПроектныеЗадачи.ГрафикРаботы
		|		ИНАЧЕ ПроектныеЗадачи.Владелец.ГрафикРаботы
		|	КОНЕЦ КАК ГрафикРаботы
		|ИЗ
		|	Справочник.ПроектныеЗадачи КАК ПроектныеЗадачи
		|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
		|			СУММА(Исполнители.ТекущийПланТрудозатраты) КАК ТекущийПланТрудозатраты,
		|			Исполнители.Ссылка КАК Ссылка
		|		ИЗ
		|			Справочник.ПроектныеЗадачи.Исполнители КАК Исполнители
		|		
		|		СГРУППИРОВАТЬ ПО
		|			Исполнители.Ссылка) КАК ПроектныеЗадачиИсполнители
		|		ПО (ПроектныеЗадачиИсполнители.Ссылка = ПроектныеЗадачи.Ссылка)
		|ГДЕ
		|	ПроектныеЗадачи.Владелец = &Проект
		|	И (НЕ ПроектныеЗадачи.ПометкаУдаления)";
	Запрос.УстановитьПараметр("Проект", Проект); 
	
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		Возврат 0;
	КонецЕсли;	
		
	ТекущийПланТрудозатраты = 0;
	Выборка = Результат.Выбрать();
	Пока Выборка.Следующий() Цикл
		ТекущийПланТрудозатраты = ТекущийПланТрудозатраты +
		ПересчитатьДлительность(Выборка.ТекущийПланТрудозатраты, 
			Выборка.ТекущийПланЕдиницаТрудозатрат, 
			Проект.ЕдиницаТрудозатратЗадач, 
			Выборка.ГрафикРаботы);
	КонецЦикла;	
	
	Возврат ТекущийПланТрудозатраты;
	
КонецФункции	

// Возвращает текущий план окончания проекта, вычисленный из проектных задач
Функция ВычислитьТекущийПланОкончанияПроекта(Проект) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЕСТЬNULL(МАКСИМУМ(СрокиПроектныхЗадач.ТекущийПланОкончание), ДАТАВРЕМЯ(1, 1, 1)) КАК ТекущийПланОкончание
		|ИЗ
		|	РегистрСведений.СрокиПроектныхЗадач КАК СрокиПроектныхЗадач
		|ГДЕ
		|	СрокиПроектныхЗадач.ПроектнаяЗадача.Владелец = &Проект
		|	И НЕ СрокиПроектныхЗадач.ПроектнаяЗадача.ПометкаУдаления";
			
	Запрос.УстановитьПараметр("Проект", Проект); 
	
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		Возврат '00010101';
	КонецЕсли;	
		
	Выборка = Результат.Выбрать();
	Выборка.Следующий();
	Возврат Выборка.ТекущийПланОкончание;
	
КонецФункции

// Рассчитывает план всего проекта
Процедура РассчитатьПланВсегоПроекта(Проект) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если Проект.РедактируетсяВСтороннейСистеме Тогда
		Возврат;
	КонецЕсли;
	
	Если Проект.СпособПланирования = Перечисления.СпособыПланированияПроекта.ОтДатыОкончанияПроекта Тогда 
		
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	ПроектныеЗадачи.Ссылка
		|ИЗ
		|	Справочник.ПроектныеЗадачи КАК ПроектныеЗадачи
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ПроектныеЗадачи.Предшественники КАК ПроектныеЗадачиПредшественники
		|		ПО ПроектныеЗадачи.Ссылка = ПроектныеЗадачиПредшественники.Предшественник
		|ГДЕ
		|	ПроектныеЗадачи.Владелец = &Проект
		|	И НЕ ПроектныеЗадачи.ПометкаУдаления
		|	И НЕ ПроектныеЗадачи.СуммарнаяЗадача
		|	И ПроектныеЗадачиПредшественники.Ссылка ЕСТЬ NULL ";
		
	Иначе
		
		ТекстЗапроса = 
		"ВЫБРАТЬ
		|	ПроектныеЗадачи.Ссылка
		|ИЗ
		|	Справочник.ПроектныеЗадачи КАК ПроектныеЗадачи
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ПроектныеЗадачи.Предшественники КАК ПроектныеЗадачиПредшественники
		|		ПО ПроектныеЗадачи.Ссылка = ПроектныеЗадачиПредшественники.Ссылка
		|ГДЕ
		|	ПроектныеЗадачи.Владелец = &Проект
		|	И НЕ ПроектныеЗадачи.ПометкаУдаления
		|	И НЕ ПроектныеЗадачи.СуммарнаяЗадача
		|	И ПроектныеЗадачиПредшественники.Ссылка ЕСТЬ NULL ";
		
	КонецЕсли;
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("Проект", Проект);		
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		РассчитатьПланПроекта(Выборка.Ссылка);
	КонецЦикла;
	
КонецПроцедуры

// Рассчитывает план проекта, начиная с переданной задачи
Процедура РассчитатьПланПроекта(ПроектнаяЗадача, МассивИзмененныхЗадач = Неопределено) Экспорт 
	
	РеквизитыПроектнойЗадачи = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
		ПроектнаяЗадача, "Владелец, Владелец.РедактируетсяВСтороннейСистеме");
		
	Если РеквизитыПроектнойЗадачи.ВладелецРедактируетсяВСтороннейСистеме Тогда 
		Возврат;
	КонецЕсли;
	
	ДанныеПроекта = ПолучитьДанныеПроекта(РеквизитыПроектнойЗадачи.Владелец);
	
	Если МассивИзмененныхЗадач = Неопределено Тогда 
		МассивИзмененныхЗадач = Новый Массив;
	КонецЕсли;	
	
	ПересчитатьПланПоЗадаче = Истина;
	
	РассчитатьПланПроектаПоЗадаче(ПроектнаяЗадача, ПересчитатьПланПоЗадаче, МассивИзмененныхЗадач, ДанныеПроекта);	
	
	// запись измененных данных
	Для Каждого Строка Из ДанныеПроекта Цикл
		Если Строка.Значение.Модифицированность Тогда 
			СтруктураДанных = Новый Структура;
			СтруктураДанных.Вставить("ТекущийПланНачало", Строка.Значение.ТекущийПланНачало);
			СтруктураДанных.Вставить("ТекущийПланОкончание", Строка.Значение.ТекущийПланОкончание);
			СтруктураДанных.Вставить("ТекущийПланДлительность", Строка.Значение.ТекущийПланДлительность);
			СтруктураДанных.Вставить("НачалоФакт", Строка.Значение.НачалоФакт);
			СтруктураДанных.Вставить("ОкончаниеФакт", Строка.Значение.ОкончаниеФакт);
			СтруктураДанных.Вставить("ДлительностьФакт", Строка.Значение.ДлительностьФакт);
		
			РаботаСПроектами.ЗаписатьСрокиПроектнойЗадачи(Строка.Ключ, СтруктураДанных);
		КонецЕсли;
	КонецЦикла;	
	
КонецПроцедуры

// Рекурсивная процедура расчета плана проекта 
Процедура РассчитатьПланПроектаПоЗадаче(ПроектнаяЗадача, Знач ПересчитатьПланПоЗадаче = Ложь, МассивИзмененныхЗадач = Неопределено, ДанныеПроекта = Неопределено)
	
	Если ДанныеПроекта = Неопределено Тогда 
		
		РеквизитыПроектнойЗадачи = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
			ПроектнаяЗадача, "Владелец, Владелец.РедактируетсяВСтороннейСистеме");
			
		Если РеквизитыПроектнойЗадачи.ВладелецРедактируетсяВСтороннейСистеме Тогда 
			Возврат;
		КонецЕсли;
			
		ДанныеПроекта = ПолучитьДанныеПроекта(РеквизитыПроектнойЗадачи.Владелец);
		
		Если ТипЗнч(ПроектнаяЗадача) = Тип("СправочникСсылка.ПроектныеЗадачи") Тогда 
			ДанныеПроектнойЗадачи = ДанныеПроекта.Получить(ПроектнаяЗадача);
		Иначе
			ДанныеПроектнойЗадачи = ПроектнаяЗадача;
		КонецЕсли;
		
	Иначе	
		
		Если ТипЗнч(ПроектнаяЗадача) = Тип("СправочникСсылка.ПроектныеЗадачи") Тогда 
			ДанныеПроектнойЗадачи = ДанныеПроекта.Получить(ПроектнаяЗадача);
		Иначе	
			ДанныеПроектнойЗадачи = ПроектнаяЗадача;
		КонецЕсли;	
		
		Если ТипЗнч(ДанныеПроектнойЗадачи) = Тип("Структура")
			И ДанныеПроектнойЗадачи.РедактируетсяВСтороннейСистеме Тогда
			Возврат;
		КонецЕсли;
		
	КонецЕсли;	
	
	Если ПересчитатьПланПоЗадаче Тогда 
		Если МассивИзмененныхЗадач <> Неопределено Тогда 
			МассивИзмененныхЗадач.Добавить(ДанныеПроектнойЗадачи.Ссылка);
		КонецЕсли;
	КонецЕсли;	
	
    // установка даты начала
  	Если ДанныеПроектнойЗадачи.СуммарнаяЗадача Тогда 
		
		ПодчиненныеЗадачи = ДанныеПроектнойЗадачи.ПодчиненныеЗадачи;
		
		Если ПересчитатьПланПоЗадаче Тогда 
			Для Каждого ПодчиненнаяЗадача Из ПодчиненныеЗадачи Цикл
				РассчитатьПланПроектаПоЗадаче(ПодчиненнаяЗадача,, МассивИзмененныхЗадач, ДанныеПроекта);
			КонецЦикла;	
		КонецЕсли;	
		
		// фактические даты
		ДатаНачала = '99990101';
		ДатаОкончания = '00010101';
		
		Для Каждого ПодчиненнаяЗадача Из ПодчиненныеЗадачи Цикл
			ДанныеПодчиненнойЗадачи = ДанныеПроекта.Получить(ПодчиненнаяЗадача);
			Если ЗначениеЗаполнено(ДанныеПодчиненнойЗадачи.НачалоФакт) И ДанныеПодчиненнойЗадачи.НачалоФакт < ДатаНачала Тогда 
				ДатаНачала = ДанныеПодчиненнойЗадачи.НачалоФакт;
			КонецЕсли;	
		КонецЦикла;	
		Если ДатаНачала = '99990101' Тогда 
			ДатаНачала = '00010101'
		КонецЕсли;	
		
		Для Каждого ПодчиненнаяЗадача Из ПодчиненныеЗадачи Цикл	
			ДанныеПодчиненнойЗадачи = ДанныеПроекта.Получить(ПодчиненнаяЗадача);
			Если Не ЗначениеЗаполнено(ДанныеПодчиненнойЗадачи.ОкончаниеФакт) Тогда 
				ДатаОкончания = '00010101';
				Прервать;
			ИначеЕсли ДанныеПодчиненнойЗадачи.ОкончаниеФакт > ДатаОкончания Тогда 	
				ДатаОкончания = ДанныеПодчиненнойЗадачи.ОкончаниеФакт;
			КонецЕсли;	
		КонецЦикла;
		
		Если (ДатаНачала <> ДанныеПроектнойЗадачи.НачалоФакт Или ДатаОкончания <> ДанныеПроектнойЗадачи.ОкончаниеФакт) Тогда 
			
			ДанныеПроектнойЗадачи.НачалоФакт = ДатаНачала;
			ДанныеПроектнойЗадачи.ОкончаниеФакт = ДатаОкончания;
			ДанныеПроектнойЗадачи.ДлительностьФакт =  
				РаботаСПроектами.РассчитатьДлительностьПериода(ДанныеПроектнойЗадачи, 
				ДатаНачала, 
				ДатаОкончания, 
				ДанныеПроектнойЗадачи.ЕдиницаДлительностиФакт);
			ДанныеПроектнойЗадачи.Модифицированность = Истина;
			
			Если МассивИзмененныхЗадач <> Неопределено Тогда 
				МассивИзмененныхЗадач.Добавить(ДанныеПроектнойЗадачи.Ссылка);
			КонецЕсли;	
			
		КонецЕсли;	
			
		// плановые даты
		ДатаНачала = '99990101';
		ДатаОкончания = '00010101';
		
		Для Каждого ПодчиненнаяЗадача Из ПодчиненныеЗадачи Цикл
			ДанныеПодчиненнойЗадачи = ДанныеПроекта.Получить(ПодчиненнаяЗадача);
			Если ДанныеПодчиненнойЗадачи.ТекущийПланНачало < ДатаНачала Тогда 
				ДатаНачала = ДанныеПодчиненнойЗадачи.ТекущийПланНачало;
			КонецЕсли;	
			
			Если ДанныеПодчиненнойЗадачи.ТекущийПланОкончание > ДатаОкончания Тогда 
				ДатаОкончания = ДанныеПодчиненнойЗадачи.ТекущийПланОкончание;
			КонецЕсли;
		КонецЦикла;	
		
		Если ДатаНачала = '99990101' Тогда 
			ДатаНачала = '00010101'
		КонецЕсли;	

		Если (ДатаНачала <> ДанныеПроектнойЗадачи.ТекущийПланНачало Или ДатаОкончания <> ДанныеПроектнойЗадачи.ТекущийПланОкончание) Тогда 
			
			ДанныеПроектнойЗадачи.ТекущийПланНачало = ДатаНачала;
			ДанныеПроектнойЗадачи.ТекущийПланОкончание = ДатаОкончания;
			ДанныеПроектнойЗадачи.ТекущийПланДлительность =  
				РаботаСПроектами.РассчитатьДлительностьПериода(ДанныеПроектнойЗадачи, 
				ДатаНачала, 
				ДатаОкончания, 
				ДанныеПроектнойЗадачи.ТекущийПланЕдиницаДлительности);
			ДанныеПроектнойЗадачи.Модифицированность = Истина;
			
			Если МассивИзмененныхЗадач <> Неопределено Тогда 
				МассивИзмененныхЗадач.Добавить(ДанныеПроектнойЗадачи.Ссылка);
			КонецЕсли;
			
			ПересчитатьПланПоЗадаче = Истина;
			
		КонецЕсли;	
		
		Если ПересчитатьПланПоЗадаче Тогда
		
			Если ЗначениеЗаполнено(ДанныеПроектнойЗадачи.Родитель) Тогда 
				РассчитатьПланПроектаПоЗадаче(ДанныеПроектнойЗадачи.Родитель,, МассивИзмененныхЗадач, ДанныеПроекта);
			КонецЕсли;

		КонецЕсли;		
			
	Иначе
		
		Если ДанныеПроектнойЗадачи.ТипОграничения = Перечисления.ТипыОграниченийПроектныхЗадач.ФиксированноеНачало Тогда 
			
			ОбработатьЗадачуФиксированноеНачало(ДанныеПроектнойЗадачи, ПересчитатьПланПоЗадаче, МассивИзмененныхЗадач, ДанныеПроекта);
			
		ИначеЕсли ДанныеПроектнойЗадачи.ТипОграничения = Перечисления.ТипыОграниченийПроектныхЗадач.ФиксированноеОкончание Тогда 	
			
			ОбработатьЗадачуФиксированноеОкончание(ДанныеПроектнойЗадачи, ПересчитатьПланПоЗадаче, МассивИзмененныхЗадач, ДанныеПроекта);
			
		ИначеЕсли ДанныеПроектнойЗадачи.ТипОграничения = Перечисления.ТипыОграниченийПроектныхЗадач.КакМожноРаньше Тогда 
			
			ОбработатьЗадачуКакМожноРаньше(ДанныеПроектнойЗадачи, ПересчитатьПланПоЗадаче, МассивИзмененныхЗадач, ДанныеПроекта);
			
		ИначеЕсли ДанныеПроектнойЗадачи.ТипОграничения = Перечисления.ТипыОграниченийПроектныхЗадач.КакМожноПозже Тогда 
			
			ОбработатьЗадачуКакМожноПозже(ДанныеПроектнойЗадачи, ПересчитатьПланПоЗадаче, МассивИзмененныхЗадач, ДанныеПроекта);
			
		ИначеЕсли ДанныеПроектнойЗадачи.ТипОграничения = Перечисления.ТипыОграниченийПроектныхЗадач.НачалоНеРанее Тогда 
			
			ОбработатьЗадачуНачалоНеРанее(ДанныеПроектнойЗадачи, ПересчитатьПланПоЗадаче, МассивИзмененныхЗадач, ДанныеПроекта);
			
		ИначеЕсли ДанныеПроектнойЗадачи.ТипОграничения = Перечисления.ТипыОграниченийПроектныхЗадач.НачалоНеПозднее Тогда 
			
			ОбработатьЗадачуНачалоНеПозднее(ДанныеПроектнойЗадачи, ПересчитатьПланПоЗадаче, МассивИзмененныхЗадач, ДанныеПроекта);
			
		ИначеЕсли ДанныеПроектнойЗадачи.ТипОграничения = Перечисления.ТипыОграниченийПроектныхЗадач.ОкончаниеНеРанее Тогда 
			
			ОбработатьЗадачуОкончаниеНеРанее(ДанныеПроектнойЗадачи, ПересчитатьПланПоЗадаче, МассивИзмененныхЗадач, ДанныеПроекта);
			
		ИначеЕсли ДанныеПроектнойЗадачи.ТипОграничения = Перечисления.ТипыОграниченийПроектныхЗадач.ОкончаниеНеПозднее Тогда 	
			
			ОбработатьЗадачуОкончаниеНеПозднее(ДанныеПроектнойЗадачи, ПересчитатьПланПоЗадаче, МассивИзмененныхЗадач, ДанныеПроекта);
			
		КонецЕсли;	
			
	КонецЕсли;
	
КонецПроцедуры	

Процедура ОбработатьЗадачуКакМожноРаньше(ДанныеПроектнойЗадачи, ПересчитатьПланПоЗадаче, МассивИзмененныхЗадач, ДанныеПроекта, НачалоНеРанее = '00010101', НачалоНеПозднее = '99990101')
	
	ТекущийПланНачало = НачалоНеРанее;
	
	ВсеПредшественники = ДанныеПроектнойЗадачи.ВсеПредшественникиВключаяНачалоПроекта;
	Для Каждого Строка Из ВсеПредшественники Цикл
		
		Если Строка.Свойство("ТекущийПланОкончание") Тогда 
			ТекущийПланОкончаниеПредшественника = Строка.ТекущийПланОкончание;
		Иначе
			ТекущийПланОкончаниеПредшественника = ДанныеПроекта.Получить(Строка.Предшественник).ТекущийПланОкончание;
		КонецЕсли;
		
		Если Строка.Свойство("ТекущийПланНачало") Тогда 
			ТекущийПланНачалоПредшественника = Строка.ТекущийПланНачало;
		Иначе
			ТекущийПланНачалоПредшественника = ДанныеПроекта.Получить(Строка.Предшественник).ТекущийПланНачало;
		КонецЕсли;
		
		Если Строка.ТипЗависимости = Перечисления.ТипыЗависимостейПроектныхЗадач.ОкончаниеНачало Тогда 
			
			НовыйПланНачало = ТекущийПланОкончаниеПредшественника;
			Если Строка.Задержка <> 0 Тогда 
				НовыйПланНачало = РаботаСПроектами.РассчитатьОкончаниеПериода(ДанныеПроектнойЗадачи, 
					НовыйПланНачало, 
					Строка.Задержка, 
					Строка.ЕдиницаЗадержки);
			КонецЕсли;		
			
			Если ПолучитьФункциональнуюОпцию("ИспользоватьГрафикиРаботы") И ДанныеПроектнойЗадачи.ТекущийПланДлительность <> 0 Тогда 
				
				ГрафикРаботы = ПолучитьГрафикРаботыПроектнойЗадачи(ДанныеПроектнойЗадачи);
				врНовыйПланНачало = ГрафикиРаботы.ПолучитьДатуОкончанияСек(ГрафикРаботы, НовыйПланНачало, 1); 
				Если врНовыйПланНачало - 1 <> НовыйПланНачало Тогда 
					НовыйПланНачало = врНовыйПланНачало - 1;
				КонецЕсли;	
				
			КонецЕсли;
			
		ИначеЕсли Строка.ТипЗависимости = Перечисления.ТипыЗависимостейПроектныхЗадач.НачалоНачало Тогда 	
			
			НовыйПланНачало = ТекущийПланНачалоПредшественника;
			Если Строка.Задержка <> 0 Тогда 
				НовыйПланНачало = РаботаСПроектами.РассчитатьОкончаниеПериода(ДанныеПроектнойЗадачи, 
					НовыйПланНачало, 
					Строка.Задержка, 
					Строка.ЕдиницаЗадержки);
			КонецЕсли;
			
		ИначеЕсли Строка.ТипЗависимости = Перечисления.ТипыЗависимостейПроектныхЗадач.ОкончаниеОкончание Тогда 
			
			НовыйПланНачало = РаботаСПроектами.РассчитатьНачалоПериода(ДанныеПроектнойЗадачи, 
				ТекущийПланОкончаниеПредшественника, 
				ДанныеПроектнойЗадачи.ТекущийПланДлительность, 
				ДанныеПроектнойЗадачи.ТекущийПланЕдиницаДлительности);
			
			Если Строка.Задержка <> 0 Тогда 
				НовыйПланНачало = РаботаСПроектами.РассчитатьНачалоПериода(ДанныеПроектнойЗадачи, 
					НовыйПланНачало, 
					Строка.Задержка, 
					Строка.ЕдиницаЗадержки);
			КонецЕсли;	
			
		ИначеЕсли Строка.ТипЗависимости = Перечисления.ТипыЗависимостейПроектныхЗадач.НачалоОкончание Тогда 
			
			НовыйПланНачало = РаботаСПроектами.РассчитатьНачалоПериода(ДанныеПроектнойЗадачи, 
				ТекущийПланНачалоПредшественника, 
				ДанныеПроектнойЗадачи.ТекущийПланДлительность, 
				ДанныеПроектнойЗадачи.ТекущийПланЕдиницаДлительности);
			
			Если Строка.Задержка <> 0 Тогда 
				НовыйПланНачало = РаботаСПроектами.РассчитатьНачалоПериода(ДанныеПроектнойЗадачи, 
					НовыйПланНачало, 
					Строка.Задержка, 
					Строка.ЕдиницаЗадержки);
			КонецЕсли;
			
		КонецЕсли;	
		
		Если ЗначениеЗаполнено(НовыйПланНачало) Тогда 
			Если НовыйПланНачало > ТекущийПланНачало Тогда 
				ТекущийПланНачало = НовыйПланНачало;
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;
	
	Если ТекущийПланНачало > НачалоНеПозднее Тогда 
		ТекущийПланНачало = НачалоНеПозднее;
	КонецЕсли;	
	
	Если (ТекущийПланНачало <> ДанныеПроектнойЗадачи.ТекущийПланНачало) Тогда 
		
		Если ДанныеПроектнойЗадачи.АвтоматическиРассчитыватьПланПроекта Тогда 
			
			ДанныеПроектнойЗадачи.ТекущийПланНачало = ТекущийПланНачало;
			ДанныеПроектнойЗадачи.ТекущийПланОкончание =  
				РаботаСПроектами.РассчитатьОкончаниеПериода(ДанныеПроектнойЗадачи, 
				ТекущийПланНачало, 
				ДанныеПроектнойЗадачи.ТекущийПланДлительность, 
				ДанныеПроектнойЗадачи.ТекущийПланЕдиницаДлительности);
			ДанныеПроектнойЗадачи.Модифицированность = Истина;
			
			Если МассивИзмененныхЗадач <> Неопределено Тогда 
				МассивИзмененныхЗадач.Добавить(ДанныеПроектнойЗадачи.Ссылка);
			КонецЕсли;
		
		КонецЕсли;
		
		ПересчитатьПланПоЗадаче = Истина;
		
	КонецЕсли;	
	
	Если ПересчитатьПланПоЗадаче Тогда 
		
		Если ЗначениеЗаполнено(ДанныеПроектнойЗадачи.Родитель) Тогда 
			РассчитатьПланПроектаПоЗадаче(ДанныеПроектнойЗадачи.Родитель,, МассивИзмененныхЗадач, ДанныеПроекта);
		КонецЕсли;	
		
		Для Каждого Строка Из ДанныеПроектнойЗадачи.ВсеПоследователи Цикл
			РассчитатьПланПроектаПоЗадаче(Строка.Ссылка,, МассивИзмененныхЗадач, ДанныеПроекта);
		КонецЦикла;
		
	КонецЕсли;	
	
КонецПроцедуры	

Процедура ОбработатьЗадачуКакМожноПозже(ДанныеПроектнойЗадачи, ПересчитатьПланПоЗадаче, МассивИзмененныхЗадач, ДанныеПроекта, ОкончаниеНеРанее = '00010101', ОкончаниеНеПозднее = '99990101')
	
	ТекущийПланОкончание = ОкончаниеНеПозднее;
	
	ВсеПоследователи = ДанныеПроектнойЗадачи.ВсеПоследователиВключаяОкончаниеПроекта;
	Для Каждого Строка Из ВсеПоследователи Цикл
		
		Если Строка.Свойство("ТекущийПланОкончание") Тогда 
			ТекущийПланОкончаниеПоследователя = Строка.ТекущийПланОкончание;
		Иначе
			ТекущийПланОкончаниеПоследователя = ДанныеПроекта.Получить(Строка.Ссылка).ТекущийПланОкончание;
		КонецЕсли;
		
		Если Строка.Свойство("ТекущийПланНачало") Тогда 
			ТекущийПланНачалоПоследователя = Строка.ТекущийПланНачало;
		Иначе
			ТекущийПланНачалоПоследователя = ДанныеПроекта.Получить(Строка.Ссылка).ТекущийПланНачало;
		КонецЕсли;
		
		Если Строка.ТипЗависимости = Перечисления.ТипыЗависимостейПроектныхЗадач.ОкончаниеНачало Тогда 
			
			НовыйПланОкончание = ТекущийПланНачалоПоследователя;
			Если Строка.Задержка <> 0 Тогда 
				НовыйПланОкончание = РаботаСПроектами.РассчитатьНачалоПериода(ДанныеПроектнойЗадачи, 
					НовыйПланОкончание, 
					Строка.Задержка, 
					Строка.ЕдиницаЗадержки);
			КонецЕсли;		
			
			Если ПолучитьФункциональнуюОпцию("ИспользоватьГрафикиРаботы") И ДанныеПроектнойЗадачи.ТекущийПланДлительность <> 0 Тогда 
				
				ГрафикРаботы = ПолучитьГрафикРаботыПроектнойЗадачи(ДанныеПроектнойЗадачи);
				врНовыйПланОкончание = ГрафикиРаботы.ПолучитьДатуНачалаПериодаСек(ГрафикРаботы, НовыйПланОкончание, 1); 
				Если врНовыйПланОкончание + 1 <> НовыйПланОкончание Тогда 
					НовыйПланОкончание = врНовыйПланОкончание + 1;
				КонецЕсли;	
				
			КонецЕсли;
			
			
		ИначеЕсли Строка.ТипЗависимости = Перечисления.ТипыЗависимостейПроектныхЗадач.НачалоНачало Тогда 	
			
			НовыйПланОкончание = РаботаСПроектами.РассчитатьОкончаниеПериода(ДанныеПроектнойЗадачи, 
				ТекущийПланНачалоПоследователя,
				ДанныеПроектнойЗадачи.ТекущийПланДлительность, 
				ДанныеПроектнойЗадачи.ТекущийПланЕдиницаДлительности);
			
			Если Строка.Задержка <> 0 Тогда 
				НовыйПланОкончание = РаботаСПроектами.РассчитатьНачалоПериода(ДанныеПроектнойЗадачи, 
					НовыйПланОкончание, 
					Строка.Задержка, 
					Строка.ЕдиницаЗадержки);
			КонецЕсли;
			
		ИначеЕсли Строка.ТипЗависимости = Перечисления.ТипыЗависимостейПроектныхЗадач.ОкончаниеОкончание Тогда 
			
			НовыйПланОкончание = ДанныеПроектнойЗадачи.ТекущийПланОкончание;
			
			Если Строка.Задержка <> 0 Тогда 
				НовыйПланОкончание = РаботаСПроектами.РассчитатьНачалоПериода(ДанныеПроектнойЗадачи, 
					НовыйПланОкончание, 
					Строка.Задержка, 
					Строка.ЕдиницаЗадержки);
			КонецЕсли;	
			
		ИначеЕсли Строка.ТипЗависимости = Перечисления.ТипыЗависимостейПроектныхЗадач.НачалоОкончание Тогда 
			
			НовыйПланОкончание = РаботаСПроектами.РассчитатьОкончаниеПериода(ДанныеПроектнойЗадачи, 
				ТекущийПланОкончаниеПоследователя, 
				ДанныеПроектнойЗадачи.ТекущийПланДлительность, 
				ДанныеПроектнойЗадачи.ТекущийПланЕдиницаДлительности);
			
			Если Строка.Задержка <> 0 Тогда 
				НовыйПланОкончание = РаботаСПроектами.РассчитатьНачалоПериода(ДанныеПроектнойЗадачи, 
					НовыйПланОкончание, 
					Строка.Задержка, 
					Строка.ЕдиницаЗадержки);
			КонецЕсли;
			
			
		КонецЕсли;	
		
		Если ЗначениеЗаполнено(НовыйПланОкончание) Тогда 
			Если НовыйПланОкончание < ТекущийПланОкончание Тогда 
				ТекущийПланОкончание = НовыйПланОкончание;
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;
	
	Если ТекущийПланОкончание < ОкончаниеНеРанее Тогда 
		ТекущийПланОкончание = ОкончаниеНеРанее;
	КонецЕсли;
	
	Если (ТекущийПланОкончание <> ДанныеПроектнойЗадачи.ТекущийПланОкончание) Тогда 
		
		Если ДанныеПроектнойЗадачи.АвтоматическиРассчитыватьПланПроекта Тогда 
		
			ДанныеПроектнойЗадачи.ТекущийПланОкончание = ТекущийПланОкончание;
			ДанныеПроектнойЗадачи.ТекущийПланНачало = 
				РаботаСПроектами.РассчитатьНачалоПериода(ДанныеПроектнойЗадачи, 
				ТекущийПланОкончание, 
				ДанныеПроектнойЗадачи.ТекущийПланДлительность, 
				ДанныеПроектнойЗадачи.ТекущийПланЕдиницаДлительности);
			ДанныеПроектнойЗадачи.Модифицированность = Истина;
			
			Если МассивИзмененныхЗадач <> Неопределено Тогда 
				МассивИзмененныхЗадач.Добавить(ДанныеПроектнойЗадачи.Ссылка);
			КонецЕсли;
		
		КонецЕсли;
		
		ПересчитатьПланПоЗадаче = Истина;
		
	КонецЕсли;	
	
	Если ПересчитатьПланПоЗадаче Тогда 
		
		Если ЗначениеЗаполнено(ДанныеПроектнойЗадачи.Родитель) Тогда 
			РассчитатьПланПроектаПоЗадаче(ДанныеПроектнойЗадачи.Родитель,, МассивИзмененныхЗадач, ДанныеПроекта);
		КонецЕсли;	
		
		Для Каждого Строка Из ДанныеПроектнойЗадачи.ВсеПредшественники Цикл
			РассчитатьПланПроектаПоЗадаче(Строка.Предшественник,, МассивИзмененныхЗадач, ДанныеПроекта);
		КонецЦикла;
		
	КонецЕсли;	
	
	
КонецПроцедуры	

Процедура ОбработатьЗадачуФиксированноеНачало(ДанныеПроектнойЗадачи, ПересчитатьПланПоЗадаче, МассивИзмененныхЗадач, ДанныеПроекта)
	
	ТекущийПланНачало = ДанныеПроектнойЗадачи.ДатаОграничения;
	
	Если (ТекущийПланНачало <> ДанныеПроектнойЗадачи.ТекущийПланНачало) Тогда 
		
		Если ДанныеПроектнойЗадачи.АвтоматическиРассчитыватьПланПроекта Тогда 
			
			ДанныеПроектнойЗадачи.ТекущийПланНачало = ТекущийПланНачало;
			ДанныеПроектнойЗадачи.ТекущийПланОкончание = 
				РаботаСПроектами.РассчитатьОкончаниеПериода(ДанныеПроектнойЗадачи, 
				ТекущийПланНачало, 
				ДанныеПроектнойЗадачи.ТекущийПланДлительность, 
				ДанныеПроектнойЗадачи.ТекущийПланЕдиницаДлительности);
			ДанныеПроектнойЗадачи.Модифицированность = Истина;
			
			Если МассивИзмененныхЗадач <> Неопределено Тогда 
				МассивИзмененныхЗадач.Добавить(ДанныеПроектнойЗадачи.Ссылка);
			КонецЕсли;
		
		КонецЕсли;
		
		ПересчитатьПланПоЗадаче = Истина;
		
	КонецЕсли;	
	
	Если ПересчитатьПланПоЗадаче Тогда 
		
		Если ЗначениеЗаполнено(ДанныеПроектнойЗадачи.Родитель) Тогда 
			РассчитатьПланПроектаПоЗадаче(ДанныеПроектнойЗадачи.Родитель,, МассивИзмененныхЗадач, ДанныеПроекта);
		КонецЕсли;	
		
		Для Каждого Строка Из ДанныеПроектнойЗадачи.ВсеПредшественники Цикл
			РассчитатьПланПроектаПоЗадаче(Строка.Предшественник,, МассивИзмененныхЗадач, ДанныеПроекта);
		КонецЦикла;
		
		Для Каждого Строка Из ДанныеПроектнойЗадачи.ВсеПоследователи Цикл
			РассчитатьПланПроектаПоЗадаче(Строка.Ссылка,, МассивИзмененныхЗадач, ДанныеПроекта);
		КонецЦикла;
		
	КонецЕсли;	
	
КонецПроцедуры	

Процедура ОбработатьЗадачуФиксированноеОкончание(ДанныеПроектнойЗадачи, ПересчитатьПланПоЗадаче, МассивИзмененныхЗадач, ДанныеПроекта)
	
	ТекущийПланОкончание = ДанныеПроектнойЗадачи.ДатаОграничения;
	
	Если (ТекущийПланОкончание <> ДанныеПроектнойЗадачи.ТекущийПланОкончание) Тогда 
		
		Если ДанныеПроектнойЗадачи.АвтоматическиРассчитыватьПланПроекта Тогда 
			
			ДанныеПроектнойЗадачи.ТекущийПланОкончание = ТекущийПланОкончание;
			ДанныеПроектнойЗадачи.ТекущийПланНачало =  
				РаботаСПроектами.РассчитатьНачалоПериода(ДанныеПроектнойЗадачи, 
				ТекущийПланОкончание, 
				ДанныеПроектнойЗадачи.ТекущийПланДлительность, 
				ДанныеПроектнойЗадачи.ТекущийПланЕдиницаДлительности);
			ДанныеПроектнойЗадачи.Модифицированность = Истина;
			
			Если МассивИзмененныхЗадач <> Неопределено Тогда 
				МассивИзмененныхЗадач.Добавить(ДанныеПроектнойЗадачи.Ссылка);
			КонецЕсли;
		
		КонецЕсли;
		
		ПересчитатьПланПоЗадаче = Истина;
		
	КонецЕсли;	
	
	Если ПересчитатьПланПоЗадаче Тогда 
		
		Если ЗначениеЗаполнено(ДанныеПроектнойЗадачи.Родитель) Тогда 
			РассчитатьПланПроектаПоЗадаче(ДанныеПроектнойЗадачи.Родитель,, МассивИзмененныхЗадач, ДанныеПроекта);
		КонецЕсли;	
		
		Для Каждого Строка Из ДанныеПроектнойЗадачи.ВсеПредшественники Цикл
			РассчитатьПланПроектаПоЗадаче(Строка.Предшественник,, МассивИзмененныхЗадач, ДанныеПроекта);
		КонецЦикла;
		
		Для Каждого Строка Из ДанныеПроектнойЗадачи.ВсеПоследователи Цикл
			РассчитатьПланПроектаПоЗадаче(Строка.Ссылка,, МассивИзмененныхЗадач, ДанныеПроекта);
		КонецЦикла;
		
	КонецЕсли;	
	
КонецПроцедуры	

Процедура ОбработатьЗадачуНачалоНеРанее(ДанныеПроектнойЗадачи, ПересчитатьПланПоЗадаче, МассивИзмененныхЗадач, ДанныеПроекта)
	
	НачалоНеРанее = ДанныеПроектнойЗадачи.ДатаОграничения;
	
	Если ДанныеПроектнойЗадачи.СпособПланирования = Перечисления.СпособыПланированияПроекта.ОтДатыНачалаПроекта Тогда 
		ОбработатьЗадачуКакМожноРаньше(ДанныеПроектнойЗадачи, ПересчитатьПланПоЗадаче, МассивИзмененныхЗадач, ДанныеПроекта, НачалоНеРанее, );
	Иначе
		ОкончаниеНеРанее = РаботаСПроектами.РассчитатьОкончаниеПериода(ДанныеПроектнойЗадачи, 
			НачалоНеРанее, 
			ДанныеПроектнойЗадачи.ТекущийПланДлительность, 
			ДанныеПроектнойЗадачи.ТекущийПланЕдиницаДлительности);
		
		ОбработатьЗадачуКакМожноПозже(ДанныеПроектнойЗадачи, ПересчитатьПланПоЗадаче, МассивИзмененныхЗадач, ДанныеПроекта, ОкончаниеНеРанее, );
	КонецЕсли;	
	
КонецПроцедуры	

Процедура ОбработатьЗадачуОкончаниеНеРанее(ДанныеПроектнойЗадачи, ПересчитатьПланПоЗадаче, МассивИзмененныхЗадач, ДанныеПроекта)
	
	ОкончаниеНеРанее = ДанныеПроектнойЗадачи.ДатаОграничения;
	
	Если ДанныеПроектнойЗадачи.СпособПланирования = Перечисления.СпособыПланированияПроекта.ОтДатыНачалаПроекта Тогда 
		
		НачалоНеРанее = РаботаСПроектами.РассчитатьНачалоПериода(ДанныеПроектнойЗадачи, 
			ОкончаниеНеРанее, 
			ДанныеПроектнойЗадачи.ТекущийПланДлительность, 
			ДанныеПроектнойЗадачи.ТекущийПланЕдиницаДлительности);
		
		ОбработатьЗадачуКакМожноРаньше(ДанныеПроектнойЗадачи, ПересчитатьПланПоЗадаче, МассивИзмененныхЗадач, ДанныеПроекта, НачалоНеРанее, );
	Иначе
		
		ОбработатьЗадачуКакМожноПозже(ДанныеПроектнойЗадачи, ПересчитатьПланПоЗадаче, МассивИзмененныхЗадач, ДанныеПроекта, ОкончаниеНеРанее, );
	КонецЕсли;	
	
КонецПроцедуры	

Процедура ОбработатьЗадачуНачалоНеПозднее(ДанныеПроектнойЗадачи, ПересчитатьПланПоЗадаче, МассивИзмененныхЗадач, ДанныеПроекта)
	
	НачалоНеПозднее = ДанныеПроектнойЗадачи.ДатаОграничения;
	
	Если ДанныеПроектнойЗадачи.СпособПланирования = Перечисления.СпособыПланированияПроекта.ОтДатыНачалаПроекта Тогда 
		ОбработатьЗадачуКакМожноРаньше(ДанныеПроектнойЗадачи, ПересчитатьПланПоЗадаче, МассивИзмененныхЗадач, ДанныеПроекта, , НачалоНеПозднее);
	Иначе
		ОкончаниеНеПозднее = РаботаСПроектами.РассчитатьОкончаниеПериода(ДанныеПроектнойЗадачи, 
			НачалоНеПозднее, 
			ДанныеПроектнойЗадачи.ТекущийПланДлительность, 
			ДанныеПроектнойЗадачи.ТекущийПланЕдиницаДлительности);
		
		ОбработатьЗадачуКакМожноПозже(ДанныеПроектнойЗадачи, ПересчитатьПланПоЗадаче, МассивИзмененныхЗадач, ДанныеПроекта, , ОкончаниеНеПозднее);
	КонецЕсли;	
	
КонецПроцедуры

Процедура ОбработатьЗадачуОкончаниеНеПозднее(ДанныеПроектнойЗадачи, ПересчитатьПланПоЗадаче, МассивИзмененныхЗадач, ДанныеПроекта)
	
	ОкончаниеНеПозднее = ДанныеПроектнойЗадачи.ДатаОграничения;
	
	Если ДанныеПроектнойЗадачи.СпособПланирования = Перечисления.СпособыПланированияПроекта.ОтДатыНачалаПроекта Тогда 
		НачалоНеПозднее = РаботаСПроектами.РассчитатьНачалоПериода(ДанныеПроектнойЗадачи, 
			ОкончаниеНеПозднее, 
			ДанныеПроектнойЗадачи.ТекущийПланДлительность, 
			ДанныеПроектнойЗадачи.ТекущийПланЕдиницаДлительности);
		
		ОбработатьЗадачуКакМожноРаньше(ДанныеПроектнойЗадачи, ПересчитатьПланПоЗадаче, МассивИзмененныхЗадач, ДанныеПроекта, , НачалоНеПозднее);
	Иначе
		ОбработатьЗадачуКакМожноПозже(ДанныеПроектнойЗадачи, ПересчитатьПланПоЗадаче, МассивИзмененныхЗадач, ДанныеПроекта, , ОкончаниеНеПозднее);
	КонецЕсли;	
	
КонецПроцедуры

// Возвращает Истина, если переданная задача является суммарной
Функция ЭтоСуммарнаяЗадача(ПроектнаяЗадача) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ИСТИНА
	|ИЗ
	|	Справочник.ПроектныеЗадачи КАК ПроектныеЗадачи
	|ГДЕ
	|	ПроектныеЗадачи.Родитель = &ПроектнаяЗадача
	|	И Не ПроектныеЗадачи.ПометкаУдаления";
				   
	Запрос.УстановитьПараметр("ПроектнаяЗадача", ПроектнаяЗадача);
	Результат = Запрос.Выполнить();
	
	Возврат Не Результат.Пустой();
	
КонецФункции	

// Возвращает все подчиненные переданной задачи с учетом суммарных задач
Функция ПолучитьВсеПодчиненныеЗадачи(ПроектнаяЗадача) 
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПроектныеЗадачи.Ссылка,
	|	ПроектныеЗадачи.ТекущийПланТрудозатраты,
	|	ПроектныеЗадачи.ТекущийПланЕдиницаТрудозатрат,
	|	ПроектныеЗадачи.ЕдиницаТрудозатратФакт,
	|	ПроектныеЗадачи.СуммарнаяЗадача,
	|	СрокиПроектныхЗадач.ТекущийПланНачало,
	|	СрокиПроектныхЗадач.ТекущийПланОкончание,
	|	СрокиПроектныхЗадач.ТекущийПланДлительность,
	|	СрокиПроектныхЗадач.ТекущийПланЕдиницаДлительности,
	|	СрокиПроектныхЗадач.НачалоФакт,
	|	СрокиПроектныхЗадач.ОкончаниеФакт,
	|	СрокиПроектныхЗадач.ДлительностьФакт,
	|	СрокиПроектныхЗадач.ЕдиницаДлительностиФакт
	|ИЗ
	|	Справочник.ПроектныеЗадачи КАК ПроектныеЗадачи
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СрокиПроектныхЗадач КАК СрокиПроектныхЗадач
	|		ПО ПроектныеЗадачи.Ссылка = СрокиПроектныхЗадач.ПроектнаяЗадача
	|ГДЕ
	|	ПроектныеЗадачи.Ссылка В ИЕРАРХИИ(&ПроектнаяЗадача)
	|	И НЕ ПроектныеЗадачи.ПометкаУдаления";
	
	Запрос.УстановитьПараметр("ПроектнаяЗадача", ПроектнаяЗадача);
	Результат = Запрос.Выполнить();
	
	Возврат Результат.Выгрузить();
	
КонецФункции

// Возвращает всех предшественников переданной задачи с учетом суммарных задач
Функция ПолучитьВсехПредшественников(ПроектнаяЗадача, ВключатьНачалоПроекта = Истина) Экспорт 
	
	УстановитьПривилегированныйРежим(Истина);
	
	СписокЗадач = Новый СписокЗначений;
	
	ТекРодитель = ПроектнаяЗадача;
	Пока ЗначениеЗаполнено(ТекРодитель) Цикл
		СписокЗадач.Добавить(ТекРодитель);
		ТекРодитель = ТекРодитель.Родитель;
	КонецЦикла;	
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ПроектныеЗадачиПредшественники.Ссылка КАК Ссылка,
	|	ПроектныеЗадачиПредшественники.ТипЗависимости,
	|	ПроектныеЗадачиПредшественники.Задержка,
	|	ПроектныеЗадачиПредшественники.ЕдиницаЗадержки,
	|	ПроектныеЗадачиПредшественники.Предшественник КАК Предшественник,
	|	ПроектныеЗадачиПредшественники.Предшественник.СуммарнаяЗадача КАК СуммарнаяЗадача,
	|	СрокиПроектныхЗадач.ТекущийПланНачало КАК ТекущийПланНачало,
	|	СрокиПроектныхЗадач.ТекущийПланОкончание КАК ТекущийПланОкончание
	|ИЗ
	|	Справочник.ПроектныеЗадачи.Предшественники КАК ПроектныеЗадачиПредшественники
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СрокиПроектныхЗадач КАК СрокиПроектныхЗадач
	|		ПО ПроектныеЗадачиПредшественники.Предшественник = СрокиПроектныхЗадач.ПроектнаяЗадача
	|ГДЕ
	|	ПроектныеЗадачиПредшественники.Ссылка В(&СписокЗадач)
	|	И НЕ ПроектныеЗадачиПредшественники.Предшественник.ПометкаУдаления";
	
	Запрос.УстановитьПараметр("СписокЗадач", СписокЗадач);
	Результат = Запрос.Выполнить().Выгрузить();
	
	ВсеПредшественники = Результат.Скопировать();
	ВсеПредшественники.Очистить();
	
	Для Каждого Строка Из Результат Цикл
		
		Если Строка.СуммарнаяЗадача Тогда 
			ПодчиненныеЗадачи = ПолучитьВсеПодчиненныеЗадачи(Строка.Предшественник);
			Для Каждого ПодчиненнаяЗадача Из ПодчиненныеЗадачи Цикл
				Если ПодчиненнаяЗадача.СуммарнаяЗадача Тогда 
					Продолжить;
				КонецЕсли;
				
				НоваяСтрока = ВсеПредшественники.Добавить();
				НоваяСтрока.Предшественник = ПодчиненнаяЗадача.Ссылка;
				НоваяСтрока.СуммарнаяЗадача = ПодчиненнаяЗадача.СуммарнаяЗадача;
				НоваяСтрока.ТекущийПланНачало = ПодчиненнаяЗадача.ТекущийПланНачало;
				НоваяСтрока.ТекущийПланОкончание = ПодчиненнаяЗадача.ТекущийПланОкончание;
				НоваяСтрока.Ссылка = Строка.Ссылка;
				НоваяСтрока.ТипЗависимости = Строка.ТипЗависимости;
				НоваяСтрока.Задержка = Строка.Задержка;
				НоваяСтрока.ЕдиницаЗадержки = Строка.ЕдиницаЗадержки;
			КонецЦикла;	
		Иначе	
			НоваяСтрока = ВсеПредшественники.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
		КонецЕсли;	
		
	КонецЦикла;	
	
	Если ВключатьНачалоПроекта Тогда 
		НачалоПроекта = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ПроектнаяЗадача.Владелец, "ТекущийПланНачало");
		Для Каждого Строка Из СписокЗадач Цикл
			Если Результат.Найти(Строка.Значение, "Ссылка") = Неопределено Тогда 
				НоваяСтрока = ВсеПредшественники.Добавить();
				НоваяСтрока.Ссылка = Строка.Значение;
				НоваяСтрока.ТипЗависимости = Перечисления.ТипыЗависимостейПроектныхЗадач.ОкончаниеНачало;
				НоваяСтрока.ТекущийПланНачало = НачалоПроекта;
				НоваяСтрока.ТекущийПланОкончание = НачалоПроекта;
				НоваяСтрока.Задержка = 0;
			КонецЕсли;
		КонецЦикла;	
	КонецЕсли;	
	
	Возврат ВсеПредшественники;
	
КонецФункции	

// Возвращает всех предшественников переданной задачи с учетом суммарных задач
Функция ПолучитьВсехПредшественниковСУчетомПодчиненных(ПроектнаяЗадача, ВключатьНачалоПроекта = Истина) Экспорт 
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПроектныеЗадачи.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.ПроектныеЗадачи КАК ПроектныеЗадачи
		|ГДЕ
		|	ПроектныеЗадачи.Ссылка В ИЕРАРХИИ(&ПроектнаяЗадача)
		|	И НЕ ПроектныеЗадачи.ПометкаУдаления
		|ИТОГИ ПО
		|	Ссылка ТОЛЬКО ИЕРАРХИЯ";

	Запрос.УстановитьПараметр("ПроектнаяЗадача", ПроектнаяЗадача);
	СписокЗадач = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ПроектныеЗадачиПредшественники.Ссылка КАК Ссылка,
		|	ПроектныеЗадачиПредшественники.Предшественник КАК Предшественник,
		|	ПроектныеЗадачиПредшественники.ТипЗависимости,
		|	ПроектныеЗадачиПредшественники.Задержка,
		|	ПроектныеЗадачиПредшественники.ЕдиницаЗадержки,
		|	СрокиПроектныхЗадач.ТекущийПланНачало КАК ТекущийПланНачало,
		|	СрокиПроектныхЗадач.ТекущийПланОкончание КАК ТекущийПланОкончание
		|ИЗ
		|	Справочник.ПроектныеЗадачи.Предшественники КАК ПроектныеЗадачиПредшественники
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СрокиПроектныхЗадач КАК СрокиПроектныхЗадач
		|		ПО ПроектныеЗадачиПредшественники.Предшественник = СрокиПроектныхЗадач.ПроектнаяЗадача
		|ГДЕ
		|	ПроектныеЗадачиПредшественники.Ссылка В(&СписокЗадач)
		|	И НЕ ПроектныеЗадачиПредшественники.Предшественник.ПометкаУдаления";
	
	Запрос.УстановитьПараметр("СписокЗадач", СписокЗадач);
	Результат = Запрос.Выполнить().Выгрузить();
	
	Если ВключатьНачалоПроекта Тогда 
		НачалоПроекта = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ПроектнаяЗадача.Владелец, "ТекущийПланНачало");
		Для Каждого Строка Из СписокЗадач Цикл
			Если Результат.Найти(Строка.Значение, "Ссылка") = Неопределено Тогда 
				НоваяСтрока = Результат.Добавить();
				НоваяСтрока.Ссылка = Строка.Значение;
				НоваяСтрока.ТипЗависимости = Перечисления.ТипыЗависимостейПроектныхЗадач.ОкончаниеНачало;
				НоваяСтрока.ТекущийПланНачало = НачалоПроекта;
				НоваяСтрока.ТекущийПланОкончание = НачалоПроекта;
			КонецЕсли;
		КонецЦикла;	
	КонецЕсли;	
	
	Возврат Результат;
	
КонецФункции

// Возвращает всех последователей переданной задачи с учетом суммарных задач
Функция ПолучитьВсехПоследователей(ПроектнаяЗадача, ВключатьОкончаниеПроекта = Истина) Экспорт 
	
	УстановитьПривилегированныйРежим(Истина);
	
	СписокЗадач = Новый СписокЗначений;
	
	ТекРодитель = ПроектнаяЗадача;
	Пока ЗначениеЗаполнено(ТекРодитель) Цикл
		СписокЗадач.Добавить(ТекРодитель);
		ТекРодитель = ТекРодитель.Родитель;
	КонецЦикла;
	
	// перемещение последующих задач
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ПроектныеЗадачиПредшественники.Ссылка,
		|	ПроектныеЗадачиПредшественники.Ссылка.СуммарнаяЗадача КАК СуммарнаяЗадача,
		|	СрокиПроектныхЗадач.ТекущийПланНачало КАК ТекущийПланНачало,
		|	СрокиПроектныхЗадач.ТекущийПланОкончание КАК ТекущийПланОкончание,
		|	ПроектныеЗадачиПредшественники.ТипЗависимости,
		|	ПроектныеЗадачиПредшественники.Задержка,
		|	ПроектныеЗадачиПредшественники.ЕдиницаЗадержки
		|ИЗ
		|	Справочник.ПроектныеЗадачи.Предшественники КАК ПроектныеЗадачиПредшественники
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СрокиПроектныхЗадач КАК СрокиПроектныхЗадач
		|		ПО ПроектныеЗадачиПредшественники.Ссылка = СрокиПроектныхЗадач.ПроектнаяЗадача
		|ГДЕ
		|	ПроектныеЗадачиПредшественники.Предшественник В(&СписокЗадач)
		|	И НЕ ПроектныеЗадачиПредшественники.Ссылка.ПометкаУдаления";
	
	Запрос.УстановитьПараметр("СписокЗадач", СписокЗадач);
	Результат = Запрос.Выполнить().Выгрузить();
	
	ВсеПоследователи = Результат.Скопировать();
	ВсеПоследователи.Очистить();
	
	Для Каждого Строка Из Результат Цикл
		
		Если Строка.СуммарнаяЗадача Тогда 
			ПодчиненныеЗадачи = ПолучитьВсеПодчиненныеЗадачи(Строка.Ссылка);
			Для Каждого ПодчиненнаяЗадача Из ПодчиненныеЗадачи Цикл
				Если ПодчиненнаяЗадача.СуммарнаяЗадача Тогда 
					Продолжить;
				КонецЕсли;
				
				НоваяСтрока = ВсеПоследователи.Добавить();
				НоваяСтрока.Ссылка = ПодчиненнаяЗадача.Ссылка;
				НоваяСтрока.СуммарнаяЗадача = ПодчиненнаяЗадача.СуммарнаяЗадача;
				НоваяСтрока.ТекущийПланНачало = ПодчиненнаяЗадача.ТекущийПланНачало;
				НоваяСтрока.ТекущийПланОкончание = ПодчиненнаяЗадача.ТекущийПланОкончание;
				НоваяСтрока.ТипЗависимости = Строка.ТипЗависимости;
				НоваяСтрока.Задержка = Строка.Задержка;
				НоваяСтрока.ЕдиницаЗадержки = Строка.ЕдиницаЗадержки;
			КонецЦикла;	
		Иначе	
			НоваяСтрока = ВсеПоследователи.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
		КонецЕсли;	
		
	КонецЦикла;	
	
	Если ВключатьОкончаниеПроекта Тогда 
		
		ОкончаниеПроекта = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ПроектнаяЗадача.Владелец, "ТекущийПланОкончание");
		Если ВсеПоследователи.Количество() = 0 Тогда 
			НоваяСтрока = ВсеПоследователи.Добавить();
			НоваяСтрока.Ссылка = ПроектнаяЗадача;
			НоваяСтрока.ТипЗависимости = Перечисления.ТипыЗависимостейПроектныхЗадач.ОкончаниеНачало;
			НоваяСтрока.Задержка = 0;
			НоваяСтрока.ТекущийПланНачало = ОкончаниеПроекта;
			НоваяСтрока.ТекущийПланОкончание = ОкончаниеПроекта;
		КонецЕсли;	
		
	КонецЕсли;
	
	Возврат ВсеПоследователи;
	
КонецФункции

// Выдает исключение, если по проектной задаче указаны некорректные предшественники
Процедура ПроверитьПредшественников(ПроектнаяЗадача) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	// Формируем родительские задачи
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПроектныеЗадачи.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.ПроектныеЗадачи КАК ПроектныеЗадачи
		|ГДЕ
		|	ПроектныеЗадачи.Ссылка = &ПроектнаяЗадача
		|	И НЕ ПроектныеЗадачи.ПометкаУдаления
		|ИТОГИ ПО
		|	Ссылка ТОЛЬКО ИЕРАРХИЯ";

	Запрос.УстановитьПараметр("ПроектнаяЗадача", ПроектнаяЗадача);

	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	ПроектныеЗадачиРодители = Новый СписокЗначений();
	
	Пока Выборка.Следующий() Цикл
		Если ЗначениеЗаполнено(Выборка.Ссылка) И ПроектныеЗадачиРодители.НайтиПоЗначению(Выборка.Ссылка) = Неопределено Тогда
			ПроектныеЗадачиРодители.Добавить(Выборка.Ссылка);	
		КонецЕсли;
	КонецЦикла;
	
	// Формируем подчиненные задачи
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПроектныеЗадачи.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.ПроектныеЗадачи КАК ПроектныеЗадачи
		|ГДЕ
		|	ПроектныеЗадачи.Ссылка В ИЕРАРХИИ(&ПроектнаяЗадача)
		|	И НЕ ПроектныеЗадачи.ПометкаУдаления
		|ИТОГИ ПО
		|	Ссылка ТОЛЬКО ИЕРАРХИЯ";

	Запрос.УстановитьПараметр("ПроектнаяЗадача", ПроектнаяЗадача);

	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	ПроектныеЗадачиПодчиненные = Новый СписокЗначений();

	Пока Выборка.Следующий() Цикл
		Если ЗначениеЗаполнено(Выборка.Ссылка) И ПроектныеЗадачиПодчиненные.НайтиПоЗначению(Выборка.Ссылка) = Неопределено Тогда
			ПроектныеЗадачиПодчиненные.Добавить(Выборка.Ссылка);	
		КонецЕсли;
	КонецЦикла;

	// Проверка предшественников
	Для Каждого СтрокаТаблЧасти Из ПроектнаяЗадача.Предшественники Цикл
		Если СтрокаТаблЧасти.Предшественник = ПроектнаяЗадача Тогда
			ВызватьИсключение(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Проектная задача не может быть предшественником самой себя'; en = 'Project task cannot be a predecessor of itself'"), ПроектнаяЗадача));	
		КонецЕсли;
		Если ПроектныеЗадачиРодители.НайтиПоЗначению(СтрокаТаблЧасти.Предшественник) <> Неопределено Тогда
			ВызватьИсключение(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Подчиненная задача %1 не может иметь в качестве предшественника одну из родительских задач'; en = 'Subordinate task %1 cannot have one of its parent tasks as a predecessor'"), ПроектнаяЗадача));	
		КонецЕсли;
		Если ПроектныеЗадачиПодчиненные.НайтиПоЗначению(СтрокаТаблЧасти.Предшественник) <> Неопределено Тогда
			ВызватьИсключение(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Суммарная задача %1 не может иметь в качестве предшественника одну из подчиненных задач'; en = 'Parent task %1 cannot have as its predecessor one of the subordinate tasks'"), ПроектнаяЗадача));	
		КонецЕсли; 
	КонецЦикла;
	
	ЗадачиОбработанные = Новый Массив;
	ЗадачиВОбработке = Новый Массив;
	
	ПроверитьПредшественниковНаЗацикливание(ПроектнаяЗадача, ЗадачиВОбработке, ЗадачиОбработанные);
	
	Для Каждого Строка Из ПроектныеЗадачиПодчиненные Цикл
		ЗадачиОбработанные = Новый Массив;
		ЗадачиВОбработке = Новый Массив;
	
		ПроверитьПредшественниковНаЗацикливание(Строка.Значение, ЗадачиВОбработке, ЗадачиОбработанные);
	КонецЦикла;	
	
КонецПроцедуры

Процедура ПроверитьПредшественниковНаЗацикливание(ПроектнаяЗадача, ЗадачиВОбработке, ЗадачиОбработанные) 
	
	Если ЗадачиОбработанные.Найти(ПроектнаяЗадача) <> Неопределено Тогда 
		Возврат;
	КонецЕсли;	
	
	Если ЗадачиВОбработке.Найти(ПроектнаяЗадача) <> Неопределено Тогда 
		
		ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Обнаружено зацикливание предшественников на задаче ""%1"".'; en = 'Detected the looping of the predecessors on task ""%1"".'"),
			Строка(ПроектнаяЗадача));
		
	КонецЕсли;
	
	ЗадачиВОбработке.Добавить(ПроектнаяЗадача);
	
	Предшественники = ПолучитьВсехПредшественниковСУчетомПодчиненных(ПроектнаяЗадача, Ложь);
	Для Каждого Строка Из Предшественники Цикл
		ПроверитьПредшественниковНаЗацикливание(Строка.Предшественник, ЗадачиВОбработке, ЗадачиОбработанные);
	КонецЦикла;	
		
	ЗадачиОбработанные.Добавить(ПроектнаяЗадача);
	
	ЗадачиВОбработке.Удалить(ЗадачиВОбработке.Найти(ПроектнаяЗадача));
	
КонецПроцедуры	

// Возвращает массив основных бизнес-процессов по проектной задачи
Функция ПолучитьОсновныеБизнесПроцессы(ПроектнаяЗадача, ТолькоНезавершенные = Ложь) Экспорт 
	
	Результат = Новый Массив;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ДанныеБизнесПроцессов.БизнесПроцесс КАК Ссылка
	|ИЗ
	|	РегистрСведений.ДанныеБизнесПроцессов КАК ДанныеБизнесПроцессов
	|ГДЕ
	|	ДанныеБизнесПроцессов.ПроектнаяЗадача = &ПроектнаяЗадача
	|	И НЕ ДанныеБизнесПроцессов.ПометкаУдаления";
	
	Если ТолькоНезавершенные Тогда 
		Запрос.Текст = Запрос.Текст + " И Не ДанныеБизнесПроцессов.Завершен ";
	КонецЕсли;	
		
	Запрос.УстановитьПараметр("ПроектнаяЗадача", ПроектнаяЗадача);
		
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Результат.Добавить(Выборка.Ссылка);
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции	

// Отмечает начало выполнения проектной задачи при завершении бизнес-процесса
Процедура ОтметитьНачалоВыполненияПроектнойЗадачи(БизнесПроцесс) Экспорт 
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если Не ПолучитьФункциональнуюОпцию("ВестиУчетПоПроектам") Тогда 
		Возврат;
	КонецЕсли;	
		
	ПроектнаяЗадача = БизнесПроцесс.ПроектнаяЗадача;
	Если Не ЗначениеЗаполнено(ПроектнаяЗадача) Тогда 
		Возврат;
	КонецЕсли;	
	
	ДанныеПроектнойЗадачи = ПолучитьДанныеПроектнойЗадачи(ПроектнаяЗадача);
	Если ДанныеПроектнойЗадачи.СуммарнаяЗадача Тогда 
		Возврат;
	КонецЕсли;	
	
	Если Не ЗначениеЗаполнено(ДанныеПроектнойЗадачи.НачалоФакт) Тогда 
		СтруктураДанных = Новый Структура;
		СтруктураДанных.Вставить("НачалоФакт", БизнесПроцесс.ДатаНачала);
		
		ЗаписатьСрокиПроектнойЗадачи(ДанныеПроектнойЗадачи.Ссылка, СтруктураДанных);
		
		Если ЗначениеЗаполнено(ДанныеПроектнойЗадачи.Родитель) Тогда 
			РассчитатьПланПроекта(ДанныеПроектнойЗадачи.Родитель);
		КонецЕсли;	
	КонецЕсли;	
	
	Если ЗначениеЗаполнено(ДанныеПроектнойЗадачи.ОкончаниеФакт) Тогда 
		СтруктураДанных = Новый Структура;
		СтруктураДанных.Вставить("ОкончаниеФакт", '00010101');
		
		ЗаписатьСрокиПроектнойЗадачи(ДанныеПроектнойЗадачи.Ссылка, СтруктураДанных);
		
		Если ЗначениеЗаполнено(ДанныеПроектнойЗадачи.Родитель) Тогда 
			РассчитатьПланПроекта(ДанныеПроектнойЗадачи.Родитель);
		КонецЕсли;
	КонецЕсли;
		
КонецПроцедуры	

// Отмечает окончание выполнения проектной задачи при завершении бизнес-процесса
Процедура ОтметитьОкончаниеВыполненияПроектнойЗадачи(БизнесПроцесс) Экспорт 
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если Не ПолучитьФункциональнуюОпцию("ВестиУчетПоПроектам") Тогда 
		Возврат;
	КонецЕсли;	
		
	ПроектнаяЗадача = БизнесПроцесс.ПроектнаяЗадача;
	Если Не ЗначениеЗаполнено(ПроектнаяЗадача) Тогда 
		Возврат;
	КонецЕсли;	
	
	ДанныеПроектнойЗадачи = ПолучитьДанныеПроектнойЗадачи(ПроектнаяЗадача);
	Если ДанныеПроектнойЗадачи.СуммарнаяЗадача Тогда 
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ДанныеПроектнойЗадачи.ОкончаниеФакт) Тогда 
		Возврат;
	КонецЕсли;
	
	// последний процесс
	МассивПроцессов = ПолучитьОсновныеБизнесПроцессы(ДанныеПроектнойЗадачи.Ссылка, Истина);
	Если Не (МассивПроцессов.Количество() = 1 И МассивПроцессов[0] = БизнесПроцесс.Ссылка) Тогда 
		Возврат;
	КонецЕсли;	
	
	СтруктураДанных = Новый Структура;
	СтруктураДанных.Вставить("ОкончаниеФакт", БизнесПроцесс.ДатаЗавершения);
	СтруктураДанных.Вставить("ДлительностьФакт", 
		РаботаСПроектами.РассчитатьДлительностьПериода(ДанныеПроектнойЗадачи, 
		ДанныеПроектнойЗадачи.НачалоФакт, 
		БизнесПроцесс.ДатаЗавершения, 
		ДанныеПроектнойЗадачи.ЕдиницаДлительностиФакт));
		
	ЗаписатьСрокиПроектнойЗадачи(ДанныеПроектнойЗадачи.Ссылка, СтруктураДанных);	
		
	Если ЗначениеЗаполнено(ДанныеПроектнойЗадачи.Родитель) Тогда 
		РассчитатьПланПроекта(ДанныеПроектнойЗадачи.Родитель);
	КонецЕсли;
	
КонецПроцедуры	

// Возвращает Истина, если по проектной задачи есть незавершенные предшественники
Функция ЕстьНезавершенныеПредшественники(ПроектнаяЗадача) Экспорт 
	
	Если Не ПолучитьФункциональнуюОпцию("ВестиУчетПоПроектам") Тогда 
		Возврат Ложь;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ИСТИНА
	|ИЗ
	|	Справочник.ПроектныеЗадачи КАК ПроектныеЗадачи
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СрокиПроектныхЗадач КАК СрокиПроектныхЗадач
	|		ПО ПроектныеЗадачи.Предшественники.Предшественник = СрокиПроектныхЗадач.ПроектнаяЗадача
	|ГДЕ
	|	ПроектныеЗадачи.Ссылка = &ПроектнаяЗадача
	|	И СрокиПроектныхЗадач.ОкончаниеФакт = ДАТАВРЕМЯ(1, 1, 1)
	|	И ПроектныеЗадачи.Предшественники.ТипЗависимости = ЗНАЧЕНИЕ(Перечисление.ТипыЗависимостейПроектныхЗадач.ОкончаниеНачало)
	|	И НЕ ПроектныеЗадачи.Предшественники.Предшественник.ПометкаУдаления";
	
	Запрос.УстановитьПараметр("ПроектнаяЗадача", ПроектнаяЗадача);
	
	Возврат Не Запрос.Выполнить().Пустой();
	
КонецФункции	
	
// Проверяет, что текущая дата позже, чем дата начала проекта 
Функция НаступилаДатаНачалаПроекта(Проект) Экспорт 
	
	Если Не ПолучитьФункциональнуюОпцию("ВестиУчетПоПроектам") Тогда 
		Возврат Истина;
	КонецЕсли;	
	
	Возврат (Проект.ТекущийПланНачало <= ТекущаяДатаСеанса());
	
КонецФункции

// Проверяет, что текущая дата позже, чем дата начала проектной задачи 
Функция НаступилаДатаНачалаЗадачи(ПроектнаяЗадача) Экспорт 
	
	Если Не ПолучитьФункциональнуюОпцию("ВестиУчетПоПроектам") Тогда 
		Возврат Истина;
	КонецЕсли;	
	
	ДанныеПроектнойЗадачи = ПолучитьСрокиПроектнойЗадачи(ПроектнаяЗадача);
	Возврат (ДанныеПроектнойЗадачи.ТекущийПланНачало <= ТекущаяДатаСеанса());
	
КонецФункции

// Возвращает проект по умолчанию
Функция ПолучитьПроектПоУмолчанию() Экспорт
	
	Если Не ПолучитьФункциональнуюОпцию("ВестиУчетПоПроектам") Тогда 
		Возврат Неопределено;
	КонецЕсли;	
	
	Возврат ХранилищеОбщихНастроек.Загрузить("НастройкиРаботыСПроектами", "ПроектПоУмолчанию");
	
КонецФункции

// Возвращает наименьшие трудозатраты среди исполнителей проектной задачи
Функция МинимальныеТрудозатратыИсполнителей(Исполнители) Экспорт 
	
	Если Исполнители.Количество() = 0 Тогда 
		Возврат 0;
	КонецЕсли;	
	
	МинимальныеТрудозатраты = Исполнители[0].ТекущийПланТрудозатраты;
	
	Для Каждого Строка Из Исполнители Цикл
		Если Строка.ТекущийПланТрудозатраты < МинимальныеТрудозатраты Тогда 
			МинимальныеТрудозатраты = Строка.ТекущийПланТрудозатраты;
		КонецЕсли;	
	КонецЦикла;	
	
	Возврат МинимальныеТрудозатраты;
	
КонецФункции	

// Заполняет реквизит "Проект" у подчиненных файлов
Процедура ЗаполнитьПроектПодчиненныхФайлов(Владелец) Экспорт
	
	Если Не ПолучитьФункциональнуюОпцию("ВестиУчетПоПроектам") Тогда
		Возврат;
	КонецЕсли;	
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Файлы.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.Файлы КАК Файлы
		|ГДЕ
		|	Файлы.ВладелецФайла = &Владелец
		|	И Файлы.Проект <> &Проект";
	
	Проект = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Владелец, "Проект");
	Запрос.УстановитьПараметр("Проект", Проект);
	Запрос.УстановитьПараметр("Владелец", Владелец);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ЗаблокироватьДанныеДляРедактирования(Выборка.Ссылка);
		Объект = Выборка.Ссылка.ПолучитьОбъект();
		Объект.Проект = Владелец.Проект;
		Объект.Записать();
		РазблокироватьДанныеДляРедактирования(Объект.Ссылка);
	КонецЦикла;
	
КонецПроцедуры

// Получает сумму фактических трудозатрат по проектной задаче
Функция ПолучитьФактическиеТрудозатратыПроектнойЗадачи(
	ПроектнаяЗадача, 
	ЕдиницаТрудозатратФакт = Неопределено) Экспорт
	
	Если Не ЗначениеЗаполнено(ПроектнаяЗадача) Тогда 
		Возврат 0;
	КонецЕсли;	
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ЕСТЬNULL(СУММА(ФактическиеТрудозатраты.Длительность), 0) КАК ФактТрудозатраты
		|ИЗ
		|	РегистрСведений.ФактическиеТрудозатраты КАК ФактическиеТрудозатраты
		|ГДЕ
		|	ФактическиеТрудозатраты.ПроектнаяЗадача = &Задача И Не Удалена";
		
	Запрос.УстановитьПараметр("Задача", ПроектнаяЗадача);		
	Результат = Запрос.Выполнить();
	Если НЕ Результат.Пустой() Тогда
		РеквизитыПроектнойЗадачи = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
			ПроектнаяЗадача, 
			"Владелец, ГрафикРаботы, ЕдиницаТрудозатратФакт");
		ГрафикРаботы = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(РеквизитыПроектнойЗадачи.Владелец, "ГрафикРаботы");
		Если ЗначениеЗаполнено(РеквизитыПроектнойЗадачи.ГрафикРаботы) Тогда
			ГрафикРаботы = РеквизитыПроектнойЗадачи.ГрафикРаботы;
		КонецЕсли;
		ЕдиницаТрудозатратДляРасчета = 
			?(ЕдиницаТрудозатратФакт = Неопределено,
			РеквизитыПроектнойЗадачи.ЕдиницаТрудозатратФакт,
			ЕдиницаТрудозатратФакт);
			
		Выборка = Результат.Выбрать();
		Если Выборка.Следующий() Тогда
			ТрудозатратыФакт = ПересчитатьТрудозатраты(
				Выборка.ФактТрудозатраты / 60,
				Перечисления.ЕдиницыТрудозатрат.ЧеловекоМинута,
				ЕдиницаТрудозатратДляРасчета,
				ГрафикРаботы);
			Возврат ТрудозатратыФакт;
		КонецЕсли;
	КонецЕсли;
	
	Возврат 0;
	
КонецФункции

// Получает фактические трудозатраты исполнителей проектной задачи.
//
// Параметры:
//	ПроектнаяЗадача - ссылка на проектную задачу.
//	ЕдиницаТрудозатратФакт - единица трудозатрат, в которых считать трудозатраты.
//		Если параметр не указан, единицы берутся из проектной задачи.
//
// Возвращаемое значение:
//	ТаблицаЗначений, содержащая исполнителей проектной задачи и их фактические трудозатраты.
//		Исполнитель - пользователь или роль исполнителей.
//		ТрудозатратыФакт - неотрицательное число (10.2).
//
Функция ПолучитьФактическиеТрудозатратыИсполнителейПроектнойЗадачи(
	ПроектнаяЗадача,
	ЕдиницаТрудозатрат = Неопределено) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ТаблицаИсполнителей = Новый ТаблицаЗначений;
	ТаблицаИсполнителей.Колонки.Добавить("Исполнитель");
	ТаблицаИсполнителей.Колонки.Добавить("ТрудозатратыФакт", Новый ОписаниеТипов("Число"));
	
	Для каждого СтрокаТаблицы из ПроектнаяЗадача.Исполнители Цикл
		НоваяСтрока = ТаблицаИсполнителей.Добавить();
		НоваяСтрока.Исполнитель = СтрокаТаблицы.Исполнитель;
		НоваяСтрока.ТрудозатратыФакт = 0;
	КонецЦикла;
	
	// Получение величин для расчета трудозатрат
	РеквизитыПроектнойЗадачи = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ПроектнаяЗадача,
		"Владелец, 
		|ГрафикРаботы, 
		|ЕдиницаТрудозатратФакт");
	РеквизитыПроекта = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(РеквизитыПроектнойЗадачи.Владелец,
		"ГрафикРаботы");
		
	ГрафикРаботы = 
		?(ЗначениеЗаполнено(РеквизитыПроектнойЗадачи.ГрафикРаботы), 
		РеквизитыПроектнойЗадачи.ГрафикРаботы, 
		РеквизитыПроекта.ГрафикРаботы);
		
	ЕдиницаТрудозатратДляРасчета = ЕдиницаТрудозатрат;
	Если ЕдиницаТрудозатрат = Неопределено Тогда
		ЕдиницаТрудозатратДляРасчета = РеквизитыПроектнойЗадачи.ЕдиницаТрудозатратФакт;
	КонецЕсли;
	
	//Выборка всех записей о трудозатратах для этой проектной задачи
	Отбор = Новый Структура("ПроектнаяЗадача", ПроектнаяЗадача);
	ВыборкаЗаписей = РегистрыСведений.ФактическиеТрудозатраты.Выбрать(Отбор);
	
	Пока ВыборкаЗаписей.Следующий() Цикл
		ТрудозатратыФакт = РаботаСПроектами.ПересчитатьТрудозатраты(
			ВыборкаЗаписей.Длительность / 60,
			Перечисления.ЕдиницыТрудозатрат.ЧеловекоМинута,
			ЕдиницаТрудозатратДляРасчета,
			ГрафикРаботы);
			
		ТрудозатратыПрисвоены = Ложь;
		
		// Поиск пользователя, которому приписать трудозатраты
		Для Каждого Участник Из ТаблицаИсполнителей Цикл
			Если ТипЗнч(Участник.Исполнитель) = Тип("СправочникСсылка.Пользователи")
				И Участник.Исполнитель = ВыборкаЗаписей.Пользователь Тогда 
				Участник.ТрудозатратыФакт = Участник.ТрудозатратыФакт + ТрудозатратыФакт;
				ТрудозатратыПрисвоены = Истина;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
		Если НЕ ТрудозатратыПрисвоены Тогда
			// Поиск роли, которой приписать трудозатраты
			Для Каждого Участник Из ТаблицаИсполнителей Цикл	
				Если ТипЗнч(Участник.Исполнитель) = Тип("СправочникСсылка.ПолныеРоли") Тогда
					Если ТипЗнч(ВыборкаЗаписей.Источник) = Тип("ЗадачаСсылка.ЗадачаИсполнителя") Тогда
						РольИсполнителяЗадачи = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(
							ВыборкаЗаписей.Источник,
							"РольИсполнителя");
						Если РольИсполнителяЗадачи = Участник.Исполнитель Тогда
							Участник.ТрудозатратыФакт = Участник.ТрудозатратыФакт + ТрудозатратыФакт;
							ТрудозатратыПрисвоены = Истина;
						КонецЕсли;
					Иначе
						ИсполнителиРоли = РегистрыСведений.ИсполнителиЗадач.ИсполнителиРоли(Участник.Исполнитель);
						Если ИсполнителиРоли.Найти(ВыборкаЗаписей.Пользователь) <> Неопределено Тогда
							Участник.ТрудозатратыФакт = Участник.ТрудозатратыФакт + ТрудозатратыФакт;
							ТрудозатратыПрисвоены = Истина;
						КонецЕсли;
					КонецЕсли;
					Если ТрудозатратыПрисвоены Тогда
						Прервать;
					КонецЕсли;
				КонецЕсли;	
			КонецЦикла;
		КонецЕсли;	
	КонецЦикла;
	
	Возврат ТаблицаИсполнителей;
	
КонецФункции

// Возвращает общие фактические трудозатраты по проекту
Функция ПолучитьФактическиеТрудозатратыПроекта(Проект) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	СуммарныеФактическиеТрудозатраты = 0;
	
	Запрос = Новый Запрос();
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПроектныеЗадачи.Ссылка
		|ИЗ
		|	Справочник.ПроектныеЗадачи КАК ПроектныеЗадачи
		|ГДЕ
		|	ПроектныеЗадачи.Владелец = &Владелец
		|	И Не ПроектныеЗадачи.ПометкаУдаления";
	Запрос.УстановитьПараметр("Владелец", Проект);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		СуммарныеФактическиеТрудозатраты = СуммарныеФактическиеТрудозатраты + 
			ПолучитьФактическиеТрудозатратыПроектнойЗадачи(Выборка.Ссылка);	
	КонецЦикла;
	
	Возврат СуммарныеФактическиеТрудозатраты;	
	
КонецФункции

// Выполняет поиск переданного пользователя среди исполнителей проектной задачи с учетом ролевой адресации
Функция НайтиИсполнителяВПроектнойЗадачеСУчетомВхожденияВРоль(
	ПроектнаяЗадача,
	Исполнитель,
	ЗадачаИсполнителя)
	
	Если ПроектнаяЗадача.Исполнители.Количество() = 0 Тогда 
		Возврат Истина;
	КонецЕсли;	
	
	ИсполнительНайден = Ложь;
	Если ЗначениеЗаполнено(ЗадачаИсполнителя) И ТипЗнч(ЗадачаИсполнителя) = Тип("ЗадачаСсылка.ЗадачаИсполнителя") Тогда
		
		РеквизитыЗадачи = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ЗадачаИсполнителя, 
			"РольИсполнителя,
			|Исполнитель");
			
		Для Каждого ИсполнительПроектнойКоманды Из ПроектнаяЗадача.Исполнители Цикл
			Если (ЗначениеЗаполнено(РеквизитыЗадачи.Исполнитель)
					И РеквизитыЗадачи.Исполнитель = ИсполнительПроектнойКоманды.Исполнитель)
				Или (ЗначениеЗаполнено(РеквизитыЗадачи.РольИсполнителя)
					И РеквизитыЗадачи.РольИсполнителя = ИсполнительПроектнойКоманды.Исполнитель) Тогда
				
				ИсполнительНайден = Истина;
				Прервать;
				
			КонецЕсли;	
		КонецЦикла;	
		
	ИначеЕсли ЗначениеЗаполнено(Исполнитель) Тогда
		
		Для Каждого ИсполнительПроектнойКоманды Из ПроектнаяЗадача.Исполнители Цикл
			
			Если ТипЗнч(ИсполнительПроектнойКоманды.Исполнитель) = Тип("СправочникСсылка.ПолныеРоли") Тогда
				
				ИсполнителиРоли = РегистрыСведений.ИсполнителиЗадач.ИсполнителиРоли(
					ИсполнительПроектнойКоманды.Исполнитель);
				
				Для Каждого ИсполнительМассива Из ИсполнителиРоли Цикл
					Если ИсполнительМассива = Исполнитель Тогда
						ИсполнительНайден = Истина;
						Прервать;
					КонецЕсли;
				КонецЦикла;	
				
			ИначеЕсли Исполнитель = ИсполнительПроектнойКоманды.Исполнитель Тогда	
				
				ИсполнительНайден = Истина;
				
			КонецЕсли;	
			
			Если ИсполнительНайден Тогда
				Прервать;
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	Возврат ИсполнительНайден;
	
КонецФункции

// Выполняет поиск переданного пользователя среди исполнителей проектной задачи с учетом ролевой адресации
Функция НайтиИсполнителяВКомандеПроектаСУчетомВхожденияВРоль(
	Проект,
	Исполнитель,
	ЗадачаИсполнителя)
	
	ПроектнаяКоманда = Проект.ПроектнаяКоманда.Выгрузить();
	Если ПроектнаяКоманда.Количество() = 0 Тогда 
		Возврат Истина;
	КонецЕсли;	
	
	НоваяСтрока = ПроектнаяКоманда.Добавить();
	НоваяСтрока.Исполнитель = Проект.Руководитель;
	
	ИсполнительНайден = Ложь;
	Если ЗначениеЗаполнено(ЗадачаИсполнителя) И ТипЗнч(ЗадачаИсполнителя) = Тип("ЗадачаСсылка.ЗадачаИсполнителя") Тогда
		
		РеквизитыЗадачи = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ЗадачаИсполнителя, 
			"РольИсполнителя,
			|Исполнитель");
			
		Для Каждого ИсполнительПроектнойКоманды Из ПроектнаяКоманда Цикл
			Если (ЗначениеЗаполнено(РеквизитыЗадачи.Исполнитель)
					И РеквизитыЗадачи.Исполнитель = ИсполнительПроектнойКоманды.Исполнитель)
				Или (ЗначениеЗаполнено(РеквизитыЗадачи.РольИсполнителя)
					И РеквизитыЗадачи.РольИсполнителя = ИсполнительПроектнойКоманды.Исполнитель) Тогда
				
				ИсполнительНайден = Истина;
				Прервать;
				
			КонецЕсли;	
		КонецЦикла;	
		
	ИначеЕсли ЗначениеЗаполнено(Исполнитель) Тогда
		
		Для Каждого ИсполнительПроектнойКоманды Из ПроектнаяКоманда Цикл
			
			Если ТипЗнч(ИсполнительПроектнойКоманды.Исполнитель) = Тип("СправочникСсылка.ПолныеРоли") Тогда
				
				ИсполнителиРоли = РегистрыСведений.ИсполнителиЗадач.ИсполнителиРоли(
					ИсполнительПроектнойКоманды.Исполнитель);
				
				Для Каждого ИсполнительМассива Из ИсполнителиРоли Цикл
					Если ИсполнительМассива = Исполнитель Тогда
						ИсполнительНайден = Истина;
						Прервать;
					КонецЕсли;
				КонецЦикла;	
				
			ИначеЕсли Исполнитель = ИсполнительПроектнойКоманды.Исполнитель Тогда	
				
				ИсполнительНайден = Истина;
				
			КонецЕсли;	
			
			Если ИсполнительНайден Тогда
				Прервать;
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	Возврат ИсполнительНайден;
	
КонецФункции

// Поверяет возможность отнесения фактических трудозатрат на проектную задачу
Функция ПроверитьЗаписьОФактическихТрудозатратах(
	Проект,
	ПроектнаяЗадача,
	Источник,
	Пользователь,
	ФормируемоеСообщениеОбОшибке) Экспорт
	
	ФормируемоеСообщениеОбОшибке = "";
	Если ЗначениеЗаполнено(ПроектнаяЗадача) Тогда 
		
		ИсполнительНайден = НайтиИсполнителяВПроектнойЗадачеСУчетомВхожденияВРоль(
			ПроектнаяЗадача,
			Пользователь,
			Источник);
			
		Если Не ИсполнительНайден Тогда
			ФормируемоеСообщениеОбОшибке = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Нельзя списать трудозатраты исполнителя ""%1"", так как он не запланирован в проектной задаче ""%2""'; en = 'It is not possible to write off time spent by performer ""%1"" because he or she is not specified in project task ""%2""'"),
				Строка(Пользователь),
				Строка(ПроектнаяЗадача));	
		КонецЕсли;
		
	ИначеЕсли ЗначениеЗаполнено(Проект) Тогда 
		
		Если Не Проект.СписыватьЗатратыНаПроект Тогда 
			ФормируемоеСообщениеОбОшибке = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Нельзя списать трудозатраты исполнителя на проект ""%1"", так как для этого проекта запрещено списывать трудозатраты на проект в целом'; en = 'It is not possible to write off time spent by performer to project ""%1"" because writing off to the project as a whole is forbidden for the project'"),
				Строка(Проект));
		Иначе		
			ИсполнительНайден = НайтиИсполнителяВКомандеПроектаСУчетомВхожденияВРоль(
				Проект,
				Пользователь,
				Источник);	
				
			Если Не ИсполнительНайден Тогда
				ФормируемоеСообщениеОбОшибке = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Нельзя списать трудозатраты исполнителя ""%1"" на проект ""%2"", так как он не входит в проектную команду'; en = 'It is not possible to write off time spent by performer ""%1"" to project ""%2"" because he or she does not belong to the project team'"),
					Строка(Пользователь),
					Строка(Проект));
			КонецЕсли;	
				
		КонецЕсли;	
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ФормируемоеСообщениеОбОшибке) Тогда
		Возврат Ложь;
	Иначе
		Возврат Истина;
	КонецЕсли;		
		
КонецФункции

// Выполняет поиск переданного пользователя среди исполнителей проектной задачи.
//
Функция НайтиИсполнителяРольВПроектнойЗадаче(
	ПроектнаяЗадача,
	Исполнитель) Экспорт
	
	Если ПроектнаяЗадача.Исполнители.Количество() = 0 Тогда 
		Возврат Истина;
	КонецЕсли;
	
	Возврат ПроектнаяЗадача.Исполнители.Найти(Исполнитель, "Исполнитель") <> Неопределено;
	
КонецФункции

// Выполняет поиск переданного пользователя среди исполнителей проектной задачи с учетом ролевой адресации
Функция НайтиИсполнителяРольВКомандеПроекта(
	Проект,
	Исполнитель) Экспорт
	
	ПроектнаяКоманда = Проект.ПроектнаяКоманда.Выгрузить();
	Если ПроектнаяКоманда.Количество() = 0 Тогда 
		Возврат Истина;
	КонецЕсли;
	
	НоваяСтрока = ПроектнаяКоманда.Добавить();
	НоваяСтрока.Исполнитель = Проект.Руководитель;
	
	Возврат ПроектнаяКоманда.Найти(Исполнитель, "Исполнитель") <> Неопределено;
	
КонецФункции

// Проверяет возможность старта бизнес-процесса по проекту и проектной задаче
Процедура ПроверитьВозможностьСтартаБизнесПроцесса(Знач Объект, ВозвращаемыйМассивОшибок, ЕстьПравоЗапуска) Экспорт 
	
	ВозвращаемыйМассивОшибок = Новый Массив;
	
	Если ЗначениеЗаполнено(Объект.ПроектнаяЗадача) Тогда 
		
		Если Не ЗначениеЗаполнено(Объект.ГлавнаяЗадача)
			И Не ДокументооборотПраваДоступа.ПолучитьПраваПоОбъекту(Объект.Проект).Изменение Тогда 
			ТекстОшибки = СтрШаблон(НСтр("ru = 'У вас нет прав на старт основного процесса по проектной задаче ""%1""'; en = 'Insufficient permissions to start main process of the project task ""%1""'"),
				Строка(Объект.ПроектнаяЗадача));
			ВозвращаемыйМассивОшибок.Добавить(ТекстОшибки);
			ЕстьПравоЗапуска = Ложь;
			Возврат;
		КонецЕсли;
		
		Если Не РаботаСПроектами.НаступилаДатаНачалаЗадачи(Объект.ПроектнаяЗадача) Тогда 
			ТекстОшибки = НСтр("ru = 'Дата начала процесса меньше, чем дата начала проектной задачи. Выполнить запуск процесса?'; en = 'Process start date is less than that of the project task. Start the process?'");
			ВозвращаемыйМассивОшибок.Добавить(ТекстОшибки);
		КонецЕсли;
			
		Если РаботаСПроектами.ЕстьНезавершенныеПредшественники(Объект.ПроектнаяЗадача) Тогда 
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'У проектной задачи ""%1"" есть незавершенные предшественники. Выполнить запуск процесса?'; en = 'Project task ""%1"" has unfinished predecessors. Start the process?'"),
				Строка(Объект.ПроектнаяЗадача));
			ВозвращаемыйМассивОшибок.Добавить(ТекстОшибки);
		КонецЕсли;
		
		ЗадатьВопрос = Ложь;
		ДанныеПроектнойЗадачи = РаботаСПроектами.ПолучитьСрокиПроектнойЗадачи(Объект.ПроектнаяЗадача);
		ПлановаяДатаОкончания = ДанныеПроектнойЗадачи.ТекущийПланОкончание;
		Если ЗначениеЗаполнено(ПлановаяДатаОкончания) Тогда
			
			Если Объект.Ссылка.Метаданные().Реквизиты.Найти("СрокИсполненияПроцесса") <> Неопределено Тогда 
				ЗадатьВопрос = (Объект.СрокИсполненияПроцесса > ПлановаяДатаОкончания);
			КонецЕсли;
			
			Если ЗадатьВопрос Тогда
				ТекстОшибки = НСтр("ru = 'Срок исполнения процесса превышает плановую дату окончания проектной задачи. Продолжить?'; en = 'Process due date exceeds the planned finish date of the project task. Continue?'");
				ВозвращаемыйМассивОшибок.Добавить(ТекстОшибки);
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;	
		
	Если ЗначениеЗаполнено(Объект.Проект) Тогда 
		
		Если Не РаботаСПроектами.НаступилаДатаНачалаПроекта(Объект.Проект) Тогда 
			ТекстОшибки = НСтр("ru = 'Дата начала процесса меньше, чем дата начала проекта. Выполнить запуск процесса?'; en = 'Process start date is less than that of the project. Start the process?'");
			ВозвращаемыйМассивОшибок.Добавить(ТекстОшибки);
		КонецЕсли;
		
		ЗадатьВопрос = Ложь;
		ПлановаяДатаОкончания = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.Проект, "ТекущийПланОкончание");
		Если ЗначениеЗаполнено(ПлановаяДатаОкончания) Тогда
			
			Если Объект.Ссылка.Метаданные().Реквизиты.Найти("СрокИсполненияПроцесса") <> Неопределено Тогда 
				ЗадатьВопрос = (Объект.СрокИсполненияПроцесса > ПлановаяДатаОкончания);
			КонецЕсли;
			
			Если ЗадатьВопрос Тогда
				ТекстОшибки = НСтр("ru = 'Срок исполнения процесса превышает плановую дату окончания проекта. Продолжить?'; en = 'Process due date exceeds the planned finish date of the project. Continue?'");
				ВозвращаемыйМассивОшибок.Добавить(ТекстОшибки);
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

// Возвращает список значений для выбора задачи проекта
Функция СформироватьДанныеВыбораЗадачиПроекта(Текст, Проект) Экспорт 
	
	ДанныеВыбора = Новый СписокЗначений;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ПроектныеЗадачи.Ссылка КАК Ссылка,
	|	ПроектныеЗадачи.КодСДР,
	|	ПроектныеЗадачи.Наименование
	|ИЗ
	|	Справочник.ПроектныеЗадачи КАК ПроектныеЗадачи
	|ГДЕ
	|	ПроектныеЗадачи.Владелец = &Проект
	|	И ПроектныеЗадачи.Наименование ПОДОБНО &Текст
	|	И НЕ ПроектныеЗадачи.ПометкаУдаления
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	ПроектныеЗадачи.Ссылка,
	|	ПроектныеЗадачи.КодСДР,
	|	ПроектныеЗадачи.Наименование
	|ИЗ
	|	Справочник.ПроектныеЗадачи КАК ПроектныеЗадачи
	|ГДЕ
	|	ПроектныеЗадачи.Владелец = &Проект
	|	И ПроектныеЗадачи.КодСДР ПОДОБНО &Текст
	|	И НЕ ПроектныеЗадачи.ПометкаУдаления";
	
	Запрос.УстановитьПараметр("Проект", Проект);
	Запрос.УстановитьПараметр("Текст", Текст+"%");
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ДанныеВыбора.Добавить(Выборка.Ссылка, Выборка.КодСДР + " " + Выборка.Наименование);
	КонецЦикла;	
	
	Возврат ДанныеВыбора;
	
КонецФункции

// Возвращает список значений для выбора исполнителя из проектной команды
Функция СформироватьДанныеВыбораИсполнителя(Проект, Текст = "") Экспорт 
	
	ДанныеВыбора = Новый СписокЗначений;
	
	ПроектнаяКоманда = Проект.ПроектнаяКоманда.Выгрузить();
	Если ПроектнаяКоманда.Найти(Проект.Руководитель, "Исполнитель") = Неопределено Тогда 
		НоваяСтрока = ПроектнаяКоманда.Добавить();
		НоваяСтрока.Исполнитель = Проект.Руководитель;
	КонецЕсли;
	
	Для Каждого Строка Из ПроектнаяКоманда Цикл
		Если Не ЗначениеЗаполнено(Строка.Исполнитель) Тогда 
			Продолжить;
		КонецЕсли;
		
		Если Текст <> "" И Нрег(Лев(Строка.Исполнитель, СтрДлина(Текст))) <> НРег(Текст) Тогда 
			Продолжить;
		КонецЕсли;
		
		ДанныеВыбора.Добавить(Строка.Исполнитель, Строка(Строка.Исполнитель));
		
	КонецЦикла;
	
	Возврат ДанныеВыбора;
	
КонецФункции

// Возвращает Истина, если проект имеет проектные задачи
Функция ЕстьПроектныеЗадачи(Проект) Экспорт 
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ИСТИНА
	|ИЗ
	|	Справочник.ПроектныеЗадачи КАК ПроектныеЗадачи
	|ГДЕ
	|	ПроектныеЗадачи.Владелец = &Проект
	|	И НЕ ПроектныеЗадачи.ПометкаУдаления";
	Запрос.УстановитьПараметр("Проект", Проект);
	
	Возврат Не Запрос.Выполнить().Пустой();
	
КонецФункции

// Возвращает Истина, если проект имеет незавершенные проектные задачи
Функция ЕстьНезавершенныеПроектныеЗадачи(Проект) Экспорт 
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ИСТИНА
	|ИЗ
	|	Справочник.ПроектныеЗадачи КАК ПроектныеЗадачи
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СрокиПроектныхЗадач КАК СрокиПроектныхЗадач
	|		ПО ПроектныеЗадачи.Ссылка = СрокиПроектныхЗадач.ПроектнаяЗадача
	|ГДЕ
	|	ПроектныеЗадачи.Владелец = &Проект
	|	И НЕ ПроектныеЗадачи.ПометкаУдаления
	|	И СрокиПроектныхЗадач.ОкончаниеФакт = ДАТАВРЕМЯ(1, 1, 1)";
	Запрос.УстановитьПараметр("Проект", Проект);
	
	Возврат Не Запрос.Выполнить().Пустой();
	
КонецФункции

// Возвращает список значений для выбора проекта \ задачи проекта
Функция СформироватьДанныеВыбораПроектаЗадачи(Текст) Экспорт 
	
	ДанныеВыбора = Новый СписокЗначений;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Проекты.Ссылка КАК Проект,
	|	НЕОПРЕДЕЛЕНО КАК ПроектнаяЗадача,
	|	ЕСТЬNULL(ПроектныеЗадачи.ЕстьПроектныеЗадачи, ЛОЖЬ) КАК ЕстьПроектныеЗадачи
	|ИЗ
	|	Справочник.Проекты КАК Проекты
	|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			ИСТИНА КАК ЕстьПроектныеЗадачи,
	|			ПроектныеЗадачи.Владелец КАК Владелец
	|		ИЗ
	|			Справочник.ПроектныеЗадачи КАК ПроектныеЗадачи
	|		ГДЕ
	|			НЕ ПроектныеЗадачи.ПометкаУдаления
	|		
	|		СГРУППИРОВАТЬ ПО
	|			ПроектныеЗадачи.Владелец) КАК ПроектныеЗадачи
	|		ПО Проекты.Ссылка = ПроектныеЗадачи.Владелец
	|ГДЕ
	|	Проекты.Наименование ПОДОБНО &Текст
	|	И НЕ Проекты.ЭтоГруппа
	|	И НЕ Проекты.ПометкаУдаления
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ПроектныеЗадачи.Владелец,
	|	ПроектныеЗадачи.Ссылка,
	|	ИСТИНА
	|ИЗ
	|	Справочник.ПроектныеЗадачи КАК ПроектныеЗадачи
	|ГДЕ
	|	ПроектныеЗадачи.Наименование ПОДОБНО &Текст
	|	И НЕ ПроектныеЗадачи.ПометкаУдаления";
	Запрос.УстановитьПараметр("Текст", Текст + "%");
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Значение = Новый Структура("Проект, ПроектнаяЗадача, ЕстьПроектныеЗадачи", Выборка.Проект, Выборка.ПроектнаяЗадача, Выборка.ЕстьПроектныеЗадачи);
		Представление = РаботаСПроектамиКлиентСервер.ПредставлениеПроектаЗадачи(Выборка.Проект, Выборка.ПроектнаяЗадача);
		
		ДанныеВыбора.Добавить(Значение, Представление);
	КонецЦикла;	
	
	Возврат ДанныеВыбора;
	
КонецФункции	

// Возвращает вид проекта по умолчанию
Функция ПолучитьВидПроектаПоУмолчанию() Экспорт
	
	ВидПроектаПоУмолчанию = Неопределено;
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьВидыПроектов") Тогда 
		Возврат ВидПроектаПоУмолчанию;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ВидыПроектов.Ссылка
	|ИЗ
	|	Справочник.ВидыПроектов КАК ВидыПроектов
	|ГДЕ
	|	НЕ ВидыПроектов.ПометкаУдаления";
	
	ВидыПроектов = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
	Если ВидыПроектов.Количество() = 1 Тогда
    	ВидПроектаПоУмолчанию = ВидыПроектов[0];
		Возврат ВидПроектаПоУмолчанию;
	КонецЕсли;
	
	ВидПроектаПоУмолчанию = ХранилищеОбщихНастроек.Загрузить(
		"НастройкиРаботыСПроектами", 
		"ВидПроектаПоУмолчанию");
		
	Возврат ВидПроектаПоУмолчанию;
			
КонецФункции

// Возвращает количество проектов с пустым видом
Функция КоличествоПроектовСПустымВидом() Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	КОЛИЧЕСТВО(*) КАК Количество
	|ИЗ
	|	Справочник.Проекты КАК Проекты
	|ГДЕ
	|	Проекты.ВидПроекта = ЗНАЧЕНИЕ(Справочник.ВидыПроектов.ПустаяСсылка)";
	
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		Возврат 0;
	КонецЕсли;	
	
	Выборка = Результат.Выбрать();
	Выборка.Следующий();
	Возврат Выборка.Количество;
	
КонецФункции

// Создает вид проекта с заданным наименованием.
// Возвращает ссылку на созданный вид проекта.
Функция СоздатьВидПроекта(Наименование) Экспорт
	
	ВидПроекта = Справочники.ВидыПроектов.СоздатьЭлемент();
	ВидПроекта.Наименование = Наименование;
	ВидПроекта.Записать();
	
	Возврат ВидПроекта.Ссылка;
	
КонецФункции

// Заполняет вид проекта
Процедура ЗаполнитьВидПроекта(ВидПроекта) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	НачатьТранзакцию();
	Попытка 
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	Проекты.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.Проекты КАК Проекты
		|ГДЕ
		|	Проекты.ВидПроекта = ЗНАЧЕНИЕ(Справочник.ВидыПроектов.ПустаяСсылка)";
		
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			ПроектОбъект = Выборка.Ссылка.ПолучитьОбъект();
			ПроектОбъект.Заблокировать();
			ПроектОбъект.ВидПроекта = ВидПроекта;
			ПроектОбъект.Записать();
		КонецЦикла;	
		
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		ВызватьИсключение;
	КонецПопытки;
	
КонецПроцедуры

// Создает структуру данных о проектной задаче,
// которая используется в различных функциях.
//
// Возвращаемое значение:
//     Структура - структура данных о проектной задачи
//
Функция СоздатьСтруктуруПроектнойЗадачи()
	
	ДанныеПроектнойЗадачи = Новый Структура(
	"Модифицированность,
	|Ссылка, Родитель, Владелец,
	|Проект, НачалоПроекта, ОкончаниеПроекта,
	|СуммарнаяЗадача, Веха,
	|СпособПланирования, ГрафикРаботы,
	|ТипОграничения, ДатаОграничения,
	|АвтоматическиРассчитыватьПланПроекта, РедактируетсяВСтороннейСистеме,
	|ТекущийПланНачало, ТекущийПланОкончание, ТекущийПланДлительность, ТекущийПланЕдиницаДлительности,
	|НачалоФакт, ОкончаниеФакт, ДлительностьФакт, ЕдиницаДлительностиФакт,
	|ВсеРодительскиеЗадачи,
	|ПодчиненныеЗадачи, ВсеПодчиненныеЗадачи,
	|ВсеПредшественники, ВсеПредшественникиВключаяНачалоПроекта,
	|ВсеПоследователи, ВсеПоследователиВключаяОкончаниеПроекта");
	
	ДанныеПроектнойЗадачи.Модифицированность = Ложь;
	ДанныеПроектнойЗадачи.ПодчиненныеЗадачи = Новый Массив;
	ДанныеПроектнойЗадачи.ВсеПодчиненныеЗадачи = Новый Массив;
	ДанныеПроектнойЗадачи.ВсеРодительскиеЗадачи = Новый Массив;
	ДанныеПроектнойЗадачи.ВсеПредшественники = Новый Массив;
	ДанныеПроектнойЗадачи.ВсеПредшественникиВключаяНачалоПроекта = Новый Массив;
	ДанныеПроектнойЗадачи.ВсеПоследователи = Новый Массив;
	ДанныеПроектнойЗадачи.ВсеПоследователиВключаяОкончаниеПроекта = Новый Массив;
	
	Возврат ДанныеПроектнойЗадачи;
	
КонецФункции	

// Получает данные всех задач проекта в виде соответствия структур,
// которое используется для расчета плана проекта.
//
// Параметры:
//     Проект - СправочникСсылка.Проекты - переданный проект.
//
// Возвращаемое значение:
//     Соответствие - соответствие структур данных о проектных задачах.
// 
Функция ПолучитьДанныеПроекта(Проект)

	УстановитьПривилегированныйРежим(Истина);
	
	ОсновнойГрафикРаботы = Константы.ОсновнойГрафикРаботы.Получить();
	
	СоответствиеДанных = Новый Соответствие;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПроектныеЗадачи.Ссылка,
	|	ПроектныеЗадачи.ПометкаУдаления,
	|	ПроектныеЗадачи.Владелец КАК Проект,
	|	ПроектныеЗадачи.Владелец.ТекущийПланНачало КАК НачалоПроекта,
	|	ПроектныеЗадачи.Владелец.ТекущийПланОкончание КАК ОкончаниеПроекта,
	|	ПроектныеЗадачи.Родитель,
	|	ПроектныеЗадачи.СуммарнаяЗадача,
	|	ПроектныеЗадачи.Веха,
	|	ПроектныеЗадачи.Владелец,
	|	ПроектныеЗадачи.ТипОграничения,
	|	ПроектныеЗадачи.ДатаОграничения,
	|	ВЫБОР
	|		КОГДА ПроектныеЗадачи.ГрафикРаботы <> ЗНАЧЕНИЕ(Справочник.ГрафикиРаботы.ПустаяСсылка)
	|			ТОГДА ПроектныеЗадачи.ГрафикРаботы
	|		ИНАЧЕ ПроектныеЗадачи.Владелец.ГрафикРаботы
	|	КОНЕЦ КАК ГрафикРаботы,
	|	ПроектныеЗадачи.Владелец.АвтоматическиРассчитыватьПланПроекта КАК АвтоматическиРассчитыватьПланПроекта,
	|	ПроектныеЗадачи.Владелец.РедактируетсяВСтороннейСистеме КАК РедактируетсяВСтороннейСистеме,
	|	ПроектныеЗадачи.Владелец.СпособПланирования КАК СпособПланирования,
	|	СрокиПроектныхЗадач.ТекущийПланНачало,
	|	СрокиПроектныхЗадач.ТекущийПланОкончание,
	|	СрокиПроектныхЗадач.ТекущийПланДлительность,
	|	СрокиПроектныхЗадач.ТекущийПланЕдиницаДлительности,
	|	СрокиПроектныхЗадач.НачалоФакт,
	|	СрокиПроектныхЗадач.ОкончаниеФакт,
	|	СрокиПроектныхЗадач.ДлительностьФакт,
	|	СрокиПроектныхЗадач.ЕдиницаДлительностиФакт
	|ИЗ
	|	Справочник.ПроектныеЗадачи КАК ПроектныеЗадачи
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СрокиПроектныхЗадач КАК СрокиПроектныхЗадач
	|		ПО ПроектныеЗадачи.Ссылка = СрокиПроектныхЗадач.ПроектнаяЗадача
	|ГДЕ
	|	ПроектныеЗадачи.Владелец = &Проект
	|	И НЕ ПроектныеЗадачи.ПометкаУдаления
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПроектныеЗадачи.Ссылка,
	|	ПроектныеЗадачи.Родитель
	|ИЗ
	|	Справочник.ПроектныеЗадачи КАК ПроектныеЗадачи
	|ГДЕ
	|	ПроектныеЗадачи.Владелец = &Проект
	|	И НЕ ПроектныеЗадачи.ПометкаУдаления
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ПроектныеЗадачиПредшественники.Ссылка КАК Ссылка,
	|	ПроектныеЗадачиПредшественники.ТипЗависимости,
	|	ПроектныеЗадачиПредшественники.Задержка,
	|	ПроектныеЗадачиПредшественники.ЕдиницаЗадержки,
	|	ПроектныеЗадачиПредшественники.Предшественник КАК Предшественник
	|ИЗ
	|	Справочник.ПроектныеЗадачи КАК ПроектныеЗадачи
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ПроектныеЗадачи.Предшественники КАК ПроектныеЗадачиПредшественники
	|		ПО ПроектныеЗадачи.Ссылка = ПроектныеЗадачиПредшественники.Ссылка
	|ГДЕ
	|	ПроектныеЗадачи.Владелец = &Проект
	|	И НЕ ПроектныеЗадачи.ПометкаУдаления
	|	И НЕ ПроектныеЗадачиПредшественники.Предшественник.ПометкаУдаления";
	
	Запрос.УстановитьПараметр("Проект", Проект);
	Результаты = Запрос.ВыполнитьПакет();
	
	Результат0 = Результаты[0].Выгрузить();
	Для Каждого Строка Из Результат0 Цикл
		ДанныеПроектнойЗадачи = СоздатьСтруктуруПроектнойЗадачи();
		ЗаполнитьЗначенияСвойств(ДанныеПроектнойЗадачи, Строка);
		
		Если Не ЗначениеЗаполнено(ДанныеПроектнойЗадачи.ГрафикРаботы) Тогда 
			ДанныеПроектнойЗадачи.ГрафикРаботы = ОсновнойГрафикРаботы;
		КонецЕсли;	
		
		СоответствиеДанных.Вставить(ДанныеПроектнойЗадачи.Ссылка, ДанныеПроектнойЗадачи);
	КонецЦикла;	
	
	Результат1 = Результаты[1].Выгрузить();	
	Для Каждого Строка Из Результат1 Цикл
		Если Не ЗначениеЗаполнено(Строка.Родитель) Тогда 
			Продолжить;
		КонецЕсли;	
		
		ДанныеЗадачи = СоответствиеДанных.Получить(Строка.Ссылка);
		
		// ПодчиненныеЗадачи
		ДанныеРодителя = СоответствиеДанных.Получить(Строка.Родитель);
		ДанныеРодителя.ПодчиненныеЗадачи.Добавить(Строка.Ссылка);
		
		// ВсеПодчиненныеЗадачи
		ТекущийРодитель = Строка.Родитель;
		Пока ЗначениеЗаполнено(ТекущийРодитель) Цикл
			ДанныеРодителя = СоответствиеДанных.Получить(ТекущийРодитель);
			ДанныеРодителя.ВсеПодчиненныеЗадачи.Добавить(Строка.Ссылка);
			ТекущийРодитель = ДанныеРодителя.Родитель;
		КонецЦикла;	
		
		// ВсеРодительскиеЗадачи
		ТекущийРодитель = Строка.Родитель;
		Пока ЗначениеЗаполнено(ТекущийРодитель) Цикл
			ДанныеЗадачи.ВсеРодительскиеЗадачи.Добавить(ТекущийРодитель);
			ДанныеРодителя = СоответствиеДанных.Получить(ТекущийРодитель);
			ТекущийРодитель = ДанныеРодителя.Родитель;
		КонецЦикла;	
	КонецЦикла;	
	
	
	Результат2 = Результаты[2].Выгрузить();	
	
	// Предшественники
	Для Каждого Строка Из Результат0 Цикл
		ДанныеЗадачи = СоответствиеДанных.Получить(Строка.Ссылка);
		
		СписокЗадач = Новый Массив;
		СписокЗадач.Добавить(Строка.Ссылка);
		Для Каждого РодительскаяЗадача Из ДанныеЗадачи.ВсеРодительскиеЗадачи Цикл
			СписокЗадач.Добавить(РодительскаяЗадача);
		КонецЦикла;	
		
		// ВсеПредшественники
		Для Каждого Задача Из СписокЗадач Цикл
			
			НайденныеСтроки = Результат2.НайтиСтроки(Новый Структура("Ссылка", Задача));
			Для Каждого НайденнаяСтрока Из НайденныеСтроки Цикл
				
				ДанныеПредшественника = СоответствиеДанных.Получить(НайденнаяСтрока.Предшественник);
				Если ДанныеПредшественника.СуммарнаяЗадача Тогда 
					Для Каждого ПодчиненнаяЗадача Из ДанныеПредшественника.ВсеПодчиненныеЗадачи Цикл
						ДанныеПодчиненнойЗадачи = СоответствиеДанных.Получить(ПодчиненнаяЗадача);
						Если ДанныеПодчиненнойЗадачи.СуммарнаяЗадача Тогда 
							Продолжить;
						КонецЕсли;	
						
						Данные = Новый Структура;
						Данные.Вставить("Ссылка",  		 		Задача);
						Данные.Вставить("Предшественник",  		ПодчиненнаяЗадача);
						Данные.Вставить("ТипЗависимости",  		НайденнаяСтрока.ТипЗависимости);
						Данные.Вставить("Задержка", 			НайденнаяСтрока.Задержка);
						Данные.Вставить("ЕдиницаЗадержки", 		НайденнаяСтрока.ЕдиницаЗадержки);
		
						ДанныеЗадачи.ВсеПредшественники.Добавить(Данные);
					КонецЦикла;	
				Иначе	
					Данные = Новый Структура;
					Данные.Вставить("Ссылка",  		  		Задача);
					Данные.Вставить("Предшественник",  		НайденнаяСтрока.Предшественник);
					Данные.Вставить("ТипЗависимости",  		НайденнаяСтрока.ТипЗависимости);
					Данные.Вставить("Задержка", 		  	НайденнаяСтрока.Задержка);
					Данные.Вставить("ЕдиницаЗадержки", 		НайденнаяСтрока.ЕдиницаЗадержки);
		
					ДанныеЗадачи.ВсеПредшественники.Добавить(Данные);
				КонецЕсли;
				
			КонецЦикла;
		КонецЦикла;	
		
		// ВсеПредшественникиВключаяНачалоПроекта
		Для Каждого Строка Из ДанныеЗадачи.ВсеПредшественники Цикл
			ДанныеЗадачи.ВсеПредшественникиВключаяНачалоПроекта.Добавить(Строка);
		КонецЦикла;	
		
		Для Каждого Задача Из СписокЗадач Цикл
			НайденныеСтроки = Результат2.НайтиСтроки(Новый Структура("Ссылка", Задача));
			Если НайденныеСтроки.Количество() = 0 Тогда 
				Данные = Новый Структура;
				Данные.Вставить("Ссылка",  		  		Задача);
				Данные.Вставить("Предшественник",  		);
				Данные.Вставить("ТипЗависимости",  		Перечисления.ТипыЗависимостейПроектныхЗадач.ОкончаниеНачало);
				Данные.Вставить("Задержка", 		  	0);
				Данные.Вставить("ЕдиницаЗадержки", 		);
				Данные.Вставить("ТекущийПланНачало", 	ДанныеЗадачи.НачалоПроекта);
				Данные.Вставить("ТекущийПланОкончание", ДанныеЗадачи.НачалоПроекта);
				
				ДанныеЗадачи.ВсеПредшественникиВключаяНачалоПроекта.Добавить(Данные);
			КонецЕсли;
		КонецЦикла;	
		
	КонецЦикла;	
	
	
	// Последователи
	Для Каждого Строка Из Результат0 Цикл
		ДанныеЗадачи = СоответствиеДанных.Получить(Строка.Ссылка);
		
		СписокЗадач = Новый Массив;
		СписокЗадач.Добавить(Строка.Ссылка);
		Для Каждого РодительскаяЗадача Из ДанныеЗадачи.ВсеРодительскиеЗадачи Цикл
			СписокЗадач.Добавить(РодительскаяЗадача);
		КонецЦикла;	
		
		// ВсеПоследователи
		Для Каждого Задача Из СписокЗадач Цикл
			
			НайденныеСтроки = Результат2.НайтиСтроки(Новый Структура("Предшественник", Задача));
			Для Каждого НайденнаяСтрока Из НайденныеСтроки Цикл
				
				ДанныеПоследователя = СоответствиеДанных.Получить(НайденнаяСтрока.Ссылка);
				Если ДанныеПоследователя.СуммарнаяЗадача Тогда 
					Для Каждого ПодчиненнаяЗадача Из ДанныеПоследователя.ВсеПодчиненныеЗадачи Цикл
						ДанныеПодчиненнойЗадачи = СоответствиеДанных.Получить(ПодчиненнаяЗадача);
						Если ДанныеПодчиненнойЗадачи.СуммарнаяЗадача Тогда 
							Продолжить;
						КонецЕсли;	
						
						Данные = Новый Структура;
						Данные.Вставить("Ссылка",  				ПодчиненнаяЗадача);
						Данные.Вставить("ТипЗависимости",  		НайденнаяСтрока.ТипЗависимости);
						Данные.Вставить("Задержка", 			НайденнаяСтрока.Задержка);
						Данные.Вставить("ЕдиницаЗадержки", 		НайденнаяСтрока.ЕдиницаЗадержки);
		
						ДанныеЗадачи.ВсеПоследователи.Добавить(Данные);
					КонецЦикла;	
				Иначе	
					Данные = Новый Структура;
					Данные.Вставить("Ссылка",  				НайденнаяСтрока.Ссылка);
					Данные.Вставить("ТипЗависимости",  		НайденнаяСтрока.ТипЗависимости);
					Данные.Вставить("Задержка", 		  	НайденнаяСтрока.Задержка);
					Данные.Вставить("ЕдиницаЗадержки", 		НайденнаяСтрока.ЕдиницаЗадержки);
		
					ДанныеЗадачи.ВсеПоследователи.Добавить(Данные);
				КонецЕсли;
				
			КонецЦикла;
		КонецЦикла;	
		
		// ВсеПоследователиВключаяНачалоПроекта
		Для Каждого Строка Из ДанныеЗадачи.ВсеПоследователи Цикл
			ДанныеЗадачи.ВсеПоследователиВключаяОкончаниеПроекта.Добавить(Строка);
		КонецЦикла;	
		
		Если ДанныеЗадачи.ВсеПоследователи.Количество() = 0 Тогда 
			Данные = Новый Структура;
			Данные.Вставить("Ссылка",  		  		ДанныеЗадачи.Ссылка);
			Данные.Вставить("ТипЗависимости",  		Перечисления.ТипыЗависимостейПроектныхЗадач.ОкончаниеНачало);
			Данные.Вставить("Задержка", 		  	0);
			Данные.Вставить("ЕдиницаЗадержки", 		);
			Данные.Вставить("ТекущийПланНачало", 	ДанныеЗадачи.ОкончаниеПроекта);
			Данные.Вставить("ТекущийПланОкончание", ДанныеЗадачи.ОкончаниеПроекта);
			
			ДанныеЗадачи.ВсеПоследователиВключаяОкончаниеПроекта.Добавить(Данные);
		КонецЕсли;	
		
	КонецЦикла;	
	
	Возврат СоответствиеДанных;
	
КонецФункции	

// Возвращает соответствие ссылок и структур с данными проектных задач, 
// необходимых для расчета плана проекта
//
// Параметры:
//  ПроектныеЗадачи - Массив - массив ссылок на проектные задачи.
//
// Возвращаемое значение:
//  Соответствие - соответствие ссылок и структур с данными проектных задач.
//
Функция ПолучитьДанныеПроектныхЗадач(ПроектныеЗадачи) Экспорт 
	
	УстановитьПривилегированныйРежим(Истина);
	
	СоответствиеДанных = Новый Соответствие;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПроектныеЗадачи.Ссылка,
	|	ПроектныеЗадачи.Владелец КАК Проект,
	|	ПроектныеЗадачи.Родитель,
	|	ПроектныеЗадачи.СуммарнаяЗадача,
	|	ПроектныеЗадачи.Веха,
	|	ПроектныеЗадачи.Владелец,
	|	ПроектныеЗадачи.Наименование,
	|	ПроектныеЗадачи.КодСДР,
	|	ПроектныеЗадачи.ВерсияДанных,
	|	ПроектныеЗадачи.ПометкаУдаления,
	|	ПроектныеЗадачи.ТекущийПланЕдиницаТрудозатрат,
	|	ПроектныеЗадачи.ТекущийПланТрудозатраты,
	|	ПроектныеЗадачи.ЕдиницаТрудозатратФакт,
	|	ПроектныеЗадачи.Исполнители.(
	|		Исполнитель,
	|		ТекущийПланТрудозатраты
	|	),
	|	ПроектныеЗадачи.Предшественники.(
	|		Предшественник,
	|		ТипЗависимости,
	|		Задержка,
	|		ЕдиницаЗадержки
	|	),
	|	ПроектныеЗадачи.ТипОграничения,
	|	ПроектныеЗадачи.ДатаОграничения,
	|	ВЫБОР
	|		КОГДА ПроектныеЗадачи.ГрафикРаботы <> ЗНАЧЕНИЕ(Справочник.ГрафикиРаботы.ПустаяСсылка)
	|			ТОГДА ПроектныеЗадачи.ГрафикРаботы
	|		ИНАЧЕ ПроектныеЗадачи.Владелец.ГрафикРаботы
	|	КОНЕЦ КАК ГрафикРаботы,
	|	ПроектныеЗадачи.Владелец.АвтоматическиРассчитыватьПланПроекта КАК АвтоматическиРассчитыватьПланПроекта,
	|	ПроектныеЗадачи.Владелец.РедактируетсяВСтороннейСистеме КАК РедактируетсяВСтороннейСистеме,
	|	ПроектныеЗадачи.Владелец.СпособПланирования КАК СпособПланирования,
	|	ЕСТЬNULL(СрокиПроектныхЗадач.ТекущийПланНачало, ДАТАВРЕМЯ(1, 1, 1)) КАК ТекущийПланНачало,
	|	ЕСТЬNULL(СрокиПроектныхЗадач.ТекущийПланОкончание, ДАТАВРЕМЯ(1, 1, 1)) КАК ТекущийПланОкончание,
	|	ЕСТЬNULL(СрокиПроектныхЗадач.ТекущийПланДлительность, 0) КАК ТекущийПланДлительность,
	|	ЕСТЬNULL(СрокиПроектныхЗадач.ТекущийПланЕдиницаДлительности, ЗНАЧЕНИЕ(Перечисление.ЕдиницыДлительности.ПустаяСсылка)) КАК ТекущийПланЕдиницаДлительности,
	|	ЕСТЬNULL(СрокиПроектныхЗадач.НачалоФакт, ДАТАВРЕМЯ(1, 1, 1)) КАК НачалоФакт,
	|	ЕСТЬNULL(СрокиПроектныхЗадач.ОкончаниеФакт, ДАТАВРЕМЯ(1, 1, 1)) КАК ОкончаниеФакт,
	|	ЕСТЬNULL(СрокиПроектныхЗадач.ДлительностьФакт, 0) КАК ДлительностьФакт,
	|	ЕСТЬNULL(СрокиПроектныхЗадач.ЕдиницаДлительностиФакт, ЗНАЧЕНИЕ(Перечисление.ЕдиницыДлительности.ПустаяСсылка)) КАК ЕдиницаДлительностиФакт,
	|	ВЫБОР
	|		КОГДА КешИнформацииОбОбъектах.СостояниеКонтроля = ЗНАЧЕНИЕ(Перечисление.СостоянияКонтроля.НаКонтроле)
	|			ТОГДА ВЫБОР
	|					КОГДА КешИнформацииОбОбъектах.СрокИсполнения = ДАТАВРЕМЯ(1, 1, 1)
	|							ИЛИ КешИнформацииОбОбъектах.СрокИсполнения >= &ТекущаяДата
	|						ТОГДА 1
	|					ИНАЧЕ 3
	|				КОНЕЦ
	|		КОГДА КешИнформацииОбОбъектах.СостояниеКонтроля = ЗНАЧЕНИЕ(Перечисление.СостоянияКонтроля.СнятСКонтроля)
	|			ТОГДА 4
	|		КОГДА КешИнформацииОбОбъектах.СостояниеКонтроля = ЗНАЧЕНИЕ(Перечисление.СостоянияКонтроля.НаКонтролеНесколько)
	|			ТОГДА ВЫБОР
	|					КОГДА КешИнформацииОбОбъектах.СрокИсполнения = ДАТАВРЕМЯ(1, 1, 1)
	|							ИЛИ КешИнформацииОбОбъектах.СрокИсполнения >= &ТекущаяДата
	|						ТОГДА 5
	|					КОГДА КешИнформацииОбОбъектах.СрокИсполненияОбщий = ДАТАВРЕМЯ(1, 1, 1)
	|							ИЛИ КешИнформацииОбОбъектах.СрокИсполненияОбщий >= &ТекущаяДата
	|						ТОГДА 8
	|					ИНАЧЕ 6
	|				КОНЕЦ
	|		КОГДА КешИнформацииОбОбъектах.СостояниеКонтроля = ЗНАЧЕНИЕ(Перечисление.СостоянияКонтроля.СнятСКонтроляНесколько)
	|			ТОГДА 7
	|		КОГДА КешИнформацииОбОбъектах.СостояниеКонтроля = ЗНАЧЕНИЕ(Перечисление.СостоянияКонтроля.СмешанноНесколько)
	|			ТОГДА ВЫБОР
	|					КОГДА КешИнформацииОбОбъектах.СрокИсполнения = ДАТАВРЕМЯ(1, 1, 1)
	|							ИЛИ КешИнформацииОбОбъектах.СрокИсполнения >= &ТекущаяДата
	|						ТОГДА 9
	|					ИНАЧЕ 10
	|				КОНЕЦ
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СостояниеКонтроля
	|ИЗ
	|	Справочник.ПроектныеЗадачи КАК ПроектныеЗадачи
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СрокиПроектныхЗадач КАК СрокиПроектныхЗадач
	|		ПО ПроектныеЗадачи.Ссылка = СрокиПроектныхЗадач.ПроектнаяЗадача
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КешИнформацииОбОбъектах КАК КешИнформацииОбОбъектах
	|		ПО ПроектныеЗадачи.Ссылка = КешИнформацииОбОбъектах.Объект
	|ГДЕ
	|	ПроектныеЗадачи.Ссылка В(&ПроектныеЗадачи)";
	
	Запрос.УстановитьПараметр("ПроектныеЗадачи", ПроектныеЗадачи);
	Запрос.УстановитьПараметр("ТекущаяДата", НачалоДня(ТекущаяДатаСеанса()));
	РезультатЗапроса = Запрос.Выполнить().Выгрузить();
	
	Для Каждого ПроектнаяЗадача Из ПроектныеЗадачи Цикл
		Результат = Новый Структура;
		Результат.Вставить("Ссылка", Справочники.ПроектныеЗадачи.ПустаяСсылка());
		Результат.Вставить("Проект", Справочники.Проекты.ПустаяСсылка());
		Результат.Вставить("Родитель", Справочники.ПроектныеЗадачи.ПустаяСсылка());
		Результат.Вставить("СуммарнаяЗадача", Ложь);
		Результат.Вставить("Веха", Ложь);
		Результат.Вставить("КодСДР", "");
		Результат.Вставить("ВерсияДанных", "");
		Результат.Вставить("ПометкаУдаления", Ложь);
		Результат.Вставить("Наименование", "");
		Результат.Вставить("ТекущийПланЕдиницаТрудозатрат", Перечисления.ЕдиницыТрудозатрат.ПустаяСсылка());
		Результат.Вставить("ТекущийПланТрудозатраты", 0);
		Результат.Вставить("ЕдиницаТрудозатратФакт", Перечисления.ЕдиницыТрудозатрат.ПустаяСсылка());
		Результат.Вставить("Владелец", Справочники.Проекты.ПустаяСсылка());
		Результат.Вставить("ТипОграничения", Перечисления.ТипыОграниченийПроектныхЗадач.ПустаяСсылка());
		Результат.Вставить("ДатаОграничения", '00010101');
		Результат.Вставить("ГрафикРаботы", Справочники.ГрафикиРаботы.ПустаяСсылка());
		Результат.Вставить("АвтоматическиРассчитыватьПланПроекта", Ложь);
		Результат.Вставить("РедактируетсяВСтороннейСистеме", Ложь);
		Результат.Вставить("СпособПланирования", Перечисления.СпособыПланированияПроекта.ПустаяСсылка());
		Результат.Вставить("ТекущийПланНачало", '00010101');
		Результат.Вставить("ТекущийПланОкончание", '00010101');
		Результат.Вставить("ТекущийПланДлительность", 0);
		Результат.Вставить("ТекущийПланЕдиницаДлительности", Перечисления.ЕдиницыДлительности.ПустаяСсылка());
		Результат.Вставить("НачалоФакт", '00010101');
		Результат.Вставить("ОкончаниеФакт", '00010101');
		Результат.Вставить("ДлительностьФакт", 0);
		Результат.Вставить("ЕдиницаДлительностиФакт", Перечисления.ЕдиницыДлительности.ПустаяСсылка());
		Результат.Вставить("СостояниеКонтроля", 0);
		Результат.Вставить("Исполнители", Новый Массив);
		Результат.Вставить("Предшественники", Новый Массив);
		
		НайденнаяСтрока = РезультатЗапроса.Найти(ПроектнаяЗадача, "Ссылка");
		Если НайденнаяСтрока <> Неопределено И ЗначениеЗаполнено(ПроектнаяЗадача) Тогда 
			
			ЗаполнитьЗначенияСвойств(Результат, НайденнаяСтрока,, "Исполнители, Предшественники");
			
			Для Каждого Строка Из НайденнаяСтрока.Исполнители Цикл
				НовыйЭлемент = Новый Структура("Исполнитель,
				|ТекущийПланТрудозатраты");
				
				ЗаполнитьЗначенияСвойств(НовыйЭлемент, Строка);
				
				Результат.Исполнители.Добавить(НовыйЭлемент);
			КонецЦикла;	
			
			Для Каждого Строка Из НайденнаяСтрока.Предшественники Цикл
				НовыйЭлемент = Новый Структура("Предшественник,
				|ТипЗависимости,
				|Задержка,
				|ЕдиницаЗадержки");
				
				ЗаполнитьЗначенияСвойств(НовыйЭлемент, Строка);
				
				Результат.Предшественники.Добавить(НовыйЭлемент);
			КонецЦикла;	
			
			Если Не ЗначениеЗаполнено(Результат.ГрафикРаботы) Тогда 
				Результат.ГрафикРаботы = Константы.ОсновнойГрафикРаботы.Получить();
			КонецЕсли;	
		КонецЕсли;	
		
		СоответствиеДанных.Вставить(ПроектнаяЗадача, Результат);
	КонецЦикла;	
	
	Возврат СоответствиеДанных;
	
КонецФункции	

// Возвращает структуру с данными проектной задачи,
// необходимыми для расчета плана проекта
//
// Параметры:
//  ПроектнаяЗадача - СправочникСсылка.ПроектныеЗадачи - ссылка на проектную задачу.
//
// Возвращаемое значение:
//  Структура - структура с данными проектной задачи.
//
Функция ПолучитьДанныеПроектнойЗадачи(ПроектнаяЗадача) Экспорт 
	
	ПроектныеЗадачи = Новый Массив;
	ПроектныеЗадачи.Добавить(ПроектнаяЗадача);
	
	СоответствиеДанных = ПолучитьДанныеПроектныхЗадач(ПроектныеЗадачи);
	
	Возврат СоответствиеДанных.Получить(ПроектнаяЗадача);
	
КонецФункции	

// Возвращает сроки проектной задачи
//
// Параметры:
//  ПроектнаяЗадача - СправочникСсылка.ПроектныеЗадачи - ссылка на проектную задачу.
//	
// Возвращаемое значение:
//  Структура - структура с данными сроков проектной задачи.
//
Функция ПолучитьСрокиПроектнойЗадачи(ПроектнаяЗадача) Экспорт 
	
	УстановитьПривилегированныйРежим(Истина);
	
	СтруктураДанных = Новый Структура;
	СтруктураДанных.Вставить("ТекущийПланНачало", '00010101');
	СтруктураДанных.Вставить("ТекущийПланОкончание", '00010101');
	СтруктураДанных.Вставить("ТекущийПланДлительность", 0);
	СтруктураДанных.Вставить("ТекущийПланЕдиницаДлительности", Перечисления.ЕдиницыДлительности.ПустаяСсылка());
	СтруктураДанных.Вставить("НачалоФакт", '00010101');
	СтруктураДанных.Вставить("ОкончаниеФакт", '00010101');
	СтруктураДанных.Вставить("ДлительностьФакт", 0);
	СтруктураДанных.Вставить("ЕдиницаДлительностиФакт", Перечисления.ЕдиницыДлительности.ПустаяСсылка());
	
	Если ЗначениеЗаполнено(ПроектнаяЗадача) Тогда 
		МенеджерЗаписи = РегистрыСведений.СрокиПроектныхЗадач.СоздатьМенеджерЗаписи();
		МенеджерЗаписи.ПроектнаяЗадача = ПроектнаяЗадача;
		МенеджерЗаписи.Прочитать();
	
		Если МенеджерЗаписи.Выбран() Тогда 
			ЗаполнитьЗначенияСвойств(СтруктураДанных, МенеджерЗаписи);
		КонецЕсли;
	КонецЕсли;	
	
	Возврат СтруктураДанных;
	
КонецФункции

// Записывает сроки проектной задачи в регистр
//
// Параметры:
//  ПроектнаяЗадача - СправочникСсылка.ПроектныеЗадачи - ссылка на проектную задачу.
//  СтруктураДанных - Структура - структура, содержащая данные о сроках проектной задачи.
//
Процедура ЗаписатьСрокиПроектнойЗадачи(ПроектнаяЗадача, СтруктураДанных) Экспорт 
	
	УстановитьПривилегированныйРежим(Истина);
	
	ЗаписьИзменена = Ложь;
	
	МенеджерЗаписи = РегистрыСведений.СрокиПроектныхЗадач.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.ПроектнаяЗадача = ПроектнаяЗадача;
	МенеджерЗаписи.Прочитать();
	
	Если Не МенеджерЗаписи.Выбран() Тогда 
		МенеджерЗаписи.ПроектнаяЗадача = ПроектнаяЗадача;
		ЗаписьИзменена = Истина;
	КонецЕсли;	
	
	Для Каждого Строка Из СтруктураДанных Цикл
		Если МенеджерЗаписи[Строка.Ключ] <> Строка.Значение Тогда 
			МенеджерЗаписи[Строка.Ключ] = Строка.Значение;
			ЗаписьИзменена = Истина;
		КонецЕсли;	
	КонецЦикла;	
	
	Если ЗаписьИзменена Тогда 
		МенеджерЗаписи.Записать();
	КонецЕсли;	
	
КонецПроцедуры

// Заполняет порядок проектных задач в общей структуре проекта
//
// Параметры:
//  Проект - СправочникСсылка.Проект - ссылка на проект, для задач которого необходимо заполнить порядок.
//
Процедура ЗаполнитьПорядокПроектныхЗадач(Проект) Экспорт 
	
	УстановитьПривилегированныйРежим(Истина);
	
	НомерПроектнойЗадачи = 1;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПроектныеЗадачи.Ссылка
	|ИЗ
	|	Справочник.ПроектныеЗадачи КАК ПроектныеЗадачи
	|ГДЕ
	|	ПроектныеЗадачи.Владелец = &Проект
	|	И ПроектныеЗадачи.Родитель = ЗНАЧЕНИЕ(Справочник.ПроектныеЗадачи.ПустаяСсылка)
	|
	|УПОРЯДОЧИТЬ ПО
	|	ПроектныеЗадачи.НомерЗадачиВУровне,
	|	ПроектныеЗадачи.Наименование";
		
	Запрос.УстановитьПараметр("Проект", Проект);
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл 
		РегистрыСведений.ПорядокПроектныхЗадач.УстановитьПорядокПроектнойЗадачи(
			Выборка.Ссылка, НомерПроектнойЗадачи);
		НомерПроектнойЗадачи = НомерПроектнойЗадачи + 1;
		ЗаполнитьПорядокПодчиненныхЗадач(Выборка.Ссылка, НомерПроектнойЗадачи)
	КонецЦикла;
	
КонецПроцедуры

// Заполняет порядок подчиненных проектных задач 
Процедура ЗаполнитьПорядокПодчиненныхЗадач(Родитель, НомерПроектнойЗадачи)
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПроектныеЗадачи.Ссылка
	|ИЗ
	|	Справочник.ПроектныеЗадачи КАК ПроектныеЗадачи
	|ГДЕ
	|	ПроектныеЗадачи.Родитель = &Родитель
	|
	|УПОРЯДОЧИТЬ ПО
	|	ПроектныеЗадачи.НомерЗадачиВУровне,
	|	ПроектныеЗадачи.Наименование";
	
	Запрос.УстановитьПараметр("Родитель", Родитель);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		РегистрыСведений.ПорядокПроектныхЗадач.УстановитьПорядокПроектнойЗадачи(
			Выборка.Ссылка, НомерПроектнойЗадачи);
		НомерПроектнойЗадачи = НомерПроектнойЗадачи + 1;
		ЗаполнитьПорядокПодчиненныхЗадач(Выборка.Ссылка, НомерПроектнойЗадачи)
	КонецЦикла;
	
КонецПроцедуры

// Меняет папку для массива проектов на новую.
//
// Параметры:
//  МассивПроектов - Массив - Проекты, у которых необходимо изменить папку.
//  НоваяПапка - СправочникСсылка.ПапкиПроектов - Новая папка проектов.
//
// Возвращаемое значение:
//  Булево - Успешное изменение папки проектов.
//
Функция ИзменитьПапкуПроектов(МассивПроектов, НоваяПапка) Экспорт
	
	// Папка не изменилась
	Если МассивПроектов[0].Папка = НоваяПапка Тогда
		Возврат Ложь;
	КонецЕсли;
	
	// Проверка прав проектов
	Для Каждого ПроектСсылка Из МассивПроектов Цикл
		Если Не ДокументооборотПраваДоступа.ПолучитьПраваПоОбъекту(ПроектСсылка).Изменение Тогда 
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'У вас нет права на изменение проекта ""%1"".'; en = 'Insufficient permissions to change project ""%1"".'"),
				Строка(ПроектСсылка));
			ВызватьИсключение ТекстОшибки;
		КонецЕсли;
	КонецЦикла;
	
	НачатьТранзакцию();
	Попытка
		Для Каждого ПроектСсылка Из МассивПроектов Цикл
			ЗаблокироватьДанныеДляРедактирования(ПроектСсылка);
			ПроектОбъект = ПроектСсылка.ПолучитьОбъект();
			ПроектОбъект.Папка = НоваяПапка;
			ПроектОбъект.Записать();
		КонецЦикла;
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		ВызватьИсключение;
	КонецПопытки;
	
	Возврат Истина;
	
КонецФункции

// Возвращает проектные задачи пользователя за определенный промежуток времени.
//
// Параметры:
//  ДатаНачала - Дата - С какой даты следует получать проектные задачи.
//  ДатаОкончания - Дата - По какую дату следует получать проектные задачи.
//  Пользователь - СправочникСсылка.Пользователи - Пользователь, проектные задачи которого необходимо получить.
//
// Возвращаемое значение:
//  ТаблицаЗначений - Таблица проектных задач пользователя.
//
Функция ПолучитьПроектныеЗадачиПользователя(ДатаНачала, ДатаОкончания, Пользователь) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ПроектныеЗадачи.Ссылка,
	|	СрокиПроектныхЗадач.ТекущийПланНачало,
	|	СрокиПроектныхЗадач.ТекущийПланОкончание,
	|	ПроектныеЗадачи.Представление
	|ИЗ
	|	Справочник.ПроектныеЗадачи КАК ПроектныеЗадачи
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СрокиПроектныхЗадач КАК СрокиПроектныхЗадач
	|		ПО ПроектныеЗадачи.Ссылка = СрокиПроектныхЗадач.ПроектнаяЗадача
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ПроектныеЗадачи.Исполнители КАК ПроектныеЗадачиИсполнители
	|		ПО ПроектныеЗадачи.Ссылка = ПроектныеЗадачиИсполнители.Ссылка
	|ГДЕ
	|	ПроектныеЗадачи.Исполнители.Исполнитель = &Исполнитель
	|	И СрокиПроектныхЗадач.ТекущийПланНачало < &ДатаОкончания
	|	И СрокиПроектныхЗадач.ТекущийПланОкончание > &ДатаНачала";
	
	Запрос.УстановитьПараметр("ДатаНачала", ДатаНачала);
	Запрос.УстановитьПараметр("ДатаОкончания", ДатаОкончания);
	Запрос.УстановитьПараметр("Исполнитель", Пользователь);
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

//Возвращает процент выполнения проекта.
//
// Параметры:
//  Проект - СправочникСсылка.Проект - ссылка на проект, для задач которого необходимо рассчитать процент выполнения.
//
// Возвращаемое значение:
//  Число - процент выполнения проекта.
//
Функция РассчитатьПроцентВыполненияПроекта(Проект) Экспорт 
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	СрокиПроектныхЗадач.ТекущийПланЕдиницаДлительности КАК ЕдиницаДлительности,
		|	СрокиПроектныхЗадач.ТекущийПланДлительность КАК ОбщаяДлительность,
		|	ВЫБОР
		|		КОГДА СрокиПроектныхЗадач.ОкончаниеФакт <> ДАТАВРЕМЯ(1, 1, 1)
		|			ТОГДА СрокиПроектныхЗадач.ТекущийПланДлительность
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК ВыполненнаяДлительность,
		|	СрокиПроектныхЗадач.ПроектнаяЗадача.ГрафикРаботы КАК ГрафикРаботыЗадачи
		|ИЗ
		|	РегистрСведений.СрокиПроектныхЗадач КАК СрокиПроектныхЗадач
		|ГДЕ
		|	СрокиПроектныхЗадач.ПроектнаяЗадача.Владелец = &Проект
		|	И НЕ СрокиПроектныхЗадач.ПроектнаяЗадача.ПометкаУдаления
		|	И НЕ СрокиПроектныхЗадач.ПроектнаяЗадача.СуммарнаяЗадача";
		
	Запрос.УстановитьПараметр("Проект", Проект);
	
	ОбщаяДлительность = 0; 
	ВыполненнаяДлительность = 0;
	ЕдиницаРасчета = Перечисления.ЕдиницыДлительности.Час;
	ГрафикРаботыПроекта = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Проект, "ГрафикРаботы");
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл 
		Если ЗначениеЗаполнено(Выборка.ГрафикРаботыЗадачи) Тогда 
			ГрафикРаботыЗадачи = Выборка.ГрафикРаботыЗадачи;
		Иначе 
			ГрафикРаботыЗадачи = ГрафикРаботыПроекта;
		КонецЕсли;
		
		Если Выборка.ОбщаяПлановаяДлительность > 0 Тогда 
			ОбщаяДлительность = ОбщаяДлительность 
				+ ПересчитатьДлительность(
					Выборка.ОбщаяДлительность,
					Выборка.ЕдиницаДлительности,
					ЕдиницаРасчета,
					ГрафикРаботыЗадачи);
		КонецЕсли;
		
		Если Выборка.ВыполненнаяДлительность > 0 Тогда 
			ВыполненнаяДлительность = ВыполненнаяДлительность 
				+ ПересчитатьДлительность(
					Выборка.ВыполненнаяДлительность,
					Выборка.ЕдиницаДлительности,
					ЕдиницаРасчета,
					ГрафикРаботыЗадачи);
		КонецЕсли;
	КонецЦикла;
	
	Если ОбщаяДлительность = 0 Тогда 
		Возврат 0;
	Иначе 
		Возврат Окр(ВыполненнаяДлительность / ОбщаяДлительность * 100, 0);
	КонецЕсли;
	
КонецФункции

//Проверяет наличие своих проектов у пользователя.
//
// Параметры:
//  Пользователь - СправочникСсылка.Пользователи - ссылка на пользователя, для которого проверяем проекты.
//
// Возвращаемое значение:
//  Булево - Истина, если у пользователя есть свои проекты,
//         - Ложь, в противном случае.
//
Функция НетСвоихПроектов(Пользователь) Экспорт 
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ИСТИНА
	|ИЗ
	|	Справочник.Проекты.ПроектнаяКоманда КАК ПроектыПроектнаяКоманда
	|ГДЕ
	|	ПроектыПроектнаяКоманда.Исполнитель = &Пользователь
	|	И НЕ ПроектыПроектнаяКоманда.Ссылка.ПометкаУдаления
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ИСТИНА
	|ИЗ
	|	Справочник.Проекты КАК СправочникПроекты
	|ГДЕ
	|	СправочникПроекты.Руководитель = &Пользователь
	|	И НЕ СправочникПроекты.ПометкаУдаления";
	
	Запрос.Параметры.Вставить("Пользователь", Пользователь);
	
	Возврат Запрос.Выполнить().Пустой();
	
КонецФункции


