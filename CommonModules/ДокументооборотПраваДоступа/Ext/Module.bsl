
// Возвращает структуру прав доступа к указанному объекту для указанного 
// пользователя. Если пользователь не указан, то используется текущий
// пользователь.
Функция ПолучитьПраваПоОбъекту(ОбъектДоступа, Пользователь = Неопределено) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	// Проверка на использование ограничения прав доступа
	Если Не ДокументооборотПраваДоступаПовтИсп.ВключеноИспользованиеПравДоступа() Тогда
		
		// Если права доступа не включены, то все разрешено
		Права = Новый Структура(
			"Добавление, Изменение, Удаление, УправлениеПравами, Чтение",
			Истина, Истина, Истина, Истина, Истина);
		
		Возврат Права;
		
	КонецЕсли;	
	
	Если Не ЗначениеЗаполнено(Пользователь) Тогда
		Пользователь = Пользователи.ТекущийПользователь();
	КонецЕсли;
	
	// Роли Полные права все разрешено	
	Если ПользователиСерверПовтИсп.ЭтоПолноправныйПользовательИБ(Пользователь) Тогда
		
		Права = Новый Структура(
			"Добавление, Изменение, Удаление, УправлениеПравами, Чтение",
			Истина, Истина, Истина, Истина, Истина);
		
		Возврат Права;
		
	КонецЕсли;
	
	Права = Новый Структура(
		"Добавление, Изменение, Удаление, УправлениеПравами, Чтение",
		Ложь, Ложь, Ложь, Ложь, Ложь);
		
	ОтборПоПользователю = Новый Массив;
	ОтборПоПользователю.Добавить(Пользователь);
	ТаблицаПрав = ПолучитьПраваПользователейПоОбъекту(ОбъектДоступа, Истина, ОтборПоПользователю);
	
	Если ТаблицаПрав.Количество() > 0 Тогда
		ЗаполнитьЗначенияСвойств(Права, ТаблицаПрав[0]);
	КонецЕсли;
	
	Возврат Права;
	
КонецФункции

// Возвращает права на переданный ОбъектДоступа
// по всем пользователям, либо с отбором по параметру Пользователи.
// Используется только для режима с включенными правами, 
// т.е. константа ДокументооборотИспользоватьОграничениеПравДоступа не проверяется.
//
// Параметры:
//  ОбъектДоступа - ссылна на объект, чьи права нужно получить
//  УчитыватьНеограниченныеПрава - Булево - признак включения в результат пользователей с неограниченными правами на таблицу.
//  Пользователи - Массив, Неопределено - массив пользователей для отбора. Если не установлен, то по всем.
// 
// Возвращаемое значение:
//  ТаблицаЗначений - таблица с колонками Пользователь, Чтение, Добавление, Изменение, Удаление, УправлениеПравами
// 
Функция ПолучитьПраваПользователейПоОбъекту(
		ОбъектДоступа, УчитыватьНеограниченныеПрава, Пользователи = Неопределено) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
			
	ОбъектыДоступа = Новый Массив;
	ОбъектыДоступа.Добавить(ОбъектДоступа);
	
	Возврат ПолучитьПраваПользователейПоОднотипнымОбъектам(
		ОбъектыДоступа, УчитыватьНеограниченныеПрава, Пользователи);
	
КонецФункции

// Возвращает права на переданные ОбъектыДоступа
// по всем пользователям, либо с отбором по параметру Пользователи.
// Переданные объекты могут иметь разные типы.
// Используется только для режима с включенными правами, 
// т.е. константа ДокументооборотИспользоватьОграничениеПравДоступа не проверяется.
//
// Параметры:
//  ОбъектыДоступа - Массив - массив ссылок на объекты, чьи права нужно получить
//  УчитыватьНеограниченныеПрава - Булево - признак включения в результат пользователей с неограниченными правами на таблицу.
//  Пользователи - Массив, Неопределено - массив пользователей для отбора. Если не установлен, то по всем.
// 
// Возвращаемое значение:
//  ТаблицаЗначений - таблица с колонками ОбъектДоступа, Пользователь, Чтение, Добавление, Изменение, Удаление, УправлениеПравами
// 
Функция ПолучитьПраваПользователейПоОбъектам(
			ОбъектыДоступа, УчитыватьНеограниченныеПрава, Пользователи = Неопределено) Экспорт
			
	УстановитьПривилегированныйРежим(Истина);
			
	ТаблицаПрав = ТаблицаПравПользователейПоОбъектам();
	
	Если ОбъектыДоступа.Количество() = 0 Тогда
		Возврат ТаблицаПрав;
	КонецЕсли;
	
	ОбъектыСТипами = Новый ТаблицаЗначений;
	ОбъектыСТипами.Колонки.Добавить("Объект");
	ОбъектыСТипами.Колонки.Добавить("Тип");
	ОбъектыСТипами.Колонки.Добавить("ИДГруппы");
	
	Для Каждого Объект Из ОбъектыДоступа Цикл
		Стр = ОбъектыСТипами.Добавить();
		Стр.Объект = Объект;
		Стр.Тип = ТипЗнч(Объект);
	КонецЦикла;
	
	ТипыНеПоддерживающиеГрупповойАлгоритм = Новый Соответствие;
	ТипыНеПоддерживающиеГрупповойАлгоритм.Вставить(
		Тип("СправочникСсылка.ДескрипторыДоступаОбъектов"), Истина);
	ТипыНеПоддерживающиеГрупповойАлгоритм.Вставить(
		Тип("СправочникСсылка.ДескрипторыДоступаРегистров"), Истина);
	
	ПоследнийИД = 0;
	ИДТипов = Новый Соответствие;
	
	Для Каждого Стр Из ОбъектыСТипами Цикл
		
		// Эти объекты обрабатываются по одному
		Если ТипыНеПоддерживающиеГрупповойАлгоритм.Получить(Стр.Тип) <> Неопределено Тогда
			Стр.ИДГруппы = ПоследнийИД;
			ПоследнийИД = ПоследнийИД + 1;
			Продолжить;
		КонецЕсли;
		
		НайденныйИД = ИДТипов.Получить(Стр.Тип);
		Если НайденныйИД <> Неопределено Тогда
			Стр.ИДГруппы = НайденныйИД;
		Иначе
			ИДТипов.Вставить(Стр.Тип, ПоследнийИД);
			Стр.ИДГруппы = ПоследнийИД;
			ПоследнийИД = ПоследнийИД + 1;
		КонецЕсли;
		
	КонецЦикла;
	
	СтруктураПоиска = Новый Структура("ИДГруппы");
	Для Сч = 0 По ПоследнийИД - 1 Цикл
		
		ОднотипныеОбъекты = Новый Массив;
		
		СтруктураПоиска.ИДГруппы = Сч;
		НайденныеСтроки = ОбъектыСТипами.НайтиСтроки(СтруктураПоиска);
		Для Каждого Стр Из НайденныеСтроки Цикл
			ОднотипныеОбъекты.Добавить(Стр.Объект);
		КонецЦикла;
		
		Таб = ПолучитьПраваПользователейПоОднотипнымОбъектам(
			ОднотипныеОбъекты, УчитыватьНеограниченныеПрава, Пользователи);
		
		Для Каждого Стр Из Таб Цикл
			ЗаполнитьЗначенияСвойств(ТаблицаПрав.Добавить(), Стр);
		КонецЦикла;
		
	КонецЦикла;
	
	Возврат ТаблицаПрав;
	
КонецФункции

// Возвращает права на переданные ОбъектыДоступа
// по всем пользователям, либо с отбором по параметру Пользователи.
// Важно! Переданные объекты должны иметь один тип.
// Используется только для режима с включенными правами, 
// т.е. константа ДокументооборотИспользоватьОграничениеПравДоступа не проверяется.
//
// Параметры:
//  ОбъектыДоступа - Массив - массив ссылок на объекты, чьи права нужно получить
//  УчитыватьНеограниченныеПрава - Булево - признак включения в результат пользователей с неограниченными правами на таблицу.
//  Пользователи - Массив, Неопределено - массив пользователей для отбора. Если не установлен, то по всем.
// 
// Возвращаемое значение:
//  ТаблицаЗначений - таблица с колонками ОбъектДоступа, Пользователь, Чтение, Добавление, Изменение, Удаление, УправлениеПравами
// 
Функция ПолучитьПраваПользователейПоОднотипнымОбъектам(
			ОбъектыДоступа, УчитыватьНеограниченныеПрава, Пользователи = Неопределено) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ТаблицаПрав = ТаблицаПравПользователейПоОбъектам();
	
	Если ОбъектыДоступа.Количество() = 0 Тогда
		Возврат ТаблицаПрав;
	КонецЕсли;
	
	ПервыйОбъектДоступа = ОбъектыДоступа[0];
	ТипОбъекта = ТипЗнч(ПервыйОбъектДоступа);
	ПолноеИмя = ПервыйОбъектДоступа.Метаданные().ПолноеИмя();
	
	// Объекты, имеющие свои алгоритмы получения прав.
	Если ОбщегоНазначенияДокументооборотПовтИсп.ЕстьМетодМенеджера(
			ПолноеИмя, "ПолучитьПраваПользователейПоОбъектам") Тогда
		
		Менеджер = ОбщегоНазначения.МенеджерОбъектаПоПолномуИмени(ПолноеИмя);
		Возврат Менеджер.ПолучитьПраваПользователейПоОбъектам(ОбъектыДоступа, Пользователи);
		
	КонецЕсли;
	
	// Файлы.
	Если ТипОбъекта = Тип("СправочникСсылка.Файлы")
		Или ТипОбъекта = Тип("СправочникСсылка.ВерсииФайлов") Тогда
		
		// Если менеджер владельца файла содержит метод ПолучитьПраваПоФайлам,
		// права нужно получить через него.
		// В остальных случаях права на чтение совпадают с правами на чтение владельца,
		// а права на изменение, добавление, удаление - с парвами на изменение владельца.
		
		Если ТипОбъекта = Тип("СправочникСсылка.Файлы") Тогда
			Запрос = Новый Запрос(
				"ВЫБРАТЬ
				|	Файлы.Ссылка КАК ОбъектДоступа,
				|	ТИПЗНАЧЕНИЯ(Файлы.ВладелецФайла) КАК ТипВладельца
				|ИЗ
				|	Справочник.Файлы КАК Файлы
				|ГДЕ
				|	Файлы.Ссылка В(&ОбъектыДоступа)");
		Иначе
			Запрос = Новый Запрос(
				"ВЫБРАТЬ
				|	ВерсииФайлов.Ссылка КАК ОбъектДоступа,
				|	ТИПЗНАЧЕНИЯ(Файлы.ВладелецФайла) КАК ТипВладельца
				|ИЗ
				|	Справочник.ВерсииФайлов КАК ВерсииФайлов
				|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Файлы КАК Файлы
				|		ПО ВерсииФайлов.Владелец = Файлы.Ссылка
				|ГДЕ
				|	ВерсииФайлов.Ссылка В(&ОбъектыДоступа)");
		КонецЕсли;
		
		Запрос.УстановитьПараметр("ОбъектыДоступа", ОбъектыДоступа);
		
		МенеджерыВладельцевПоТипам = Новый Соответствие;
		ФайлыПоМенеджерамВладельцев = Новый Соответствие;
		
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			
			МенеджерВладельца = МенеджерыВладельцевПоТипам.Получить(Выборка.ТипВладельца);
			Если МенеджерВладельца = Неопределено Тогда
				
				ПолноеИмяВладельца = Метаданные.НайтиПоТипу(Выборка.ТипВладельца).ПолноеИмя();
				МенеджерВладельца = ОбщегоНазначения.МенеджерОбъектаПоПолномуИмени(ПолноеИмяВладельца);
				
				Если Не ОбщегоНазначенияДокументооборот.ЕстьМетодМенеджера(
							МенеджерВладельца, "ПолучитьПраваПоФайлам") Тогда
					МенеджерВладельца = Справочники.Файлы;  // Значение по умолчанию.
				КонецЕсли;
				
				МенеджерыВладельцевПоТипам.Вставить(Выборка.ТипВладельца, МенеджерВладельца);
				
			КонецЕсли;
			
			ФайлыТекущегоМенеджера = ФайлыПоМенеджерамВладельцев.Получить(МенеджерВладельца);
			Если ФайлыТекущегоМенеджера = Неопределено Тогда
				ФайлыТекущегоМенеджера = Новый Массив;
				ФайлыПоМенеджерамВладельцев.Вставить(МенеджерВладельца, ФайлыТекущегоМенеджера);
			КонецЕсли;
			
			ФайлыТекущегоМенеджера.Добавить(Выборка.ОбъектДоступа);
			
		КонецЦикла;
		
		Для Каждого Эл Из ФайлыПоМенеджерамВладельцев Цикл
			
			МенеджерВладельца = Эл.ключ;
			Файлы = Эл.Значение;
			
			Если МенеджерВладельца = Справочники.Файлы Тогда
				// Права считаются на основании прав владельцев.
				ТекущаяТаблицаПрав = ПолучитьПраваПользователейПоФайламОтВладельцев(
					Файлы, УчитыватьНеограниченныеПрава, Пользователи);
			Иначе
				// Есть свой алгоритм расчета прав.
				ТекущаяТаблицаПрав = МенеджерВладельца.ПолучитьПраваПоФайлам(
					Файлы, Пользователи);
			КонецЕсли;
			
			Для Каждого Стр Из ТекущаяТаблицаПрав Цикл
				ЗаполнитьЗначенияСвойств(ТаблицаПрав.Добавить(), Стр);
			КонецЦикла;
			
		КонецЦикла;
		
		Возврат ТаблицаПрав;
		
	КонецЕсли;
	
	// Остальные объекты.
	МетаданныеОбъекта = ПервыйОбъектДоступа.Метаданные();
	ИдентификаторОМ = ИдентификаторМетаданныхДляПроверкиПрав(ПервыйОбъектДоступа);
	
	МассивТипов = Новый Массив;
	МассивТипов.Добавить(ТипОбъекта);
	ТаблицаПрав.Колонки.Удалить("ОбъектДоступа");
	ТаблицаПрав.Колонки.Добавить("ОбъектДоступа", Новый ОписаниеТипов(МассивТипов));
	
	ЗаменяемыеПодстроки = Новый Соответствие;
	Запрос = ДокументооборотПраваДоступаПереопределяемый.ПолучитьЗапросНаПраваПоОбъекту(
		ОбъектыДоступа, ТипОбъекта, МетаданныеОбъекта, ЗаменяемыеПодстроки, Пользователи);
	
	Если ЗаменяемыеПодстроки.Получить("%ОтборПоПользователю%") = Неопределено Тогда
		ЗаменяемыеПодстроки.Вставить("%ОтборПоПользователю%", 
			?(Пользователи = Неопределено, "",
				"И ПраваПоДескрипторамДоступаОбъектов.Пользователь В (&Пользователи)"));
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Запрос.Текст) Тогда
		
		// Запрос сформирован в переопределяемом модуле
		
	ИначеЕсли ТипОбъекта = Тип("ЗадачаСсылка.ЗадачаИсполнителя") Тогда
		
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	ЗадачаИсполнителя.Ссылка КАК ОбъектДоступа,
			|	СоставСубъектов.Пользователь,
			|	ИСТИНА КАК Чтение,
			|	МАКСИМУМ(ВЫБОР
			|			КОГДА ЗадачаИсполнителя.Автор = СоставСубъектов.Пользователь
			|				ТОГДА ИСТИНА
			|			ИНАЧЕ ЛОЖЬ
			|		КОНЕЦ) КАК Добавление
			|ПОМЕСТИТЬ ПраваНаЧтениеДобавление
			|ИЗ
			|	Задача.ЗадачаИсполнителя КАК ЗадачаИсполнителя
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.УчастникиПроцессов КАК УчастникиПроцессов
			|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СоставСубъектовПравДоступа КАК СоставСубъектов
			|			ПО УчастникиПроцессов.Участник = СоставСубъектов.Субъект
			|				И (СоставСубъектов.ИмяОбластиДелегирования В (&ОбластиДелегирования))
			|				%ОтборПоПользователю%
			|		ПО ЗадачаИсполнителя.БизнесПроцесс = УчастникиПроцессов.Процесс
			|ГДЕ
			|	ЗадачаИсполнителя.Ссылка В (&ОбъектыДоступа)
			|
			|СГРУППИРОВАТЬ ПО
			|	ЗадачаИсполнителя.Ссылка,
			|	СоставСубъектов.Пользователь
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	ЗадачаИсполнителя.Ссылка КАК ОбъектДоступа,
			|	СоставСубъектов.Пользователь,
			|	ИСТИНА КАК Изменение
			|ПОМЕСТИТЬ ПраваНаИзменение
			|ИЗ
			|	Задача.ЗадачаИсполнителя КАК ЗадачаИсполнителя
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СоставСубъектовПравДоступа КАК СоставСубъектов
			|		ПО ЗадачаИсполнителя.ТекущийИсполнитель = СоставСубъектов.Субъект
			|			И (СоставСубъектов.ИмяОбластиДелегирования В (&ОбластиДелегирования))
			|			%ОтборПоПользователю%
			|ГДЕ
			|	ЗадачаИсполнителя.Ссылка В (&ОбъектыДоступа)
			|
			|СГРУППИРОВАТЬ ПО
			|	ЗадачаИсполнителя.Ссылка,
			|	СоставСубъектов.Пользователь
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ПраваНаЧтениеДобавление.ОбъектДоступа,
			|	ПраваНаЧтениеДобавление.Пользователь КАК Пользователь,
			|	ПраваНаЧтениеДобавление.Чтение,
			|	ЕСТЬNULL(ПраваНаИзменение.Изменение, ЛОЖЬ) КАК Изменение,
			|	ПраваНаЧтениеДобавление.Добавление,
			|	ЛОЖЬ КАК Удаление,
			|	ЛОЖЬ КАК УправлениеПравами
			|ИЗ
			|	ПраваНаЧтениеДобавление КАК ПраваНаЧтениеДобавление
			|		ЛЕВОЕ СОЕДИНЕНИЕ ПраваНаИзменение КАК ПраваНаИзменение
			|		ПО ПраваНаЧтениеДобавление.ОбъектДоступа = ПраваНаИзменение.ОбъектДоступа
			|		И ПраваНаЧтениеДобавление.Пользователь = ПраваНаИзменение.Пользователь";
			
		ЗаменяемыеПодстроки.Вставить("%ОтборПоПользователю%", 
			?(Пользователи = Неопределено, "", "И СоставСубъектов.Пользователь В (&Пользователи)"));
		
		ОбластиДелегирования = 
			ДокументооборотПраваДоступаПереопределяемый.ПолучитьОбластиДелегированияПоИОМ(ИдентификаторОМ);
		Запрос.УстановитьПараметр("ОбластиДелегирования", ОбластиДелегирования);
		
	ИначеЕсли ОбщегоНазначения.ЭтоБизнесПроцесс(МетаданныеОбъекта) Тогда
		
		// Права на чтение имеют все участники процесса, права на изменение - только автор.
		// Также учитываются делегаты и руководители.
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	ВложенныйЗапрос.ОбъектДоступа КАК ОбъектДоступа,
			|	ВложенныйЗапрос.Пользователь КАК Пользователь,
			|	МАКСИМУМ(ВложенныйЗапрос.Чтение) КАК Чтение,
			|	МАКСИМУМ(ВложенныйЗапрос.Изменение) КАК Добавление,
			|	МАКСИМУМ(ВложенныйЗапрос.Изменение) КАК Изменение,
			|	МАКСИМУМ(ВложенныйЗапрос.Изменение) КАК Удаление,
			|	ЛОЖЬ КАК УправлениеПравами
			|ИЗ
			|	(ВЫБРАТЬ
			|		ТаблицаПроцесса.Ссылка КАК ОбъектДоступа,
			|		СоставСубъектов.Пользователь КАК Пользователь,
			|		ИСТИНА КАК Чтение,
			|		ЛОЖЬ КАК Изменение
			|	ИЗ
			|		%ТаблицаПроцесса% КАК ТаблицаПроцесса
			|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.УчастникиПроцессов КАК УчастникиПроцессов
			|				ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СоставСубъектовПравДоступа КАК СоставСубъектов
			|				ПО УчастникиПроцессов.Участник = СоставСубъектов.Субъект
			|					И (СоставСубъектов.ИмяОбластиДелегирования В (&ОбластиДелегирования))
			|					%ОтборПоПользователю%
			|			ПО ТаблицаПроцесса.Ссылка = УчастникиПроцессов.Процесс
			|	ГДЕ
			|		ТаблицаПроцесса.Ссылка В(&ОбъектыДоступа)
			|	
			|	ОБЪЕДИНИТЬ ВСЕ
			|	
			|	ВЫБРАТЬ
			|		ТаблицаПроцесса.Ссылка,
			|		СоставСубъектов.Пользователь,
			|		ИСТИНА,
			|		ИСТИНА
			|	ИЗ
			|		%ТаблицаПроцесса% КАК ТаблицаПроцесса
			|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СоставСубъектовПравДоступа КАК СоставСубъектов
			|			ПО ТаблицаПроцесса.Автор = СоставСубъектов.Субъект
			|				И (СоставСубъектов.ИмяОбластиДелегирования В (&ОбластиДелегирования))
			|				%ОтборПоПользователю%
			|	ГДЕ
			|		ТаблицаПроцесса.Ссылка В(&ОбъектыДоступа)) КАК ВложенныйЗапрос
			|
			|СГРУППИРОВАТЬ ПО
			|	ВложенныйЗапрос.ОбъектДоступа,
			|	ВложенныйЗапрос.Пользователь";
			
		ЗаменяемыеПодстроки.Вставить("%ТаблицаПроцесса%", МетаданныеОбъекта.ПолноеИмя());
		ЗаменяемыеПодстроки.Вставить("%ОтборПоПользователю%", 
			?(Пользователи = Неопределено, "", "И СоставСубъектов.Пользователь В (&Пользователи)"));
		
		ОбластиДелегирования = 
			ДокументооборотПраваДоступаПереопределяемый.ПолучитьОбластиДелегированияПоИОМ(ИдентификаторОМ);
		Запрос.УстановитьПараметр("ОбластиДелегирования", ОбластиДелегирования);
		
	ИначеЕсли ТипОбъекта = Тип("СправочникСсылка.ЗаметкиДокументооборота") Тогда
		
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	ВЫБОР
			|		КОГДА ЗаметкиДокументооборота.Автор = СоставСубъектов.Пользователь
			|			ТОГДА ИСТИНА
			|		ИНАЧЕ ЛОЖЬ
			|	КОНЕЦ КАК Чтение,
			|	ИСТИНА КАК Добавление,
			|	ИСТИНА КАК Изменение,
			|	ЛОЖЬ КАК Удаление,
			|	ЛОЖЬ КАК УправлениеПравами
			|ИЗ
			|	Справочник.ЗаметкиДокументооборота КАК ЗаметкиДокументооборота
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СоставСубъектовПравДоступа КАК СоставСубъектов
			|		ПО ЗаметкиДокументооборота.Автор = СоставСубъектов.Субъект
			|			%ОтборПоПользователю%
			|ГДЕ
			|	ЗаметкиДокументооборота.Ссылка В (&ОбъектыДоступа)";
			
		ЗаменяемыеПодстроки.Вставить("%ОтборПоПользователю%", 
			?(Пользователи = Неопределено, "", "И СоставСубъектов.Пользователь В (&Пользователи)"));
		
	ИначеЕсли ТипОбъекта = Тип("СправочникСсылка.ДескрипторыДоступаОбъектов") Тогда
		
		// Для отображения прав в форме дескриптора
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	ПраваПоДескрипторамДоступаОбъектов.Дескриптор КАК ОбъектДоступа,
			|	ПраваПоДескрипторамДоступаОбъектов.Пользователь,
			|	МАКСИМУМ(ПраваПоДескрипторамДоступаОбъектов.Добавление) КАК Добавление,
			|	МАКСИМУМ(ПраваПоДескрипторамДоступаОбъектов.Изменение) КАК Изменение,
			|	МАКСИМУМ(ПраваПоДескрипторамДоступаОбъектов.Удаление) КАК Удаление,
			|	МАКСИМУМ(ПраваПоДескрипторамДоступаОбъектов.УправлениеПравами) КАК УправлениеПравами,
			|	МАКСИМУМ(ПраваПоДескрипторамДоступаОбъектов.Чтение) КАК Чтение
			|ИЗ
			|	РегистрСведений.ПраваПоДескрипторамДоступаОбъектов КАК ПраваПоДескрипторамДоступаОбъектов
			|ГДЕ
			|	ПраваПоДескрипторамДоступаОбъектов.Дескриптор В (&ОбъектыДоступа)
			|	%ДопУсловия%
			|	%ОтборПоПользователю%
			|
			|СГРУППИРОВАТЬ ПО
			|	ПраваПоДескрипторамДоступаОбъектов.Дескриптор,
			|	ПраваПоДескрипторамДоступаОбъектов.Пользователь";
			
		ТипОбъектаДоступа = ТипЗнч(ОбщегоНазначения.ЗначениеРеквизитаОбъекта(
			ИдентификаторОМ, "ЗначениеПустойСсылки"));
		
		ДопНастройки = ПолучитьДопНастройкиДляПолученияПравПоОбъекту(ТипОбъектаДоступа);
		
		ДопУсловия = "";
		Если Не ДопНастройки.ДобавлятьРуководителейИДелегатов Тогда
			ДопУсловия = ДопУсловия
				+ "И ПраваПоДескрипторамДоступаОбъектов.ОбъектОснование = Неопределено";
		КонецЕсли;
		
		ЗаменяемыеПодстроки.Вставить("%ДопУсловия%", ДопУсловия);
		
	ИначеЕсли ТипОбъекта = Тип("СправочникСсылка.ДескрипторыДоступаРегистров")Тогда
		
		// Для отображения прав в форме дескриптора
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	ПраваПоДескрипторамДоступаРегистров.Дескриптор КАК ОбъектДоступа,
			|	ПраваПоДескрипторамДоступаРегистров.Пользователь,
			|	МАКСИМУМ(ПраваПоДескрипторамДоступаРегистров.Добавление) КАК Добавление,
			|	МАКСИМУМ(ПраваПоДескрипторамДоступаРегистров.Изменение) КАК Изменение,
			|	МАКСИМУМ(ПраваПоДескрипторамДоступаРегистров.Удаление) КАК Удаление,
			|	МАКСИМУМ(ПраваПоДескрипторамДоступаРегистров.УправлениеПравами) КАК УправлениеПравами,
			|	МАКСИМУМ(ПраваПоДескрипторамДоступаРегистров.Чтение) КАК Чтение
			|ИЗ
			|	РегистрСведений.ПраваПоДескрипторамДоступаРегистров КАК ПраваПоДескрипторамДоступаРегистров
			|ГДЕ
			|	ПраваПоДескрипторамДоступаРегистров.Дескриптор В (&ОбъектыДоступа)
			|	%ДопУсловия%
			|	%ОтборПоПользователю%
			|
			|СГРУППИРОВАТЬ ПО
			|	ПраваПоДескрипторамДоступаРегистров.Дескриптор,
			|	ПраваПоДескрипторамДоступаРегистров.Пользователь";
			
		ТипОбъектаДоступа = ТипЗнч(ОбщегоНазначения.ЗначениеРеквизитаОбъекта(
			ИдентификаторОМ, "ЗначениеПустойСсылки"));
		
		ДопНастройки = ПолучитьДопНастройкиДляПолученияПравПоОбъекту(ТипОбъектаДоступа);
		
		ДопУсловия = "";
		Если Не ДопНастройки.ДобавлятьРуководителейИДелегатов Тогда
			ДопУсловия = ДопУсловия
				+ "И ПраваПоДескрипторамДоступаОбъектов.ОбъектОснование = Неопределено";
		КонецЕсли;
		
		ЗаменяемыеПодстроки.Вставить("%ДопУсловия%", ДопУсловия);
		
	Иначе // Права объектов, связанных с дескрипторами
		
		Запрос.Текст =
			"ВЫБРАТЬ
			|	ДескрипторыДляОбъектов.Объект КАК ОбъектДоступа,
			|	ПраваПоДескрипторамДоступаОбъектов.Пользователь,
			|	ИСТИНА КАК Чтение,
			|	МАКСИМУМ(ПраваПоДескрипторамДоступаОбъектов.Добавление) КАК Добавление,
			|	МАКСИМУМ(ПраваПоДескрипторамДоступаОбъектов.Изменение) КАК Изменение,
			|	МАКСИМУМ(ПраваПоДескрипторамДоступаОбъектов.Удаление) КАК Удаление,
			|	МАКСИМУМ(ПраваПоДескрипторамДоступаОбъектов.УправлениеПравами) КАК УправлениеПравами
			|ИЗ
			|	РегистрСведений.ДескрипторыДляОбъектов КАК ДескрипторыДляОбъектов
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ПраваПоДескрипторамДоступаОбъектов КАК ПраваПоДескрипторамДоступаОбъектов
			|		ПО ДескрипторыДляОбъектов.Дескриптор = ПраваПоДескрипторамДоступаОбъектов.Дескриптор
			|ГДЕ
			|	ДескрипторыДляОбъектов.Объект В(&ОбъектыДоступа)
			|	И НЕ ДескрипторыДляОбъектов.Отключен
			|	%ДопУсловия%
			|	%ОтборПоПользователю%
			|
			|СГРУППИРОВАТЬ ПО
			|	ДескрипторыДляОбъектов.Объект,
			|	ПраваПоДескрипторамДоступаОбъектов.Пользователь";
			
		ДопНастройки = ПолучитьДопНастройкиДляПолученияПравПоОбъекту(ТипОбъекта);
		
		ДопУсловия = "";
		Если Не ДопНастройки.ДобавлятьРуководителейИДелегатов Тогда
			ДопУсловия = ДопУсловия
				+ "И ПраваПоДескрипторамДоступаОбъектов.ОбъектОснование = Неопределено";
		КонецЕсли;
		
		ЗаменяемыеПодстроки.Вставить("%ДопУсловия%", ДопУсловия);
		
	КонецЕсли;
	
	Для Каждого Эл Из ЗаменяемыеПодстроки Цикл
		Запрос.Текст = СтрЗаменить(Запрос.Текст, Эл.Ключ, Эл.Значение);
	КонецЦикла;
	
	Запрос.УстановитьПараметр("ОбъектыДоступа", ОбъектыДоступа);
	Запрос.УстановитьПараметр("Пользователи", Пользователи);
	Запрос.УстановитьПараметр("ИдентификаторОМ", ИдентификаторОМ);
	Запрос.УстановитьПараметр("ОбластиДелегирования",
		ДокументооборотПраваДоступаПереопределяемый.ПолучитьОбластиДелегированияПоИОМ(ИдентификаторОМ));
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ЗаполнитьЗначенияСвойств(ТаблицаПрав.Добавить(), Выборка);
	КонецЦикла;
	
	// Добавление пользователей с неограниченными правами на таблицу
	Если УчитыватьНеограниченныеПрава Тогда
		РасширитьТаблицуПравНеограниченнымиПравами(
			ТаблицаПрав, ИдентификаторОМ, ОбъектыДоступа, Пользователи);
	КонецЕсли;
	
	Возврат ТаблицаПрав;
	
КонецФункции

// Возвращает таблицу прав для файлов, копируя в нее права владельцев.
// 
Функция ПолучитьПраваПользователейПоФайламОтВладельцев(
			ОбъектыДоступа, УчитыватьНеограниченныеПрава, Пользователи)
			
	ТаблицаПрав = ТаблицаПравПользователейПоОбъектам();		
			
	ТипОбъекта = ТипЗнч(ОбъектыДоступа[0]);
	Запрос = Новый Запрос;
	
	Если ТипОбъекта = Тип("СправочникСсылка.Файлы") Тогда
		
		Запрос.Текст =
			"ВЫБРАТЬ
			|	Файлы.Ссылка КАК ОбъектДоступа,
			|	Файлы.ВладелецФайла КАК ВладелецФайла
			|ИЗ
			|	Справочник.Файлы КАК Файлы
			|ГДЕ
			|	Файлы.Ссылка В(&ОбъектыДоступа)";
		
	ИначеЕсли ТипОбъекта = Тип("СправочникСсылка.ВерсииФайлов") Тогда
		
		Запрос.Текст =
			"ВЫБРАТЬ
			|	ВерсииФайлов.Ссылка КАК ОбъектДоступа,
			|	ВерсииФайлов.Владелец.ВладелецФайла КАК ВладелецФайла
			|ИЗ
			|	Справочник.ВерсииФайлов КАК ВерсииФайлов
			|ГДЕ
			|	ВерсииФайлов.Ссылка В(&ОбъектыДоступа)";
		
	КонецЕсли;
	
	Запрос.УстановитьПараметр("ОбъектыДоступа", ОбъектыДоступа);
	
	ОбъектыСВладельцамиФайлов = Запрос.Выполнить().Выгрузить();
	УникальныеВладельцы = Новый Массив;
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(
		УникальныеВладельцы, ОбъектыСВладельцамиФайлов.ВыгрузитьКолонку("ВладелецФайла"));
	
	ПраваВладельцев = ПолучитьПраваПользователейПоОбъектам(
		УникальныеВладельцы, УчитыватьНеограниченныеПрава, Пользователи);
	ПраваВладельцев.Индексы.Добавить("ОбъектДоступа");
	
	Для Каждого Стр Из ОбъектыСВладельцамиФайлов Цикл
		
		СтрокиПравВладельца = ПраваВладельцев.НайтиСтроки(
			Новый Структура("ОбъектДоступа", Стр.ВладелецФайла));
			
		Для Каждого СтрокаПравВладельца из СтрокиПравВладельца Цикл 
			НоваяСтрока = ТаблицаПрав.Добавить();
			НоваяСтрока.ОбъектДоступа = Стр.ОбъектДоступа;
			НоваяСтрока.Пользователь = СтрокаПравВладельца.Пользователь;
			НоваяСтрока.Чтение = СтрокаПравВладельца.Чтение;
			НоваяСтрока.Добавление = СтрокаПравВладельца.Изменение;
			НоваяСтрока.Изменение = СтрокаПравВладельца.Изменение;
			НоваяСтрока.Удаление = СтрокаПравВладельца.Изменение;
		КонецЦикла;
		
	КонецЦикла;
	
	Возврат ТаблицаПрав;
	
КонецФункции

// Возвращает права пользователя на таблицу, не ограниченные RLS.
//
// Параметры:
//  Таблица - ОбъектМетаданных - объект метаданных конфигурации;
//          - Тип - тип, который можно успешно использовать в функции Метаданные.НайтиПоТипу();
//          - Строка - полное имя объекта метаданных, которое можно успешно использовать
//            		   в функции Метаданные.НайтиПоПолномуИмени().
//
// Возвращаемое значение:
//  Структура - структура с полями Чтение, Изменение.
// 
Функция НеограниченныеПраваНаТаблицу(Таблица, Пользователь = Неопределено) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если Не ЗначениеЗаполнено(Пользователь) Тогда
		Пользователь = Пользователи.ТекущийПользователь();
	КонецЕсли;
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	ЕСТЬNULL(МАКСИМУМ(ПраваРолей.ЧтениеБезОграничения), ЛОЖЬ) КАК Чтение,
		|	ЕСТЬNULL(МАКСИМУМ(ПраваРолей.ИзменениеБезОграничения), ЛОЖЬ) КАК Изменение
		|ИЗ
		|	РегистрСведений.ПолномочияПользователей КАК ПолномочияПользователей
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ПользователиВКонтейнерах КАК ПользователиВКонтейнерах
		|		ПО ПолномочияПользователей.Владелец = ПользователиВКонтейнерах.Контейнер
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ПрофилиГруппДоступа.Роли КАК ПрофилиГруппДоступаРоли
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ПраваРолей КАК ПраваРолей
		|			ПО ПрофилиГруппДоступаРоли.Роль = ПраваРолей.Роль
		|		ПО ПолномочияПользователей.Полномочия = ПрофилиГруппДоступаРоли.Ссылка
		|ГДЕ
		|	ПользователиВКонтейнерах.Пользователь = &Пользователь
		|	И ПраваРолей.ОбъектМетаданных = &ОбъектМетаданных");
		
	ОбъектМетаданных = ОбщегоНазначения.ИдентификаторОбъектаМетаданных(Таблица);
	Запрос.УстановитьПараметр("ОбъектМетаданных", ОбъектМетаданных);
	Запрос.УстановитьПараметр("Пользователь", Пользователь);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Выборка.Следующий();
	
	Результат = Новый Структура("Чтение, Изменение");
	ЗаполнитьЗначенияСвойств(Результат, Выборка);
	
	Возврат Результат;
	
КонецФункции

// Возвращает таблицу прав объекта, отсортированную по наименованию пользователя и с колонкой "Картинка"
// 
Функция ПолучитьТаблицуПравДляОтображенияВИнтерфейсе(ОбъектДоступа) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ТаблицаПрав = ДокументооборотПраваДоступа.ПолучитьПраваПользователейПоОбъекту(ОбъектДоступа, Истина);
	
	// Сортировка таблицы и фильтр по ролям пользователей
	Запрос = Новый Запрос(
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ПользователиВКонтейнерах.Пользователь
		|ПОМЕСТИТЬ ПользователиСРолями
		|ИЗ
		|	РегистрСведений.ПраваРолей КАК ПраваРолей
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ПрофилиГруппДоступа.Роли КАК ПрофилиГруппДоступаРоли
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ПолномочияПользователей КАК ПолномочияПользователей
		|				ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ПользователиВКонтейнерах КАК ПользователиВКонтейнерах
		|				ПО ПолномочияПользователей.Владелец = ПользователиВКонтейнерах.Контейнер
		|			ПО ПрофилиГруппДоступаРоли.Ссылка = ПолномочияПользователей.Полномочия
		|		ПО ПраваРолей.Роль = ПрофилиГруппДоступаРоли.Роль
		|ГДЕ
		|	ПраваРолей.ОбъектМетаданных = &ОбъектМетаданных
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаПрав.*
		|ПОМЕСТИТЬ ТаблицаПрав
		|ИЗ
		|	&ТаблицаПрав КАК ТаблицаПрав
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаПрав.*,
		|	Пользователи.Наименование КАК НаименованиеПользователя
		|ИЗ
		|	ТаблицаПрав КАК ТаблицаПрав
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Пользователи КАК Пользователи
		|		ПО ТаблицаПрав.Пользователь = Пользователи.Ссылка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПользователиСРолями КАК ПользователиСРолями
		|		ПО ТаблицаПрав.Пользователь = ПользователиСРолями.Пользователь
		|
		|УПОРЯДОЧИТЬ ПО
		|	НаименованиеПользователя");
		
	Запрос.УстановитьПараметр("ТаблицаПрав", ТаблицаПрав);
	Запрос.УстановитьПараметр("ОбъектМетаданных",
		ИдентификаторМетаданныхДляПроверкиПрав(ОбъектДоступа));
	
	ТаблицаПрав = Запрос.Выполнить().Выгрузить();
	
	// Добавление картинки
	ТаблицаПрав.Колонки.Добавить("Картинка");
	ТаблицаПрав.ЗаполнитьЗначения(Истина, "Картинка");
	
	Возврат ТаблицаПрав;
	
КонецФункции

Функция ИдентификаторМетаданныхДляПроверкиПрав(ОбъектДоступа)
	
	Результат = Неопределено;
	ТипОбъекта = ТипЗнч(ОбъектДоступа);
	
	Если ТипОбъекта = Тип("СправочникСсылка.ДескрипторыДоступаОбъектов") Тогда
		Результат = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ОбъектДоступа, "ИдентификаторОбъектаМетаданных");
	ИначеЕсли ТипОбъекта = Тип("СправочникСсылка.ДескрипторыДоступаРегистров") Тогда
		Результат = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ОбъектДоступа, "ОбъектМетаданных");
	Иначе
		Результат = ОбщегоНазначения.ИдентификаторОбъектаМетаданных(ОбъектДоступа.Метаданные());
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Возвращает структуру настроек, которые будут использоваться при получении прав объекта
//
// Параметры:
//  ОбъектДоступа - Ссылка - объект доступа
//  ТипОбъекта - Тип - тип объекта доступа
//	
// Возвращаемое значение: 
//  Структура - структура настроек
//
Функция ПолучитьДопНастройкиДляПолученияПравПоОбъекту(ТипОбъекта) Экспорт
	
	ДопНастройки = Новый Структура;
	ДопНастройки.Вставить("ДобавлятьРуководителейИДелегатов", Истина);
	
	// Исключения
	Если ТипОбъекта = Тип("СправочникСсылка.Инструкции") Тогда
		ДопНастройки.ДобавлятьРуководителейИДелегатов = Ложь;
	КонецЕсли;
	
	// Переопределяемая часть
	ДокументооборотПраваДоступаПереопределяемый.ЗаполнитьДопНастройкиДляПолученияПравПоОбъекту(
		ДопНастройки, ТипОбъекта);
	
	Возврат ДопНастройки;
	
КонецФункции

// Обновляет права доступа по указанному дескриптору
//
Процедура ОбновитьПраваДоступаПоДескриптору(ДескрипторСсылка, Немедленно = Неопределено) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Менеджер = ОбщегоНазначения.МенеджерОбъектаПоСсылке(ДескрипторСсылка);
	Менеджер.ОбновитьПрава(ДескрипторСсылка,, Немедленно);
	
КонецПроцедуры

// Определяет дескрипторы доступа для указанного объекта доступа и записывает их в базу.
// Параметры:
//  ОбъектДоступа - Ссылка, Объект - объект, дескрипторы которого надо определить или ссылка на него.
//  ОпределятьЗависимыеПрава - Булево - признак необходимости определения зависимых прав.
//  ПротоколРасчетаПрав - Массив, Неопределено - протокол расчет прав для отображения в интерфейсе.
//
// Возвращаемое значение:
//  Таблица значений - итоговая таблица дескрипторов объекта.
//
Функция ОпределитьДескрипторыОбъекта(ОбъектДоступа, ОпределятьЗависимыеПрава = Истина, ПротоколРасчетаПрав = Неопределено) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Набор = РегистрыСведений.ДескрипторыДляОбъектов.СоздатьНаборЗаписей();
	ТаблицаДескрипторов = Набор.ВыгрузитьКолонки();
	
	МетаданныеОбъекта = ОбъектДоступа.Метаданные();
	ПолноеИмя = МетаданныеОбъекта.ПолноеИмя();
	МенеджерОбъекта = ОбщегоНазначения.МенеджерОбъектаПоПолномуИмени(ПолноеИмя);
	
	ОписаниеОбъекта = ОписаниеОбъектаДоступа(ОбъектДоступа);
	ОбъектДоступаСсылка = ОписаниеОбъекта.Ссылка;
	Если Не ЗначениеЗаполнено(ОбъектДоступаСсылка) Тогда
		ОбъектДоступаСсылка = ОбъектДоступа.ПолучитьСсылкуНового();
	КонецЕсли;
	
	ЕстьМетодЗаполнитьДескрипторыОбъекта = ОбщегоНазначенияДокументооборотПовтИсп.ЕстьМетодМенеджера(
		ПолноеИмя, "ЗаполнитьДескрипторыОбъекта");
	
	Если ЕстьМетодЗаполнитьДескрипторыОбъекта Тогда
		МенеджерОбъекта.ЗаполнитьДескрипторыОбъекта(ОписаниеОбъекта, ТаблицаДескрипторов, ПротоколРасчетаПрав);
	Иначе
		ЗаполнитьДескрипторыОбъектаСтандартно(ОписаниеОбъекта, ТаблицаДескрипторов);
	КонецЕсли;
	
	// Если используются рабочие группы, другие дескрипторы отключаются,
	// кроме дескрипторов локальных администраторов.
	Если ТаблицаДескрипторов.Найти(1, "ТипДескриптора") <> Неопределено Тогда
		
		Для Каждого СтрокаДескриптора Из ТаблицаДескрипторов Цикл
			Если СтрокаДескриптора.ТипДескриптора <> 1			// Рабочая группа.
				И СтрокаДескриптора.ТипДескриптора <> 4 Тогда	// Локальный администратор.
				СтрокаДескриптора.Отключен = Истина;
			КонецЕсли;
		КонецЦикла;
		
		Если ПротоколРасчетаПрав <> Неопределено Тогда
			ЗаписьПротокола = Новый Структура("Элемент, Описание",
				"РабочаяГруппа", НСтр("ru = 'Рабочая группа'; en = 'Working group'"));
			ПротоколРасчетаПрав.Добавить(ЗаписьПротокола);
		КонецЕсли;
		
	КонецЕсли;
	
	РегистрыСведений.ДескрипторыДляОбъектов.Сохранить(
		ТаблицаДескрипторов, ОбъектДоступаСсылка, ОпределятьЗависимыеПрава);
	
	Возврат ТаблицаДескрипторов;
	
КонецФункции

// Определяет дескрипторы доступа для указанного объекта доступа.
// 
Процедура ЗаполнитьДескрипторыОбъектаСтандартно(ОписаниеОбъекта, ТаблицаДескрипторов) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ЗаполнитьДескрипторОбъектаОсновной(ОписаниеОбъекта, ТаблицаДескрипторов);
	ЗаполнитьДескрипторыОбъектаПоРабочейГруппе(ОписаниеОбъекта, ТаблицаДескрипторов);
	
	// Дескриптор для локальных администраторов.
	Если ПолучитьФункциональнуюОпцию("ИспользоватьЛокальныхАдминистраторов") Тогда
		ТипыСсылокКОбработке = ДокументооборотПраваДоступаПовтИсп.ТипыСсылокИспользующиеРазрезыДоступа();
		Если ТипыСсылокКОбработке.Найти(ТипЗнч(ОписаниеОбъекта.Ссылка)) <> Неопределено Тогда
			ЗаполнитьДескрипторОбъектаДляЛокальныхАдминистраторов(ОписаниеОбъекта, ТаблицаДескрипторов);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// Определяет дескриптор доступа для указанного объекта доступа, 
// добавляет строку в переданную таблицу дескрипторов.
// 
Процедура ЗаполнитьДескрипторОбъектаОсновной(ОписаниеОбъекта, ТаблицаДескрипторов) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ДескрипторСсылка = ПолучитьОсновнойДескриптор(ОписаниеОбъекта);
	Стр = ТаблицаДескрипторов.Добавить();
	Стр.Дескриптор = ДескрипторСсылка;
	Стр.ТипДескриптора = 0;
	
КонецПроцедуры

// Определяет дескрипторы объекта в соответствии с рабочей группой,
// Возвращает Истина, если есть такие дескрипторы.
// 
Функция ЗаполнитьДескрипторыОбъектаПоРабочейГруппе(ОписаниеОбъекта, ТаблицаДескрипторов) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ТипыОбъектовСРабочейГруппой = Метаданные.РегистрыСведений.РабочиеГруппы.Измерения.Объект.Тип;
	Если Не ТипыОбъектовСРабочейГруппой.СодержитТип(ТипЗнч(ОписаниеОбъекта.Ссылка)) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	РабочиеГруппы.Участник КАК Участник,
		|	РабочиеГруппы.Изменение КАК Изменение,
		|	ЕСТЬNULL(ДескрипторыДоступаОбъектов.Ссылка, НЕОПРЕДЕЛЕНО) КАК Дескриптор
		|ИЗ
		|	РегистрСведений.РабочиеГруппы КАК РабочиеГруппы
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ДескрипторыДоступаОбъектов КАК ДескрипторыДоступаОбъектов
		|		ПО РабочиеГруппы.Участник = ДескрипторыДоступаОбъектов.КонтейнерПользователей
		|			И РабочиеГруппы.Изменение = ДескрипторыДоступаОбъектов.Изменение
		|			И (ДескрипторыДоступаОбъектов.ИдентификаторОбъектаМетаданных = &ОбъектМетаданных)
		|ГДЕ
		|	РабочиеГруппы.Объект = &Объект
		|	И РабочиеГруппы.Участник <> НЕОПРЕДЕЛЕНО");
		
	ОбъектМетаданных = ОбщегоНазначения.ИдентификаторОбъектаМетаданных(ОписаниеОбъекта.Ссылка.Метаданные());
	Запрос.УстановитьПараметр("Объект", ОписаниеОбъекта.Ссылка);
	Запрос.УстановитьПараметр("ОбъектМетаданных", ОбъектМетаданных);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		Дескриптор = Выборка.Дескриптор;
		Если Не ЗначениеЗаполнено(Дескриптор) Тогда
			ДескрипторОбъект = Справочники.ДескрипторыДоступаОбъектов.СоздатьДескрипторИндивидуальный(
				ОбъектМетаданных, Выборка.Участник, Выборка.Изменение);
			ДескрипторОбъект.Записать();
			Дескриптор = ДескрипторОбъект.Ссылка;
		КонецЕсли;
		
		Стр = ТаблицаДескрипторов.Добавить();
		Стр.Дескриптор = Дескриптор;
		Стр.ТипДескриптора = 1;
		
	КонецЦикла;
	
	ЕстьДескрпиторыРГ = Выборка.Количество() > 0;
	
	Возврат ЕстьДескрпиторыРГ;
	
КонецФункции

// Определяет дескриптор доступа, который будет хранить права лок. администраторов,
// добавляет строку в переданную таблицу дескрипторов.
// 
Функция ЗаполнитьДескрипторОбъектаДляЛокальныхАдминистраторов(ОписаниеОбъекта, ТаблицаДескрипторов) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	НайденнаяСтрока = ТаблицаДескрипторов.Найти(0, "ТипДескриптора");
	ОсновнойДескриптор = ?(НайденнаяСтрока = Неопределено, Неопределено, НайденнаяСтрока.Дескриптор);
	ДескрипторСсылка = ПолучитьДескрипторДляЛокальныхАдминистраторов(ОписаниеОбъекта, ОсновнойДескриптор);
	
	Стр = ТаблицаДескрипторов.Добавить();
	Стр.Дескриптор = ДескрипторСсылка;
	Стр.ТипДескриптора = 4;
	
КонецФункции

// Добавляет в таблицу дескрипторов объекта индивидуальный дескриптор,
// найденный по переданному контейнеру пользователей.
// 
Процедура ДобавитьИндивидуальныйДескриптор(
			ОписаниеОбъекта, ТаблицаДескрипторов, КонтейнерПользователей, Изменение) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если Не ЗначениеЗаполнено(КонтейнерПользователей) Тогда
		Возврат;
	КонецЕсли;
	
	Дескриптор = ПолучитьИндивидуальныйДескриптор(ОписаниеОбъекта, КонтейнерПользователей, Изменение);
	
	Стр = ТаблицаДескрипторов.Добавить();
	Стр.Дескриптор = Дескриптор;
	Стр.ТипДескриптора = 2; // Индивидуальный дескриптор.
	
КонецПроцедуры

// Заполняет дескрипторы от владельца.
// 
Процедура ЗаполнитьДескрипторыОбъектаОтВладельца(ОписаниеОбъекта, ТаблицаДескрипторов, Владелец) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ДескрипторыДляОбъектов.Дескриптор
		|ИЗ
		|	РегистрСведений.ДескрипторыДляОбъектов КАК ДескрипторыДляОбъектов
		|ГДЕ
		|	ДескрипторыДляОбъектов.Объект = &Владелец
		|	И НЕ ДескрипторыДляОбъектов.Отключен");
		
		
	Запрос.УстановитьПараметр("Владелец", Владелец);
	
	Результат = Запрос.Выполнить();
	Если Не Результат.Пустой() Тогда
		
		Выборка = Результат.Выбрать();
		Пока Выборка.Следующий() Цикл
			НоваяСтрока = ТаблицаДескрипторов.Добавить();
			НоваяСтрока.Дескриптор = Выборка.Дескриптор;
			НоваяСтрока.ТипДескриптора = 3;
			НоваяСтрока.ПолученОт = Владелец;
		КонецЦикла;
		
	Иначе
		
		// Если у владельца нет дескрипторов, подставляется индивидуальный дескриптор текущего пользователя.
		// Тогда при появлении дескрипторов у владельца они будут назначены и подчиненному объекту.
		// Если оставить таблицу пустой, этого не произойдет.
		
		НоваяСтрока = ТаблицаДескрипторов.Добавить();
		НоваяСтрока.Дескриптор = ПолучитьИндивидуальныйДескриптор(
			ОписаниеОбъекта, Пользователи.ТекущийПользователь(), Ложь);
		НоваяСтрока.ТипДескриптора = 3;
		НоваяСтрока.ПолученОт = Владелец;
		
	КонецЕсли;
	
КонецПроцедуры

// Определяет основной дескриптор доступа для указанного объекта доступа.
// Работает по аналогии с ОпределитьДескрипторыОбъекта, но предназначен для тех случаев,
// когда нужно переопределить только основной дескриптор, не трогая остальные.
// 
Функция ОпределитьДескрипторОбъектаОсновной(ОбъектДоступаСсылка,
			ОпределятьЗависимыеПрава = Истина, ПроверятьНаличиеОсновногоДескриптора = Истина) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	ДескрипторыДляОбъектов.Дескриптор,
		|	ДескрипторыДляОбъектов.Отключен
		|ИЗ
		|	РегистрСведений.ДескрипторыДляОбъектов КАК ДескрипторыДляОбъектов
		|ГДЕ
		|	ДескрипторыДляОбъектов.Объект = &Объект
		|	И ДескрипторыДляОбъектов.ТипДескриптора = 0");
		
	Запрос.УстановитьПараметр("Объект", ОбъектДоступаСсылка);
	Выборка = Запрос.Выполнить().Выбрать();
	Результат = Запрос.Выполнить();
	
	Если ПроверятьНаличиеОсновногоДескриптора И Результат.Пустой() Тогда
		// Объект не использует основной дескриптор, ничего делать не нужно.
		Возврат Неопределено;
	КонецЕсли;
	
	СтарыйДескриптор = Неопределено;
	СтарыйДескрипторОтключен = Ложь;
	
	Если Не Результат.Пустой() Тогда
		Выборка = Результат.Выбрать();
		Выборка.Следующий();
		СтарыйДескриптор = Выборка.Дескриптор;
		СтарыйДескрипторОтключен = Выборка.Отключен;
	КонецЕсли;
	
	НовыйДескриптор = ПолучитьОсновнойДескриптор(ОбъектДоступаСсылка);
	Если НовыйДескриптор <> СтарыйДескриптор Тогда
		
		Набор = РегистрыСведений.ДескрипторыДляОбъектов.СоздатьНаборЗаписей();
		Набор.Отбор.Объект.Установить(ОбъектДоступаСсылка);
		Набор.Отбор.ТипДескриптора.Установить(0);
		
		Стр = Набор.Добавить();
		Стр.Объект = ОбъектДоступаСсылка;
		Стр.Дескриптор = НовыйДескриптор;
		Стр.ТипДескриптора = 0;
		Стр.Отключен = СтарыйДескрипторОтключен;
		
		Если Не ОпределятьЗависимыеПрава Тогда
			Набор.ДополнительныеСвойства.Вставить("ПропуститьОпределениеЗависимыхПрав", Истина);
		КонецЕсли;
		
		Набор.Записать();
		
	КонецЕсли;
	
	Возврат НовыйДескриптор;
	
КонецФункции

// Определяет дескриптор(ы) доступа для указанного набора записей
// При необходимости создает новый дескриптор и вычисляет права
Функция ОпределитьДескрипторДоступаНабораЗаписей(НаборЗаписей) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	МетаданныеНабораЗаписей = НаборЗаписей.Метаданные();
	ИдентификаторОбъектаМетаданных = ОбщегоНазначения.ИдентификаторОбъектаМетаданных(МетаданныеНабораЗаписей);
	
	Для каждого Запись Из НаборЗаписей Цикл
		
		Дескриптор = ПолучитьДескрипторДоступаЗаписиНабораЗаписей(
			ИдентификаторОбъектаМетаданных, 
			Запись);
		
	КонецЦикла;
	
	Возврат Дескриптор;
	
КонецФункции	

// Копирует права указанного дескриптора в указанные ПраваДоступа
Процедура СкопироватьПраваДескриптора(ДескрипторДоступа, ПраваДоступа) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Для Каждого Эл из ПраваДоступа Цикл
		ВызватьИсключение НСтр("ru = 'Для копирования прав объекта указан не пустой набор прав.'; en = 'Not empty permissions set is specifyed for copying permissions.'");
	КонецЦикла;
	
	Если ЗначениеЗаполнено(ДескрипторДоступа) Тогда
	
		Запрос = Новый Запрос;
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	ПраваПоДескрипторамДоступаОбъектов.Добавление,
			|	ПраваПоДескрипторамДоступаОбъектов.Изменение,
			|	ПраваПоДескрипторамДоступаОбъектов.Удаление,
			|	ПраваПоДескрипторамДоступаОбъектов.УправлениеПравами,
			|	ПраваПоДескрипторамДоступаОбъектов.Чтение,
			|	ПраваПоДескрипторамДоступаОбъектов.Пользователь
			|ИЗ
			|	РегистрСведений.ПраваПоДескрипторамДоступаОбъектов КАК ПраваПоДескрипторамДоступаОбъектов
			|ГДЕ
			|	ПраваПоДескрипторамДоступаОбъектов.Дескриптор = &Дескриптор";

		Запрос.УстановитьПараметр("Дескриптор", ДескрипторДоступа);
		Результат = Запрос.Выполнить();
		ВыборкаДетальныеЗаписи = Результат.Выбрать();

		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			
			ПраваПользователя = Новый Структура("Чтение, Добавление, Изменение, Удаление, УправлениеПравами");
			ПраваПользователя.Чтение = ВыборкаДетальныеЗаписи.Чтение;
			ПраваПользователя.Добавление = ВыборкаДетальныеЗаписи.Изменение; // По умолчанию право добавления не отличается от права изменения
			ПраваПользователя.Изменение = ВыборкаДетальныеЗаписи.Изменение;
			ПраваПользователя.Удаление = ВыборкаДетальныеЗаписи.Изменение; // По умолчанию право удаления не отличается от права изменения
			ПраваПользователя.УправлениеПравами = ВыборкаДетальныеЗаписи.УправлениеПравами;
			
			ПраваДоступа.Вставить(ВыборкаДетальныеЗаписи.Пользователь, ПраваПользователя);
			
		КонецЦикла;
		
	КонецЕсли;	
	
КонецПроцедуры

// Удаляет все данные, связанные с правами доступа
// Используется для полного обновления прав доступа ко всем данным информационной базы
Процедура УдалитьВсеДанныеОПравахДоступа(Немедленно = Неопределено) Экспорт
	
	Если Немедленно <> Истина
		И ДокументооборотПраваДоступаПовтИсп.ОтложенноеОбновлениеПравДоступа() Тогда
		
		// Полная очистка очереди.
		РегистрыСведений.ОчередьОбновленияПравДоступа.Очистить(Ложь);
		
		// Задание на удаление прав.
	  	РегистрыСведений.ОчередьОбновленияПравДоступа.Добавить(
			Перечисления.ЗаданияОчередиОбновленияПрав.ОчиститьВсеПрава, Дата("00000000"), 2);
		
		Возврат;
		
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	// Удаление всех прав объектов
	Набор = РегистрыСведений.ПраваПоДескрипторамДоступаОбъектов.СоздатьНаборЗаписей();
	Набор.Записать();
	
	Набор = РегистрыСведений.ДескрипторыДляОбъектов.СоздатьНаборЗаписей();
	Набор.Записать();
	
	Выборка = Справочники.ДескрипторыДоступаОбъектов.Выбрать();
	Пока Выборка.Следующий() Цикл
		Объект = Выборка.ПолучитьОбъект();
		Объект.Удалить();	
	КонецЦикла;
	
	// Удаление всех прав регистров 
	Набор = РегистрыСведений.ПраваПоДескрипторамДоступаРегистров.СоздатьНаборЗаписей();
	Набор.Записать();
	
	Выборка = Справочники.ДескрипторыДоступаРегистров.Выбрать();
	Пока Выборка.Следующий() Цикл
		Объект = Выборка.ПолучитьОбъект();
		Объект.Удалить();	
	КонецЦикла;
	
	// Очистка очереди
	РегистрыСведений.ОчередьОбновленияПравДоступа.Очистить();
	
КонецПроцедуры

// Обновляет сведения о правах доступа всех данных информационной базы.
Процедура ОбновитьПраваВсехДанных(Немедленно = Неопределено) Экспорт
	
	Если Немедленно <> Истина
		И ДокументооборотПраваДоступаПовтИсп.ОтложенноеОбновлениеПравДоступа() Тогда
		
		РегистрыСведений.ОчередьОбновленияПравДоступа.Добавить(
			Перечисления.ЗаданияОчередиОбновленияПрав.РассчитатьВсеПрава, Дата("00000000"), 2);
		
		Возврат;
		
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	// ТСК Талько Э.Г.; 09.06.2018; Управление доступом {
	ра_ОбщегоНазначения.ОбновитьОсновныеДескрипторыДоступа();
	// ТСК Талько Э.Г.; 09.06.2018; Управление доступом }
	
	// Обновление состава субъектов
	РегистрыСведений.СоставСубъектовПравДоступа.ОбновитьВсеДанные();
	
	// Объекты, права по которым должны рассчитываться в последнюю очередь
	ЗависимыеОбъекты = ДокументооборотПраваДоступаПереопределяемый.ОбъектыСЗависимымиПравами();
	
	// Пересчет прав
	ОбъектыМеханизмаПрав = ДокументооборотПраваДоступаПовтИсп.ВсеОбъектыМеханизмаПравДоступа();
	Для Каждого ОбъектМетаданных Из ОбъектыМеханизмаПрав Цикл 
		Если ЗависимыеОбъекты.Найти(ОбъектМетаданных) = Неопределено Тогда
			ОпределитьПраваОбъектовТаблицы(ОбъектМетаданных);
		КонецЕсли;
	КонецЦикла;
	
	// Пересчет прав зависимых объектов
	Для Каждого ЗависимыйОбъект Из ЗависимыеОбъекты Цикл 
		ОпределитьПраваОбъектовТаблицы(ЗависимыйОбъект);
	КонецЦикла;
	
	// Права объектов, которые не входят в механизм прав, но имеют файлы.
	Запрос = Новый Запрос(
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Файлы.ВладелецФайла
		|ИЗ
		|	Справочник.Файлы КАК Файлы
		|ГДЕ
		|	ТИПЗНАЧЕНИЯ(Файлы.ВладелецФайла) В (&ДоступныеТипыВладельцев)
		|	И НЕ ТИПЗНАЧЕНИЯ(Файлы.ВладелецФайла) В (&ТипыСсылокМеханизмаПрав)");
	
	Запрос.УстановитьПараметр("ДоступныеТипыВладельцев",
		Метаданные.РегистрыСведений.ДескрипторыДляОбъектов.Измерения.Объект.Тип.Типы());
	Запрос.УстановитьПараметр("ТипыСсылокМеханизмаПрав",
		ДокументооборотПраваДоступаПовтИсп.ТипыСсылокИспользующихДоступПоДескрипторам());
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ДокументооборотПраваДоступа.ОпределитьДескрипторыОбъекта(Выборка.ВладелецФайла);
	КонецЦикла;
	
	// Пересчет прав всех регистров
	ТипыВладельцев = Метаданные.ПодпискиНаСобытия.ДокументооборотПраваДоступаПередЗаписьюНабораЗаписей.Источник.Типы();
	Для Каждого Тип Из ТипыВладельцев Цикл 
		
		МетаданныеРегистра = Метаданные.НайтиПоТипу(Тип);
		ПолноеИмя = МетаданныеРегистра.ПолноеИмя(); 
		ОпределитьПраваПоИмениРегистра(ПолноеИмя);
		
	КонецЦикла;
	
КонецПроцедуры	

// Определяет права доступа для всех объектов указанной таблицы
// 
Процедура ОпределитьПраваОбъектовТаблицы(ОбъектМетаданных, Немедленно = Ложь) Экспорт
	
	Если Немедленно <> Истина
		И ДокументооборотПраваДоступаПовтИсп.ОтложенноеОбновлениеПравДоступа() Тогда
		
		РегистрыСведений.ОчередьОбновленияПравДоступа.Добавить(
			ОбъектМетаданных, Дата("00000000"), 2, "ОпределитьПрава");
		
		Возврат;
		
	КонецЕсли;
	
    Запрос = Новый Запрос;
    Запрос.Текст = 
		"ВЫБРАТЬ
		|    Данные.Ссылка
		|ИЗ
		|    [Таблица] КАК Данные";
	
    Запрос.Текст = СтрЗаменить(Запрос.Текст, "[Таблица]",
		ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ОбъектМетаданных, "ПолноеИмя"));
	
    Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Попытка
			ДокументооборотПраваДоступа.ОпределитьДескрипторыОбъекта(Выборка.Ссылка);
		Исключение
			ЗаписьЖурналаРегистрации(
				НСтр("ru = 'Обновление прав доступа'; en = 'Permissions update'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка()),
				УровеньЖурналаРегистрации.Ошибка, Выборка.Ссылка.Метаданные(), Выборка.Ссылка,
				ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		КонецПопытки;
	КонецЦикла;
	
КонецПроцедуры

Процедура ОпределитьПраваОбъектовТаблицыДляЛокальныхАдминиистраторов(ОбъектМетаданных, Немедленно = Ложь) Экспорт
	
	Если Немедленно <> Истина
		И ДокументооборотПраваДоступаПовтИсп.ОтложенноеОбновлениеПравДоступа() Тогда
		
		РегистрыСведений.ОчередьОбновленияПравДоступа.Добавить(
			ОбъектМетаданных, Дата("00000000"), 2, "ОпределитьПраваДляЛокальныхАдминиистраторов");
		
		Возврат;
		
	КонецЕсли;
	
	ДанныеОбработаны = Ложь;
	Пока Не ДанныеОбработаны Цикл
		
	    Запрос = Новый Запрос;
	    Запрос.Текст = 
			"ВЫБРАТЬ ПЕРВЫЕ 10000
			|	Данные.Ссылка,
			|	ДескрипторыДляОбъектовОсновные.Дескриптор
			|ИЗ
			|	[Таблица] КАК Данные
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ДескрипторыДляОбъектов КАК ДескрипторыДляОбъектовОсновные
			|		ПО Данные.Ссылка = ДескрипторыДляОбъектовОсновные.Объект
			|			И (ДескрипторыДляОбъектовОсновные.ТипДескриптора = 0)
			|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДескрипторыДляОбъектов КАК ДескрипторыДляОбъектовЛокАдм
			|		ПО Данные.Ссылка = ДескрипторыДляОбъектовЛокАдм.Объект
			|			И (ДескрипторыДляОбъектовЛокАдм.ТипДескриптора = 4)
			|ГДЕ
			|	ДескрипторыДляОбъектовЛокАдм.Объект ЕСТЬ NULL";
		
	    Запрос.Текст = СтрЗаменить(Запрос.Текст, "[Таблица]",
			ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ОбъектМетаданных, "ПолноеИмя")); 
		
	    Выборка = Запрос.Выполнить().Выбрать(); 
		Пока Выборка.Следующий() Цикл 
			
			Попытка
				ОписаниеОбъекта = ОписаниеОбъектаДоступа(Выборка.Ссылка);
				Дескриптор = ПолучитьДескрипторДляЛокальныхАдминистраторов(ОписаниеОбъекта, Выборка.Дескриптор);
				Запись = РегистрыСведений.ДескрипторыДляОбъектов.СоздатьМенеджерЗаписи();
				Запись.Объект = Выборка.Ссылка;
				Запись.Дескриптор = Дескриптор;
				Запись.ОбъектМетаданных = ОбъектМетаданных;
				Запись.ТипДескриптора = 4;
				Запись.Записать();
			Исключение
				ЗаписьЖурналаРегистрации(
					НСтр("ru = 'Назначение дескриптора для локальных администраторов'; en = 'Descriptor assignment for local administrators'",
						ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка()),
					УровеньЖурналаРегистрации.Ошибка, Выборка.Ссылка.Метаданные(), Выборка.Ссылка,
					ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			КонецПопытки;
			
		КонецЦикла;
		
		ДанныеОбработаны = Выборка.Количество() = 0;
		
	КонецЦикла;
	
КонецПроцедуры		

// Определяет права доступа для всех объектов указанной таблицы
// 
Процедура РассчитатьПраваОбъектовТаблицы(ОбъектМетаданных, Немедленно = Ложь) Экспорт
	
	Если Немедленно <> Истина
		И ДокументооборотПраваДоступаПовтИсп.ОтложенноеОбновлениеПравДоступа() Тогда
		
		РегистрыСведений.ОчередьОбновленияПравДоступа.Добавить(
			ОбъектМетаданных, Дата("00000000"), 2, "РассчитатьПрава");
		
		Возврат;
		
	КонецЕсли;
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	ДескрипторыДоступаОбъектов.Ссылка
		|ИЗ
		|	Справочник.ДескрипторыДоступаОбъектов КАК ДескрипторыДоступаОбъектов
		|ГДЕ
		|	ДескрипторыДоступаОбъектов.ИдентификаторОбъектаМетаданных = &ИдентификаторОбъектаМетаданных");
	
	Запрос.УстановитьПараметр("ИдентификаторОбъектаМетаданных", ОбъектМетаданных);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Справочники.ДескрипторыДоступаОбъектов.ОбновитьПрава(Выборка.Ссылка);
	КонецЦикла;
	
КонецПроцедуры		

// Определяет права доступа для всех записей указанного регистра.
// 
// Параметры:
// 	ПолноеИмя - Строка - полное имя регистра.
// 
Процедура ОпределитьПраваПоИмениРегистра(ПолноеИмя) Экспорт
	
	МенеджерРегистра = ОбщегоНазначения.МенеджерОбъектаПоПолномуИмени(ПолноеИмя);
	
	СведенияОПолях = Новый Структура("ОбъектДоступа1, ОбъектДоступа2, ОбъектДоступа3");
	МенеджерРегистра.ЗаполнитьСведенияОПоляхДоступа(СведенияОПолях);
	
	// Пересчет прав для всех уникальных сочетаний правообразующих измерений всех регистров.
	Запрос = Новый Запрос; 
	Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ ";
	
	// Добавление к запросу правообразующих полей
	Запрос.Текст = Запрос.Текст + "Регистр." + СведенияОПолях.ОбъектДоступа1;
	Если СведенияОПолях.ОбъектДоступа2 <> Неопределено Тогда
		Запрос.Текст = Запрос.Текст + ", Регистр." + СведенияОПолях.ОбъектДоступа2;
	КонецЕсли;	
	Если СведенияОПолях.ОбъектДоступа3 <> Неопределено Тогда
		Запрос.Текст = Запрос.Текст + ", Регистр." + СведенияОПолях.ОбъектДоступа3;
	КонецЕсли;
	
	Запрос.Текст = Запрос.Текст + Символы.ПС + "ИЗ " + ПолноеИмя + " КАК Регистр";
	
	Выборка = Запрос.Выполнить().Выбрать();
	НаборЗаписей = МенеджерРегистра.СоздатьНаборЗаписей();
	
	Пока Выборка.Следующий() Цикл
		
		Запись = НаборЗаписей.Добавить();
		Запись[СведенияОПолях.ОбъектДоступа1] = Выборка[СведенияОПолях.ОбъектДоступа1];
		Если СведенияОПолях.ОбъектДоступа2 <> Неопределено Тогда
			Запись[СведенияОПолях.ОбъектДоступа2] = Выборка[СведенияОПолях.ОбъектДоступа2];
		КонецЕсли;
		Если СведенияОПолях.ОбъектДоступа3 <> Неопределено Тогда
			Запись[СведенияОПолях.ОбъектДоступа3] = Выборка[СведенияОПолях.ОбъектДоступа3];
		КонецЕсли;
		
	КонецЦикла;
	
	// Определение дескриптора для набора записей
	ДокументооборотПраваДоступа.ОпределитьДескрипторДоступаНабораЗаписей(НаборЗаписей);
	
КонецПроцедуры

// Обновляет сведения о правах доступа всех разрезов доступа - 
//  организаций, видов документов и т.д.
// 
Процедура ОбновитьПраваВсехРазрезовДоступа(Немедленно = Неопределено) Экспорт
	
	Если Немедленно <> Истина
		И ДокументооборотПраваДоступаПовтИсп.ОтложенноеОбновлениеПравДоступа() Тогда
		
		РегистрыСведений.ОчередьОбновленияПравДоступа.Добавить(
			Перечисления.ЗаданияОчередиОбновленияПрав.РассчитатьПраваРазрезовДоступа,, 2);
		
		Возврат;
		
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	// Пересчет прав.
	Запрос = Новый Запрос(
 		"ВЫБРАТЬ
 		|	ДескрипторыДоступаОбъектов.Ссылка
 		|ИЗ
 		|	Справочник.ДескрипторыДоступаОбъектов КАК ДескрипторыДоступаОбъектов
 		|ГДЕ
 		|	ДескрипторыДоступаОбъектов.ИдентификаторОбъектаМетаданных В(&Идентификаторы)");
 	
 	Запрос.УстановитьПараметр("Идентификаторы", ОбъектыМетаданныхВсехЗначенийДоступа());
	
	Выборка = Запрос.Выполнить().Выбрать();
 	Пока Выборка.Следующий() Цикл
 		Справочники.ДескрипторыДоступаОбъектов.ОбновитьПрава(Выборка.Ссылка);
 	КонецЦикла;
	
КонецПроцедуры

Функция ОбъектыМетаданныхВсехЗначенийДоступа() Экспорт
	
	Результат = Новый Массив;
	
	ПараметрыОграниченияДоступа = 
		СтандартныеПодсистемыПовтИсп.ПараметрыРаботыПрограммы("ПараметрыОграниченияДоступа");
	ТаблицаСвойствВидовДоступа = ПараметрыОграниченияДоступа.СвойстваВидовДоступа.Таблица.Получить();
	
	Для Каждого СтрВидаДоступа Из ТаблицаСвойствВидовДоступа Цикл
		Для Каждого ИмяТаблицы Из СтрВидаДоступа.Таблицы Цикл
			Результат.Добавить(ОбщегоНазначения.ИдентификаторОбъектаМетаданных(ИмяТаблицы));
		КонецЦикла;
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

// Возвращает Истина если у указанного пользователя есть право регистрации переданного документа.
// 
Функция ЕстьПравоРегистрации(Документ, Пользователь = Неопределено) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если Не ДокументооборотПраваДоступаПовтИсп.ВключеноИспользованиеПравДоступа() Тогда
		Возврат Истина;
	КонецЕсли;
	
	Если Пользователь = Неопределено Тогда
		Пользователь = ПользователиКлиентСервер.ТекущийПользователь();
	КонецЕсли;
	
	Если ПользователиСерверПовтИсп.ЭтоПолноправныйПользовательИБ(Пользователь) Тогда
		Возврат Истина;
	КонецЕсли;
	
	// Проверка политик доступа.
	
	ЕстьПраво = Ложь;
	МетаданныеДокумента = Документ.Метаданные();
	ДескрипторОбъекта = ПолучитьОсновнойДескриптор(Документ);
	ОбъектМетаданных = ОбщегоНазначения.ИдентификаторОбъектаМетаданных(МетаданныеДокумента);
	
	ОтключенныеРазрезыДоступа = ДокументооборотПраваДоступаПовтИсп.ОтключенныеРазрезыДоступа();
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ИСТИНА КАК ЕстьЗаписи
		|ИЗ
		|	РегистрСведений.РазрешенияДоступаИсключительные КАК РазрешенияДоступаИсключительные
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ПользователиВКонтейнерах КАК ПользователиВКонтейнерах
		|		ПО РазрешенияДоступаИсключительные.Пользователь = ПользователиВКонтейнерах.Контейнер
		|ГДЕ
		|	РазрешенияДоступаИсключительные.Предмет = &Предмет
		|	И ПользователиВКонтейнерах.Пользователь = &Пользователь");
	
	Запрос.УстановитьПараметр("Пользователь", Пользователь);
	Запрос.УстановитьПараметр("Предмет", ОбъектМетаданных);
	
	ИспользуютсяСпециальныеРазрешения = Не Запрос.Выполнить().Пустой();
	Если ИспользуютсяСпециальныеРазрешения Тогда
		
		// Пользователю назначены спец. разрешения, общие разрешения не проверяются.
		ЗапросПоДескрипторам = ДокументооборотПраваДоступаПовтИсп.ЗапросПоДескрипторамСВидамиДоступа();
		ЗапросПоДескрипторам.УстановитьПараметр("Дескрипторы", ДескрипторОбъекта);
		ТаблицаДескрипторов = ЗапросПоДескрипторам.Выполнить().Выгрузить();
		
		ЗаполненныеРазрезыДоступа = Новый Массив;
		Для Каждого Стр Из ТаблицаДескрипторов Цикл
			Если ЗначениеЗаполнено(Стр.ЗначениеДоступа)
				И ОтключенныеРазрезыДоступа.Найти(Стр.ЗначениеДоступа) = Неопределено
				И ЗаполненныеРазрезыДоступа.Найти(Стр.ЗначениеДоступа) = Неопределено Тогда
				ЗаполненныеРазрезыДоступа.Добавить(Стр.ЗначениеДоступа);
			КонецЕсли;
		КонецЦикла;
		
		Если ЗаполненныеРазрезыДоступа.Количество() = 0 Тогда
			// Политики доступа не влияют на право регистрации.
			Возврат Истина;
		КонецЕсли;
		
		Запрос = Новый Запрос(
			"ВЫБРАТЬ ПЕРВЫЕ 1
			|	РазрешенияИсключительные.Коллекция,
			|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ РазрешенияИсключительные.ЗначениеДоступа) КАК КолРазрезовДоступа
			|ИЗ
			|	(ВЫБРАТЬ РАЗЛИЧНЫЕ
			|		КоллекцииЗначенийДоступаЗначенияДоступа.Ссылка КАК Коллекция,
			|		РазрешенияДляРазрезовДоступа.РазрезДоступа КАК ЗначениеДоступа
			|	ИЗ
			|		РегистрСведений.РазрешенияДляРазрезовДоступа КАК РазрешенияДляРазрезовДоступа
			|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КоллекцииЗначенийДоступа.ЗначенияДоступа КАК КоллекцииЗначенийДоступаЗначенияДоступа
			|				ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.РазрешенияДоступаИсключительные КАК РазрешенияДоступаИсключительные
			|					ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ПользователиВКонтейнерах КАК ПользователиВКонтейнерах
			|					ПО РазрешенияДоступаИсключительные.Пользователь = ПользователиВКонтейнерах.Контейнер
			|					ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.УровниДоступа КАК УровниДоступа
			|					ПО РазрешенияДоступаИсключительные.УровеньДоступа = УровниДоступа.Ссылка
			|				ПО КоллекцииЗначенийДоступаЗначенияДоступа.Ссылка = РазрешенияДоступаИсключительные.КоллекцияЗначенийДоступа
			|			ПО РазрешенияДляРазрезовДоступа.Разрешение = КоллекцииЗначенийДоступаЗначенияДоступа.Значение
			|	ГДЕ
			|		УровниДоступа.Регистрация
			|		И РазрешенияДляРазрезовДоступа.РазрезДоступа В(&РазрезыДоступаДескриптора)
			|		И ПользователиВКонтейнерах.Пользователь = &Пользователь
			|		И РазрешенияДоступаИсключительные.Предмет = &Предмет) КАК РазрешенияИсключительные
			|
			|СГРУППИРОВАТЬ ПО
			|	РазрешенияИсключительные.Коллекция
			|
			|ИМЕЮЩИЕ
			|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ РазрешенияИсключительные.ЗначениеДоступа) = &КоличествоРазрезов");
			
		Запрос.УстановитьПараметр("Пользователь", Пользователь);
		Запрос.УстановитьПараметр("Предмет", ОбъектМетаданных);
		Запрос.УстановитьПараметр("РазрезыДоступаДескриптора", ЗаполненныеРазрезыДоступа);
		Запрос.УстановитьПараметр("КоличествоРазрезов", ЗаполненныеРазрезыДоступа.Количество());
		
	Иначе
		
		// Общие разрешения.
		ПроверяемыйРазрезДоступа = Неопределено;
		Если МетаданныеДокумента = Метаданные.Справочники.ВнутренниеДокументы Тогда
			ПроверяемыйРазрезДоступа = ПланыВидовХарактеристик.ВидыДоступа.ВидыВнутреннихДокументов;
		ИначеЕсли МетаданныеДокумента = Метаданные.Справочники.ВходящиеДокументы Тогда
			ПроверяемыйРазрезДоступа = ПланыВидовХарактеристик.ВидыДоступа.ВидыВходящихДокументов;
		ИначеЕсли МетаданныеДокумента = Метаданные.Справочники.ИсходящиеДокументы Тогда
			ПроверяемыйРазрезДоступа = ПланыВидовХарактеристик.ВидыДоступа.ВидыИсходящихДокументов;
		// ТСК Талько Э.Г.; 09.06.2018; Управление доступом {
		ИначеЕсли ра_ОбщегоНазначенияКлиентСервер.ЭтоДокументКачества(МетаданныеДокумента) Тогда
			ПроверяемыйРазрезДоступа = ПланыВидовХарактеристик.ВидыДоступа.ВидыВнутреннихДокументов;
		// ТСК Талько Э.Г.; 09.06.2018; Управление доступом }
		КонецЕсли;
		
		Если ОтключенныеРазрезыДоступа.Найти(ПроверяемыйРазрезДоступа) <> Неопределено Тогда
			// Если виды документов не используются в качестве разрезов,
			// общие разрешения не влияют на право регистрации, в отличие от специальных.
			Возврат Истина;
		КонецЕсли;
		
		Запрос = Новый Запрос(
			"ВЫБРАТЬ ПЕРВЫЕ 1
			|	ИСТИНА КАК ЕстьЗаписи
			|ИЗ
			|	РегистрСведений.РазрешенияДляРазрезовДоступа КАК РазрешенияДляРазрезовДоступа
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.РазрешенияДоступаОбщие КАК РазрешенияДоступаОбщие
			|		ПО РазрешенияДляРазрезовДоступа.Разрешение = РазрешенияДоступаОбщие.ЗначениеДоступа
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ПользователиВКонтейнерах КАК ПользователиВКонтейнерах
			|		ПО (РазрешенияДоступаОбщие.Пользователь = ПользователиВКонтейнерах.Контейнер)
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.УровниДоступа КАК УровниДоступа
			|		ПО (РазрешенияДоступаОбщие.УровеньДоступа = УровниДоступа.Ссылка)
			|ГДЕ
			|	ПользователиВКонтейнерах.Пользователь = &Пользователь
			|	И УровниДоступа.Регистрация
			|	И РазрешенияДляРазрезовДоступа.РазрезДоступа = &ЗначениеДоступа");
		
		Запрос.УстановитьПараметр("Пользователь", Пользователь);
		Запрос.УстановитьПараметр("ЗначениеДоступа",
			ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДескрипторОбъекта, "ВидОбъекта"));
		
	КонецЕсли;
	
	ЕстьПраво = Не Запрос.Выполнить().Пустой();
	
	Если Не ЕстьПраво И Константы.ИспользоватьЛокальныхАдминистраторов.Получить() Тогда
		
		// Проверка локальных администраторов.
		
		ЗапросПоДескрипторам = ДокументооборотПраваДоступаПовтИсп.ЗапросПоДескрипторамСВидамиДоступа();
		ЗапросПоДескрипторам.УстановитьПараметр("Дескрипторы", ДескрипторОбъекта);
		ТаблицаДескрипторов = ЗапросПоДескрипторам.Выполнить().Выгрузить();
		
		РазрезыДоступа = Новый Массив;
		Для Каждого Стр Из ТаблицаДескрипторов Цикл
			Если ЗначениеЗаполнено(Стр.ЗначениеДоступа)
				И РазрезыДоступа.Найти(Стр.ЗначениеДоступа) = Неопределено Тогда
				РазрезыДоступа.Добавить(Стр.ЗначениеДоступа);
			КонецЕсли;
		КонецЦикла;
		
		Если РазрезыДоступа.Количество() = 0 Тогда
			// Если все разрезы отключены, политики доступа не влияют на право регистрации.
			Возврат Истина;
		КонецЕсли;
		
		Запрос = Новый Запрос(
			"ВЫБРАТЬ ПЕРВЫЕ 1
			|	РазрешенияИсключительные.Коллекция,
			|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ РазрешенияИсключительные.ЗначениеДоступа) КАК КолРазрезовДоступа
			|ИЗ
			|	(ВЫБРАТЬ РАЗЛИЧНЫЕ
			|		КоллекцииЗначенийДоступаЗначенияДоступа.Ссылка КАК Коллекция,
			|		РазрешенияДляРазрезовДоступа.РазрезДоступа КАК ЗначениеДоступа
			|	ИЗ
			|		РегистрСведений.РазрешенияДляРазрезовДоступа КАК РазрешенияДляРазрезовДоступа
			|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КоллекцииЗначенийДоступа.ЗначенияДоступа КАК КоллекцииЗначенийДоступаЗначенияДоступа
			|				ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.РазрешенияДляЛокальныхАдминистраторов КАК РазрешенияДляЛокальныхАдминистраторов
			|					ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ПользователиВКонтейнерах КАК ПользователиВКонтейнерах
			|					ПО РазрешенияДляЛокальныхАдминистраторов.Пользователь = ПользователиВКонтейнерах.Контейнер
			|					ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.УровниДоступа КАК УровниДоступа
			|					ПО РазрешенияДляЛокальныхАдминистраторов.УровеньДоступа = УровниДоступа.Ссылка
			|				ПО КоллекцииЗначенийДоступаЗначенияДоступа.Ссылка = РазрешенияДляЛокальныхАдминистраторов.КоллекцияЗначенийДоступа
			|			ПО РазрешенияДляРазрезовДоступа.Разрешение = КоллекцииЗначенийДоступаЗначенияДоступа.Значение
			|	ГДЕ
			|		УровниДоступа.ИзменениеБезОграничения
			|		И РазрешенияДляРазрезовДоступа.РазрезДоступа В(&РазрезыДоступаДескриптора)
			|		И ПользователиВКонтейнерах.Пользователь = &Пользователь
			|		И РазрешенияДляЛокальныхАдминистраторов.Предмет = &Предмет) КАК РазрешенияИсключительные
			|
			|СГРУППИРОВАТЬ ПО
			|	РазрешенияИсключительные.Коллекция
			|
			|ИМЕЮЩИЕ
			|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ РазрешенияИсключительные.ЗначениеДоступа) = &КоличествоРазрезов");
			
		Запрос.УстановитьПараметр("Пользователь", Пользователь);
		Запрос.УстановитьПараметр("Предмет", ОбъектМетаданных);
		Запрос.УстановитьПараметр("РазрезыДоступаДескриптора", РазрезыДоступа);
		Запрос.УстановитьПараметр("КоличествоРазрезов", РазрезыДоступа.Количество());
		
		ЕстьПраво = Не Запрос.Выполнить().Пустой();
		
	КонецЕсли;
	
	Возврат ЕстьПраво;
	
КонецФункции

// Заполняет ТЧ дескриптора НастройкаПрав по настройкам папки
Процедура ЗаполнитьНастройкиДескриптора(ДескрипторДоступа, Папка) Экспорт
	
	НастройкиПравПапки = ПолучитьНастройкиПравПапки(Папка);
	ДескрипторДоступа.НастройкаПрав.Загрузить(НастройкиПравПапки);
	
КонецПроцедуры

// Возвращает таблицу настроек прав папки
Функция ПолучитьНастройкиПравПапки(Папка) Экспорт
	
	НастройкиПравПапки = Новый ТаблицаЗначений;
	НастройкиПравПапки.Колонки.Добавить("Пользователь");
	НастройкиПравПапки.Колонки.Добавить("Право");
	НастройкиПравПапки.Колонки.Добавить("ПравоЗапрещено");
	НастройкиПравПапки.Колонки.Добавить("НаследованиеРазрешено");
	
	// Собственные права папки
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	НастройкиПравОбъектов.Пользователь КАК Пользователь,
		|	НастройкиПравОбъектов.Право КАК Право,
		|	НастройкиПравОбъектов.ПравоЗапрещено КАК ПравоЗапрещено,
		|	НастройкиПравОбъектов.НаследованиеРазрешено КАК НаследованиеРазрешено,
		|	НаследованиеНастроекПравОбъектов.Наследовать КАК Наследовать
		|ИЗ
		|	РегистрСведений.НаследованиеНастроекПравОбъектов КАК НаследованиеНастроекПравОбъектов
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкиПравОбъектов КАК НастройкиПравОбъектов
		|		ПО (НастройкиПравОбъектов.Объект = НаследованиеНастроекПравОбъектов.Родитель)
		|ГДЕ
		|	НаследованиеНастроекПравОбъектов.Объект = &Объект
		|	И НаследованиеНастроекПравОбъектов.Родитель = &Объект");
		
	Запрос.УстановитьПараметр("Объект", Папка.Ссылка);
	Результат = Запрос.Выполнить();
	
	Наследовать = Истина;
	Если Не Результат.Пустой() Тогда
		ТаблицаПрав = Результат.Выгрузить();
		Наследовать = ТаблицаПрав[0].Наследовать;
		Если ЗначениеЗаполнено(ТаблицаПрав[0].Право) Тогда
			НастройкиПравПапки = ТаблицаПрав;
		КонецЕсли;
	КонецЕсли;
	
	// Наследуемые права
	Если Наследовать Тогда
		
		Запрос.Текст =
			"ВЫБРАТЬ
			|	НастройкиПравОбъектов.Пользователь,
			|	НастройкиПравОбъектов.Право,
			|	НастройкиПравОбъектов.ПравоЗапрещено,
			|	ИСТИНА КАК НаследованиеРазрешено
			|ИЗ
			|	РегистрСведений.НастройкиПравОбъектов КАК НастройкиПравОбъектов
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.НаследованиеНастроекПравОбъектов КАК НаследованиеНастроекПравОбъектов
			|		ПО (НаследованиеНастроекПравОбъектов.Объект = &Объект)
			|			И (НаследованиеНастроекПравОбъектов.Родитель <> &Объект)
			|			И (НаследованиеНастроекПравОбъектов.Наследовать)
			|			И НастройкиПравОбъектов.Объект = НаследованиеНастроекПравОбъектов.Родитель
			|			И (НастройкиПравОбъектов.НаследованиеРазрешено)
			|
			|УПОРЯДОЧИТЬ ПО
			|	НастройкиПравОбъектов.Пользователь,
			|	НастройкиПравОбъектов.Право";
			
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			ЗаполнитьЗначенияСвойств(НастройкиПравПапки.Добавить(), Выборка);
		КонецЦикла;
		
	КонецЕсли;
	
	Возврат НастройкиПравПапки;
	
КонецФункции

// Включает регл. задание обработки оперативной очереди
Процедура ВключитьОбработкуОперативнойОчереди() Экспорт
	
	Задание = РегламентныеЗадания.НайтиПредопределенное("ДокументооборотОбновлениеПравДоступаОперативное");
	Задание.Использование = Истина;
	Задание.Записать();
	
КонецПроцедуры

// Включает регл. задание обработки долгой очереди
Процедура ВключитьОбработкуДолгойОчереди() Экспорт
	
	Задание = РегламентныеЗадания.НайтиПредопределенное("ДокументооборотОбновлениеПравДоступаДолгое");
	Задание.Использование = Истина;
	Задание.Записать();
	
КонецПроцедуры

// Останавливает регл. задание обработки оперативной очереди
// Возвращаемое значение:
// Истина, если регл. задание было активно
// Ложь, если регл. задание уже было отключено
Функция ОтключитьОбработкуОперативнойОчереди(ТолькоФоновоеЗадание = Ложь) Экспорт
	
	// Отключение регл. задания
	Задание = РегламентныеЗадания.НайтиПредопределенное("ДокументооборотОбновлениеПравДоступаОперативное");
	ЗаданиеАктивно = Задание.Использование;
	
	Если Не ТолькоФоновоеЗадание И ЗаданиеАктивно Тогда
		Задание.Использование = Ложь;
		Задание.Записать();
	КонецЕсли;
	
	// Остановка фонового задания
	Попытка
		ИДЗадания = Задание.УникальныйИдентификатор;
		Свойства = РегламентныеЗаданияСлужебный.ПолучитьСвойстваПоследнегоФоновогоЗаданияВыполненияРегламентногоЗадания(Задание);
		РегламентныеЗаданияСлужебный.ОтменитьФоновоеЗадание(Свойства.Идентификатор);
	Исключение
	КонецПопытки;
	
	Возврат ЗаданиеАктивно;
	
КонецФункции

// Останавливает регл. задание обработки долгой очереди
// Возвращаемое значение:
// Истина, если регл. задание было активно
// Ложь, если регл. задание уже было отключено
Функция ОтключитьОбработкуДолгойОчереди(ТолькоФоновоеЗадание = Ложь) Экспорт
	
	// Отключение регл. задания
	Задание = РегламентныеЗадания.НайтиПредопределенное("ДокументооборотОбновлениеПравДоступаДолгое");
	ЗаданиеАктивно = Задание.Использование;
	
	Если ЗаданиеАктивно Тогда
		
		Если Не ТолькоФоновоеЗадание Тогда
			Задание.Использование = Ложь;
			Задание.Записать();
		КонецЕсли;
		
		// Остановка фонового задания
		Попытка
			ИДЗадания = Задание.УникальныйИдентификатор;
			Свойства = РегламентныеЗаданияСлужебный.ПолучитьСвойстваПоследнегоФоновогоЗаданияВыполненияРегламентногоЗадания(Задание);
			РегламентныеЗаданияСлужебный.ОтменитьФоновоеЗадание(Свойства.Идентификатор);
		Исключение
		КонецПопытки;

	КонецЕсли;
	
	Возврат ЗаданиеАктивно;
	
КонецФункции

// Определяет активность каждого правила делегирования на текущий момент времени с учетом
// срока действия.
//
Процедура ОбработкаПравилДелегированияПрав() Экспорт
	
	ОбщегоНазначения.ПриНачалеВыполненияРегламентногоЗадания();
	
	УстановитьПривилегированныйРежим(Истина);
	
	ЗаписьЖурналаРегистрации(
		НСтр("ru = 'Делегирование прав'; en = 'Access delegation'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка()),
		УровеньЖурналаРегистрации.Информация,,
		,
		НСтр("ru = 'Начата обработка правил делегирования прав'; en = 'Access delegation rules processing is started'"));
		
	// Выборка недействующих правил с наступившим сроком действия и действующих с истекшим сроком действия.
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ДелегированиеПрав.Ссылка,
		|	ДелегированиеПрав.Кому,
		|	ДелегированиеПрав.ОтКого,
		|	ДелегированиеПрав.Действует,
		|	ДелегированиеПрав.ДатаНачалаДействия,
		|	ДелегированиеПрав.ДатаОкончанияДействия
		|ИЗ
		|	Справочник.ДелегированиеПрав КАК ДелегированиеПрав
		|ГДЕ
		|	НЕ ДелегированиеПрав.ПометкаУдаления
		|	И (ДелегированиеПрав.ДатаНачалаДействия < &ТекущаяДата
		|			И (ДелегированиеПрав.ДатаОкончанияДействия = ДАТАВРЕМЯ(1, 1, 1)
		|				ИЛИ ДелегированиеПрав.ДатаОкончанияДействия > &ТекущаяДата)) 
		|			<> ДелегированиеПрав.Действует";
		
	Запрос.УстановитьПараметр("ТекущаяДата", ТекущаяДатаСеанса());
	Выборка = Запрос.Выполнить().Выбрать();
	
	Обработано = 0;
	КоличествоЗаписейДляОбработки = Выборка.Количество();
	
	Пока Выборка.Следующий() Цикл
		
		Попытка
			ЗаблокироватьДанныеДляРедактирования(Выборка.Ссылка);
		Исключение
			// Объект уже заблокирован
			Продолжить;
		КонецПопытки;
		
		Элемент = Выборка.Ссылка.ПолучитьОбъект();
		
		// Проверка: не поменялись ли данные?
		Если Элемент.Кому <> Выборка.Кому
			ИЛИ Элемент.ОтКого <> Выборка.ОтКого
			ИЛИ Элемент.Действует <> Выборка.Действует
			ИЛИ Элемент.ДатаНачалаДействия <> Выборка.ДатаНачалаДействия
			ИЛИ Элемент.ДатаОкончанияДействия <> Выборка.ДатаОкончанияДействия Тогда
			
			РазблокироватьДанныеДляРедактирования(Элемент.Ссылка);
			Продолжить;
			
		КонецЕсли;
		
		Элемент.Записать();
		РазблокироватьДанныеДляРедактирования(Элемент.Ссылка);
		
		Обработано = Обработано + 1;
		
	КонецЦикла;
	
	ЗаписьЖурналаРегистрации(
		НСтр("ru = 'Делегирование прав'; en = 'Access delegation'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка()),
		УровеньЖурналаРегистрации.Информация,,
		,
	 	СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Завершена обработка правил делегирования прав. Обработано записей: %1.'; en = 'Access delegation rules processing is completed. Records processed: %1.'"),
			Обработано
		));
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Обработчики подписок и регламентных заданий

// Обработчик подписки ДокументооборотПраваДоступаПередЗаписьюОбъектаДоступа
Процедура ДокументооборотПраваДоступаПередЗаписьюОбъектаДоступа(Источник, Отказ) Экспорт
	
	Если Не ДокументооборотПраваДоступаПовтИсп.ВключеноИспользованиеПравДоступа() Тогда
		Возврат;
	КонецЕсли;
	
	ЭтоНовый = Источник.ЭтоНовый();
	ЭтоУдаление = Ложь;
	
	Если Не ЭтоНовый Тогда
		ЭтоУдаление = Источник.ПометкаУдаления И Источник.ПометкаУдаления <>
			ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Источник.Ссылка, "ПометкаУдаления");
	КонецЕсли;
	
	Источник.ДополнительныеСвойства.Вставить("ЭтоНовый", ЭтоНовый);
	Источник.ДополнительныеСвойства.Вставить("ЭтоУдаление", ЭтоУдаление);
	
	// Проверка прав на изменение объекта в его начальном состоянии.
	// Выполняется только для объектов, которые имеют собственный алгоритм получения прав на изменение,
	// т.к. для таких объектов RLS на изменение отсутствует.
	
	ПолноеИмяМетаданных = Источник.Метаданные().ПолноеИмя();
	Если ЭтоНовый
		Или ПривилегированныйРежим() 
		Или ПользователиСерверПовтИсп.ЭтоПолноправныйПользовательИБ()
		Или Не ОбщегоНазначенияДокументооборотПовтИсп.ЕстьМетодМенеджера(
				ПолноеИмяМетаданных, "ПолучитьПраваПользователейПоОбъектам") Тогда
		Возврат;
	КонецЕсли;
	
	ПроверяемоеПраво = "Изменение";
	Если ЭтоУдаление Тогда
		ПроверяемоеПраво = "Удаление";
	КонецЕсли;
	
	Права = ПолучитьПраваПоОбъекту(Источник.Ссылка);
	НетПрав = Не Права[ПроверяемоеПраво];
	
	Если НетПрав Тогда
		
		МетаданныеИсточника = Источник.Метаданные();
		ПредставлениеОбъекта = МетаданныеИсточника.ПредставлениеОбъекта;
		Если Не ЗначениеЗаполнено(ПредставлениеОбъекта) Тогда
			ПредставлениеОбъекта = МетаданныеИсточника.Синоним;
		КонецЕсли;
		
		ТекстОшибки = СтрШаблон(
			НСтр("ru = 'Недостаточно прав для выполнения операции. 
				|Обратитесь к администратору.
				|
				|Объект: %1 (%2)
				|Право: %3';
				|en = 'Not enough permissions to perform the operation. 
				|Contact the administrator.
				|Object: %1 (%2)
				|Permission: %3'"),
			Источник, ПредставлениеОбъекта, ПроверяемоеПраво);
		
		ВызватьИсключение ТекстОшибки;
		
	КонецЕсли;
	
КонецПроцедуры

// Обработчик подписки ДокументооборотПраваДоступаПередЗаписьюДокумента
Процедура ДокументооборотПраваДоступаПередЗаписьюДокумента(Источник, Отказ, РежимЗаписи, РежимПроведения) Экспорт
	
	ДокументооборотПраваДоступаПередЗаписьюОбъектаДоступа(Источник, Отказ);
	
КонецПроцедуры

// Обработчик подписки ДокументооборотПраваДоступаПриЗаписиОбъектаДоступа
Процедура ДокументооборотПраваДоступаПриЗаписиОбъектаДоступа(Источник, Отказ) Экспорт
	
	Если Не ДокументооборотПраваДоступаПовтИсп.ВключеноИспользованиеПравДоступа() Тогда
		Возврат;
	КонецЕсли;
	
	Если Источник.ДополнительныеСвойства.Свойство(
			"ПропуститьОпределениеДескриптораДоступаИПроверкуПрав") Тогда
		Возврат;
	КонецЕсли;
	
	Если Источник.ДополнительныеСвойства.Свойство("ЗагрузкаПриоритетныхДанных") Тогда
		Возврат;
	КонецЕсли;
	
	ОпределитьДескрипторыОбъекта(Источник);
	
	Если ПривилегированныйРежим() Или ПользователиСерверПовтИсп.ЭтоПолноправныйПользовательИБ() Тогда
		Возврат;
	КонецЕсли;
	
	ПроверяемоеПраво = "Изменение";
	Если Источник.ДополнительныеСвойства.ЭтоНовый Тогда
		ПроверяемоеПраво = "Добавление";
	ИначеЕсли Источник.ДополнительныеСвойства.ЭтоУдаление Тогда
		ПроверяемоеПраво = "Удаление";
	КонецЕсли;
		
	Права = ПолучитьПраваПоОбъекту(Источник.Ссылка);
	НетПрав = Не Права[ПроверяемоеПраво];
	
	Если НетПрав Тогда
		
		МетаданныеИсточника = Источник.Метаданные();
		ПредставлениеОбъекта = МетаданныеИсточника.ПредставлениеОбъекта;
		Если Не ЗначениеЗаполнено(ПредставлениеОбъекта) Тогда
			ПредставлениеОбъекта = МетаданныеИсточника.Синоним;
		КонецЕсли;
		
		ТекстОшибки = СтрШаблон(
			НСтр("ru = 'Недостаточно прав для выполнения операции. 
				|Обратитесь к администратору.
				|
				|Объект: %1 (%2)
				|Право: %3';
				|en = 'Not enough permissions to perform the operation. 
				|Contact the administrator.
				|Object: %1 (%2)
				|Permission: %3'"),
			Источник, ПредставлениеОбъекта, ПроверяемоеПраво);
		
		ВызватьИсключение ТекстОшибки;
		
	КонецЕсли;
	
КонецПроцедуры

// Обработчик подписки ДокументооборотПраваДоступаПриЗаписиФайла
Процедура ДокументооборотПраваДоступаПередЗаписьюФайла(Источник, Отказ) Экспорт
	
	Если Не ДокументооборотПраваДоступаПовтИсп.ВключеноИспользованиеПравДоступа() Тогда
		Возврат;
	КонецЕсли;
	
	ЭтоНовый = Источник.ЭтоНовый();
	ЭтоУдаление = Ложь;
	
	Если Не ЭтоНовый Тогда
		ЗначенияРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
			Источник.Ссылка, "ПометкаУдаления, ВладелецФайла");
		ЭтоУдаление = Источник.ПометкаУдаления <> ЗначенияРеквизитов.ПометкаУдаления;
		Источник.ДополнительныеСвойства.Вставить("СтарыйВладелец", ЗначенияРеквизитов.ВладелецФайла);
	КонецЕсли;
	
	Источник.ДополнительныеСвойства.Вставить("ЭтоНовый", ЭтоНовый);
	Источник.ДополнительныеСвойства.Вставить("ЭтоУдаление", ЭтоУдаление);
	
	
	// Создание дескриптора для владельцев файлов, которые сами не используют дескрипторы в RLS.
	ТипВладельца = ТипЗнч(Источник.ВладелецФайла);
	ТипыСсылок = ДокументооборотПраваДоступаПовтИсп.ТипыСсылокИспользующихДоступПоДескрипторам();
	
	Если ТипыСсылок.Найти(ТипВладельца) = Неопределено
		И Не РегистрыСведений.ДескрипторыДляОбъектов.ЕстьДескрипторыОбъекта(Источник.ВладелецФайла) Тогда
		ОпределитьДескрипторыОбъекта(Источник.ВладелецФайла);
	КонецЕсли;
	
КонецПроцедуры

// Обработчик подписки ДокументооборотПраваДоступаПриЗаписиФайла
Процедура ДокументооборотПраваДоступаПриЗаписиФайла(Источник, Отказ) Экспорт
	
	Если Не ДокументооборотПраваДоступаПовтИсп.ВключеноИспользованиеПравДоступа() Тогда
		Возврат;
	КонецЕсли;
	
	// Обновление зависимых прав.
	Если Источник.ДополнительныеСвойства.Свойство("СтарыйВладелец")
		И Источник.ДополнительныеСвойства.СтарыйВладелец <> Источник.Владелец Тогда
		ОбновитьПраваЗависящиеОтФайла(Источник.Ссылка);
	КонецЕсли;
	
	// Проверка прав на запись файла.
	Если ПривилегированныйРежим() Или ПользователиСерверПовтИсп.ЭтоПолноправныйПользовательИБ() Тогда
		Возврат;
	КонецЕсли;
	
	ПроверяемоеПраво = "Изменение";
	Если Источник.ДополнительныеСвойства.ЭтоНовый Тогда
		ПроверяемоеПраво = "Добавление";
	ИначеЕсли Источник.ДополнительныеСвойства.ЭтоУдаление Тогда
		ПроверяемоеПраво = "Удаление";
	КонецЕсли;
		
	Права = ПолучитьПраваПоОбъекту(Источник.Ссылка);
	НетПрав = Не Права[ПроверяемоеПраво];
	
	Если НетПрав Тогда
		
		ТекстОшибки = СтрШаблон(
			НСтр("ru = 'Недостаточно прав для выполнения операции с файлом. 
				|Обратитесь к администратору.
				|
				|Файл: %1
				|Право: %2';
				|en = 'Not enough permissions to perform the operation with the file. 
				|Contact the administrator.
				|
				|File: %1
				|Permission: %2'"),
			Источник, ПроверяемоеПраво);
		
		ВызватьИсключение ТекстОшибки;
		
	КонецЕсли;
	
КонецПроцедуры	

// Обработчик подписки ДокументооборотПраваДоступаПередЗаписьюНабораЗаписей
Процедура ДокументооборотПраваДоступаПередЗаписьюНабораЗаписей(Источник, Отказ, Замещение) Экспорт
	
	Если Не ДокументооборотПраваДоступаПовтИсп.ВключеноИспользованиеПравДоступа() Тогда
		Возврат;
	КонецЕсли;
	
	ОпределитьДескрипторДоступаНабораЗаписей(Источник);
	
КонецПроцедуры

// Обработчик подписки ДокументооборотПередЗаписьюПравообразующихОбъектов
Процедура ДокументооборотПередЗаписьюПравообразующихОбъектов(Источник, Отказ) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если Не ДокументооборотПраваДоступаПовтИсп.ВключеноИспользованиеПравДоступа() Тогда
		Возврат;
	КонецЕсли;
	
	Если ДокументооборотПраваДоступаПереопределяемый.ДокументооборотПередЗаписьюПравообразующихОбъектов(Источник, Отказ) Тогда
		Возврат;
	КонецЕсли;
	
	// Проверка на то, что изменились правообразующие реквизиты
	Если ТипЗнч(Источник) = Тип("СправочникОбъект.РабочиеГруппы") Тогда
		
		Если Источник.ЭтоНовый() Тогда
			Возврат;
		КонецЕсли;
		
		РеквизитыСсылки = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Источник.Ссылка,
			"Состав, Родитель, ПометкаУдаления");
			
		Если РеквизитыСсылки.Состав = Неопределено Тогда
			СтарыйСостав = Источник.Состав.ВыгрузитьКолонки();
		Иначе
			СтарыйСостав = РеквизитыСсылки.Состав.Выгрузить();
		КонецЕсли;
			
		Если Источник.Родитель <> РеквизитыСсылки.Родитель
			И (СтарыйСостав.Количество() > 0 Или Источник.Состав.Количество() > 0 ) Тогда
			
			Источник.ДополнительныеСвойства.Вставить("ЗначениеИзменено", Истина);
			Источник.ДополнительныеСвойства.Вставить("СтарыйРодитель", РеквизитыСсылки.Родитель);
			
		ИначеЕсли Источник.ПометкаУдаления <> РеквизитыСсылки.ПометкаУдаления
			Или ОбщегоНазначенияДокументооборотКлиентСервер.ЕстьОтличияВТаблицах(
				Источник.Состав, СтарыйСостав, "Пользователь") Тогда
			
			Источник.ДополнительныеСвойства.Вставить("ЗначениеИзменено", Истина);
			
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(Источник) = Тип("СправочникОбъект.Пользователи") Тогда
		
		ЭтоНовыйПользователь = Источник.ЭтоНовый();
		Источник.ДополнительныеСвойства.Вставить("ЭтоНовый", ЭтоНовыйПользователь);
		
		СтараяПометкаУдаления = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(
			Источник.Ссылка, "ПометкаУдаления");
		
		ИзмененаПометкаУдаления = Источник.ПометкаУдаления <> СтараяПометкаУдаления;
		Источник.ДополнительныеСвойства.Вставить("ИзмененаПометкаУдаления", ИзмененаПометкаУдаления);
		
		Если ЭтоНовыйПользователь Или ИзмененаПометкаУдаления Тогда
			Источник.ДополнительныеСвойства.Вставить("ЗначениеИзменено", Истина);
			СтарыеКонтейнеры = ПользователиДокументооборот.КонтейнерыПользователя(Источник.Ссылка);
			Источник.ДополнительныеСвойства.Вставить("СтарыеКонтейнеры", СтарыеКонтейнеры);
		КонецЕсли;
		
		// Запрет пометки руководителей и делегатов
		Если Не ЭтоНовыйПользователь
			И ИзмененаПометкаУдаления
			И Источник.ПометкаУдаления
			И НЕ Источник.ОбменДанными.Загрузка Тогда
			
			Запрос = Новый Запрос(
				"ВЫБРАТЬ ПЕРВЫЕ 1
				|	СтруктураПредприятия.Представление КАК Подразделение
				|ИЗ
				|	Справочник.СтруктураПредприятия КАК СтруктураПредприятия
				|ГДЕ
				|	СтруктураПредприятия.Руководитель = &Пользователь
				|	И НЕ СтруктураПредприятия.ПометкаУдаления");
				
			Запрос.УстановитьПараметр("Пользователь", Источник.Ссылка);
			Выборка = Запрос.Выполнить().Выбрать();
			
			ЭтоРуководитель = Выборка.Следующий();
			Если ЭтоРуководитель Тогда
				Отказ = Истина;
				ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Нельзя пометить на удаление пользователя ""%1"", 
						|т.к. он является руководителем подразделения ""%2"".';
						|en = 'You cannot mark for deletion user ""%1"",
						|because he is manager of department ""%2"".'"),
					Источник.Ссылка,
					Выборка.Подразделение);
				ВызватьИсключение ТекстОшибки;
			КонецЕсли;
			
			Запрос = Новый Запрос(
				"ВЫБРАТЬ ПЕРВЫЕ 1
				|	ИСТИНА КАК ЕстьЗаписи
				|ИЗ
				|	Справочник.ДелегированиеПрав КАК ДелегированиеПрав
				|ГДЕ
				|	ДелегированиеПрав.Кому = &Пользователь
				|	И ДелегированиеПрав.Действует");
			
			Запрос.УстановитьПараметр("Пользователь", Источник.Ссылка);
			Запрос.УстановитьПараметр("ТекущаяДата", ТекущаяДата());
			
			ЭтоДелегат = Не Запрос.Выполнить().Пустой();
			Если ЭтоДелегат Тогда
				Отказ = Истина;
				ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Нельзя пометить на удаление пользователя ""%1"",
						|т.к. ему делегированы права других пользователей.';
						|en = 'You cannot mark to deletion user ""%1"",
						|because he has delegated permissions to other users.'"),
					Источник.Ссылка);
				ВызватьИсключение ТекстОшибки;
			КонецЕсли;
			
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(Источник) = Тип("СправочникОбъект.СтруктураПредприятия") Тогда	
		
		Если Не Источник.ЭтоНовый() Тогда
			
			РеквизитыСсылки = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
				Источник.Ссылка, "Родитель, Руководитель");
			
			Если Источник.Руководитель <> РеквизитыСсылки.Руководитель Тогда
				Источник.ДополнительныеСвойства.Вставить("ЗначениеИзменено", Истина);
				Источник.ДополнительныеСвойства.Вставить("ИзмененРуководитель", Истина);
			КонецЕсли;
			
			СтарыйРодитель = РеквизитыСсылки.Родитель;
			Если Источник.Родитель <> СтарыйРодитель Тогда
				
				Источник.ДополнительныеСвойства.Вставить("ЗначениеИзменено", Истина);
				Источник.ДополнительныеСвойства.Вставить("ИзмененРодитель", Истина);
				Источник.ДополнительныеСвойства.Вставить("СтарыйРодитель", СтарыйРодитель);
				
				// Нужно обновить права руководителей по подразделениям, которые
				// являются старшими непересекающимися родителями для старого и нового состояний элемента.
				// А также обработать изменение состава контейнеров по всем затронутым подразделениям.
				
				СтарыеРодители = Новый Массив;
				НовыеРодители = Новый Массив;
				
				Если ЗначениеЗаполнено(СтарыйРодитель) Тогда
					СтарыеРодители = ОбщегоНазначенияДокументооборот.ВсеРодителиЭлемента(СтарыйРодитель);
					СтарыеРодители.Вставить(0, СтарыйРодитель);
				КонецЕсли;
				
				Если ЗначениеЗаполнено(Источник.Родитель) Тогда
					НовыеРодители = ОбщегоНазначенияДокументооборот.ВсеРодителиЭлемента(Источник.Родитель);
					НовыеРодители.Вставить(0, Источник.Родитель);
				КонецЕсли;
				
				ЭлементыКПересчету = НепересекающиесяПодразделения(НовыеРодители, СтарыеРодители);
				Источник.ДополнительныеСвойства.Вставить("ВсеПодразделенияКПересчету", ЭлементыКПересчету.Все);
				Источник.ДополнительныеСвойства.Вставить("СтаршиеПодразделенияКПересчету", ЭлементыКПересчету.Старшие);
				
			КонецЕсли;
			
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(Источник) = Тип("СправочникОбъект.Контрагенты") Тогда
		
		Если Не Источник.ЭтоНовый() Тогда
			СтараяГруппаДоступа = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Источник.Ссылка, "ГруппаДоступа");
			Если Источник.ГруппаДоступа <> СтараяГруппаДоступа Тогда
				Источник.ДополнительныеСвойства.Вставить("ЗначениеИзменено", Истина);
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры	

// Обработчик подписки ДокументооборотПриЗаписиПравообразующихОбъектов
Процедура ДокументооборотПриЗаписиПравообразующихОбъектов(Источник, Отказ) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если Не ДокументооборотПраваДоступаПовтИсп.ВключеноИспользованиеПравДоступа() Тогда
		Возврат;
	КонецЕсли;
	
	ТипИсточника = ТипЗнч(Источник);
	
	// Проверка на то, что правообразующие свойства источника были изменены
	Если Не Источник.ДополнительныеСвойства.Свойство("ЗначениеИзменено") 
		Или Не Источник.ДополнительныеСвойства.ЗначениеИзменено Тогда
		
		Возврат;
		
	КонецЕсли;
	
	ОбновитьПромежуточныеДанныеДляРасчетаПрав(Источник, ТипИсточника);
	
	Если ДокументооборотПраваДоступаПереопределяемый.ДокументооборотПриЗаписиПравообразующихОбъектов(
		Источник, Отказ) Тогда
		Возврат;
	КонецЕсли;
	
	Если ТипИсточника = Тип("СправочникОбъект.РабочиеГруппы") Тогда
		
		ПриИзмененииСоставаКонтейнеровПользователей(Источник, ТипИсточника);
		
	ИначеЕсли ТипИсточника = Тип("СправочникОбъект.Пользователи") Тогда
		
		ПриИзмененииСоставаКонтейнеровПользователей(Источник, ТипИсточника);
		
	ИначеЕсли ТипИсточника = Тип("СправочникОбъект.СтруктураПредприятия") Тогда
		
		Если Источник.ДополнительныеСвойства.Свойство("ИзмененРуководитель") Тогда
			
			ОбновитьПраваПриИзмененииРуководителяПодразделения(Источник.Ссылка);
			АктуализироватьПраваПользователейПоСоставуСубъектов(Источник.Руководитель);
			АктуализироватьПраваПользователейПоСоставуСубъектов(Источник.ДополнительныеСвойства.СтарыйРуководитель);
			
		ИначеЕсли Источник.ДополнительныеСвойства.Свойство("ИзмененРодитель") Тогда
			
			ПриИзмененииСоставаКонтейнеровПользователей(Источник, ТипИсточника);
			
			// Нужно обновить права руководителей по подразделениям, которые
			// являются старшими непересекающимися родителями для старого и нового состояний элемента.
			СтаршиеПодразделенияКПересчету = Источник.ДополнительныеСвойства.СтаршиеПодразделенияКПересчету;
			Если ЗначениеЗаполнено(СтаршиеПодразделенияКПересчету) Тогда
				АктуализироватьПраваПользователейПодразделенийПоССПД(СтаршиеПодразделенияКПересчету);
			КонецЕсли;
			
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(Источник) = Тип("СправочникОбъект.ДелегированиеПрав") Тогда
		
		Если Источник.Действует Тогда
			
			// Актуализация прав дескрипторов, доступных делегирующему.
			АктуализироватьПраваПользователейПоСоставуСубъектов(Источник.ОтКого);
			
			СтарыйОтКого = Неопределено;
			Источник.ДополнительныеСвойства.Свойство("СтарыйОтКого", СтарыйОтКого);
			Если ЗначениеЗаполнено(СтарыйОтКого) Тогда
				АктуализироватьПраваПользователейПоСоставуСубъектов(СтарыйОтКого);
			КонецЕсли;
			
		Иначе
			
			// Удаление всех записей по этому делегированию.
			Набор = РегистрыСведений.ПраваПоДескрипторамДоступаОбъектов.СоздатьНаборЗаписей();
			Набор.Отбор.ОбъектОснование.Установить(Источник.Ссылка);
			Набор.Записать();
			
			Набор = РегистрыСведений.ПраваПоДескрипторамДоступаРегистров.СоздатьНаборЗаписей();
			Набор.Отбор.ОбъектОснование.Установить(Источник.Ссылка);
			Набор.Записать();
			
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(Источник) = Тип("СправочникОбъект.Контрагенты") Тогда
		
		ОбновитьПраваПоКонтрагенту(Источник.Ссылка);
		
	ИначеЕсли ТипИсточника = Тип("СправочникОбъект.ПрофилиГруппДоступа") Тогда
		
		// Если изменился состав ролей, все необходимые изменения будут выполнены
		// в процедуре ОбновитьПраваПриИзмененииРолейПользователей.
		
	КонецЕсли;
	
КонецПроцедуры	

// Обновляет закэшированные данные, такие как
//  - состав субъектов прав доступа
//  - состав контейнеров пользователей
// 
Процедура ОбновитьПромежуточныеДанныеДляРасчетаПрав(Источник, ТипИсточника)
	
	Если Источник.ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ДокументооборотПраваДоступаПереопределяемый.ОбновитьПромежуточныеДанныеДляРасчетаПрав(
			Источник, ТипИсточника) Тогда
		Возврат;
	КонецЕсли;
	
	Если ТипИсточника = Тип("СправочникОбъект.Пользователи") Тогда
		
		Если Источник.ДополнительныеСвойства.ЭтоНовый Тогда
			РегистрыСведений.СоставСубъектовПравДоступа.ДобавлениеПользователя(Источник.Ссылка);
		КонецЕсли;
		
	ИначеЕсли ТипИсточника = Тип("СправочникОбъект.СтруктураПредприятия") Тогда
		
		Если Источник.ДополнительныеСвойства.Свойство("ИзмененРуководитель") Тогда
			РегистрыСведений.СоставСубъектовПравДоступа.ИзменениеРуководителяПодразделения(Источник);
			
		ИначеЕсли Источник.ДополнительныеСвойства.Свойство("ИзмененРодитель") Тогда
			РегистрыСведений.СоставСубъектовПравДоступа.ОбновитьВсеДанные();
			
		КонецЕсли;
		
	ИначеЕсли ТипИсточника = Тип("СправочникОбъект.ДелегированиеПрав") Тогда
		
		РегистрыСведений.СоставСубъектовПравДоступа.ЗаписьДелегирования(Источник.Ссылка);
		// ТСК Близнюк С.И.; 21.12.2018; task#2231{
		//РегистрыСведений.РабочиеГруппы.ОбновитьЗаписиПоДелегированию(Источник.Ссылка);
		// ТСК Близнюк С.И.; 21.12.2018; task#2231}
		
	ИначеЕсли ТипИсточника = Тип("РегистрСведенийНаборЗаписей.ИсполнителиЗадач") Тогда
		
		РегистрыСведений.СоставСубъектовПравДоступа.ЗаписьИсполнителейРолей(Источник);
		
	ИначеЕсли ТипИсточника = Тип("РегистрСведенийНаборЗаписей.СведенияОПользователяхДокументооборот") Тогда
		
		РегистрыСведений.СоставСубъектовПравДоступа.ЗаписьСведенийОПользователях(Источник);
		
	ИначеЕсли ТипИсточника = Тип("КонстантаМенеджерЗначения.ДобавлятьРуководителямДоступПодчиненных") Тогда
		
		Если Источник.Значение Тогда
			РегистрыСведений.СоставСубъектовПравДоступа.ЗаполнитьВсехРуководителей();
		Иначе
			РегистрыСведений.СоставСубъектовПравДоступа.УдалитьВсехРуководителей();
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

// Обновляет права дескрипторов, доступных пользователям, в части руководителей и делегатов.
// 
// Параметры:
//  ОтборПоПользователям - Пользователь, Массив - пользователи, по правам которых будут отобраны дескрипторы.
//  Немедленно - Булево - если ложь, то задание будет поставлено в очередь.
// 
Процедура АктуализироватьПраваПользователейПоСоставуСубъектов(
			ОтборПоПользователям = Неопределено, Немедленно = Ложь) Экспорт
	
	КоличествоЗначенийОтбора = 1;
	Если ТипЗнч(ОтборПоПользователям) = Тип("Массив") Тогда
		КоличествоЗначенийОтбора = ОтборПоПользователям.Количество();
	КонецЕсли;
	
	// Проверка на отложенное обновление прав доступа
	Если КоличествоЗначенийОтбора = 1
		И ДокументооборотПраваДоступаПовтИсп.ОтложенноеОбновлениеПравДоступа() 
		И Немедленно <> Истина Тогда
		
		// Добавление в очередь
		РегистрыСведений.ОчередьОбновленияПравДоступа.Добавить(
			ОтборПоПользователям,, 2, "АктуализироватьПраваРуководителейИДелегатов");
		
		Возврат;
		
	КонецЕсли;
	
	// Права по дескрипторам объектов.
	Запрос = Новый Запрос(
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ПраваПоДескрипторамДоступаОбъектов.Дескриптор
		|ИЗ
		|	РегистрСведений.ПраваПоДескрипторамДоступаОбъектов КАК ПраваПоДескрипторамДоступаОбъектов
		|ГДЕ
		|	НЕ ПраваПоДескрипторамДоступаОбъектов.ОбъектОснование ССЫЛКА Справочник.ДелегированиеПрав
		|	И ПраваПоДескрипторамДоступаОбъектов.Пользователь В(&Пользователи)");
		
	Если ОтборПоПользователям = Неопределено Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, 
			"И ПраваПоДескрипторамДоступаОбъектов.Пользователь В(&Пользователи)", "");
	КонецЕсли;
	
	Запрос.УстановитьПараметр("Пользователи", ОтборПоПользователям);
	
	ДескрипторыКОбновлению = Новый Массив;
	РазмерПорции = ДокументооборотПраваДоступаПовтИсп.РазмерПорцииДескрипторовДляРасширенияПравСоставомСубъектов();
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		ДескрипторыКОбновлению.Добавить(Выборка.Дескриптор);
		
		Если ДескрипторыКОбновлению.Количество() >= РазмерПорции Тогда
			Справочники.ДескрипторыДоступаОбъектов.ОбновитьПраваПоСоставуСубъектов(ДескрипторыКОбновлению);
			ДескрипторыКОбновлению.Очистить();
		КонецЕсли;
		
	КонецЦикла;
	
	Справочники.ДескрипторыДоступаОбъектов.ОбновитьПраваПоСоставуСубъектов(ДескрипторыКОбновлению);
	
	// Права по дескрипторам регистров.
	Запрос = Новый Запрос(
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ПраваПоДескрипторамДоступаРегистров.Дескриптор
		|ИЗ
		|	РегистрСведений.ПраваПоДескрипторамДоступаРегистров КАК ПраваПоДескрипторамДоступаРегистров
		|ГДЕ
		|	НЕ ПраваПоДескрипторамДоступаРегистров.ОбъектОснование ССЫЛКА Справочник.ДелегированиеПрав
		|	И ПраваПоДескрипторамДоступаРегистров.Пользователь В(&Пользователи)");
		
	Если ОтборПоПользователям = Неопределено Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, 
			"И ПраваПоДескрипторамДоступаРегистров.Пользователь В(&Пользователи)", "");
	КонецЕсли;
	
	Запрос.УстановитьПараметр("Пользователи", ОтборПоПользователям);
	
	ДескрипторыКОбновлению = Новый Массив;
	РазмерПорции = ДокументооборотПраваДоступаПовтИсп.РазмерПорцииДескрипторовДляРасширенияПравСоставомСубъектов();
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		ДескрипторыКОбновлению.Добавить(Выборка.Дескриптор);
		
		Если ДескрипторыКОбновлению.Количество() >= РазмерПорции Тогда
			Справочники.ДескрипторыДоступаРегистров.ОбновитьПраваПоСоставуСубъектов(ДескрипторыКОбновлению);
			ДескрипторыКОбновлению.Очистить();
		КонецЕсли;
		
	КонецЦикла;
	
	Справочники.ДескрипторыДоступаРегистров.ОбновитьПраваПоСоставуСубъектов(ДескрипторыКОбновлению);
	
КонецПроцедуры

// Обновляет права дескрипторов, доступных пользователям подразделения, в части руководителей и делегатов.
// 
// Параметры:
//  Подразделения - Массив - подразделения, по правам пользователей которых будут отобраны дескрипторы.
//  Немедленно - Булево - если Ложь, то задание будет поставлено в очередь.
// 
Процедура АктуализироватьПраваПользователейПодразделенийПоССПД(Подразделения, Немедленно = Ложь) Экспорт
	
	// Проверка на отложенное обновление прав доступа
	Если ДокументооборотПраваДоступаПовтИсп.ОтложенноеОбновлениеПравДоступа() 
		И Немедленно <> Истина Тогда
		
		// Добавление в очередь
		Для Каждого Подр Из Подразделения Цикл
			РегистрыСведений.ОчередьОбновленияПравДоступа.Добавить(Подр,, 2, "АктуализироватьПраваРуководителей");
		КонецЦикла;
		
		Возврат;
		
	КонецЕсли;
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	СтруктураПредприятия.Ссылка КАК Подразделение
		|ПОМЕСТИТЬ ПодразделенияДляОтбора
		|ИЗ
		|	Справочник.СтруктураПредприятия КАК СтруктураПредприятия
		|ГДЕ
		|	СтруктураПредприятия.Ссылка В ИЕРАРХИИ(&Подразделения)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	СведенияОПользователяхДокументооборот.Пользователь
		|ИЗ
		|	РегистрСведений.СведенияОПользователяхДокументооборот КАК СведенияОПользователяхДокументооборот
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПодразделенияДляОтбора КАК ПодразделенияДляОтбора
		|		ПО СведенияОПользователяхДокументооборот.Подразделение = ПодразделенияДляОтбора.Подразделение");
	
	Запрос.УстановитьПараметр("Подразделения", Подразделения);
	ПользователиПодразделений = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Пользователь");
	
	АктуализироватьПраваПользователейПоСоставуСубъектов(ПользователиПодразделений, Истина);
	
КонецПроцедуры

// Выполняет обновление прав при изменении руководителя подразделения.
//
// Параметры:
//  Подразделение - СправлчникСсылка - подразделение.
//  Немедленно - Булево - если Ложь, то задание будет поставлено в очередь.
// 
Процедура ОбновитьПраваПриИзмененииРуководителяПодразделения(Подразделение, Немедленно = Ложь) Экспорт
	
	// Проверка на отложенное обновление прав доступа.
	Если ДокументооборотПраваДоступаПовтИсп.ОтложенноеОбновлениеПравДоступа() 
		И Немедленно <> Истина Тогда
		
		// Добавление в очередь.
		РегистрыСведений.ОчередьОбновленияПравДоступа.Добавить(Подразделение,, 2, "ОтработатьИзменениеРуководителя");
		
		Возврат;
		
	КонецЕсли;
	
	// Права по дескрипторам объектов.
	Набор = РегистрыСведений.ПраваПоДескрипторамДоступаОбъектов.СоздатьНаборЗаписей();
	Набор.Отбор.ОбъектОснование.Установить(Подразделение);
	
	НовыйРуководитель = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Подразделение, "Руководитель");
	Если ЗначениеЗаполнено(НовыйРуководитель) Тогда
		
		Запрос = Новый Запрос(
			"ВЫБРАТЬ
			|	ПраваПоДескрипторамДоступаОбъектов.Дескриптор,
			|	&НовыйРуководитель КАК Пользователь,
			|	ПраваПоДескрипторамДоступаОбъектов.ОбъектОснование,
			|	ПраваПоДескрипторамДоступаОбъектов.Добавление,
			|	ПраваПоДескрипторамДоступаОбъектов.Изменение,
			|	ПраваПоДескрипторамДоступаОбъектов.Удаление,
			|	ПраваПоДескрипторамДоступаОбъектов.УправлениеПравами,
			|	ПраваПоДескрипторамДоступаОбъектов.Чтение
			|ИЗ
			|	РегистрСведений.ПраваПоДескрипторамДоступаОбъектов КАК ПраваПоДескрипторамДоступаОбъектов
			|ГДЕ
			|	ПраваПоДескрипторамДоступаОбъектов.ОбъектОснование = &Подразделение");
		
		Запрос.УстановитьПараметр("Подразделение", Подразделение);
		Запрос.УстановитьПараметр("НовыйРуководитель", НовыйРуководитель);
		
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			ЗаполнитьЗначенияСвойств(Набор.Добавить(), Выборка);
		КонецЦикла;
		
	КонецЕсли;
	
	Набор.Записать();
	
	// Права по дескрипторам регистров.
	Набор = РегистрыСведений.ПраваПоДескрипторамДоступаРегистров.СоздатьНаборЗаписей();
	Набор.Отбор.ОбъектОснование.Установить(Подразделение);
	
	НовыйРуководитель = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Подразделение, "Руководитель");
	Если ЗначениеЗаполнено(НовыйРуководитель) Тогда
		
		Запрос = Новый Запрос(
			"ВЫБРАТЬ
			|	ПраваПоДескрипторамДоступаРегистров.Дескриптор,
			|	&НовыйРуководитель КАК Пользователь,
			|	ПраваПоДескрипторамДоступаРегистров.ОбъектОснование,
			|	ПраваПоДескрипторамДоступаРегистров.Добавление,
			|	ПраваПоДескрипторамДоступаРегистров.Изменение,
			|	ПраваПоДескрипторамДоступаРегистров.Удаление,
			|	ПраваПоДескрипторамДоступаРегистров.УправлениеПравами,
			|	ПраваПоДескрипторамДоступаРегистров.Чтение
			|ИЗ
			|	РегистрСведений.ПраваПоДескрипторамДоступаРегистров КАК ПраваПоДескрипторамДоступаРегистров
			|ГДЕ
			|	ПраваПоДескрипторамДоступаРегистров.ОбъектОснование = &Подразделение");
		
		Запрос.УстановитьПараметр("Подразделение", Подразделение);
		Запрос.УстановитьПараметр("НовыйРуководитель", НовыйРуководитель);
		
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			ЗаполнитьЗначенияСвойств(Набор.Добавить(), Выборка);
		КонецЦикла;
		
	КонецЕсли;
	
	Набор.Записать();
	
КонецПроцедуры

Процедура ПриИзмененииСоставаКонтейнеровПользователей(Источник, ТипИсточника)
	
	УстановитьПривилегированныйРежим(Истина);
	
	ИзмененнныеКонтейнеры = Новый Массив;
	
	// Формирование полного массива измененных контейнеров.
	Если ТипИсточника = Тип("СправочникОбъект.Пользователи") Тогда
		
		ИзмененнныеКонтейнеры = ПользователиДокументооборот.КонтейнерыПользователя(Источник.Ссылка);
		Если Источник.ДополнительныеСвойства.Свойство("СтарыеКонтейнеры") Тогда
			ОбщегоНазначенияКлиентСервер.ДополнитьМассив(
				ИзмененнныеКонтейнеры, Источник.ДополнительныеСвойства.СтарыеКонтейнеры, Истина);
		КонецЕсли;
		
	ИначеЕсли ТипИсточника = Тип("СправочникОбъект.СтруктураПредприятия") Тогда
		
		ВсеПодразделенияКПересчету = Неопределено;
		Источник.ДополнительныеСвойства.Свойство("ВсеПодразделенияКПересчету", ВсеПодразделенияКПересчету);
		
		Если ТипЗнч(ВсеПодразделенияКПересчету) = Тип("Массив") Тогда
			ИзмененнныеКонтейнеры = ВсеПодразделенияКПересчету;
		КонецЕсли;
		
	ИначеЕсли ТипИсточника = Тип("СправочникОбъект.РабочиеГруппы") Тогда
		
		ИзмененнныеКонтейнеры.Добавить(Источник.Ссылка);
		ВсеРодители = ОбщегоНазначенияДокументооборот.ВсеРодителиЭлемента(Источник.Ссылка);
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ИзмененнныеКонтейнеры, ВсеРодители);
		
		СтарыйРодитель = Неопределено;
		Если Источник.ДополнительныеСвойства.Свойство("СтарыйРодитель", СтарыйРодитель) Тогда
			ИзмененнныеКонтейнеры.Добавить(СтарыйРодитель);
			ВсеРодители = ОбщегоНазначенияДокументооборот.ВсеРодителиЭлемента(СтарыйРодитель);
			ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ИзмененнныеКонтейнеры, ВсеРодители);
		КонецЕсли;
		
	ИначеЕсли ТипИсточника = Тип("РегистрСведенийНаборЗаписей.ИсполнителиЗадач") Тогда
		
		ОтборПоРоли = Источник.Отбор.РольИсполнителя;
		Если ОтборПоРоли.Использование Тогда
			ИзмененнныеКонтейнеры.Добавить(ОтборПоРоли.Значение);
		Иначе
			РолиНабора = Источник.ВыгрузитьКолонку("РольИсполнителя");
			ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ИзмененнныеКонтейнеры, РолиНабора);
		КонецЕсли;
		
	ИначеЕсли ТипИсточника = Тип("РегистрСведенийНаборЗаписей.СведенияОПользователяхДокументооборот") Тогда
		
		НовоеПодразделение = Источник.ДополнительныеСвойства.НовоеПодразделение;
		СтароеПодразделение = Источник.ДополнительныеСвойства.СтароеПодразделение;
		
		НовыеПодразделения = ОбщегоНазначенияДокументооборот.ВсеРодителиЭлемента(НовоеПодразделение);
		СтарыеПодразделения = ОбщегоНазначенияДокументооборот.ВсеРодителиЭлемента(СтароеПодразделение);
		
		Если ЗначениеЗаполнено(НовоеПодразделение) Тогда
			НовыеПодразделения.Вставить(0, НовоеПодразделение);
		КонецЕсли;
		
		Если ЗначениеЗаполнено(СтароеПодразделение) Тогда
			СтарыеПодразделения.Вставить(0, СтароеПодразделение);
		КонецЕсли;
		
		НепересекающиесяПодразделения = НепересекающиесяПодразделения(НовыеПодразделения, СтарыеПодразделения);
		ИзмененнныеКонтейнеры = НепересекающиесяПодразделения.Все;
		
	КонецЕсли;
	
	Если ИзмененнныеКонтейнеры.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ОбновитьПраваПоСоставуКонтейнеров(ИзмененнныеКонтейнеры);
	
КонецПроцедуры

// Пересчитывает права, зависящие от состава переданных контейнеров.
//
// Параметры:
//  ИзмененнныеКонтейнеры - Массив - массив контейнеров.
//  Немедленно - Булево - если Ложь, то задание будет поставлено в очередь.
//
// 
Процедура ОбновитьПраваПоСоставуКонтейнеров(ИзмененнныеКонтейнеры, Немедленно = Ложь) Экспорт
	
	Если ДокументооборотПраваДоступаПовтИсп.ОтложенноеОбновлениеПравДоступа() И Не Немедленно Тогда
		
		Для Каждого Контейнер Из ИзмененнныеКонтейнеры Цикл
			РегистрыСведений.ОчередьОбновленияПравДоступа.Добавить(
				Контейнер, Дата("00000000"), 2, "ИзменениеСоставаКонтейнера");
		КонецЦикла;
		
		Возврат;
		
	КонецЕсли;
	
	// Политики доступа.
	
	// Права по общим разрешениям.
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	РазрешенияДоступаОбщие.ЗначениеДоступа
		|ИЗ
		|	РегистрСведений.РазрешенияДоступаОбщие КАК РазрешенияДоступаОбщие
		|ГДЕ
		|	РазрешенияДоступаОбщие.Пользователь В(&ИзмененнныеКонтейнеры)");
	
	Запрос.УстановитьПараметр("ИзмененнныеКонтейнеры", ИзмененнныеКонтейнеры);
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ОбновитьПраваПоЗначениюРазрезаДоступа(Выборка.ЗначениеДоступа);
	КонецЦикла;
	
	// Права по специальным разрешениям.
	Запрос = Новый Запрос(
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	РазрешенияДоступаИсключительные.Предмет
		|ИЗ
		|	РегистрСведений.РазрешенияДоступаИсключительные КАК РазрешенияДоступаИсключительные
		|ГДЕ
		|	РазрешенияДоступаИсключительные.Пользователь В(&ИзмененнныеКонтейнеры)");
	
	Запрос.УстановитьПараметр("ИзмененнныеКонтейнеры", ИзмененнныеКонтейнеры);
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		РассчитатьПраваОбъектовТаблицы(Выборка.Предмет);
	КонецЦикла;
	
	// Права разрезов доступа из спец. разрешений.
	Запрос = Новый Запрос(
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ДескрипторыДляОбъектов.Дескриптор
		|ИЗ
		|	РегистрСведений.РазрешенияДоступаИсключительные КАК РазрешенияДоступаИсключительные
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КоллекцииЗначенийДоступа.ЗначенияДоступа КАК КоллекцииЗначенийДоступаЗначенияДоступа
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.РазрешенияДляРазрезовДоступа КАК РазрешенияДляРазрезовДоступа
		|				ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ДескрипторыДляОбъектов КАК ДескрипторыДляОбъектов
		|				ПО РазрешенияДляРазрезовДоступа.РазрезДоступа = ДескрипторыДляОбъектов.Объект
		|			ПО КоллекцииЗначенийДоступаЗначенияДоступа.Значение = РазрешенияДляРазрезовДоступа.Разрешение
		|		ПО РазрешенияДоступаИсключительные.КоллекцияЗначенийДоступа = КоллекцииЗначенийДоступаЗначенияДоступа.Ссылка
		|ГДЕ
		|	РазрешенияДоступаИсключительные.Пользователь В(&ИзмененнныеКонтейнеры)");
	
	Запрос.УстановитьПараметр("ИзмененнныеКонтейнеры", ИзмененнныеКонтейнеры);
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ОбновитьПраваДоступаПоДескриптору(Выборка.Дескриптор);
	КонецЦикла;
	
	// Поля дескриптора доступа объекта.
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ДескрипторыДоступаОбъектов.Ссылка
		|ИЗ
		|	Справочник.ДескрипторыДоступаОбъектов КАК ДескрипторыДоступаОбъектов
		|ГДЕ
		|	ДескрипторыДоступаОбъектов.КонтейнерПользователей В(&ИзмененнныеКонтейнеры)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ДескрипторыДоступаОбъектовНастройкаПрав.Ссылка
		|ИЗ
		|	Справочник.ДескрипторыДоступаОбъектов.НастройкаПрав КАК ДескрипторыДоступаОбъектовНастройкаПрав
		|ГДЕ
		|	ДескрипторыДоступаОбъектовНастройкаПрав.Пользователь В(&ИзмененнныеКонтейнеры)";
	
	Запрос.УстановитьПараметр("ИзмененнныеКонтейнеры", ИзмененнныеКонтейнеры);
	Результат = Запрос.Выполнить();
	
	Выборка = Результат.Выбрать();
	Пока Выборка.Следующий() Цикл
		Справочники.ДескрипторыДоступаОбъектов.ОбновитьПрава(Выборка.Ссылка);
	КонецЦикла;
	
	// Объектные дескрипторы - дескрипторы процессов и проч.
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ДескрипторыДоступаОбъектов.Ссылка
		|ИЗ
		|	Справочник.ДескрипторыДоступаОбъектов КАК ДескрипторыДоступаОбъектов
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.УчастникиПроцессов КАК УчастникиПроцессов
		|		ПО ДескрипторыДоступаОбъектов.ОбъектДоступа = УчастникиПроцессов.Процесс
		|ГДЕ
		|	УчастникиПроцессов.Участник В(&ИзмененнныеКонтейнеры)";
	
	Запрос.УстановитьПараметр("ИзмененнныеКонтейнеры", ИзмененнныеКонтейнеры);
	Результат = Запрос.Выполнить();
	
	Выборка = Результат.Выбрать();
	Пока Выборка.Следующий() Цикл
		Справочники.ДескрипторыДоступаОбъектов.ОбновитьПрава(Выборка.Ссылка);
	КонецЦикла;
	
	// Если изменена группа ВсеПользователи, 
	// то выполняется обновление пустых дескрипторов и дескрипторов групп.
	Если ИзмененнныеКонтейнеры.Найти(Справочники.РабочиеГруппы.ВсеПользователи) <> Неопределено Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	ДескрипторыДоступаОбъектов.Ссылка
			|ИЗ
			|	Справочник.ДескрипторыДоступаОбъектов КАК ДескрипторыДоступаОбъектов
			|ГДЕ
			|	ДескрипторыДоступаОбъектов.ЭтоПустойДескриптор
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	ДескрипторыДоступаОбъектов.Ссылка
			|ИЗ
			|	Справочник.ДескрипторыДоступаОбъектов КАК ДескрипторыДоступаОбъектов
			|ГДЕ
			|	ДескрипторыДоступаОбъектов.Группа";
		
		Результат = Запрос.Выполнить();
		
		Выборка = Результат.Выбрать();
		Пока Выборка.Следующий() Цикл
			Справочники.ДескрипторыДоступаОбъектов.ОбновитьПрава(Выборка.Ссылка);
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

// Обработчик подписки ДокументооборотПередЗаписьюПравообразующихРегистров
Процедура ДокументооборотПередЗаписьюПравообразующихРегистров(Источник, Отказ, Замещение) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если Не ДокументооборотПраваДоступаПовтИсп.ВключеноИспользованиеПравДоступа() Тогда
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(Источник) = Тип("РегистрСведенийНаборЗаписей.СведенияОПользователяхДокументооборот") Тогда
		
		// Групповая запись не обрабатывается подпиской.
		Пользователь = Источник.Отбор.Пользователь.Значение;
		Если Не ЗначениеЗаполнено(Пользователь) Тогда
			Возврат;
		КонецЕсли;
		
		СведенияОПользователе =
			РегистрыСведений.СведенияОПользователяхДокументооборот.ПолучитьСведенияОПользователе(Пользователь);
			
		Если СведенияОПользователе.Свойство("Подразделение") Тогда
			СтароеПодразделение = СведенияОПользователе.Подразделение;
		Иначе
			СтароеПодразделение = Справочники.СтруктураПредприятия.ПустаяСсылка();
		КонецЕсли;
		
		Если Источник.Количество() = 1 Тогда
			НовоеПодразделение = Источник[0].Подразделение;
		Иначе
			НовоеПодразделение = Справочники.СтруктураПредприятия.ПустаяСсылка();
		КонецЕсли;
		
		Если СтароеПодразделение <> НовоеПодразделение Тогда
			Источник.ДополнительныеСвойства.Вставить("ЗначениеИзменено", Истина);
			Источник.ДополнительныеСвойства.Вставить("СтароеПодразделение", СтароеПодразделение);
			Источник.ДополнительныеСвойства.Вставить("НовоеПодразделение", НовоеПодразделение);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

// Обработчик подписки ДокументооборотПриЗаписиПравообразующихРегистров
Процедура ДокументооборотПриЗаписиПравообразующихРегистров(Источник, Отказ, Замещение) Экспорт
	
	Если Источник.ДополнительныеСвойства.Свойство("РеорганизацияРолейСОбъектамиАдресации") Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если Не ДокументооборотПраваДоступаПовтИсп.ВключеноИспользованиеПравДоступа() Тогда
		Возврат;
	КонецЕсли;
	
	ТипИсточника = ТипЗнч(Источник);
	ОбновитьПромежуточныеДанныеДляРасчетаПрав(Источник, ТипИсточника);
	
	Если ДокументооборотПраваДоступаПереопределяемый.ДокументооборотПриЗаписиПравообразующихРегистров(Источник, Отказ) Тогда
		Возврат;
	КонецЕсли;
	
	Если ТипИсточника = Тип("РегистрСведенийНаборЗаписей.ДескрипторыДляОбъектов") Тогда
		
		УникальныеОбъекты = Новый Соответствие;
		Для Каждого Эл Из Источник Цикл
			Если УникальныеОбъекты.Получить(Эл.Объект) = Неопределено Тогда
				УникальныеОбъекты.Вставить(Эл.Объект, Истина);
			КонецЕсли;
		КонецЦикла;
		
		Для Каждого Эл Из УникальныеОбъекты Цикл
			
			ОбъектДоступа = Эл.Ключ;
			
			Если ЭтоПапка(ТипЗнч(ОбъектДоступа)) Тогда
				ОбновитьПраваПоПапке(ОбъектДоступа);
			КонецЕсли;
			
			// Переопределение дескрипторов связанных объектов
			Если Не Источник.ДополнительныеСвойства.Свойство("ПропуститьОпределениеЗависимыхПрав")
				Или Не Источник.ДополнительныеСвойства.ПропуститьОпределениеЗависимыхПрав Тогда
				
				ПереопределитьДескрипторыЗависимыхОбъектов(ОбъектДоступа);
				
			КонецЕсли;
			
		КонецЦикла;
		
	ИначеЕсли ТипИсточника = Тип("РегистрСведенийНаборЗаписей.РазрешенияДоступаОбщие") Тогда
		
		ОтборПоЗначениюДоступа = Источник.Отбор.ЗначениеДоступа;
		
		Если Не ОтборПоЗначениюДоступа.Использование Тогда
			Возврат;
		КонецЕсли;
		
		ЗначениеСсылки = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ОтборПоЗначениюДоступа.Значение, "Ссылка");
		Если ЗначениеСсылки = Неопределено Тогда
			Если Не Источник.ОбменДанными.Загрузка Или Источник.Количество() = 0 Тогда
				// Это удаление элемента, ставить его в очередь не нужно.
				Возврат;
			КонецЕсли;
		КонецЕсли;
		
		ОбновитьПраваПоЗначениюРазрезаДоступа(ОтборПоЗначениюДоступа.Значение);
		
	ИначеЕсли ТипИсточника = Тип("РегистрСведенийНаборЗаписей.РазрешенияДоступаИсключительные") Тогда
		
		ПредметыКОбновлению = Неопределено;
		Источник.ДополнительныеСвойства.Свойство("ИзмененныеПредметы", ПредметыКОбновлению);
		
		Если Не ЗначениеЗаполнено(ПредметыКОбновлению) Тогда
			ОтборПоПредмету = Источник.Отбор.Предмет;
			Если ОтборПоПредмету.Использование Тогда
				ПредметыКОбновлению = Новый Массив;
				ПредметыКОбновлению.Добавить(ОтборПоПредмету.Значение);
			КонецЕсли;
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(ПредметыКОбновлению) Тогда
			Возврат;
		КонецЕсли;
		
		Запрос = Новый Запрос(
			"ВЫБРАТЬ
			|	ДескрипторыДоступаОбъектов.Ссылка
			|ИЗ
			|	Справочник.ДескрипторыДоступаОбъектов КАК ДескрипторыДоступаОбъектов
			|ГДЕ
			|	ДескрипторыДоступаОбъектов.ИдентификаторОбъектаМетаданных В(&ПредметыКОбновлению)
			|	И ДескрипторыДоступаОбъектов.КонтейнерПользователей = НЕОПРЕДЕЛЕНО
			|	И НЕ ДескрипторыДоступаОбъектов.ДляЛокальныхАдминистраторов");
			
		Запрос.УстановитьПараметр("ПредметыКОбновлению", ПредметыКОбновлению);
		
		ТекущийПриоритетОчередиОбновленияПрав = ПараметрыСеанса.ПриоритетОчередиОбновленияПрав;
		ПараметрыСеанса.ПриоритетОчередиОбновленияПрав = 2; // Долгая очередь
		
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			Справочники.ДескрипторыДоступаОбъектов.ОбновитьПрава(Выборка.Ссылка);
		КонецЦикла;
		
		ПараметрыСеанса.ПриоритетОчередиОбновленияПрав = ТекущийПриоритетОчередиОбновленияПрав;
		
	ИначеЕсли ТипИсточника = Тип("РегистрСведенийНаборЗаписей.РазрешенияДляЛокальныхАдминистраторов") Тогда
		
		ПредметыКОбновлению = Неопределено;
		Источник.ДополнительныеСвойства.Свойство("ИзмененныеПредметы", ПредметыКОбновлению);
		
		Если Не ЗначениеЗаполнено(ПредметыКОбновлению) Тогда
			ОтборПоПредмету = Источник.Отбор.Предмет;
			Если ОтборПоПредмету.Использование Тогда
				ПредметыКОбновлению = Новый Массив;
				ПредметыКОбновлению.Добавить(ОтборПоПредмету.Значение);
			КонецЕсли;
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(ПредметыКОбновлению) Тогда
			Возврат;
		КонецЕсли;
		
		Запрос = Новый Запрос(
			"ВЫБРАТЬ
			|	ДескрипторыДоступаОбъектов.Ссылка
			|ИЗ
			|	Справочник.ДескрипторыДоступаОбъектов КАК ДескрипторыДоступаОбъектов
			|ГДЕ
			|	ДескрипторыДоступаОбъектов.ИдентификаторОбъектаМетаданных В(&ПредметыКОбновлению)
			|	И ДескрипторыДоступаОбъектов.КонтейнерПользователей = НЕОПРЕДЕЛЕНО
			|	И ДескрипторыДоступаОбъектов.ДляЛокальныхАдминистраторов");
			
		Запрос.УстановитьПараметр("ПредметыКОбновлению", ПредметыКОбновлению);
		
		ТекущийПриоритетОчередиОбновленияПрав = ПараметрыСеанса.ПриоритетОчередиОбновленияПрав;
		ПараметрыСеанса.ПриоритетОчередиОбновленияПрав = 2; // Долгая очередь
		
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			Справочники.ДескрипторыДоступаОбъектов.ОбновитьПрава(Выборка.Ссылка);
		КонецЦикла;
		
		ПараметрыСеанса.ПриоритетОчередиОбновленияПрав = ТекущийПриоритетОчередиОбновленияПрав;
		
	ИначеЕсли ТипИсточника = Тип("РегистрСведенийНаборЗаписей.РазрешенияДляРазрезовДоступа") Тогда
		
		// Обновление прав выполняется в ПриЗаписиРазрезаДоступа
		
	ИначеЕсли ТипИсточника = Тип("РегистрСведенийНаборЗаписей.ИсполнителиЗадач") Тогда
		
		ПриИзмененииСоставаКонтейнеровПользователей(Источник, ТипИсточника);
		
	ИначеЕсли ТипИсточника = Тип("РегистрСведенийНаборЗаписей.НастройкиПравОбъектов") Тогда
		
		Папка = Источник.Отбор.Объект.Значение;
		Если ЗначениеЗаполнено(Папка) Тогда
			ПапкаОбъект = Папка.ПолучитьОбъект();
			Если ПапкаОбъект <> Неопределено Тогда  // Проверка на удаление папки
				
				// Аналог ОпределитьДескрипторыОбъекта, но с принудительной записью
				// для обработки изменения ресурса НаследованиеРазрешено
				
				ТаблицаДескрипторов = ОпределитьДескрипторыОбъекта(ПапкаОбъект);
				ТаблицаДескрипторов.ЗаполнитьЗначения(Папка, "Объект");
				ТаблицаДескрипторов.ЗаполнитьЗначения(
					ОбщегоНазначения.ИдентификаторОбъектаМетаданных(Папка.Метаданные()), "ОбъектМетаданных");
				
				НаборЗаписей = РегистрыСведений.ДескрипторыДляОбъектов.СоздатьНаборЗаписей();
				НаборЗаписей.Отбор.Объект.Установить(Папка);
				НаборЗаписей.Загрузить(ТаблицаДескрипторов);
				НаборЗаписей.Записать();
				
			КонецЕсли;
		КонецЕсли;
		
	ИначеЕсли ТипИсточника = Тип("РегистрСведенийНаборЗаписей.НаследованиеНастроекПравОбъектов") Тогда
		
		Если Источник.Количество() > 0 Тогда
			Папка = Источник.Отбор.Объект.Значение;
			Если ЗначениеЗаполнено(Папка)
				// Для новых папок ничего не делаем.
				И ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Папка, "Ссылка") <> Неопределено Тогда
				ОпределитьДескрипторыОбъекта(Папка);
			КонецЕсли;
		КонецЕсли;
		
	ИначеЕсли ТипИсточника = Тип("РегистрСведенийНаборЗаписей.СведенияОПользователяхДокументооборот") Тогда	
		
		Если Источник.ДополнительныеСвойства.Свойство("ЗначениеИзменено") Тогда
			ПриИзмененииСоставаКонтейнеровПользователей(Источник, ТипИсточника);
			Пользователь = Источник.Отбор.Пользователь.Значение;
			АктуализироватьПраваПользователейПоСоставуСубъектов(Пользователь);
		КонецЕсли;
		
	ИначеЕсли ТипИсточника = Тип("РегистрСведенийНаборЗаписей.УчастникиПроцессов") Тогда
		
		// Обновление прав файлов процессов.
		МассивПроцессов = Новый Массив;
		
		Процесс = Источник.Отбор.Процесс.Значение;
		Если ЗначениеЗаполнено(Процесс) Тогда
			МассивПроцессов.Добавить(Процесс);
		Иначе
			Для Каждого СтрокаУчастника Из Источник Цикл
				Процесс = СтрокаУчастника.Процесс;
				Если МассивПроцессов.Найти(Процесс) = Неопределено Тогда
					МассивПроцессов.Добавить(Процесс);
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
		ОбновитьПраваЗависящиеОтСоставаУчастниковПроцесса(МассивПроцессов);
		
	КонецЕсли;
	
КонецПроцедуры

// Обработчик подписки ДокументооборотПраваДоступаПередЗаписьюПравообразующихКонстант
Процедура ДокументооборотПраваДоступаПередЗаписьюПравообразующихКонстант(Источник, Отказ) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если Не ДокументооборотПраваДоступаПовтИсп.ВключеноИспользованиеПравДоступа() Тогда
		Возврат;
	КонецЕсли;
	
	Если Константы[Источник.Метаданные().Имя].Получить() <> Источник.Значение Тогда
		Источник.ДополнительныеСвойства.Вставить("ЗначениеИзменено", Истина);
	КонецЕсли;
	
	Если ТипЗнч(Источник) = Тип("КонстантаМенеджерЗначения.ИспользованиеРазрезовДоступа") Тогда
		
		// Сравнение хранилищ значений.
		СтарыйСписок = Константы[Источник.Метаданные().Имя].Получить().Получить();
		
		Если СтарыйСписок = Неопределено Тогда
			// Это первоначальная установка, дополнительные действия не нужны.
			Источник.ДополнительныеСвойства.Вставить("ЗначениеИзменено", Ложь);
			Возврат;
		КонецЕсли;
		
		ИзмененныеРазрезы = Новый Массив;
		НовыйСписок = Источник.Значение.Получить();
		
		Для Каждого ЭлСтарогоСписка Из СтарыйСписок Цикл
			ТекущийРазрез = ЭлСтарогоСписка.Значение;
			ЭлНовогоСписка = НовыйСписок.НайтиПоЗначению(ТекущийРазрез);
			Если ЭлНовогоСписка = Неопределено Или ЭлСтарогоСписка.Пометка <> ЭлНовогоСписка.Пометка Тогда
				ИзмененныеРазрезы.Добавить(ТекущийРазрез);
			КонецЕсли;
		КонецЦикла;
		
		ЕстьИзменения = ИзмененныеРазрезы.Количество() > 0;
		Источник.ДополнительныеСвойства.Вставить("ЗначениеИзменено", ЕстьИзменения);
		Источник.ДополнительныеСвойства.Вставить("ИзмененныеРазрезы", ИзмененныеРазрезы);
		
	КонецЕсли;
	
КонецПроцедуры

// Обработчик подписки ДокументооборотПраваДоступаПриЗаписиПравообразующихКонстант
Процедура ДокументооборотПраваДоступаПриЗаписиПравообразующихКонстант(Источник, Отказ) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если Не ДокументооборотПраваДоступаПовтИсп.ВключеноИспользованиеПравДоступа() Тогда
		Возврат;
	КонецЕсли;
	
	Если Не Источник.ДополнительныеСвойства.Свойство("ЗначениеИзменено") 
		Или Не Источник.ДополнительныеСвойства.ЗначениеИзменено Тогда
		Возврат;
	КонецЕсли;
	
	ОбновитьПромежуточныеДанныеДляРасчетаПрав(Источник, ТипЗнч(Источник));
	
	// Вызов переопределяемого метода
	Если ДокументооборотПраваДоступаПереопределяемый.ДокументооборотПраваДоступаПриЗаписиПравообразующихКонстант(Источник, Отказ) Тогда
		Возврат;
	КонецЕсли;
	
	ТекущийПриоритетОчередиОбновленияПрав = ПараметрыСеанса.ПриоритетОчередиОбновленияПрав;
	ПараметрыСеанса.ПриоритетОчередиОбновленияПрав = 2; // Долгая очередь
	Попытка
		ОбновитьПовторноИспользуемыеЗначения();
		ПриЗаписиКонстанты(Источник);
		ПараметрыСеанса.ПриоритетОчередиОбновленияПрав = ТекущийПриоритетОчередиОбновленияПрав;
	Исключение	
		ПараметрыСеанса.ПриоритетОчередиОбновленияПрав = ТекущийПриоритетОчередиОбновленияПрав;
		ВызватьИсключение;
	КонецПопытки;
	
КонецПроцедуры

// Обработчик подписки ДокументооборотПраваДоступаПередЗаписьюПапок
Процедура ДокументооборотПраваДоступаПередЗаписьюПапок(Источник, Отказ) Экспорт
	
	Если Источник.ОбменДанными.Загрузка = Истина Тогда
		Возврат;
	КонецЕсли;
	
	ПредыдущееЗначениеРодитель = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Источник.Ссылка,"Родитель");
	Если Не Источник.ЭтоНовый() И Источник.Родитель <> ПредыдущееЗначениеРодитель Тогда
		
		Если ЗначениеЗаполнено(Источник.Родитель)
			И (Источник.Родитель = Источник.Ссылка 
				Или Источник.Родитель.ПринадлежитЭлементу(Источник.Ссылка)) Тогда
			
			ВызватьИсключение НСтр("ru = 'Нельзя перемещать папки в собственные подпапки.'; en = 'You cannot move a folder into its own subfolder.'");
			
			Возврат;
			
		КонецЕсли;
		
		СообщениеОбОшибке = "";
		
		Права = ДокументооборотПраваДоступа.ПолучитьПраваПоОбъекту(Источник.Ссылка);
		Если Не Права.Изменение Тогда
			СообщениеОбОшибке = НСтр("ru = 'Недостаточно прав на изменение папки.'; en = 'Insufficient permissions to modify folder.'");
			ВызватьИсключение СообщениеОбОшибке;
		КонецЕсли;
		
		Если Не ЕстьПравоПеремещенияПапки(
			ПредыдущееЗначениеРодитель, Источник.Родитель, СообщениеОбОшибке) Тогда
			
			ВызватьИсключение СообщениеОбОшибке;
			
		КонецЕсли;
		
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	РегистрыСведений.НаследованиеНастроекПравОбъектов.Обновить(Источник);
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

// Обработчик регламентного задания ДокументооборотОбновлениеПравДоступаОперативное
Процедура ДокументооборотОбновлениеПравДоступаОперативное() Экспорт
	
	ОбщегоНазначения.ПриНачалеВыполненияРегламентногоЗадания();
	
	ОбновитьПовторноИспользуемыеЗначения();
	
	ОбработатьОчередьОбновленияПравДоступа(1);
	
КонецПроцедуры

Процедура ПередЗаписьюРазрезаДоступа(Источник, Отказ) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если Источник.ЭтоНовый() Или (Источник.Метаданные().Иерархический
			И Источник.Родитель <> ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Источник.Ссылка, "Родитель")) Тогда
		
		Источник.ДополнительныеСвойства.Вставить("ЗначениеИзменено", Истина);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриЗаписиРазрезаДоступа(Источник, Отказ) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	// Проверка на то, что правообразующие свойства источника были изменены
	Если Не Источник.ДополнительныеСвойства.Свойство("ЗначениеИзменено") 
		Или Не Источник.ДополнительныеСвойства.ЗначениеИзменено Тогда
		
		Возврат;
		
	КонецЕсли;
	
	// Актуализация данных РС РазрешенияДляРазрезовДоступа.
	МетаданныеИсточника = Источник.Метаданные();
	ЭтоИерархическийСправочник = МетаданныеИсточника.Иерархический;
	ЭлементыРазрешений = Новый Массив;
	
	Если Не ЭтоИерархическийСправочник Тогда
		
		ЭлементыРазрешений.Добавить(Источник.Ссылка);
		
	Иначе
		
		Запрос = Новый Запрос(
			"ВЫБРАТЬ
			|	ТаблицаЗначенияДоступа.Ссылка
			|ИЗ
			|	#ТаблицаЗначенияДоступа КАК ТаблицаЗначенияДоступа
			|ГДЕ
			|	ТаблицаЗначенияДоступа.Ссылка В ИЕРАРХИИ(&Родитель)
			|	И НЕ ТаблицаЗначенияДоступа.ЭтоГруппа");
		
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "#ТаблицаЗначенияДоступа", МетаданныеИсточника.ПолноеИмя());
			
		Если МетаданныеИсточника.ВидИерархии = Метаданные.СвойстваОбъектов.ВидИерархии.ИерархияЭлементов Тогда
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "И НЕ ТаблицаЗначенияДоступа.ЭтоГруппа", "");
		КонецЕсли;
		
		Запрос.УстановитьПараметр("Родитель", Источник.Ссылка);
		ЭлементыРазрешений = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
		
	КонецЕсли;
	
	Для Каждого ЭлементРазрешения Из ЭлементыРазрешений Цикл
		
		ВозможныеРазрешения = Новый Массив;
		ВозможныеРазрешения.Добавить(ЭлементРазрешения);
		ВозможныеРазрешения.Добавить(ДокументооборотПраваДоступа.РазрезДоступаПоЗначению(ЭлементРазрешения));
		
		Если ЭтоИерархическийСправочник Тогда
			Родитель = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ЭлементРазрешения, "Родитель");
			Пока ЗначениеЗаполнено(Родитель) Цикл
				ВозможныеРазрешения.Добавить(Родитель);
				Родитель = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Родитель, "Родитель");
			КонецЦикла;
		КонецЕсли;
		
		// Обновление разрешений для элемента.
		Набор = РегистрыСведений.РазрешенияДляРазрезовДоступа.СоздатьНаборЗаписей();
		Набор.Отбор.РазрезДоступа.Установить(ЭлементРазрешения);
		
		Для Каждого Разрешение Из ВозможныеРазрешения Цикл
			Стр = Набор.Добавить();
			Стр.РазрезДоступа = ЭлементРазрешения;
			Стр.Разрешение = Разрешение;
		КонецЦикла;
		
		Набор.Записать();
		ОбновитьПраваПоЗначениюРазрезаДоступа(ЭлементРазрешения);
		
	КонецЦикла;
	
	ОбновитьПовторноИспользуемыеЗначения();
	
КонецПроцедуры

// Обработчик регламентного задания ДокументооборотОбновлениеПравДоступаДолгое
Процедура ДокументооборотОбновлениеПравДоступаДолгое() Экспорт
	
	ОбщегоНазначения.ПриНачалеВыполненияРегламентногоЗадания();
	
	УстановитьПривилегированныйРежим(Истина);
	ОбновитьПовторноИспользуемыеЗначения();
	
	ТекущийПриоритетОчередиОбновленияПрав = ПараметрыСеанса.ПриоритетОчередиОбновленияПрав;
	ПараметрыСеанса.ПриоритетОчередиОбновленияПрав = 2; 
	
	ОбработатьОчередьОбновленияПравДоступа(2);
	
	ПараметрыСеанса.ПриоритетОчередиОбновленияПрав = ТекущийПриоритетОчередиОбновленияПрав;
	
КонецПроцедуры

Процедура ОбработатьОчередьОбновленияПравДоступа(Приоритет) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ПараметрыСеанса.ПриоритетОчередиОбновленияПрав = Приоритет;
	
	ЗаписьЖурналаРегистрации(
		НСтр("ru = 'Права доступа'; en = 'Permissions'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка()),
		УровеньЖурналаРегистрации.Информация,,
		Приоритет,
		НСтр("ru = 'Начато обновление прав доступа'; en = 'Permissions update is started'"));
	
	Количество = РегистрыСведений.ОчередьОбновленияПравДоступа.ОбработатьПорцию();
	Пока Количество <> 0 Цикл
		
		Количество = РегистрыСведений.ОчередьОбновленияПравДоступа.ОбработатьПорцию();
		
	КонецЦикла;	

	ЗаписьЖурналаРегистрации(
		НСтр("ru = 'Права доступа'; en = 'Permissions'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка()),
		УровеньЖурналаРегистрации.Информация,,
		Приоритет,
		НСтр("ru = 'Закончено обновление прав доступа'; en = 'Permissions update is finished'"));
	
КонецПроцедуры

// Обновляет права всех дескрипторов, которые зависят от указанного дескриптора
Процедура ОбновитьПраваСвязанныхДескрипторовПоДескриптору(ДескрипторСсылка) Экспорт
	
	Дескрипторы = Новый Массив;
	Дескрипторы.Добавить(ДескрипторСсылка);
	ОбновитьПраваСвязанныхДескрипторовПоДескрипторам(Дескрипторы);
	
КонецПроцедуры

// Обновляет права всех дескрипторов, которые зависят от указанных дескрипторов.
// 
Процедура ОбновитьПраваСвязанныхДескрипторовПоДескрипторам(Дескрипторы) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ЗначениеПустойСсылки = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Дескрипторы[0],
		"ИдентификаторОбъектаМетаданных.ЗначениеПустойСсылки");
	ТипСсылки = ТипЗнч(ЗначениеПустойСсылки);
	
	Если Не ЭтоПапка(ТипСсылки) Или ТипСсылки = Тип("СправочникСсылка.ПапкиФайлов") Тогда
		Возврат;
	КонецЕсли;
	
	// Обработка прав содержимого папок.
	Запрос = Новый Запрос(
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ДескрипторыДоступаОбъектов.Ссылка
		|ИЗ
		|	РегистрСведений.ДескрипторыДляОбъектов КАК ДескрипторыДляОбъектов
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ДескрипторыДоступаОбъектов КАК ДескрипторыДоступаОбъектов
		|		ПО ДескрипторыДляОбъектов.Объект = ДескрипторыДоступаОбъектов.Папка
		|ГДЕ
		|	ДескрипторыДляОбъектов.Дескриптор В(&ДескрипторыПапок)
		|	И НЕ ДескрипторыДляОбъектов.Отключен");
	
	Запрос.УстановитьПараметр("ДескрипторыПапок", Дескрипторы);
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Справочники.ДескрипторыДоступаОбъектов.ОбновитьПрава(Выборка.Ссылка);
	КонецЦикла;
	
КонецПроцедуры

// Обновляет права всех дескрипторов, которые содержат ссылку на значение разреза доступа.
// 
Процедура ОбновитьПраваПоЗначениюРазрезаДоступа(РазрезДоступа, Немедленно = Ложь) Экспорт
	
	Если Не ЗначениеЗаполнено(РазрезДоступа) Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ДокументооборотПраваДоступаПовтИсп.ВключеноИспользованиеПравДоступа() Тогда
		Возврат;
	КонецЕсли;
	
	Если Немедленно <> Истина
		И ДокументооборотПраваДоступаПовтИсп.ОтложенноеОбновлениеПравДоступа() Тогда
		
		РегистрыСведений.ОчередьОбновленияПравДоступа.Добавить(
			РазрезДоступа, Дата("00000000"), 2, "ОбновитьПраваПоЗначениюРазрезаДоступа");
		
		Возврат;
		
	КонецЕсли;
	
	СтрокаВидаДоступа = Неопределено;
	ВидДоступа = РазрезДоступаПоЗначению(РазрезДоступа);
	
	ТаблицаРазрезовДоступа = ДокументооборотПраваДоступаПовтИсп.ТаблицаРазрезовДоступа(Ложь);
	СтрокаВидаДоступа = ТаблицаРазрезовДоступа.Найти(ВидДоступа, "ВидДоступа");
	
	Если СтрокаВидаДоступа <> Неопределено Тогда
		
		Запрос = Новый Запрос(
			"ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	ТаблицаДескриптора.Ссылка
			|ИЗ
			|	Справочник.ДескрипторыДоступаОбъектов%1 КАК ТаблицаДескриптора
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.РазрешенияДляРазрезовДоступа КАК РазрешенияДляРазрезовДоступа
			|		ПО ТаблицаДескриптора.%2 = РазрешенияДляРазрезовДоступа.РазрезДоступа
			|ГДЕ
			|	РазрешенияДляРазрезовДоступа.Разрешение = &РазрезДоступа");
			
		Запрос.Текст = СтрШаблон(Запрос.Текст,
			?(ЗначениеЗаполнено(СтрокаВидаДоступа.ИмяТЧДескриптора),
				"." + СтрокаВидаДоступа.ИмяТЧДескриптора, ""),
			СтрокаВидаДоступа.ИмяПоляТаблицыДескриптора);
		
		Запрос.УстановитьПараметр("РазрезДоступа", РазрезДоступа);
		
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			Справочники.ДескрипторыДоступаОбъектов.ОбновитьПрава(Выборка.Ссылка);
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры
		
// Переопределяет дескрипторы объектов, зависимых от данного
//
// Параметры:
//  ОбъектВладелец - Ссылка - ссылка на объект, зависимые объекты которого нужно обработать
//  Немедленно - Булево - если ложь, то задание будет поставлено в очередь.
//
Процедура ПереопределитьДескрипторыЗависимыхОбъектов(ОбъектВладелец, Немедленно = Ложь) Экспорт
	
	ТипОбъекта = ТипЗнч(ОбъектВладелец);
	ЭтоПапка = ЭтоПапка(ТипОбъекта);
	
	// Для папок выполняется через очередь (кроме папок файлов).
	Если ДокументооборотПраваДоступаПовтИсп.ОтложенноеОбновлениеПравДоступа() И Немедленно <> Истина
		И ЭтоПапка И ТипОбъекта <> Тип("СправочникСсылка.ПапкиФайлов") Тогда
		
		// Добавление в очередь
		РегистрыСведений.ОчередьОбновленияПравДоступа.Добавить(
			ОбъектВладелец,, 2, "ПереопределитьПраваСодержимогоПапки");
		Возврат;
		
	КонецЕсли;
	
	// Содержимое папок
	Если ЭтоПапка Тогда
		ПереопределитьПраваСодержимогоПапки(ОбъектВладелец, ТипОбъекта);
	КонецЕсли;
	
	// Дескрипторы от владельца с признаком ПолученОт.
	Запрос = Новый Запрос(
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ДескрипторыДляОбъектов.Объект КАК ЗависимыйОбъект
		|ИЗ
		|	РегистрСведений.ДескрипторыДляОбъектов КАК ДескрипторыДляОбъектов
		|ГДЕ
		|	ДескрипторыДляОбъектов.ПолученОт = &ОбъектВладелец");
		
	Запрос.УстановитьПараметр("ОбъектВладелец", ОбъектВладелец);
	Результат = Запрос.Выполнить();
	
	Если Не Результат.Пустой() Тогда
		
		ЗапросПоДескрипторамВладельца = Новый Запрос(
			"ВЫБРАТЬ
			|	ДескрипторыДляОбъектов.Дескриптор,
			|	МИНИМУМ(ДескрипторыДляОбъектов.Отключен) КАК Отключен
			|ИЗ
			|	РегистрСведений.ДескрипторыДляОбъектов КАК ДескрипторыДляОбъектов
			|ГДЕ
			|	ДескрипторыДляОбъектов.Объект = &ОбъектВладелец
			|
			|СГРУППИРОВАТЬ ПО
			|	ДескрипторыДляОбъектов.Дескриптор");
		
		ЗапросПоДескрипторамВладельца.УстановитьПараметр("ОбъектВладелец", ОбъектВладелец);
		ТаблицаДескрипторовВладельца = ЗапросПоДескрипторамВладельца.Выполнить().Выгрузить();
		
		Выборка = Результат.Выбрать();
		Пока Выборка.Следующий() Цикл
			
			Набор = РегистрыСведений.ДескрипторыДляОбъектов.СоздатьНаборЗаписей();
			Набор.Отбор.Объект.Установить(Выборка.ЗависимыйОбъект);
			Набор.Отбор.ТипДескриптора.Установить(3);
			
			ОбъектМетаданных = ОбщегоНазначения.ИдентификаторОбъектаМетаданных(
				Выборка.ЗависимыйОбъект.Метаданные());
			
			Для Каждого СтрВладельца Из ТаблицаДескрипторовВладельца Цикл
				НоваяЗапись = Набор.Добавить();
				НоваяЗапись.Объект = Выборка.ЗависимыйОбъект;
				НоваяЗапись.ОбъектМетаданных = ОбъектМетаданных;
				НоваяЗапись.ТипДескриптора = 3;
				НоваяЗапись.ПолученОт = ОбъектВладелец;
				НоваяЗапись.Дескриптор = СтрВладельца.Дескриптор;
				НоваяЗапись.Отключен = СтрВладельца.Отключен;
			КонецЦикла;
			
			Набор.Записать();
			
		КонецЦикла;
		
	КонецЕсли;
	
	// ТСК Талько Э.Г.; 09.06.2018; Управление доступом {
	ра_ОбщегоНазначения.ПереопределитьДескрипторыЗависимыхОбъектов(ОбъектВладелец);
	// ТСК Талько Э.Г.; 09.06.2018; Управление доступом }
	
КонецПроцедуры

Процедура ПереопределитьПраваСодержимогоПапки(Папка, ТипПапки)
	
	УстановитьПривилегированныйРежим(Истина);
	
	ВсеТипыПапокИСодержимого = ДокументооборотПраваДоступаПовтИсп.ТипыПапокИСодержимого();
	МетаданныеСодержимогоПапки = ВсеТипыПапокИСодержимого.Получить(ТипПапки);
	
	Если МетаданныеСодержимогоПапки = Неопределено Тогда
		ВызватьИсключение НСтр("ru = 'Ошибка при обновлении содержимого папки: тип не определен.'; en = 'Error updating the contents of the folder: type not defined.'");
	КонецЕсли;
	
	// Переопределение прав зависимых папок
	Запрос = Новый Запрос(
		"ВЫБРАТЬ &Папка КАК Объект
		|ПОМЕСТИТЬ ЗависимыеПапки
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	НаследованиеНастроекПравОбъектов.Объект
		|ИЗ
		|	РегистрСведений.НаследованиеНастроекПравОбъектов КАК НаследованиеНастроекПравОбъектов
		// Проверка флага наследования прав от владельцев
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.НаследованиеНастроекПравОбъектов КАК НаследованиеНастроекОтВладельцев
		|		ПО НаследованиеНастроекПравОбъектов.Объект = НаследованиеНастроекОтВладельцев.Объект
		|			И НаследованиеНастроекПравОбъектов.Объект = НаследованиеНастроекОтВладельцев.Родитель
		|			И (НаследованиеНастроекОтВладельцев.Наследовать)
		|ГДЕ
		|	НаследованиеНастроекПравОбъектов.Родитель = &Папка
		|	И НаследованиеНастроекПравОбъектов.Объект <> &Папка
		|	И НаследованиеНастроекПравОбъектов.Наследовать
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДескрипторыДляОбъектов.Объект,
		|	ДескрипторыДляОбъектов.Дескриптор КАК Дескриптор
		|ИЗ
		|	ЗависимыеПапки КАК ЗависимыеПапки
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ДескрипторыДляОбъектов КАК ДескрипторыДляОбъектов
		|		ПО ЗависимыеПапки.Объект = ДескрипторыДляОбъектов.Объект
		|ГДЕ
		// Дескриптор самой папки определять не нужно
		|	ЗависимыеПапки.Объект <> &Папка
		|
		|УПОРЯДОЧИТЬ ПО
		|	Дескриптор");
		
	МВТ = Новый МенеджерВременныхТаблиц;
	Запрос.МенеджерВременныхТаблиц = МВТ;
	
	Запрос.УстановитьПараметр("Папка", Папка);
	Выборка = Запрос.Выполнить().Выбрать();
	
	ЭтоПапкаФайлов = ТипПапки = Тип("СправочникСсылка.ПапкиФайлов");
	ПереопределитьПраваПоГруппамОбъектов(Выборка, Ложь);
	
	// Переопределение прав содержимого папки и зависимых папок
	Если Не ЭтоПапкаФайлов Тогда
		
		Если ПапкаИмеетИндивидуальныеНастройкиПрав(Папка) Тогда
			
			// Нужно переопределить дескрипторы для объектов,
			// в дескрипторах которых указана папка, не входящая в иерархию измененной папки.
			
			ШаблонТекстаЗапросаПоСодержимому = 
				"ВЫБРАТЬ
				|	ДескрипторыДляОбъектов.Объект,
				|	ДескрипторыДляОбъектов.Дескриптор КАК Дескриптор
				|ИЗ
				|	%ТаблицаСодержимогоПапки% КАК ТаблицаСодержимогоПапки
				|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ДескрипторыДляОбъектов КАК ДескрипторыДляОбъектов
				|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ДескрипторыДоступаОбъектов КАК ДескрипторыДоступаОбъектов
				|			ПО ДескрипторыДляОбъектов.Дескриптор = ДескрипторыДоступаОбъектов.Ссылка
				|		ПО ТаблицаСодержимогоПапки.Ссылка = ДескрипторыДляОбъектов.Объект
				|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ЗависимыеПапки КАК ЗависимыеПапки
				|		ПО ТаблицаСодержимогоПапки.Папка = ЗависимыеПапки.Объект
				|ГДЕ
				|	ДескрипторыДляОбъектов.ТипДескриптора = 0
				|	И ДескрипторыДоступаОбъектов.Папка <> Неопределено
				|	И НЕ ДескрипторыДоступаОбъектов.Папка В ИЕРАРХИИ (&Папка)
				|
				|УПОРЯДОЧИТЬ ПО
				|	Дескриптор,
				|	ЗависимыеПапки.Объект";
			
		Иначе
			
			// Нужно переопределить дескрипторы для объектов, которые
			// находятся в папках без индивидуальных настроек.
			// Для проверки инд. настроек достаточно обращения к РС НастройкиПравОбъектов,
			// условие по наследованию уже проверено выше.
			
			ШаблонТекстаЗапросаПоСодержимому = 
				"ВЫБРАТЬ
				|	ДескрипторыДляОбъектов.Объект КАК Объект,
				|	ДескрипторыДляОбъектов.Дескриптор КАК Дескриптор
				|ИЗ
				|	%ТаблицаСодержимогоПапки% КАК ТаблицаСодержимогоПапки
				|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ДескрипторыДляОбъектов КАК ДескрипторыДляОбъектов
				|		ПО ТаблицаСодержимогоПапки.Ссылка = ДескрипторыДляОбъектов.Объект
				|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ЗависимыеПапки КАК ЗависимыеПапки
				|		ПО ТаблицаСодержимогоПапки.Папка = ЗависимыеПапки.Объект
				|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкиПравОбъектов КАК НастройкиПравОбъектов
				|		ПО ТаблицаСодержимогоПапки.Папка = НастройкиПравОбъектов.Объект
				|ГДЕ
				|	НастройкиПравОбъектов.Объект ЕСТЬ NULL";
			
		КонецЕсли;
		
		ЗапросПоСодержимому = Новый Запрос;
		ЗапросПоСодержимому.МенеджерВременныхТаблиц = МВТ;
		ЗапросПоСодержимому.УстановитьПараметр("Папка", Папка);
		
		Для Каждого ОбъектМетаданныхСодержимого Из МетаданныеСодержимогоПапки Цикл
			
			ЗапросПоСодержимому.Текст = СтрЗаменить(ШаблонТекстаЗапросаПоСодержимому, 
				"%ТаблицаСодержимогоПапки%", 
				ОбъектМетаданныхСодержимого.ПолноеИмя());
			
			Выборка = ЗапросПоСодержимому.Выполнить().Выбрать();
			ПереопределитьПраваПоГруппамОбъектов(Выборка);
			
		КонецЦикла;
		
		МВТ.Закрыть();
		
	КонецЕсли;
	
КонецПроцедуры

// Переопределяет права объектов выборки с полями Объект, Дескриптор
// выборка должна быть отсортирована по дескриптору
//
Процедура ПереопределитьПраваПоГруппамОбъектов(Выборка, ОпределятьЗависимыеПрава = Истина) Экспорт
	
	ТекущийДескриптор = Неопределено;
	ДескрипторДляСравнения = Неопределено;	
	ДескрипторИзменился = Ложь;
	
	Пока Выборка.Следующий() Цикл
		
		Если Выборка.Дескриптор <> ДескрипторДляСравнения Тогда
			ДескрипторДляСравнения = Выборка.Дескриптор;
			ТекущийДескриптор = ОпределитьДескрипторОбъектаОсновной(Выборка.Объект, ОпределятьЗависимыеПрава);
			ДескрипторИзменился = Выборка.Дескриптор <> ТекущийДескриптор;
		ИначеЕсли ДескрипторИзменился Тогда
			РегистрыСведений.ДескрипторыДляОбъектов.СохранитьОсновнойДескриптор(
				ТекущийДескриптор, Выборка.Объект, ОпределятьЗависимыеПрава);
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

// Обновляет права всех дескрипторов, которые зависят от указанных 
// групп доступа физлиц
Процедура ОбновитьПраваПоГруппамДоступаФизлиц(ГруппыДоступа) Экспорт
	
	Если ГруппыДоступа.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	// ДескрипторыДоступаОбъектов
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ДескрипторыДоступаОбъектовФизическиеЛица.Ссылка
		|ИЗ
		|	Справочник.ДескрипторыДоступаОбъектов.ФизическиеЛица КАК ДескрипторыДоступаОбъектовФизическиеЛица
		|ГДЕ
		|	ДескрипторыДоступаОбъектовФизическиеЛица.ГруппаДоступа В(&ГруппыДоступа)";

	Запрос.УстановитьПараметр("ГруппыДоступа", ГруппыДоступа);
	Результат = Запрос.Выполнить();
	ВыборкаДетальныеЗаписи = Результат.Выбрать();

	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Справочники.ДескрипторыДоступаОбъектов.ОбновитьПрава(ВыборкаДетальныеЗаписи.Ссылка);
	КонецЦикла;
	
	// ДескрипторыДоступаРегистров
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ДескрипторыДоступаРегистров.Ссылка
		|ИЗ
		|	Справочник.ДескрипторыДоступаРегистров КАК ДескрипторыДоступаРегистров
		|ГДЕ
		|	ДескрипторыДоступаРегистров.ОбъектДоступа1 В(&ГруппыДоступа)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ДескрипторыДоступаРегистров.Ссылка
		|ИЗ
		|	Справочник.ДескрипторыДоступаРегистров КАК ДескрипторыДоступаРегистров
		|ГДЕ
		|	ДескрипторыДоступаРегистров.ОбъектДоступа2 В(&ГруппыДоступа)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ДескрипторыДоступаРегистров.Ссылка
		|ИЗ
		|	Справочник.ДескрипторыДоступаРегистров КАК ДескрипторыДоступаРегистров
		|ГДЕ
		|	ДескрипторыДоступаРегистров.ОбъектДоступа3 В(&ГруппыДоступа)";

	Запрос.УстановитьПараметр("ГруппыДоступа", ГруппыДоступа);

	Результат = Запрос.Выполнить();

	ВыборкаДетальныеЗаписи = Результат.Выбрать();

	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		
		Справочники.ДескрипторыДоступаРегистров.ОбновитьПрава(ВыборкаДетальныеЗаписи.Ссылка);
		
	КонецЦикла;
	
КонецПроцедуры

// Обновляет права объектов, которые зависят от прав на файл.
// 
// Параметры:
// 	ФайлСсылка - СправочникСсылка - ссылка на файл.
// 
Процедура ОбновитьПраваЗависящиеОтФайла(ФайлСсылка) Экспорт
	
	ДокументооборотПраваДоступаПереопределяемый.ОбновитьПраваЗависящиеОтФайла(ФайлСсылка);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Служебные методы

Функция ПолучитьОсновнойДескриптор(ОбъектДоступа)
	
	УстановитьПривилегированныйРежим(Истина);
	
	НовыйДескриптор = Справочники.ДескрипторыДоступаОбъектов.СоздатьДескрипторОсновной(ОбъектДоступа);
	
	Возврат ПолучитьДескрипторПоОбразцу(НовыйДескриптор);
	
КонецФункции

Функция ПолучитьДескрипторДляЛокальныхАдминистраторов(ОбъектДоступа, ОсновнойДескрипторСсылка = Неопределено)
	
	УстановитьПривилегированныйРежим(Истина);
	
	// Копирование и модификация основного дескриптора.
	
	Если ОсновнойДескрипторСсылка = Неопределено Тогда
		НовыйДескриптор = Справочники.ДескрипторыДоступаОбъектов.СоздатьДескрипторОсновной(ОбъектДоступа);
	Иначе
		НовыйДескриптор = ОсновнойДескрипторСсылка.Скопировать();
	КонецЕсли;
	
	НеИспользуемыеРеквизиты = Новый Массив;
	НеИспользуемыеРеквизиты.Добавить("Папка");
	НеИспользуемыеРеквизиты.Добавить("ПапкаПередаетВсеПрава");
	НеИспользуемыеРеквизиты.Добавить("КонтейнерПользователей");
	НеИспользуемыеРеквизиты.Добавить("Изменение");
	НеИспользуемыеРеквизиты.Добавить("ОбъектДоступа");
	
	Для Каждого ИмяРеквизита Из НеИспользуемыеРеквизиты Цикл
		НовыйДескриптор[ИмяРеквизита] = Неопределено;
	КонецЦикла;
	
	НовыйДескриптор.НастройкаПрав.Очистить();
	НовыйДескриптор.ДляЛокальныхАдминистраторов = Истина;
	НовыйДескриптор.РассчитатьХэш();
	
	Возврат ПолучитьДескрипторПоОбразцу(НовыйДескриптор);
	
КонецФункции

Функция ПолучитьИндивидуальныйДескриптор(ОбъектДоступа, КонтейнерПользователей, Изменение)
	
	УстановитьПривилегированныйРежим(Истина);
	
	ОбъектМетаданных = ОбщегоНазначения.ИдентификаторОбъектаМетаданных(ОбъектДоступа.Ссылка.Метаданные());
	НовыйДескриптор = Справочники.ДескрипторыДоступаОбъектов.СоздатьДескрипторИндивидуальный(
		ОбъектМетаданных, КонтейнерПользователей, Изменение);
	
	Дескриптор = Справочники.ДескрипторыДоступаОбъектов.НайтиДескрипторПоОбразцу(НовыйДескриптор);
	Если Не ЗначениеЗаполнено(Дескриптор) Тогда
		НовыйДескриптор.Записать();
		Дескриптор = НовыйДескриптор.Ссылка;
	КонецЕсли;
	
	Возврат Дескриптор;
	
КонецФункции

Функция ПолучитьДескрипторПоОбразцу(НовыйДескриптор)
	
	ГотовыйДескриптор = Справочники.ДескрипторыДоступаОбъектов.НайтиДескрипторПоОбразцу(НовыйДескриптор);
	
	Если ЗначениеЗаполнено(ГотовыйДескриптор) Тогда
		Возврат ГотовыйДескриптор;
	КонецЕсли;
	
	// Запись нового дескриптора
	НовыйДескриптор.Записать();
	
	Возврат НовыйДескриптор.Ссылка;
	
КонецФункции

Функция ПолучитьДескрипторДоступаЗаписиНабораЗаписей(ИдентификаторОбъектаМетаданных, Запись)
	
	УстановитьПривилегированныйРежим(Истина);
	
	НовыйДескриптор = Справочники.ДескрипторыДоступаРегистров.СоздатьНовыйДескриптор(ИдентификаторОбъектаМетаданных, Запись);
	
	ГотовыйДескриптор = Справочники.ДескрипторыДоступаРегистров.НайтиДескрипторПоОбразцу(НовыйДескриптор);
	
	Если ЗначениеЗаполнено(ГотовыйДескриптор) Тогда
		Возврат ГотовыйДескриптор;
	КонецЕсли;	

	// Запись нового дескриптора
	НовыйДескриптор.Записать();
	
	Возврат НовыйДескриптор.Ссылка;
	
КонецФункции	

Процедура ОбновлениеРолейПередЗаписьюОбъекта(Источник, Отказ) Экспорт
	
	Если ПользователиДокументооборот.ЭтоКонтейнер(Источник.Ссылка) Тогда
		
		Если ТипЗнч(Источник) = Тип("СправочникОбъект.Пользователи") Тогда
			Источник.ДополнительныеСвойства.Вставить("ЭтоНовый", Источник.ЭтоНовый());
			
		ИначеЕсли Не Источник.ЭтоНовый() И Источник.Метаданные().Иерархический Тогда
			Источник.ДополнительныеСвойства.Вставить("СтарыйРодитель",
				ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Источник.Ссылка, "Родитель"));
			
		КонецЕсли;
		
		СоставКонтейнера = РегистрыСведений.ПользователиВКонтейнерах.ПользователиВКонтейнере(Источник.Ссылка);
		Источник.ДополнительныеСвойства.Вставить("СтарыйСостав", СоставКонтейнера);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбновлениеРолейПриЗаписиОбъекта(Источник, Отказ) Экспорт
	
	ПользователиКПересчету = Новый Массив;
	
	Если ТипЗнч(Источник) = Тип("СправочникОбъект.ПрофилиГруппДоступа") Тогда
		
		Запрос = Новый Запрос(
			"ВЫБРАТЬ
			|	ПользователиВКонтейнерах.Пользователь
			|ИЗ
			|	РегистрСведений.ПолномочияПользователей КАК ПолномочияПользователей
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ПользователиВКонтейнерах КАК ПользователиВКонтейнерах
			|		ПО ПолномочияПользователей.Владелец = ПользователиВКонтейнерах.Контейнер
			|ГДЕ
			|	ПолномочияПользователей.Полномочия = &Полномочия");
		
		Запрос.УстановитьПараметр("Полномочия", Источник.Ссылка);
		ПользователиКПересчету = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Пользователь");
		
	КонецЕсли;		
	
	// Обновление ролей.
	Если ПользователиКПересчету.Количество() > 0 Тогда
		УправлениеДоступом.ОбновитьРолиПользователей(ПользователиКПересчету);
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбновлениеРолейПриЗаписиРегистра(Источник, Отказ, Замещение) Экспорт
	
	ПользователиКПересчету = Новый Массив;
	
	Если ТипЗнч(Источник) = Тип("РегистрСведенийНаборЗаписей.ПолномочияПользователей") Тогда
		
		ОтборПоВладельцу = Источник.Отбор.Владелец;
		Если ОтборПоВладельцу.Использование Тогда
			ПользователиКПересчету = РегистрыСведений.ПользователиВКонтейнерах.ПользователиВКонтейнере(
				ОтборПоВладельцу.Значение);
		Иначе
			ПользователиКПересчету = РегистрыСведений.ПользователиВКонтейнерах.ПользователиВКонтейнере(
				Справочники.РабочиеГруппы.ВсеПользователи);
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(Источник) = Тип("РегистрСведенийНаборЗаписей.ПользователиВКонтейнерах") Тогда
		
		ОтборПоКонтейнеру = Источник.Отбор.Контейнер;
		ОтборПоПользователю = Источник.Отбор.Пользователь;
		
		Если ОтборПоПользователю.Использование Тогда
			
			ПользователиКПересчету.Добавить(ОтборПоПользователю.Значение);
			
		Иначе
			
			Если Не ОтборПоКонтейнеру.Использование Тогда
				Возврат;
			КонецЕсли;
			
			Контейнер = ОтборПоКонтейнеру.Значение;
			УдаленныеПользователи = Неопределено;
			ДобавленныеПользователи = Неопределено;
			Источник.ДополнительныеСвойства.Свойство("УдаленныеПользователи", УдаленныеПользователи);
			Источник.ДополнительныеСвойства.Свойство("ДобавленныеПользователи", ДобавленныеПользователи);
			
			Если УдаленныеПользователи = Неопределено
					Или ДобавленныеПользователи = Неопределено Тогда
				Возврат
			КонецЕсли;
				
			Запрос = Новый Запрос(
				"ВЫБРАТЬ
				|	ИСТИНА КАК ЕстьЗаписи
				|ИЗ
				|	РегистрСведений.ПолномочияПользователей КАК ПолномочияПользователей
				|ГДЕ
				|	ПолномочияПользователей.Владелец = &Контейнер");
			
			Запрос.УстановитьПараметр("Контейнер", Контейнер);
			Если Запрос.Выполнить().Пустой() Тогда
				// Нет полномочий, назначенных пользователям через этот контейнер.
				Возврат;
			КонецЕсли;
			
			ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ПользователиКПересчету, УдаленныеПользователи, Истина);
			ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ПользователиКПересчету, ДобавленныеПользователи, Истина);
			
		КонецЕсли;
		
	КонецЕсли;
	
	// Обновление ролей.
	Если ПользователиКПересчету.Количество() > 0 Тогда
		УправлениеДоступом.ОбновитьРолиПользователей(ПользователиКПересчету);
	КонецЕсли;
	
КонецПроцедуры

// Определяет идентификаторы объектов,
// права на которые изменились в результате изменения ролей.
// 
Функция ОбъектыСОтличиямиВПравахРолей(Роли1, Роли2)
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	ПраваРолей.ОбъектМетаданных,
		|	МАКСИМУМ(ПраваРолей.Добавление) КАК Добавление,
		|	МАКСИМУМ(ПраваРолей.Изменение) КАК Изменение,
		|	МАКСИМУМ(ПраваРолей.ЧтениеБезОграничения) КАК ЧтениеБезОграничения,
		|	МАКСИМУМ(ПраваРолей.ИзменениеБезОграничения) КАК ИзменениеБезОграничения
		|ПОМЕСТИТЬ Права1
		|ИЗ
		|	РегистрСведений.ПраваРолей КАК ПраваРолей
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ИдентификаторыОбъектовМетаданных КАК ИдентификаторыРолей
		|		ПО ПраваРолей.Роль = ИдентификаторыРолей.Ссылка
		|ГДЕ
		|	ИдентификаторыРолей.Ссылка В(&Роли1)
		|	И ПраваРолей.ОбъектМетаданных В(&ОбъектыМеханизмаПрав)
		|
		|СГРУППИРОВАТЬ ПО
		|	ПраваРолей.ОбъектМетаданных
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПраваРолей.ОбъектМетаданных,
		|	МАКСИМУМ(ПраваРолей.Добавление) КАК Добавление,
		|	МАКСИМУМ(ПраваРолей.Изменение) КАК Изменение,
		|	МАКСИМУМ(ПраваРолей.ЧтениеБезОграничения) КАК ЧтениеБезОграничения,
		|	МАКСИМУМ(ПраваРолей.ИзменениеБезОграничения) КАК ИзменениеБезОграничения
		|ПОМЕСТИТЬ Права2
		|ИЗ
		|	РегистрСведений.ПраваРолей КАК ПраваРолей
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ИдентификаторыОбъектовМетаданных КАК ИдентификаторыРолей
		|		ПО ПраваРолей.Роль = ИдентификаторыРолей.Ссылка
		|ГДЕ
		|	ИдентификаторыРолей.Ссылка В(&Роли2)
		|	И ПраваРолей.ОбъектМетаданных В(&ОбъектыМеханизмаПрав)
		|
		|СГРУППИРОВАТЬ ПО
		|	ПраваРолей.ОбъектМетаданных
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЕСТЬNULL(Права1.ОбъектМетаданных, Права2.ОбъектМетаданных) КАК Таблица
		|ИЗ
		|	Права1 КАК Права1
		|		ПОЛНОЕ СОЕДИНЕНИЕ Права2 КАК Права2
		|		ПО Права1.ОбъектМетаданных = Права2.ОбъектМетаданных
		|			И Права1.Добавление = Права2.Добавление
		|			И Права1.Изменение = Права2.Изменение
		|			И Права1.ЧтениеБезОграничения = Права2.ЧтениеБезОграничения
		|			И Права1.ИзменениеБезОграничения = Права2.ИзменениеБезОграничения
		|ГДЕ
		|	(Права1.ОбъектМетаданных ЕСТЬ NULL 
		|			ИЛИ Права2.ОбъектМетаданных ЕСТЬ NULL )");
		
	Запрос.УстановитьПараметр("Роли1", Роли1);
	Запрос.УстановитьПараметр("Роли2", Роли2);
	Запрос.УстановитьПараметр("ОбъектыМеханизмаПрав",
		ДокументооборотПраваДоступаПовтИсп.ВсеОбъектыМеханизмаПравДоступа());
	
	ИзмененныеИдентификаторы = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Таблица");
	
	Возврат ИзмененныеИдентификаторы;
	
КонецФункции

// Пересчитывает права по дескрипторам при изменении ролей пользователей.
// 
// Параметры:
// 	РолиПользователей - Соответствие
// 
Процедура ОбновитьПраваПриИзмененииРолейПользователей(РолиПользователей) Экспорт
	
	Если Не ДокументооборотПраваДоступаПовтИсп.ВключеноИспользованиеПравДоступа() Тогда
		Возврат;
	КонецЕсли;
	
	ВсеИзмененныеОбъекты = Новый Массив;
	
	Для Каждого КлючИЗначение Из РолиПользователей Цикл
		
		СтруктураПараметров = КлючИЗначение.Значение;
		
		Пользователь = СтруктураПараметров.ПользовательСсылка;
		ИменаРолейДляДобавления = СтруктураПараметров.РолиДляДобавления;
		ИменаРолейДляУдаления = СтруктураПараметров.РолиДляУдаления;
		
		ДобавленныеРоли = Новый Массив;
		УдаленныеРоли = Новый Массив;
		
		Для Каждого Эл Из ИменаРолейДляДобавления Цикл
			ИмяРоли = Эл.Ключ;
			Роль = ОбщегоНазначения.ИдентификаторОбъектаМетаданных(Метаданные.Роли.Найти(ИмяРоли));
			ДобавленныеРоли.Добавить(Роль);
		КонецЦикла;
		
		Для Каждого Эл Из ИменаРолейДляУдаления Цикл
			ИмяРоли = Эл.Ключ;
			Роль = ОбщегоНазначения.ИдентификаторОбъектаМетаданных(Метаданные.Роли.Найти(ИмяРоли));
			УдаленныеРоли.Добавить(Роль);
		КонецЦикла;
		
		Запрос = Новый Запрос(
			"ВЫБРАТЬ
			|	ПрофилиГруппДоступаРоли.Роль
			|ИЗ
			|	РегистрСведений.ПользователиВКонтейнерах КАК ПользователиВКонтейнерах
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ПолномочияПользователей КАК ПолномочияПользователей
			|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ПрофилиГруппДоступа.Роли КАК ПрофилиГруппДоступаРоли
			|			ПО ПолномочияПользователей.Полномочия = ПрофилиГруппДоступаРоли.Ссылка
			|		ПО ПользователиВКонтейнерах.Контейнер = ПолномочияПользователей.Владелец
			|ГДЕ
			|	ПользователиВКонтейнерах.Пользователь = &Пользователь
			|	И НЕ ПрофилиГруппДоступаРоли.Роль В (&ДобавленныеРоли)
			|	И НЕ ПрофилиГруппДоступаРоли.Роль В (&УдаленныеРоли)");
			
		Запрос.УстановитьПараметр("Пользователь", Пользователь);
		Запрос.УстановитьПараметр("ДобавленныеРоли", ДобавленныеРоли);
		Запрос.УстановитьПараметр("УдаленныеРоли", УдаленныеРоли);
		
		НеИзмененныеРоли = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Роль");
		
		// Добавленные права.
		РолиДоДобавления = ОбщегоНазначенияКлиентСервер.СкопироватьМассив(НеИзмененныеРоли);
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(РолиДоДобавления, УдаленныеРоли);
		ИзмененныеОбъекты = ОбъектыСОтличиямиВПравахРолей(РолиДоДобавления, ДобавленныеРоли);
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ВсеИзмененныеОбъекты, ИзмененныеОбъекты);
		
		// Удаленные права.
		РолиДоУдаления = ОбщегоНазначенияКлиентСервер.СкопироватьМассив(НеИзмененныеРоли);
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(РолиДоУдаления, ДобавленныеРоли);
		ИзмененныеОбъекты = ОбъектыСОтличиямиВПравахРолей(РолиДоУдаления, УдаленныеРоли);
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ВсеИзмененныеОбъекты, ИзмененныеОбъекты);
		
	КонецЦикла;
	
	ВсеИзмененныеОбъекты = ОбщегоНазначенияКлиентСервер.СвернутьМассив(ВсеИзмененныеОбъекты);
	Для Каждого ИдентификаторОбъекта Из ВсеИзмененныеОбъекты Цикл
		РассчитатьПраваОбъектовТаблицы(ИдентификаторОбъекта);
	КонецЦикла;
	
КонецПроцедуры

// Обрабатывает запись правообразующих констант
Процедура ПриЗаписиКонстанты(Источник)
	
	Если ТипЗнч(Источник) = Тип("КонстантаМенеджерЗначения.ИспользоватьУчетПоОрганизациям") Тогда

		// Обработка всех дескрипторов, у которых установлен реквизит Организация
		Запрос = Новый Запрос;
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	ДескрипторыДоступаОбъектов.Ссылка
			|ИЗ
			|	Справочник.ДескрипторыДоступаОбъектов КАК ДескрипторыДоступаОбъектов
			|ГДЕ
			|	ДескрипторыДоступаОбъектов.Организация <> &ПустаяОрганизация";

		Запрос.УстановитьПараметр("ПустаяОрганизация", Справочники.Организации.ПустаяСсылка());

		Результат = Запрос.Выполнить();

		ВыборкаДетальныеЗаписи = Результат.Выбрать();

		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			
			Дескриптор = ВыборкаДетальныеЗаписи.Ссылка;
			Справочники.ДескрипторыДоступаОбъектов.ОбновитьПрава(Дескриптор);
			
		КонецЦикла;

	ИначеЕсли ТипЗнч(Источник) = Тип("КонстантаМенеджерЗначения.ИспользоватьГрифыДоступа") Тогда

		// Обработка всех дескрипторов, у которых установлен реквизит ГрифДоступа
		Запрос = Новый Запрос;
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	ДескрипторыДоступаОбъектов.Ссылка
			|ИЗ
			|	Справочник.ДескрипторыДоступаОбъектов КАК ДескрипторыДоступаОбъектов
			|ГДЕ
			|	ДескрипторыДоступаОбъектов.ГрифДоступа <> &ГрифДоступа";

		Запрос.УстановитьПараметр("ГрифДоступа", Справочники.ГрифыДоступа.ПустаяСсылка());

		Результат = Запрос.Выполнить();

		ВыборкаДетальныеЗаписи = Результат.Выбрать();

		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			
			Дескриптор = ВыборкаДетальныеЗаписи.Ссылка;
			Справочники.ДескрипторыДоступаОбъектов.ОбновитьПрава(Дескриптор);
			
		КонецЦикла;
		
	ИначеЕсли ТипЗнч(Источник) = Тип("КонстантаМенеджерЗначения.ИспользоватьВопросыДеятельности") Тогда

		// Обработка всех дескрипторов, у которых установлен реквизит ВопросДеятельности
		Запрос = Новый Запрос;
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	ДескрипторыДоступаОбъектов.Ссылка
			|ИЗ
			|	Справочник.ДескрипторыДоступаОбъектов КАК ДескрипторыДоступаОбъектов
			|ГДЕ
			|	ДескрипторыДоступаОбъектов.ВопросДеятельности <> &ВопросДеятельности";

		Запрос.УстановитьПараметр("ВопросДеятельности", Справочники.ВопросыДеятельности.ПустаяСсылка());

		Результат = Запрос.Выполнить();

		ВыборкаДетальныеЗаписи = Результат.Выбрать();

		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			
			Дескриптор = ВыборкаДетальныеЗаписи.Ссылка;
			Справочники.ДескрипторыДоступаОбъектов.ОбновитьПрава(Дескриптор);
			
		КонецЦикла;
		
	ИначеЕсли ТипЗнч(Источник) = Тип("КонстантаМенеджерЗначения.ИспользоватьВидыИсходящихДокументов") Тогда

		// Обработка всех дескрипторов, у которых установлен реквизит ВидОбъекта 
		// типа Справочник.ВидыИсходящихДокументов
		Запрос = Новый Запрос;
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	ДескрипторыДоступаОбъектов.Ссылка
			|ИЗ
			|	Справочник.ДескрипторыДоступаОбъектов КАК ДескрипторыДоступаОбъектов
			|ГДЕ
			|	ДескрипторыДоступаОбъектов.ВидОбъекта <> &ВидОбъекта
			| 	И ТИПЗНАЧЕНИЯ(ДескрипторыДоступаОбъектов.ВидОбъекта) = ТИП(Справочник.ВидыИсходящихДокументов)";

		Запрос.УстановитьПараметр("ВидОбъекта", Справочники.ВидыИсходящихДокументов.ПустаяСсылка());

		Результат = Запрос.Выполнить();

		ВыборкаДетальныеЗаписи = Результат.Выбрать();

		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			
			Дескриптор = ВыборкаДетальныеЗаписи.Ссылка;
			Справочники.ДескрипторыДоступаОбъектов.ОбновитьПрава(Дескриптор);
			
		КонецЦикла;
		
	ИначеЕсли ТипЗнч(Источник) = Тип("КонстантаМенеджерЗначения.ИспользоватьВидыВходящихДокументов") Тогда

		// Обработка всех дескрипторов, у которых установлен реквизит ВидОбъекта 
		// типа Справочник.ВидыВходящихДокументов
		Запрос = Новый Запрос;
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	ДескрипторыДоступаОбъектов.Ссылка
			|ИЗ
			|	Справочник.ДескрипторыДоступаОбъектов КАК ДескрипторыДоступаОбъектов
			|ГДЕ
			|	ДескрипторыДоступаОбъектов.ВидОбъекта <> &ВидОбъекта
			| 	И ТИПЗНАЧЕНИЯ(ДескрипторыДоступаОбъектов.ВидОбъекта) = ТИП(Справочник.ВидыВходящихДокументов)";

		Запрос.УстановитьПараметр("ВидОбъекта", Справочники.ВидыВходящихДокументов.ПустаяСсылка());

		Результат = Запрос.Выполнить();

		ВыборкаДетальныеЗаписи = Результат.Выбрать();

		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			
			Дескриптор = ВыборкаДетальныеЗаписи.Ссылка;
			Справочники.ДескрипторыДоступаОбъектов.ОбновитьПрава(Дескриптор);
			
		КонецЦикла;
		
	ИначеЕсли ТипЗнч(Источник) = Тип("КонстантаМенеджерЗначения.ИспользоватьВидыВнутреннихДокументов") Тогда

		// Обработка всех дескрипторов, у которых установлен реквизит ВидОбъекта 
		// типа Справочник.ВидыВнутреннихДокументов
		Запрос = Новый Запрос;
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	ДескрипторыДоступаОбъектов.Ссылка
			|ИЗ
			|	Справочник.ДескрипторыДоступаОбъектов КАК ДескрипторыДоступаОбъектов
			|ГДЕ
			|	ДескрипторыДоступаОбъектов.ВидОбъекта <> &ВидОбъекта
			|	И ТИПЗНАЧЕНИЯ(ДескрипторыДоступаОбъектов.ВидОбъекта) = ТИП(Справочник.ВидыВнутреннихДокументов)";

		Запрос.УстановитьПараметр("ВидОбъекта", Справочники.ВидыВнутреннихДокументов.ПустаяСсылка());

		Результат = Запрос.Выполнить();

		ВыборкаДетальныеЗаписи = Результат.Выбрать();

		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			
			Дескриптор = ВыборкаДетальныеЗаписи.Ссылка;
			Справочники.ДескрипторыДоступаОбъектов.ОбновитьПрава(Дескриптор);
			
		КонецЦикла;
		
	ИначеЕсли ТипЗнч(Источник) = Тип("КонстантаМенеджерЗначения.ДобавлятьРуководителямДоступПодчиненных") Тогда	
		
		// Обновление всех дескрипторов
		АктуализироватьПраваПользователейПоСоставуСубъектов();
		
		// ДескрипторыДоступаРегистров
		Запрос = Новый Запрос;
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	ДескрипторыДоступаРегистров.Ссылка
			|ИЗ
			|	Справочник.ДескрипторыДоступаРегистров КАК ДескрипторыДоступаРегистров";
			
		Результат = Запрос.Выполнить();
		ВыборкаДетальныеЗаписи = Результат.Выбрать();
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			Справочники.ДескрипторыДоступаРегистров.ОбновитьПрава(ВыборкаДетальныеЗаписи.Ссылка);
		КонецЦикла;
		
	ИначеЕсли ТипЗнч(Источник) = Тип("КонстантаМенеджерЗначения.СпецРазрешенияВПолитикахДоступа") Тогда
		
		Если Не Константы.СпецРазрешенияВПолитикахДоступа.Получить() Тогда
			
			// Очистка спец. разрешений.
			Запрос = Новый Запрос(
				"ВЫБРАТЬ РАЗЛИЧНЫЕ
				|	РазрешенияДоступаИсключительные.Предмет
				|ИЗ
				|	РегистрСведений.РазрешенияДоступаИсключительные КАК РазрешенияДоступаИсключительные");
				
			Результат = Запрос.Выполнить();
			ЕстьЗаписи = Не Результат.Пустой();
			Если ЕстьЗаписи Тогда
				Набор = РегистрыСведений.РазрешенияДоступаИсключительные.СоздатьНаборЗаписей();
				Набор.ДополнительныеСвойства.Вставить(
					"ИзмененныеПредметы", Результат.Выгрузить().ВыгрузитьКолонку("Предмет"));
				Набор.Записать();
			КонецЕсли;
			
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(Источник) = Тип("КонстантаМенеджерЗначения.ИспользоватьЛокальныхАдминистраторов") Тогда
		
		Если Источник.Значение Тогда
			// При включении настройки нужно назначить объектам дескрипторы для лок. администраторов.
			ПредметыДоступаСРазрезами = ДокументооборотПраваДоступаПереопределяемый.ПредметыДоступаСРазрезами();
			Для Каждого СтрокаПредмета Из ПредметыДоступаСРазрезами Цикл
				ОпределитьПраваОбъектовТаблицыДляЛокальныхАдминиистраторов(СтрокаПредмета.ОбъектМетаданных);
			КонецЦикла;
		Иначе
			// Удаление связей дескрипторов лок. администраторов с объектами.
			ОтключитьДескрипторыЛокАдминистраторов();
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(Источник) = Тип("КонстантаМенеджерЗначения.ИспользованиеРазрезовДоступа") Тогда
		
		// Переопределение дескрипторов всех объектов, использующих измененные разрезы.
		
		ИзмененныеПредметы = Новый Массив;
		ИзмененныеРазрезы = Источник.ДополнительныеСвойства.ИзмененныеРазрезы;
		ПредметыДоступаСРазрезами = ДокументооборотПраваДоступаПереопределяемый.ПредметыДоступаСРазрезами();
		
		Для Каждого СтрПредмета Из ПредметыДоступаСРазрезами Цикл
			Для Каждого Разрез Из ИзмененныеРазрезы Цикл
				Если СтрПредмета.ВидыДоступа.НайтиПоЗначению(Разрез) <> Неопределено Тогда
					ОпределитьПраваОбъектовТаблицы(СтрПредмета.ОбъектМетаданных);
					Прервать;
				КонецЕсли;
			КонецЦикла;
		КонецЦикла;
		
		// Переопределение дескрипторов самих разрезов.
		
		ПараметрыОграниченияДоступа = 
			СтандартныеПодсистемыПовтИсп.ПараметрыРаботыПрограммы("ПараметрыОграниченияДоступа");
		ТаблицаСвойствВидовДоступа = ПараметрыОграниченияДоступа.СвойстваВидовДоступа.Таблица.Получить();
		ВсеОбъектыМеханизмаПравДоступа = ДокументооборотПраваДоступаПовтИсп.ВсеОбъектыМеханизмаПравДоступа();
		
		Для Каждого Разрез Из ИзмененныеРазрезы Цикл
			СтрВида = ТаблицаСвойствВидовДоступа.Найти(Разрез, "ВидДоступа");
			Если СтрВида <> Неопределено Тогда
				Для Каждого ИмяОбъектаМетаданных Из СтрВида.Таблицы Цикл
					ОбъектМетаданных = ОбщегоНазначения.ИдентификаторОбъектаМетаданных(ИмяОбъектаМетаданных);
					Если ВсеОбъектыМеханизмаПравДоступа.Найти(ОбъектМетаданных) <> Неопределено Тогда
						ОпределитьПраваОбъектовТаблицы(ОбъектМетаданных);
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

// Возвращает право текущего пользователя перемещать папки
// из папки Источник в папку Приемник.
// В случае отсутствия права формируется СообщениеОбОшибке.
//
Функция ЕстьПравоПеремещенияПапки(Источник, Приемник, СообщениеОбОшибке) Экспорт
	
	СообщениеОбОшибке = "";
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если Не ЗначениеЗаполнено(Источник)
		И Не РольДоступна("ПолныеПрава") Тогда
		СообщениеОбОшибке = НСтр("ru = 'Недостаточно прав на перемещение корневых папок.'; en = 'Insufficient permissions to move the root folders.'");
		Возврат Ложь;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Приемник)
		И Не РольДоступна("ПолныеПрава") Тогда
		СообщениеОбОшибке = НСтр("ru = 'Недостаточно прав на перемещение папок в корень.'; en = 'Insufficient permissions to move folders to the root.'");
		Возврат Ложь;
	КонецЕсли;
	
	ТекущийПользователь = Пользователи.ТекущийПользователь();
	
	Если ЗначениеЗаполнено(Источник) Тогда
		
		Права = ДокументооборотПраваДоступа.ПолучитьПраваПоОбъекту(Источник, ТекущийПользователь);
		
		Если Не Права.Удаление Тогда
			
			СообщениеОбОшибке = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Недостаточно прав на удаление из папки ""%1"".'; en = 'Insufficient permissions to delete from the folder ""%1"".'"),
				Строка(Источник));
			
			Возврат Ложь;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Приемник) Тогда
		
		Права = ДокументооборотПраваДоступа.ПолучитьПраваПоОбъекту(Приемник, ТекущийПользователь);
		
		Если Не Права.Добавление Тогда
			
			СообщениеОбОшибке = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Недостаточно прав на добавление в папку ""%1"".'; en = 'Insufficient permissions to add in the folder ""%1"".'"),
				Строка(Приемник));
			
			Возврат Ложь;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

// Включает\выключает использование прав доступа
//   используется в обработках обновления ИБ
Процедура УстановитьИспользованиеПравДоступа(НовоеЗначение) Экспорт
	
	Если Не ПараметрыСеанса.ДокументооборотИспользоватьОграниченияПравДоступа Тогда
		Возврат;
	КонецЕсли;
	
	СтароеЗначение = Константы.ДокументооборотИспользоватьОграничениеПравДоступа.Получить();
	Если СтароеЗначение <> НовоеЗначение Тогда
		НачатьТранзакцию();
		Константы.ДокументооборотИспользоватьОграничениеПравДоступа.Установить(НовоеЗначение);
		Константы.ДокументооборотВключитьПраваДоступа.Установить(СтароеЗначение);
		ЗафиксироватьТранзакцию();
		ОбновитьПовторноИспользуемыеЗначения();
	КонецЕсли;
	
КонецПроцедуры

// Возвращает Истина, если переданный тип - ссылка на справочник
// папок внутренних документов, файлов и т.д.
//
// Параметры:
//  ТипСсылки - Тип - тип проверяемого объекта
//
Функция ЭтоПапка(ТипСсылки) Экспорт
	
	Результат = Метаданные.ОпределяемыеТипы.Папки.Тип.Типы().Найти(ТипСсылки) <> Неопределено;
	
	Возврат Результат;
	
КонецФункции

// Обновляет права файлов переданных процессов 
//  и шаблонов, подчиненных комплексным процессам
//
Процедура ОбновитьПраваЗависящиеОтСоставаУчастниковПроцесса(МассивПроцессов)
	
	// Обновление прав дескрипторов процессов.
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	ДескрипторыДоступаОбъектов.Ссылка
		|ИЗ
		|	Справочник.ДескрипторыДоступаОбъектов КАК ДескрипторыДоступаОбъектов
		|ГДЕ
		|	ДескрипторыДоступаОбъектов.ОбъектДоступа В(&МассивПроцессов)");
		
	Запрос.УстановитьПараметр("МассивПроцессов", МассивПроцессов);
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Справочники.ДескрипторыДоступаОбъектов.ОбновитьПрава(Выборка.Ссылка);
	КонецЦикла;
	
	ДокументооборотПраваДоступаПереопределяемый.ОбновитьПраваЗависящиеОтСоставаУчастниковПроцесса(
		МассивПроцессов);
	
КонецПроцедуры

// Обновляет права документов с указанным контрагентом
//
Процедура ОбновитьПраваПоКонтрагенту(Контрагент, Немедленно = Неопределено) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	// Проверка на отложенное обновление прав доступа
	Если ДокументооборотПраваДоступаПовтИсп.ОтложенноеОбновлениеПравДоступа() 
		И Немедленно <> Истина Тогда
		
		// Добавление в очередь
		РегистрыСведений.ОчередьОбновленияПравДоступа.Добавить(Контрагент,, 2);
		Возврат;
		
	КонецЕсли;
	
	ОписанияТаблиц = Новый Соответствие;
	
	ОписанияТаблиц.Вставить("Справочник.ВнутренниеДокументы", "Контрагент");
	ОписанияТаблиц.Вставить("Справочник.ВходящиеДокументы", "Отправитель");
	ОписанияТаблиц.Вставить("Справочник.ИсходящиеДокументы.Получатели", "Получатель");
	
	ОписанияТаблиц.Вставить("Справочник.СпискиРассылкиПоКонтрагентам.Получатели", "Получатель");
	
	ОписанияТаблиц.Вставить("Справочник.ШаблоныВнутреннихДокументов", "Контрагент");
	ОписанияТаблиц.Вставить("Справочник.ШаблоныВходящихДокументов", "Отправитель");
	ОписанияТаблиц.Вставить("Справочник.ШаблоныИсходящихДокументов.Получатели", "Получатель");
	
	ДокументооборотПраваДоступаПереопределяемый.ДополнитьОписанияТаблицСоСсылкамиНаКонтрагентов(
		ОписанияТаблиц);
	
	ШаблонТекстаЗапроса = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ДескрипторыДляОбъектов.Объект
		|ИЗ
		|	%ТаблицаОбъектов% КАК ТаблицаОбъектов
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ДескрипторыДляОбъектов КАК ДескрипторыДляОбъектов
		|		ПО ТаблицаОбъектов.Ссылка = ДескрипторыДляОбъектов.Объект
		|		И ДескрипторыДляОбъектов.ТипДескриптора = 0
		|ГДЕ
		|	ТаблицаОбъектов.%ИмяПоляКонтрагента% = &Контрагент";
		
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Контрагент", Контрагент);
	
	Для Каждого Эл Из ОписанияТаблиц Цикл
		
		Запрос.Текст = СтрЗаменить(ШаблонТекстаЗапроса, "%ТаблицаОбъектов%", Эл.Ключ);
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "%ИмяПоляКонтрагента%", Эл.Значение);
		
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			ДокументооборотПраваДоступа.ОпределитьДескрипторыОбъекта(Выборка.Объект);
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

// Обновляет права всех дескрипторов, которые имеют ссылку на переданную папку.
// 
Процедура ОбновитьПраваПоПапке(Папка, Немедленно = Ложь) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если ДокументооборотПраваДоступаПовтИсп.ОтложенноеОбновлениеПравДоступа()
		И Немедленно <> Истина Тогда
		
		РегистрыСведений.ОчередьОбновленияПравДоступа.Добавить(
			Папка,, 1, "ОбновитьПраваСодержимогоПапки");
		
		Возврат;
		
	КонецЕсли;
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	ДескрипторыДоступаОбъектов.Ссылка
		|ИЗ
		|	Справочник.ДескрипторыДоступаОбъектов КАК ДескрипторыДоступаОбъектов
		|ГДЕ
		|	ДескрипторыДоступаОбъектов.Папка = &Папка");
	
	Запрос.УстановитьПараметр("Папка", Папка);
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Справочники.ДескрипторыДоступаОбъектов.ОбновитьПрава(Выборка.Ссылка);
	КонецЦикла;
	
КонецПроцедуры

// Возвращает вид доступа по ссылке на значение доступа.
// 
Функция РазрезДоступаПоЗначению(ЗначениеДоступа) Экспорт
	
	РазрезДоступа = Неопределено;
	
	Если Не ЗначениеЗаполнено(ЗначениеДоступа) Тогда
		Возврат РазрезДоступа;
	КонецЕсли;
	
	ЗначениеВМассиве = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ЗначениеДоступа);
	РазрезыДоступаПоЗначениям = РазрезыДоступаПоЗначениям(ЗначениеВМассиве);
	
	Если РазрезыДоступаПоЗначениям.Количество() > 0 Тогда
		РазрезДоступа = РазрезыДоступаПоЗначениям[0];
	КонецЕсли;
	
	Возврат РазрезДоступа;
	
КонецФункции

// Возвращает массив уникальных видов доступа, соответствующих переданным значениям.
// 
Функция РазрезыДоступаПоЗначениям(ЗначенияДоступа) Экспорт
	
	Результат = Новый Массив;
	ИменаМетаданныхЗначенийДоступа = Новый Массив;
	
	Для Каждого ЗначениеДоступа Из ЗначенияДоступа Цикл
		
		Если Не ЗначениеЗаполнено(ЗначениеДоступа) Тогда
			Продолжить;
		КонецЕсли;
		
		ТипЗначенияДоступа = ТипЗнч(ЗначениеДоступа);
		Если ТипЗначенияДоступа = Тип("ПланВидовХарактеристикСсылка.ВидыДоступа") Тогда
			Результат.Добавить(ЗначениеДоступа);
		Иначе
			ИменаМетаданныхЗначенийДоступа.Добавить(ЗначениеДоступа.Метаданные().ПолноеИмя());
		КонецЕсли;
		
	КонецЦикла;
	
	ИменаМетаданныхЗначенийДоступа = 
		ОбщегоНазначенияКлиентСервер.СвернутьМассив(ИменаМетаданныхЗначенийДоступа);
	ТаблицаРазрезовДоступа = ДокументооборотПраваДоступаПовтИсп.ТаблицаРазрезовДоступа(Ложь);
	
	Для Каждого ИмяМетаданных Из ИменаМетаданныхЗначенийДоступа Цикл
		НайденнаяСтрока = ТаблицаРазрезовДоступа.Найти(ИмяМетаданных, "ИмяТаблицыЗначенийДоступа");
		Если НайденнаяСтрока <> Неопределено Тогда
			Результат.Добавить(НайденнаяСтрока.ВидДоступа);
		Иначе
			ВызватьИсключение 
				СтрШаблон(НСтр("ru = 'Не найден вид доступа для значения %1'; en = 'Access type for %1 value is not found'"), ЗначениеДоступа);
		КонецЕсли;
	КонецЦикла;
	
	Результат = ОбщегоНазначенияКлиентСервер.СвернутьМассив(Результат);
	
	Возврат Результат;
	
КонецФункции

Функция ТаблицаПравилОбработкиНастроекПапки() Экспорт
	
	ТаблицаПравил = Новый ТаблицаЗначений;
	
	ТипСтрока = Новый ОписаниеТипов("Строка",, Новый КвалификаторыСтроки(100));
	ТипБулево = Новый ОписаниеТипов("Булево");
	
	ТаблицаПравил.Колонки.Добавить("Настройка", ТипСтрока);
	ТаблицаПравил.Колонки.Добавить("Чтение", ТипБулево);
	ТаблицаПравил.Колонки.Добавить("Добавление", ТипБулево);
	ТаблицаПравил.Колонки.Добавить("Изменение", ТипБулево);
	ТаблицаПравил.Колонки.Добавить("Удаление", ТипБулево);
	ТаблицаПравил.Колонки.Добавить("УправлениеПравами", ТипБулево);
	
	Возврат ТаблицаПравил;
	
КонецФункции

// Заполняет папку дескриптора.
// Это первая вверх по иерархии папка, которая не наследует полностью настройки от родителей.
// 
Процедура ЗаполнитьПапкуДескриптораОбъекта(ОбъектДоступа, ДескрипторДоступа) Экспорт
	
	ТекущаяПапка = ОбъектДоступа.Папка;
	
	ПапкаДляДескриптора = Неопределено;
	Пока ПапкаДляДескриптора = Неопределено Цикл
		
		Если ПапкаИмеетИндивидуальныеНастройкиПрав(ТекущаяПапка) Тогда
			ПапкаДляДескриптора = ТекущаяПапка;
		Иначе
			ТекущаяПапка = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ТекущаяПапка, "Родитель");
		КонецЕсли;
		
	КонецЦикла;
	
	Если ПапкаДляДескриптора = Неопределено Тогда
		ВызватьИсключение НСтр("ru = 'При поиске папки для дескриптора произошла ошибка: папка не найдена'; en = 'Error searching folder for descriptor: folder not found.'");
	КонецЕсли;
	
	ДескрипторДоступа.Папка = ПапкаДляДескриптора;
	ДескрипторДоступа.ПапкаПередаетВсеПрава = ОбъектДоступа.Папка = ПапкаДляДескриптора;
	
КонецПроцедуры

// Проверяет, имеет ли папка свои настройки, или полностью наследует родительские.
//
// Параметры:
//  Папка - Ссылка - ссылка на папку.
//
// Возвращаемое значение:
//  Булево - Истина, если у папки есть индивидуальные права.
//
Функция ПапкаИмеетИндивидуальныеНастройкиПрав(Папка) Экспорт
	
	Если Не ЗначениеЗаполнено(Папка) Тогда
		Возврат Истина;
	КонецЕсли;
	
	Результат = Неопределено;
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	НЕ НастройкиПравОбъектов.Объект ЕСТЬ NULL КАК ЕстьСобственнаяНастройка,
		|	НаследованиеНастроекПравОбъектов.Наследовать КАК НаследуетПрава,
		|	ТаблицаПапок.Родитель КАК Родитель
		|ИЗ
		|	#ТаблицаПапок КАК ТаблицаПапок
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.НаследованиеНастроекПравОбъектов КАК НаследованиеНастроекПравОбъектов
		|		ПО (НаследованиеНастроекПравОбъектов.Объект = ТаблицаПапок.Ссылка)
		|			И (НаследованиеНастроекПравОбъектов.Родитель = ТаблицаПапок.Ссылка)
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкиПравОбъектов КАК НастройкиПравОбъектов
		|		ПО (НастройкиПравОбъектов.Объект = ТаблицаПапок.Ссылка)
		|ГДЕ
		|	ТаблицаПапок.Ссылка = &Папка");
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "#ТаблицаПапок", Папка.Метаданные().ПолноеИмя());
	Запрос.УстановитьПараметр("Папка", Папка);
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		Результат = 
			Выборка.ЕстьСобственнаяНастройка 
			Или Не Выборка.НаследуетПрава
			Или Не ЗначениеЗаполнено(Выборка.Родитель); // Это корневая папка
	Иначе
		Результат = Истина;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Дополняет текст запроса по правам обращением к составу субъектов
// и старым права по дескрипторам.
// 
// Параметры:
//   ТекстЗапроса - Строка - текст запроса. Должен содержать временную таблицу "НовыеПрава".
//   ДляДескрипторовРегистров - Булево - определяет, права каких дескрипторов
//   	рассчитываются в запросе (объектов или регистров).
// 
Процедура ДополнитьТекстЗапросаПоПравам(ТекстЗапроса, ДляДескрипторовРегистров = Ложь) Экспорт
	
	ДополнениеТекстаЗапроса = "
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВложенныйЗапрос.Дескриптор,
		|	ВложенныйЗапрос.Пользователь,
		|	ВложенныйЗапрос.ОбъектОснование,
		|	МАКСИМУМ(ВложенныйЗапрос.Добавление) КАК Добавление,
		|	МАКСИМУМ(ВложенныйЗапрос.Изменение) КАК Изменение,
		|	МАКСИМУМ(ВложенныйЗапрос.Удаление) КАК Удаление,
		|	МАКСИМУМ(ВложенныйЗапрос.УправлениеПравами) КАК УправлениеПравами,
		|	МАКСИМУМ(ВложенныйЗапрос.Чтение) КАК Чтение
		|ПОМЕСТИТЬ НовыеПраваСУчетомССПД
		|ИЗ
		|	(ВЫБРАТЬ
		|		НовыеПрава.Дескриптор КАК Дескриптор,
		|		СоставСубъектовПравДоступа.Пользователь КАК Пользователь,
		|		СоставСубъектовПравДоступа.ОбъектОснование КАК ОбъектОснование,
		|		НовыеПрава.Добавление КАК Добавление,
		|		НовыеПрава.Изменение КАК Изменение,
		|		НовыеПрава.Удаление КАК Удаление,
		|		НовыеПрава.УправлениеПравами КАК УправлениеПравами,
		|		НовыеПрава.Чтение КАК Чтение
		|	ИЗ
		|		НовыеПрава КАК НовыеПрава
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ %1 КАК ДескрипторыДоступа
		|			ПО ДескрипторыДоступа.Ссылка = НовыеПрава.Дескриптор
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СоставСубъектовПравДоступа КАК СоставСубъектовПравДоступа
		|			ПО (НовыеПрава.Пользователь = СоставСубъектовПравДоступа.Субъект)
		|				И ДескрипторыДоступа.%2 = СоставСубъектовПравДоступа.ОбъектМетаданных
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		НовыеПрава.Дескриптор,
		|		СоставСубъектовПравДоступа.Пользователь,
		|		СоставСубъектовПравДоступа.ОбъектОснование,
		|		НовыеПрава.Добавление,
		|		НовыеПрава.Изменение,
		|		НовыеПрава.Удаление,
		|		НовыеПрава.УправлениеПравами,
		|		НовыеПрава.Чтение
		|	ИЗ
		|		НовыеПрава КАК НовыеПрава
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СоставСубъектовПравДоступа КАК СоставСубъектовПравДоступа
		|			ПО (НовыеПрава.Пользователь = СоставСубъектовПравДоступа.Субъект)
		|				И (СоставСубъектовПравДоступа.ОбъектМетаданных = ЗНАЧЕНИЕ(Справочник.ИдентификаторыОбъектовМетаданных.ПустаяСсылка))) КАК ВложенныйЗапрос
		|
		|СГРУППИРОВАТЬ ПО
		|	ВложенныйЗапрос.Дескриптор,
		|	ВложенныйЗапрос.Пользователь,
		|	ВложенныйЗапрос.ОбъектОснование
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	НовыеПраваСУчетомССПД.Дескриптор КАК Дескриптор
		|ПОМЕСТИТЬ ИзмененныеДескрипторы
		|ИЗ
		|	НовыеПраваСУчетомССПД КАК НовыеПраваСУчетомССПД
		|		ЛЕВОЕ СОЕДИНЕНИЕ %3 КАК ПраваПоДескрипторамДоступа
		|		ПО НовыеПраваСУчетомССПД.Дескриптор = ПраваПоДескрипторамДоступа.Дескриптор
		|			И НовыеПраваСУчетомССПД.Пользователь = ПраваПоДескрипторамДоступа.Пользователь
		|			И НовыеПраваСУчетомССПД.ОбъектОснование = ПраваПоДескрипторамДоступа.ОбъектОснование
		|			И НовыеПраваСУчетомССПД.Добавление = ПраваПоДескрипторамДоступа.Добавление
		|			И НовыеПраваСУчетомССПД.Изменение = ПраваПоДескрипторамДоступа.Изменение
		|			И НовыеПраваСУчетомССПД.Удаление = ПраваПоДескрипторамДоступа.Удаление
		|			И НовыеПраваСУчетомССПД.УправлениеПравами = ПраваПоДескрипторамДоступа.УправлениеПравами
		|ГДЕ
		|	ПраваПоДескрипторамДоступа.Дескриптор ЕСТЬ NULL 
		|
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ
		|	ПраваПоДескрипторамДоступа.Дескриптор
		|ИЗ
		|	%3 КАК ПраваПоДескрипторамДоступа
		|		ЛЕВОЕ СОЕДИНЕНИЕ НовыеПраваСУчетомССПД КАК НовыеПраваСУчетомССПД
		|		ПО (НовыеПраваСУчетомССПД.Дескриптор = ПраваПоДескрипторамДоступа.Дескриптор)
		|			И (НовыеПраваСУчетомССПД.Пользователь = ПраваПоДескрипторамДоступа.Пользователь)
		|			И (НовыеПраваСУчетомССПД.ОбъектОснование = ПраваПоДескрипторамДоступа.ОбъектОснование)
		|			И (НовыеПраваСУчетомССПД.Добавление = ПраваПоДескрипторамДоступа.Добавление)
		|			И (НовыеПраваСУчетомССПД.Изменение = ПраваПоДескрипторамДоступа.Изменение)
		|			И (НовыеПраваСУчетомССПД.Удаление = ПраваПоДескрипторамДоступа.Удаление)
		|			И (НовыеПраваСУчетомССПД.УправлениеПравами = ПраваПоДескрипторамДоступа.УправлениеПравами)
		|ГДЕ
		|	ПраваПоДескрипторамДоступа.Дескриптор В(&Дескрипторы)
		|	И НовыеПраваСУчетомССПД.Дескриптор ЕСТЬ NULL 
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	НовыеПраваСУчетомССПД.Дескриптор КАК Дескриптор,
		|	НовыеПраваСУчетомССПД.Пользователь,
		|	НовыеПраваСУчетомССПД.ОбъектОснование,
		|	НовыеПраваСУчетомССПД.Чтение,
		|	НовыеПраваСУчетомССПД.Добавление,
		|	НовыеПраваСУчетомССПД.Изменение,
		|	НовыеПраваСУчетомССПД.Удаление,
		|	НовыеПраваСУчетомССПД.УправлениеПравами,
		|	НЕ ИзмененныеДескрипторы.Дескриптор ЕСТЬ NULL  КАК ПраваИзменены
		|ИЗ
		|	НовыеПраваСУчетомССПД КАК НовыеПраваСУчетомССПД
		|		ЛЕВОЕ СОЕДИНЕНИЕ ИзмененныеДескрипторы КАК ИзмененныеДескрипторы
		|		ПО НовыеПраваСУчетомССПД.Дескриптор = ИзмененныеДескрипторы.Дескриптор
		|ИТОГИ
		|	МАКСИМУМ(ПраваИзменены)
		|ПО
		|	Дескриптор";
	
	ИмяСправочникаДескрипторов = "Справочник.ДескрипторыДоступаОбъектов";
	ИмяРеквизитаИОМ = "ИдентификаторОбъектаМетаданных";
	ИмяРегистраПрав = "РегистрСведений.ПраваПоДескрипторамДоступаОбъектов";
	
	Если ДляДескрипторовРегистров Тогда
		ИмяСправочникаДескрипторов = "Справочник.ДескрипторыДоступаРегистров";
		ИмяРеквизитаИОМ = "ОбъектМетаданных";
		ИмяРегистраПрав = "РегистрСведений.ПраваПоДескрипторамДоступаРегистров";
	КонецЕсли;
		
	ТекстЗапроса = ТекстЗапроса + СтрШаблон(ДополнениеТекстаЗапроса,
		ИмяСправочникаДескрипторов, ИмяРеквизитаИОМ, ИмяРегистраПрав);
		
КонецПроцедуры

// Возвращает описание объекта доступа.
// 
// Параметры:
// 	ОбъектДоступа - Объект, Ссылка
// 
// Возвращаемое значение:
// 	Объект, Структура - для объекта возвращает его самого, для ссылки возвращает структуру реквизитов.
// 
Функция ОписаниеОбъектаДоступа(ОбъектДоступа) Экспорт
	
	ОписаниеОбъекта = ОбъектДоступа;
	
	Если ОбщегоНазначения.ЭтоСсылка(ТипЗнч(ОбъектДоступа)) Тогда
		
		// Для ссылки дескриптор заполняется на основании описания.
		МенеджерОбъекта = ОбщегоНазначения.МенеджерОбъектаПоСсылке(ОбъектДоступа);
		СведенияОПоляхДоступа = МенеджерОбъекта.ПолучитьПоляДоступа();
		СведенияОПоляхДоступа = СтрЗаменить(СведенияОПоляхДоступа, " ", "");
		ОписаниеОбъекта = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
			ОбъектДоступа, СведенияОПоляхДоступа);
		
		Если Не ЗначениеЗаполнено(ОписаниеОбъекта.Ссылка) Тогда
			ОписаниеОбъекта.Ссылка = ОбъектДоступа;
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат ОписаниеОбъекта;
	
КонецФункции

Функция НепересекающиесяПодразделения(Подразделения1, Подразделения2)
	
	Все = Новый Массив;
	Старшие = Новый Массив;
	
	Результат = Новый Структура("Все, Старшие", Все, Старшие);
	
	Для Каждого Эл Из Подразделения1 Цикл
		Если Подразделения2.Найти(Эл) = Неопределено Тогда
			Все.Добавить(Эл);
		КонецЕсли;
	КонецЦикла;
	
	// Последнее из добавленных является старшим.
	Если Все.Количество() > 0 Тогда
		Старшие.Добавить(Все[Все.ВГраница()]);
	КонецЕсли;
	
	Для Каждого Эл Из Подразделения2 Цикл
		Если Подразделения1.Найти(Эл) = Неопределено Тогда
			Все.Добавить(Эл);
		КонецЕсли;
	КонецЦикла;
	
	// Последнее из добавленных является старшим.
	Если Все.Количество() > 0 Тогда
		Старшие.Добавить(Все[Все.ВГраница()]);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Возвращает пустую таблицу для хранения прав пользователей по объектам.
// 
Функция ТаблицаПравПользователейПоОбъектам() Экспорт
	
	ТипыОбъектов = Справочники.ТипВсеСсылки().Типы();
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ТипыОбъектов, Документы.ТипВсеСсылки().Типы());
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ТипыОбъектов, БизнесПроцессы.ТипВсеСсылки().Типы());
	
	ТипОбъектаДоступа = Новый ОписаниеТипов(ТипыОбъектов);
	ТипБулево = Новый ОписаниеТипов("Булево");
	
	ТаблицаПрав = Новый ТаблицаЗначений;
	ТаблицаПрав.Колонки.Добавить("ОбъектДоступа", ТипОбъектаДоступа);
	ТаблицаПрав.Колонки.Добавить("Пользователь", Новый ОписаниеТипов("СправочникСсылка.Пользователи"));
	ТаблицаПрав.Колонки.Добавить("Чтение", ТипБулево);
	ТаблицаПрав.Колонки.Добавить("Изменение", ТипБулево);
	ТаблицаПрав.Колонки.Добавить("Добавление", ТипБулево);
	ТаблицаПрав.Колонки.Добавить("Удаление", ТипБулево);
	ТаблицаПрав.Колонки.Добавить("УправлениеПравами", ТипБулево);
	
	Возврат ТаблицаПрав;
	
КонецФункции

// Добавляет пользователей с неограниченными правами в таблицу прав пользователей по объектам.
// 
// Параметры:
//   ТаблицаПрав - таблица значений - таблица с правами пользователей,
//   	структуру таблицы см. в ТаблицаПравПользователейПоОбъектам().
// 
Процедура РасширитьТаблицуПравНеограниченнымиПравами(ТаблицаПрав, ИдентификаторОМ, ОбъектыДоступа, Пользователи = Неопределено) Экспорт
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	ПользователиВКонтейнерах.Пользователь КАК Пользователь,
		|	МАКСИМУМ(ПраваРолей.ЧтениеБезОграничения) КАК Чтение,
		|	МАКСИМУМ(ПраваРолей.ИзменениеБезОграничения) КАК Изменение,
		|	МАКСИМУМ(ПраваРолей.ИзменениеБезОграничения) КАК Добавление,
		|	МАКСИМУМ(ПраваРолей.ИзменениеБезОграничения) КАК Удаление,
		|	МАКСИМУМ(ПраваРолей.ЧтениеБезОграничения)
		|		И МАКСИМУМ(ПраваРолей.ИзменениеБезОграничения) КАК УправлениеПравами
		|ИЗ
		|	РегистрСведений.ПолномочияПользователей КАК ПолномочияПользователей
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ПользователиВКонтейнерах КАК ПользователиВКонтейнерах
		|		ПО ПолномочияПользователей.Владелец = ПользователиВКонтейнерах.Контейнер
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ПрофилиГруппДоступа.Роли КАК ПрофилиГруппДоступаРоли
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ПраваРолей КАК ПраваРолей
		|			ПО ПрофилиГруппДоступаРоли.Роль = ПраваРолей.Роль
		|		ПО ПолномочияПользователей.Полномочия = ПрофилиГруппДоступаРоли.Ссылка
		|ГДЕ
		|	ПраваРолей.ОбъектМетаданных = &ИдентификаторОМ
		|	И ПраваРолей.ЧтениеБезОграничения = ИСТИНА
		|	%ОтборПоПользователю%
		|
		|СГРУППИРОВАТЬ ПО
		|	ПользователиВКонтейнерах.Пользователь");
	
	Если Пользователи = Неопределено Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "%ОтборПоПользователю%", "");
	Иначе
		Запрос.Текст = СтрЗаменить(Запрос.Текст,
			"%ОтборПоПользователю%", "И ПользователиВКонтейнерах.Пользователь В(&Пользователи)");
	КонецЕсли;
	
	Запрос.УстановитьПараметр("Пользователи", Пользователи);
	Запрос.УстановитьПараметр("ИдентификаторОМ", ИдентификаторОМ);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Для Каждого ОбъектДоступа Из ОбъектыДоступа Цикл
			Стр = ТаблицаПрав.Добавить();
			ЗаполнитьЗначенияСвойств(Стр, Выборка);
			Стр.ОбъектДоступа = ОбъектДоступа;
		КонецЦикла;
	КонецЦикла;
	
	ТаблицаПрав.Свернуть("ОбъектДоступа, Пользователь", "Чтение, Изменение, Добавление, Удаление, УправлениеПравами");
	
КонецПроцедуры

// Добавляет руководителей и делегатов в таблицу прав пользователей по объектам.
// 
// Параметры:
//   ТаблицаПрав - таблица значений - таблица с правами пользователей,
//   	структуру таблицы см. в ТаблицаПравПользователейПоОбъектам().
// 
Процедура РасширитьТаблицуПравРуководителямиИДелегатами(ТаблицаПрав) Экспорт
	
	Если ТаблицаПрав.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ИдентификаторОМ = ОбщегоНазначения.ИдентификаторОбъектаМетаданных(
		ТаблицаПрав[0].ОбъектДоступа.Метаданные());
	ОбластиДелегирования =
		ДокументооборотПраваДоступаПереопределяемый.ПолучитьОбластиДелегированияПоИОМ(ИдентификаторОМ);
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	ТаблицаПрав.ОбъектДоступа,
		|	ТаблицаПрав.Пользователь,
		|	ТаблицаПрав.Чтение,
		|	ТаблицаПрав.Изменение,
		|	ТаблицаПрав.Добавление,
		|	ТаблицаПрав.Удаление,
		|	ТаблицаПрав.УправлениеПравами
		|ПОМЕСТИТЬ ТаблицаПрав
		|ИЗ
		|	&ТаблицаПрав КАК ТаблицаПрав
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаПрав.ОбъектДоступа,
		|	СоставСубъектовПравДоступа.Пользователь,
		|	МАКСИМУМ(ТаблицаПрав.Чтение) КАК Чтение,
		|	МАКСИМУМ(ТаблицаПрав.Изменение) КАК Изменение,
		|	МАКСИМУМ(ТаблицаПрав.Добавление) КАК Добавление,
		|	МАКСИМУМ(ТаблицаПрав.Удаление) КАК Удаление,
		|	МАКСИМУМ(ТаблицаПрав.УправлениеПравами) КАК УправлениеПравами
		|ИЗ
		|	ТаблицаПрав КАК ТаблицаПрав
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СоставСубъектовПравДоступа КАК СоставСубъектовПравДоступа
		|		ПО ТаблицаПрав.Пользователь = СоставСубъектовПравДоступа.Субъект
		|
		|СГРУППИРОВАТЬ ПО
		|	ТаблицаПрав.ОбъектДоступа,
		|	СоставСубъектовПравДоступа.Пользователь");
	
	Запрос.УстановитьПараметр("ОбластиДелегирования", ОбластиДелегирования);
	Запрос.УстановитьПараметр("ТаблицаПрав", ТаблицаПрав);
	
	Выборка = Запрос.Выполнить().Выбрать();
	ТаблицаПрав.Очистить();
	
	// Построчное заполнение вместо выгрузки таблицы, т.к. нужно сохранить типы колонок.
	Пока Выборка.Следующий() Цикл
		ЗаполнитьЗначенияСвойств(ТаблицаПрав.Добавить(), Выборка);
	КонецЦикла;
	
КонецПроцедуры

// Удаляет связи между объектами и дескрипторами лок. администраторов.
// 
Процедура ОтключитьДескрипторыЛокАдминистраторов(Немедленно = Ложь) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если Немедленно <> Истина
		И ДокументооборотПраваДоступаПовтИсп.ОтложенноеОбновлениеПравДоступа() Тогда
		
		РегистрыСведений.ОчередьОбновленияПравДоступа.Добавить(
			Перечисления.ЗаданияОчередиОбновленияПрав.ОтключитьДескрипторыЛокАдминистраторов, Дата("00000000"), 2);
		
		Возврат;
		
	КонецЕсли;
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ДескрипторыДляОбъектов.Дескриптор
		|ИЗ
		|	РегистрСведений.ДескрипторыДляОбъектов КАК ДескрипторыДляОбъектов
		|ГДЕ
		|	ДескрипторыДляОбъектов.ТипДескриптора = &ТипДескриптора");
		
	ТипДескриптораЛА = 4;
	Запрос.УстановитьПараметр("ТипДескриптора", ТипДескриптораЛА);
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		Набор = РегистрыСведений.ДескрипторыДляОбъектов.СоздатьНаборЗаписей();
		Набор.Отбор.Дескриптор.Установить(Выборка.Дескриптор);
		Набор.Отбор.ТипДескриптора.Установить(ТипДескриптораЛА);
		Набор.Записать();
		
	КонецЦикла;
	
КонецПроцедуры


