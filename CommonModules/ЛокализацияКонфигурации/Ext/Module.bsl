#Область ПрограммныйИнтерфейс

// Возвращает код языка указанного пользователя.
//
// Параметры:
//   Пользователь - СправочникСсылка.Пользователи - пользователь, язык которого определяется.
//
// Возвращаемое значение:
//   Строка - код языка пользователя, если указан, или код основного языка конфигурации.
//
Функция КодЯзыкаПользователя(Пользователь) Экспорт
	
	Если ТипЗнч(Пользователь) = Тип("СправочникСсылка.Пользователи") Тогда
		КодЯзыка = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Пользователь, "КодЯзыка");
		Если Не ЗначениеЗаполнено(КодЯзыка) Тогда
			КодЯзыка = Метаданные.ОсновнойЯзык.КодЯзыка;
		КонецЕсли;
	Иначе
		КодЯзыка = Метаданные.ОсновнойЯзык.КодЯзыка;
	КонецЕсли;
	
	Возврат КодЯзыка;
	
КонецФункции

// Возвращает представление типа для указанного пользователя.
//
Функция ПредставлениеТипаДляПользователя(Тип, Пользователь) Экспорт
	
	Представление = Неопределено;
	
	ЛокализацияКонфигурацииПереопределяемый.ПриОпределенииПредставленияТипаДляПользователя(
		Тип,
		Пользователь,
		Представление);
	
	Если Представление <> Неопределено Тогда
		Возврат Представление;
	Иначе
		Возврат Строка(Тип);
	КонецЕсли;
	
КонецФункции

// Возвращает код языка исполнителя задачи или, если язык не указан явно - основной язык
// конфигурации. В случае, когда исполнитель не указан, возвращается код языка текущего
// пользователя.
//
// Параметры:
//   Задача - ЗадачаОбъект.ЗадачаИсполнителя - задача, для которой определяется код языка.
//
// Возвращаемое значение:
//   Строка - код языка.
//
Функция КодЯзыкаИсполнителяЗадачи(Задача) Экспорт
	
	КодЯзыка = Неопределено;
	ЛокализацияКонфигурацииПереопределяемый.ПриОпределенииКодаЯзыкаИсполнителяЗадачи(Задача, КодЯзыка);
	Если КодЯзыка <> Неопределено Тогда
		
		Возврат КодЯзыка;
		
	КонецЕсли;
	
	Если ТипЗнч(Задача) = Тип("ЗадачаОбъект.ЗадачаИсполнителя")
		И ЗначениеЗаполнено(Задача.Исполнитель) Тогда
		
		КодЯзыка = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Задача.Исполнитель, "КодЯзыка");
		
		Если ЗначениеЗаполнено(КодЯзыка) Тогда
		 	Возврат КодЯзыка;
		Иначе
			Возврат Метаданные.ОсновнойЯзык.КодЯзыка;
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат КодЯзыкаПользователя(Пользователи.ТекущийПользователь());
	
КонецФункции

#КонецОбласти
