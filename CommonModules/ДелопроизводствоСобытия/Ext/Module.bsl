// Обработчик подписки на событие ЗаписатьОбщиеРеквизитыДокументов.
//
Процедура ЗаписатьОбщиеРеквизитыДокументов(Источник, Отказ) Экспорт
	
	Если Источник.ОбменДанными.Загрузка Тогда 
		Возврат;
	КонецЕсли; 
	
	УстановитьПривилегированныйРежим(Истина);
	
	КонтрагентыДляСписков = "";
	Если ТипЗнч(Источник.Ссылка) = Тип("СправочникСсылка.ИсходящиеДокументы") Тогда 
		КонтрагентыДляСписков = Делопроизводство.ПолучитьКонтрагентовДляСписков(
			Источник.Получатели.Выгрузить(), "Получатель");
	ИначеЕсли ТипЗнч(Источник.Ссылка) = Тип("СправочникСсылка.ВнутренниеДокументы") Тогда 
		КонтрагентыДляСписков = Делопроизводство.ПолучитьКонтрагентовДляСписков(
			Источник.Контрагенты.Выгрузить());
	КонецЕсли;	
	
	РегистрационныйНомерИДата = "";
	Если ЗначениеЗаполнено(Источник.РегистрационныйНомер) 
		И ЗначениеЗаполнено(Источник.ДатаРегистрации) Тогда 
		РегистрационныйНомерИДата = 
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = '%1 от %2'; en = '%1 of %2'"),
			СокрЛП(Источник.РегистрационныйНомер),
			Формат(Источник.ДатаРегистрации, "ДЛФ=D"));
	КонецЕсли;	
		
	ИсходящийНомерИДата	= "";
	Если ТипЗнч(Источник.Ссылка) = Тип("СправочникСсылка.ВходящиеДокументы")
		И ЗначениеЗаполнено(Источник.ИсходящийНомер)
		И ЗначениеЗаполнено(Источник.ИсходящаяДата) Тогда 
		ИсходящийНомерИДата = 
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = '%1 от %2'; en = '%1 of %2'"),
			СокрЛП(Источник.ИсходящийНомер),
			Формат(Источник.ИсходящаяДата, "ДЛФ=D"));
	КонецЕсли;
	
	ДатаСортировки = '00010101';
	Если ЗначениеЗаполнено(Источник.ДатаРегистрации) Тогда 
		ДатаСортировки = Источник.ДатаРегистрации;
	Иначе
		ДатаСортировки = Источник.ДатаСоздания;
	КонецЕсли;
	
	// Запись в регистр ОбщиеРеквизитыДокументов
	НаборЗаписей = РегистрыСведений.ОбщиеРеквизитыДокументов.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Документ.Установить(Источник.Ссылка);
	НаборЗаписей.Прочитать();
	
	Если НаборЗаписей.Количество() > 0 Тогда 
		ЗаписьРегистра = НаборЗаписей[0];
	Иначе
		ЗаписьРегистра = НаборЗаписей.Добавить();
	КонецЕсли;
	
	ЗаписьРегистра.Документ 				= Источник.Ссылка;
	ЗаписьРегистра.РегистрационныйНомер 	= Источник.РегистрационныйНомер;
	ЗаписьРегистра.ДатаРегистрации 			= Источник.ДатаРегистрации;
	ЗаписьРегистра.РегистрационныйНомерИДата= РегистрационныйНомерИДата;
	ЗаписьРегистра.ИсходящийНомерИДата 		= ИсходящийНомерИДата;
	ЗаписьРегистра.Сумма 					= Источник.Сумма;
	ЗаписьРегистра.Валюта 					= Источник.Валюта;
	ЗаписьРегистра.СрокИсполнения 			= Источник.СрокИсполнения;
	ЗаписьРегистра.Заголовок 				= Источник.Заголовок;
	ЗаписьРегистра.КонтрагентыДляСписков 	= КонтрагентыДляСписков;
	ЗаписьРегистра.ДатаСортировки 			= ДатаСортировки;
	НаборЗаписей.Записать();
	
	// Запись в регистры ДанныеДокументов
	Если ТипЗнч(Источник.Ссылка) = Тип("СправочникСсылка.ВнутренниеДокументы") Тогда  
		ИмяРегистра = "ДанныеВнутреннихДокументов";
	ИначеЕсли ТипЗнч(Источник.Ссылка) = Тип("СправочникСсылка.ВходящиеДокументы") Тогда 
		ИмяРегистра = "ДанныеВходящихДокументов";
	ИначеЕсли ТипЗнч(Источник.Ссылка) = Тип("СправочникСсылка.ИсходящиеДокументы") Тогда 
		ИмяРегистра = "ДанныеИсходящихДокументов";
	КонецЕсли;
	
	НаборЗаписей = РегистрыСведений[ИмяРегистра].СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Документ.Установить(Источник.Ссылка);
	НаборЗаписей.Прочитать();
	
	Если НаборЗаписей.Количество() > 0 Тогда 
		ЗаписьРегистра = НаборЗаписей[0];
	Иначе
		ЗаписьРегистра = НаборЗаписей.Добавить();
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(ЗаписьРегистра, Источник);
	ЗаписьРегистра.Документ = Источник.Ссылка;
	ЗаписьРегистра.ДатаСортировки = ДатаСортировки;
	ЗаписьРегистра.РегистрационныйНомерИДата = РегистрационныйНомерИДата;
	ЗаписьРегистра.КонтрагентыДляСписков = КонтрагентыДляСписков;
	Если ТипЗнч(Источник.Ссылка) = Тип("СправочникСсылка.ВходящиеДокументы") Тогда 
		ЗаписьРегистра.ИсходящийНомерИДата = ИсходящийНомерИДата;
	КонецЕсли;	
	НаборЗаписей.Записать();
		
КонецПроцедуры


