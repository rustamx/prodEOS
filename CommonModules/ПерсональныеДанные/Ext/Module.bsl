Процедура ЗаполнитьПерсональныеДанныеФайлов(Владелец) Экспорт
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьУчетДоступаКПерсональнымДанным") Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	Файлы.Ссылка КАК Ссылка
			|ИЗ
			|	Справочник.Файлы КАК Файлы
			|ГДЕ
			|	Файлы.ВладелецФайла = &Владелец";

		Запрос.УстановитьПараметр("Владелец", Владелец);

		Результат = Запрос.Выполнить();
		Выборка = Результат.Выбрать();
		Пока Выборка.Следующий() Цикл
			
			Объект = Выборка.Ссылка.ПолучитьОбъект();
			ЗаблокироватьДанныеДляРедактирования(Объект.Ссылка);
			
			ПерсональныеДанные.ЗаполнитьСписокСубъектовПерсональныхДанныхФайла(Объект);
			
			Объект.Записать();
			
		КонецЦикла;
	
	КонецЕсли;	
	
КонецПроцедуры

// Заполняет сведения о персональных данных во всех версиях файла
Процедура ЗаполнитьПерсональныеДанныеВерсийФайла(ФайлСсылка) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВерсииФайлов.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.ВерсииФайлов КАК ВерсииФайлов
		|ГДЕ
		|	ВерсииФайлов.Владелец = &Владелец";

	Запрос.УстановитьПараметр("Владелец", ФайлСсылка);

	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	Пока Выборка.Следующий() Цикл
		
		ВерсияОбъект = Выборка.Ссылка.ПолучитьОбъект();
		ЗаблокироватьДанныеДляРедактирования(ВерсияОбъект.Ссылка);
		
		ВерсияОбъект.СубъектыПерсональныхДанных.Очистить();
	
		Для каждого Субъект Из ФайлСсылка.СубъектыПерсональныхДанных Цикл
			Строка = ВерсияОбъект.СубъектыПерсональныхДанных.Добавить();
			Строка.ФизическоеЛицо = Субъект.ФизическоеЛицо;
		КонецЦикла;	
	
		ВерсияОбъект.Записать();
		РазблокироватьДанныеДляРедактирования(ВерсияОбъект.Ссылка);
		
	КонецЦикла;

КонецПроцедуры

Функция ИзменилсяСписокПерсональныхДанных(Объект) Экспорт
	
	Запрос = Новый Запрос;
	
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	СубъектыПерсональныхДанных.ФизическоеЛицо
		|ИЗ
		|	%1%.СубъектыПерсональныхДанных КАК СубъектыПерсональныхДанных
		|ГДЕ
		|	СубъектыПерсональныхДанных.Ссылка = &Ссылка";
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "%1%", Объект.Метаданные().ПолноеИмя());

	Запрос.УстановитьПараметр("Ссылка", Объект.Ссылка);

	Результат = Запрос.Выполнить();

	ТаблицаРезультатов = Результат.Выгрузить();

	Если ТаблицаРезультатов.Количество() <> Объект.СубъектыПерсональныхДанных.Количество() Тогда
		Возврат Истина;
	КонецЕсли;
	
	Для каждого Эл Из Объект.СубъектыПерсональныхДанных Цикл
		Строка = ТаблицаРезультатов.Найти(Эл.ФизическоеЛицо, "ФизическоеЛицо");
		Если Строка = Неопределено Тогда
			Возврат Истина;
		КонецЕсли;	
		ТаблицаРезультатов.Удалить(Строка);
	КонецЦикла;	
	
	Если ТаблицаРезультатов.Количество() <> 0 Тогда
		Возврат Истина;
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции	

Процедура ЗаполнитьСписокСубъектовПерсональныхДанныхФайла(ФайлОбъект) Экспорт
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьУчетДоступаКПерсональнымДанным") Тогда
	
		Если ТипЗнч(ФайлОбъект.ВладелецФайла) = Тип("СправочникСсылка.ВходящиеДокументы") 
			Или ТипЗнч(ФайлОбъект.ВладелецФайла) = Тип("СправочникСсылка.ИсходящиеДокументы") 
			Или ТипЗнч(ФайлОбъект.ВладелецФайла) = Тип("СправочникСсылка.ВнутренниеДокументы") Тогда
			
			ФайлОбъект.СубъектыПерсональныхДанных.Очистить();
			Для каждого Субъект Из ФайлОбъект.ВладелецФайла.СубъектыПерсональныхДанных Цикл
				Строка = ФайлОбъект.СубъектыПерсональныхДанных.Добавить();
				Строка.ФизическоеЛицо = Субъект.ФизическоеЛицо;
			КонецЦикла;	
			
		КонецЕсли;	
	
	КонецЕсли;
	
КонецПроцедуры


