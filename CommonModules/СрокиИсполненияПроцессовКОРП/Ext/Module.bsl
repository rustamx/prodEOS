
////////////////////////////////////////////////////////////////////////////////
// Сроки исполнения процессов КОРП: содержит серверные процедуры и функции по работе
// со сроками процессов в редакциях КОРП/ДГУ.
//  
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Заполняет сроки исполнения этапов комплексного процесса
// в таблице Этапы.
//
// Параметры:
//  Этапы - ДанныеФормыКоллекция, ТаблицаЗначений - таблица этапов комплексного процесса.
//
Процедура ЗаполнитьСрокиИсполненияЭтаповКомплексногоПроцесса(Этапы) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Для Каждого Этап Из Этапы Цикл
		
		СтруктураПоиска = Новый Структура;
		СтруктураПоиска.Вставить("ШаблонБизнесПроцесса", Неопределено);
		СтруктураПоиска.Вставить("ЗапущенныйБизнесПроцесс", Неопределено);
		СтруктураПоиска.Вставить("Удален", Ложь);
		
		ЗаполнитьЗначенияСвойств(СтруктураПоиска, Этап);
		
		Если СтруктураПоиска.Удален = Истина Тогда
			Продолжить;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(СтруктураПоиска.ЗапущенныйБизнесПроцесс) Тогда
			
			ПараметрыЭтапа = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
				СтруктураПоиска.ЗапущенныйБизнесПроцесс,
				"СрокИсполненияПроцесса");
			
		Иначе
			
			ПараметрыЭтапа = СрокиИсполненияПроцессов.
				ДлительностьИсполненияПроцесса(СтруктураПоиска.ШаблонБизнесПроцесса);
			
			ПараметрыЭтапа.Вставить("СрокИсполненияПроцесса",
				ОбщегоНазначения.ЗначениеРеквизитаОбъекта(
					СтруктураПоиска.ШаблонБизнесПроцесса, "СрокИсполненияПроцесса"));
			
		КонецЕсли;
		
		ЗаполнитьЗначенияСвойств(Этап, ПараметрыЭтапа);
		
	КонецЦикла;
	
КонецПроцедуры

// Заполняет срок исполнения задачи по параметрам.
//
// Параметры:
//  Задача - ЗадачаОбъект.ЗадачаИсполнителя
//  ВариантУстановкиСрокаИсполнения - Перечисление.ВариантыУстановкиСрокаИсполнения - 
//                                    вариант установки срока в процессе, для текущей задачи.
//  СрокИсполнения - Дата - срок исполнения в процессе.
//  СрокИсполненияДни - Число - относительный срок исполнения в процессе.
//  СрокИсполненияЧасы - Число - относительный срок исполнения в процессе.
//  СрокИсполненияМинуты - Число - относительный срок исполнения в процессе.
//
Процедура ЗаполнитьСрокИсполненияЗадачи(Задача,
	ВариантУстановкиСрокаИсполнения,
	СрокИсполнения,
	СрокИсполненияДни,
	СрокИсполненияЧасы,
	СрокИсполненияМинуты) Экспорт
	
	Задача.СрокИсполнения = Дата(1,1,1);
	
	Если Не ЗначениеЗаполнено(СрокИсполнения)
		И Не ЗначениеЗаполнено(СрокИсполненияДни)
		И Не ЗначениеЗаполнено(СрокИсполненияЧасы)
		И Не ЗначениеЗаполнено(СрокИсполненияМинуты)
		Или Не ЗначениеЗаполнено(Задача.Дата) Тогда
		
		Возврат;
		
	ИначеЕсли ВариантУстановкиСрокаИсполнения = 
		Перечисления.ВариантыУстановкиСрокаИсполнения.ОтносительныйСрок Тогда
		
		ПараметрыДляОпределенияТочногоСрока = ПараметрыДлительностиИсполненияДляОпределенияТочногоСрока();
		ПараметрыДляОпределенияТочногоСрока.Дни = СрокИсполненияДни;
		ПараметрыДляОпределенияТочногоСрока.Часы = СрокИсполненияЧасы;
		ПараметрыДляОпределенияТочногоСрока.Минуты = СрокИсполненияМинуты;
		ПараметрыДляОпределенияТочногоСрока.ДатаОтсчета = Задача.Дата;
		
		ИсполнительЗадачи = Неопределено;
		Если ЗначениеЗаполнено(Задача.Исполнитель) Тогда
			ИсполнительЗадачи = Задача.Исполнитель;
		ИначеЕсли ЗначениеЗаполнено(Задача.РольИсполнителя) Тогда
			ИсполнительЗадачи = Задача.РольИсполнителя;
		КонецЕсли;
		
		ПараметрыДляОпределенияТочногоСрока.ГрафикРаботы = ГрафикРаботыУчастникаПроцесса(ИсполнительЗадачи);
		
		Задача.СрокИсполнения = СрокИсполненияПоДлительности(ПараметрыДляОпределенияТочногоСрока);
		
	Иначе
		
		Задача.СрокИсполнения = СрокИсполнения;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ПрограммныйИнтерфейс_РасчетСроковПроцессов

// Возвращает график работы для участника процесса.
//
// Параметры:
//  Участник - СправочникСсылка.Пользователи
//             СправочникСсылка.ПолныеРоли,
//             Строка - участник процесса (пользователь/роль/автоподстановка).
//
Функция ГрафикРаботыУчастникаПроцесса(Участник) Экспорт
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьГрафикиРаботы") Тогда
		Возврат Справочники.ГрафикиРаботы.ПустаяСсылка();
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Участник) Тогда
		Возврат СрокиИсполненияПроцессовПовтИспКОРП.ПолучитьОсновнойГрафикРаботы();
	КонецЕсли;
	
	Если ТипЗнч(Участник) = Тип("СправочникСсылка.Пользователи") Тогда
		Возврат СрокиИсполненияПроцессовПовтИспКОРП.ПолучитьГрафикРаботыПользователя(Участник);
	Иначе
		Возврат СрокиИсполненияПроцессовПовтИспКОРП.ПолучитьОсновнойГрафикРаботы();
	КонецЕсли;
	
КонецФункции

// Составные процессы

// Возвращает данные действия составного процесса для расчета срока.
//
// Параметры:
//  Действие - БизнесПроцессСсылка, СправочникСсылка.<Имя шаблона процесса> - ссылка на действие
//             составного/комплексногопроцесса процесса/шаблона.
//  ИдентификаторыИзмененныхДействий - Массив - массив идентификаторов измененных действий комплексного процесса/шаблона.
//
// Параметры:
//  Структура - см. СрокиИсполненияПроцессов.ДанныеДляРасчетаСроков
//
Функция ДанныеДляРасчетаСрокаДействияСоставногоПроцесса(
	Действие, ИдентификаторыИзмененныхДействий = Неопределено) Экспорт
	
	ДанныеДляРасчета = Новый Структура;
	
	ТипДействия = ТипЗнч(Действие);
	
	Параметры = СрокиИсполненияПроцессов.ПараметрыДляРасчетаСроков();
	Параметры.Смещение = СмещенияДатыОтсчета(Действие);
	
	Если ТипДействия = Тип("СправочникСсылка.ШаблоныИсполнения")
		Или ТипДействия = Тип("БизнесПроцессСсылка.Исполнение") Тогда
		
		СтруктураДляРасчета = СрокиИсполненияПроцессов.
			СтруктураДляРасчетаСрокаПроцессаИсполнения(Действие);
		
		ДанныеДляРасчета = СрокиИсполненияПроцессов.
			ДанныеДляРасчетаСроковПроцессаИсполнения(СтруктураДляРасчета, Параметры);
	
	ИначеЕсли ТипДействия = Тип("СправочникСсылка.ШаблоныКомплексныхБизнесПроцессов")
		Или ТипДействия = Тип("БизнесПроцессСсылка.КомплексныйПроцесс") Тогда
		
		СтруктураДляРасчета = СтруктураДляРасчетаСрокаКомплексногоПроцесса(Действие);
		
		ДанныеДляРасчета = ДанныеДляРасчетаСроковКомплексногоПроцесса(
			СтруктураДляРасчета, Параметры, ИдентификаторыИзмененныхДействий);
	
	ИначеЕсли ТипДействия = Тип("СправочникСсылка.ШаблоныОзнакомления")
		Или ТипДействия = Тип("БизнесПроцессСсылка.Ознакомление") Тогда
		
		СтруктураДляРасчета = СрокиИсполненияПроцессов.
			СтруктураДляРасчетаСрокаОзнакомления(Действие);
		
		ДанныеДляРасчета = СрокиИсполненияПроцессов.
			ДанныеДляРасчетаСроковОзнакомления(СтруктураДляРасчета, Параметры);
		
	ИначеЕсли ТипДействия = Тип("СправочникСсылка.ШаблоныПоручения")
		Или ТипДействия = Тип("БизнесПроцессСсылка.Поручение") Тогда
		
		СтруктураДляРасчета = СрокиИсполненияПроцессов.СтруктураДляРасчетаСрокаПоручения(Действие);
		
		ДанныеДляРасчета = СрокиИсполненияПроцессов.
			ДанныеДляРасчетаСроковПоручения(СтруктураДляРасчета, Параметры);
			
	ИначеЕсли ТипДействия = Тип("СправочникСсылка.ШаблоныПриглашения")
		Или ТипДействия = Тип("БизнесПроцессСсылка.Приглашение") Тогда
		
		СтруктураДляРасчета = СтруктураДляРасчетаСрокаПриглашения(Действие);
		ДанныеДляРасчета = ДанныеДляРасчетаСроковПриглашения(СтруктураДляРасчета, Параметры);
		
	ИначеЕсли ТипДействия = Тип("СправочникСсылка.ШаблоныРассмотрения")
		Или ТипДействия = Тип("БизнесПроцессСсылка.Рассмотрение") Тогда
		
		СтруктураДляРасчета = СрокиИсполненияПроцессов.СтруктураДляРасчетаСрокаРассмотрения(Действие);
		
		ДанныеДляРасчета = СрокиИсполненияПроцессов.
			ДанныеДляРасчетаСроковРассмотрения(СтруктураДляРасчета, Параметры);
		
	ИначеЕсли ТипДействия = Тип("СправочникСсылка.ШаблоныРегистрации")
		Или ТипДействия = Тип("БизнесПроцессСсылка.Регистрация") Тогда
		
		СтруктураДляРасчета = СрокиИсполненияПроцессов.СтруктураДляРасчетаСрокаРегистрации(Действие);
		
		ДанныеДляРасчета = СрокиИсполненияПроцессов.
			ДанныеДляРасчетаСроковРегистрации(СтруктураДляРасчета, Параметры);
		
	ИначеЕсли ТипДействия = Тип("СправочникСсылка.ШаблоныСогласования")
		Или ТипДействия = Тип("БизнесПроцессСсылка.Согласование") Тогда
		
		СтруктураДляРасчета = СрокиИсполненияПроцессов.СтруктураДляРасчетаСрокаСогласования(Действие);
		
		ДанныеДляРасчета = СрокиИсполненияПроцессов.
			ДанныеДляРасчетаСроковСогласования(СтруктураДляРасчета, Параметры);
	
	ИначеЕсли ТипДействия = Тип("СправочникСсылка.ШаблоныСоставныхБизнесПроцессов")
		Или ТипДействия = Тип("БизнесПроцессСсылка.ОбработкаВнутреннегоДокумента")
		Или ТипДействия = Тип("БизнесПроцессСсылка.ОбработкаВходящегоДокумента")
		Или ТипДействия = Тип("БизнесПроцессСсылка.ОбработкаИсходящегоДокумента") Тогда
		
		СтруктураДляРасчета = СрокиИсполненияПроцессов.
			СтруктураДляРасчетаСрокаСоставногоПроцесса(Действие);
			
		ДанныеДляРасчета = СрокиИсполненияПроцессов.
			ДанныеДляРасчетаСроковСоставногоПроцесса(СтруктураДляРасчета, Параметры);
	
	ИначеЕсли ТипДействия = Тип("СправочникСсылка.ШаблоныУтверждения")
		Или ТипДействия = Тип("БизнесПроцессСсылка.Утверждение") Тогда
		
		СтруктураДляРасчета = СрокиИсполненияПроцессов.СтруктураДляРасчетаСрокаУтверждения(Действие);
		
		ДанныеДляРасчета = СрокиИсполненияПроцессов.
			ДанныеДляРасчетаСроковУтверждения(СтруктураДляРасчета, Параметры);
		
	КонецЕсли;
	
	Возврат ДанныеДляРасчета;
	
КонецФункции

// Комплексный процесс

// Возвращает структура для расчета срока комплексного процесса/шаблона/схемы.
//
// Параметры:
//  Объект - БизнесПроцессСсылка.КомплексныйПроцесс,
//           СправочникСсылка.ШаблоныКомплексныхБизнесПроцессов,
//           БизнесПроцессОбъект.КомплексныйПроцесс,
//           СправочникОбъект.ШаблоныКомплексныхБизнесПроцессов - объект/ссылка комплексного процесса/шаблона.
//  Схема - СправочникСсылка.СхемыКомплексныхПроцессов,
//          СправочникОбъект.СхемыКомплексныхПроцессов,
//          ДанныеФормыСтруктура - ссылка/объект схемы комплексного процесса/шаблона.
//
// Возвращаемое значение:
//  Структура - реквизиты и табличные части процесса/шаблона.
//   * Ссылка
//   * ИдентификаторСсылки
//   * Этапы
//      ** СрокИсполненияПроцесса - Дата - дополнительные колонка, срок исполнения этапа.
//   * ПредшественникиЭтапов
//   * ПараметрыДействий
//   * ПредшественникиЭлементовСхемы
//   * ЭлементыСхемы
//   * СрокИсполненияПроцесса
//
Функция СтруктураДляРасчетаСрокаКомплексногоПроцесса(Объект = Неопределено, Схема = Неопределено) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ТипОбъекта = ТипЗнч(Объект);
	
	РеквизитыДляРасчета = 
		"Ссылка,
		|ИдентификаторСсылки,
		|Этапы,
		|ПредшественникиЭтапов,
		|ПараметрыДействий,
		|ПредшественникиЭлементовСхемы,
		|ЭлементыСхемы,
		|СрокИсполненияПроцесса";
	
	СтруктураДляРасчета = Новый Структура(РеквизитыДляРасчета);
	
	Если Объект <> Неопределено Тогда
		
		Если ОбщегоНазначения.ЭтоСсылка(ТипОбъекта) Тогда // если это ссылка на комплексный процесс/шаблон
			
			РеквизитыПроцессаСтрокой = 
				"ИдентификаторСсылки, Схема, Этапы, ПредшественникиЭтапов, СрокИсполненияПроцесса";
				
			РеквизитыПроцесса = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
				Объект, РеквизитыПроцессаСтрокой);
			
			СтруктураДляРасчета.Ссылка = Объект;
			СтруктураДляРасчета.ИдентификаторСсылки = РеквизитыПроцесса.ИдентификаторСсылки;
			СтруктураДляРасчета.СрокИсполненияПроцесса = РеквизитыПроцесса.СрокИсполненияПроцесса;
			
			СтруктураДляРасчета.Этапы = РеквизитыПроцесса.Этапы.Выгрузить();
			СтруктураДляРасчета.ПредшественникиЭтапов = РеквизитыПроцесса.ПредшественникиЭтапов.Выгрузить();
			
			СхемаОбъекта = РеквизитыПроцесса.Схема;
			
		Иначе // если это объект, структура в данных формы или структура процесса/шаблона.
			
			СтруктураДляРасчета.Ссылка = Объект.Ссылка;
			СтруктураДляРасчета.ИдентификаторСсылки = Объект.ИдентификаторСсылки;
			СтруктураДляРасчета.СрокИсполненияПроцесса = Объект.СрокИсполненияПроцесса;
			
			Если ТипОбъекта = Тип("Структура") Тогда
				СтруктураДляРасчета.Этапы = Объект.Этапы;
				СтруктураДляРасчета.ПредшественникиЭтапов = Объект.ПредшественникиЭтапов;
			Иначе
				СтруктураДляРасчета.Этапы = Объект.Этапы.Выгрузить();
				СтруктураДляРасчета.ПредшественникиЭтапов = Объект.ПредшественникиЭтапов.Выгрузить();
			КонецЕсли;
			
			СхемаОбъекта = Объект.Схема;
			
		КонецЕсли;
		
		Если ТипОбъекта = Тип("ДанныеФормыСтруктура") Тогда
			ИсключитьУдаленныеЭтапыИзТаблицКомплексногоПроцесса(
				СтруктураДляРасчета.Этапы, СтруктураДляРасчета.ПредшественникиЭтапов);
		Иначе
			МассивТипов = Новый Массив;
			МассивТипов.Добавить(Тип("Дата"));
			ОписаниеТиповДата = Новый ОписаниеТипов(МассивТипов);
			СтруктураДляРасчета.Этапы.Колонки.Добавить("СрокИсполненияПроцесса", ОписаниеТиповДата);
			
			ЗаполнитьСрокиИсполненияЭтаповКомплексногоПроцесса(СтруктураДляРасчета.Этапы);
		КонецЕсли;
		
		Если Схема = Неопределено И ЗначениеЗаполнено(СхемаОбъекта) Тогда
			Схема = СхемаОбъекта;
		КонецЕсли;
		
		СтруктураДляРасчета.Этапы.Сортировать("ПорядокСортировки");
		
	КонецЕсли;
	
	Если Схема <> Неопределено Тогда
		
		ТипСхемы = ТипЗнч(Схема);
		
		Если ТипСхемы = Тип("СправочникСсылка.СхемыКомплексныхПроцессов") Тогда
		
			РеквизитыСхемы = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
				Схема, "ПараметрыДействий, ЭлементыСхемы, ПредшественникиЭлементовСхемы");
		
			СтруктураДляРасчета.ПараметрыДействий = РеквизитыСхемы.ПараметрыДействий.Выгрузить();
			СтруктураДляРасчета.ПредшественникиЭлементовСхемы = РеквизитыСхемы.ПредшественникиЭлементовСхемы.Выгрузить();
			СтруктураДляРасчета.ЭлементыСхемы = РеквизитыСхемы.ЭлементыСхемы.Выгрузить();
			
		Иначе
			
			СтруктураДляРасчета.ПараметрыДействий = Схема.ПараметрыДействий.Выгрузить();
			СтруктураДляРасчета.ПредшественникиЭлементовСхемы = Схема.ПредшественникиЭлементовСхемы.Выгрузить();
			СтруктураДляРасчета.ЭлементыСхемы = Схема.ЭлементыСхемы.Выгрузить();
			
		КонецЕсли;
		
		Если ТипСхемы <> Тип("ДанныеФормыСтруктура") Тогда
			
			МассивТипов = Новый Массив;
			МассивТипов.Добавить(Тип("Дата"));
			ОписаниеТиповДата = Новый ОписаниеТипов(МассивТипов);
			СтруктураДляРасчета.ПараметрыДействий.Колонки.Добавить("СрокИсполненияПроцесса", ОписаниеТиповДата);
			
			МассивТипов = Новый Массив;
			МассивТипов.Добавить(Тип("ПеречислениеСсылка.СостоянияБизнесПроцессов"));
			ОписаниеТиповСостоянияБизнесПроцессов = Новый ОписаниеТипов(МассивТипов);
			СтруктураДляРасчета.ПараметрыДействий.Колонки.Добавить(
				"СостояниеПроцесса", ОписаниеТиповСостоянияБизнесПроцессов);
			
			КэшДанныхДействий = 
				РаботаСКомплекснымиБизнесПроцессамиСервер.КэшДанныхДействий(СтруктураДляРасчета.ПараметрыДействий);
			
			СрокиИсполненияПроцессовКлиентСерверКОРП.
				ЗаполнитьСрокиВПараметрахДействийСхемыКомплексногоПроцесса(
					СтруктураДляРасчета.ПараметрыДействий,
					СтруктураДляРасчета.ЭлементыСхемы,
					КэшДанныхДействий);
			
		КонецЕсли;
		
		СтруктураДляРасчета.ПараметрыДействий.Сортировать("ПорядокСортировки");
		
	КонецЕсли;
	
	Возврат СтруктураДляРасчета;
	
КонецФункции

// Возвращает данные для расчета срока процесса КомплексныйПроцесс.
//
// Параметры:
//  Объект - Структура - см. СтруктураДляРасчетаСрокаКомплексногоПроцесса
//  Параметры - Структура - см. СрокиИсполненияПроцессов.ПараметрыДляРасчетаСроков
//  ИдентификаторыИзмененныхДействий - Массив -  массив идентификаторов измененных действий комплексного процесса/шаблона.
//   * УникальныйИдентификатор, Строка.
//
// Возвращаемое значение:
//  Структура - см. СрокиИсполненияПроцессов.ДанныеДляРасчетаСроков
//
Функция ДанныеДляРасчетаСроковКомплексногоПроцесса(Объект, Параметры,
	ИдентификаторыИзмененныхДействий = Неопределено, ПроцессыДляПрерывания = Неопределено) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ДатаОтсчета = Параметры.ДатаОтсчета;
	Смещение = Параметры.Смещение;
	
	// Данные для расчета сроков (возвращаемые данные).
	Сроки = СрокиИсполненияПроцессов.ПустаяТаблицаСроков();
	Предшественники = СрокиИсполненияПроцессов.ПустаяТаблицаПредшественников();
	ИдентификаторыСроков = Новый Соответствие;
	
	// Добавляем дату отсчета.
	СрокиИсполненияПроцессов.ДобавитьДатуОтсчетаВДанныеДляРасчета(
		Сроки, Предшественники, ИдентификаторыСроков, Параметры, Новый Массив);
	
	Если Объект.ПараметрыДействий <> Неопределено Тогда
		
		// Сформируем кэш строк элементов схемы.
		КэшЭлементовСхемы = Новый Соответствие;
		Для Каждого СтрокаТаблицы Из Объект.ЭлементыСхемы Цикл
			КэшЭлементовСхемы.Вставить(СтрокаТаблицы.Имя, СтрокаТаблицы);
		КонецЦикла;
		
		ИдентификаторОбъекта = Строка(Объект.ИдентификаторСсылки) + "_";
		
		Если ПроцессыДляПрерывания = Неопределено Тогда
			ПроцессыДляПрерывания = Новый СписокЗначений;
		КонецЕсли;
		
		// Добавим действия схемы в таблицу сроков.
		Для Каждого ПараметрыДействия Из Объект.ПараметрыДействий Цикл
			
			СтрокаСрок = Сроки.Добавить();
			СтрокаСрок.IDСрока = ИдентификаторОбъекта + ПараметрыДействия.Имя;
			
			ПараметрыЭлемента = КэшЭлементовСхемы.Получить(ПараметрыДействия.Имя);
			
			Действие = РаботаСКомплекснымиБизнесПроцессамиКлиентСервер.СсылкаНаДействиеСхемы(
				ПараметрыДействия,
				ПараметрыЭлемента,
				ПараметрыДействия.СостояниеПроцесса,
				ПроцессыДляПрерывания);
			
			СтрокаСрок.ПодчиненныеСроки = ДанныеДляРасчетаСрокаДействияСоставногоПроцесса(
				Действие, ИдентификаторыИзмененныхДействий);
			
			СтрокаСрок.СрокИсполнения = ПараметрыДействия.СрокИсполненияПроцесса;
			
			СтрокаСрок.Пройден = КэшЭлементовСхемы.Получить(ПараметрыДействия.Имя).Пройден;
			
		КонецЦикла;
		
		// Получим возможные пути КП.
		СтрокаЭлементаСтарта = Объект.ЭлементыСхемы.Найти(
			Перечисления.ТипыЭлементовСхемыКомплексногоПроцесса.Старт, "Тип");
		
		ИмяЭлементаСтарта = "";
		Если СтрокаЭлементаСтарта <> Неопределено Тогда
			ИмяЭлементаСтарта = СтрокаЭлементаСтарта.Имя;
		КонецЕсли;
		
		ПутиСхемы = РаботаСКомплекснымиБизнесПроцессамиКлиентСервер.ПутиСхемыКомплексногоПроцесса(
			ИмяЭлементаСтарта, Объект.ПредшественникиЭлементовСхемы);
		
		// Удалим из путей все элементы кроме действий, т.к. только они влияют на рассчет сроков.
		СрокиИсполненияПроцессовКлиентСерверКОРП.ВыделитьПоследовательностиДействийВПутях(
			ПутиСхемы, Объект.ПараметрыДействий);
		
		// Заполним таблицу предшественников сроков.
		Для Каждого Путь Из ПутиСхемы Цикл
			
			// Каждый путь начинается от старта.
			// И сроком исполнения предшественника для первого действия является
			// дата отсчета.
			ПредшественникЭлемента = УникальныйИдентификаторПустой();
			
			Для Каждого ЭлементПути Из Путь Цикл
				
				СтрПредшественник = Предшественники.Добавить();
				СтрПредшественник.Последователь = ИдентификаторОбъекта + ЭлементПути;
				СтрПредшественник.Предшественник = ПредшественникЭлемента;
				
				ПредшественникЭлемента = СтрПредшественник.Последователь;
				
			КонецЦикла;
			
		КонецЦикла;
		
		Предшественники.Свернуть("Последователь, Предшественник");
		
		// Заполним реквизит Пройден в таблице Сроки.
		Если ЗначениеЗаполнено(ИдентификаторыИзмененныхДействий) Тогда
			
			ПройденныеДействия = Новый Соответствие;
			
			Для Каждого ИдентификаторИзмененныхДействий Из ИдентификаторыИзмененныхДействий Цикл
				
				Если ТипЗнч(ИдентификаторИзмененныхДействий) <> Тип("Строка") Тогда
					Продолжить;
				КонецЕсли;
				
				ИдентификаторыОбъектаИДействия = СтрРазделить(ИдентификаторИзмененныхДействий, "_");
				ИдентификаторДействия = ИдентификаторыОбъектаИДействия[1];
				
				Для Каждого Путь Из ПутиСхемы Цикл
					Если Путь.Найти(ИдентификаторДействия) = Неопределено Тогда
						Продолжить;
					КонецЕсли;
					
					Для Каждого ЭлементПути Из Путь Цикл
						Если ЭлементПути = ИдентификаторДействия Тогда
							Прервать;
						КонецЕсли;
						ПройденныеДействия.Вставить(ИдентификаторИзмененныхДействий, Истина);
					КонецЦикла;
					
				КонецЦикла;
				
			КонецЦикла;
			
			Для Каждого СтрокаСрок Из Сроки Цикл
				Если ПройденныеДействия.Получить(СтрокаСрок.IDСрока) = Истина Тогда
					СтрокаСрок.Пройден = Истина;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
	Иначе
		
		// Добавим этапы в таблицу сроков
		Для Каждого СтрЭтап Из Объект.Этапы Цикл
			
			СтрокаСрок = Сроки.Добавить();
			СтрокаСрок.IDСрока = СтрЭтап.ИдентификаторЭтапа;
			
			ДействиеЭтапа = ДействиеЭтапа(СтрЭтап);
			
			СтрокаСрок.ПодчиненныеСроки = ДанныеДляРасчетаСрокаДействияСоставногоПроцесса(
				ДействиеЭтапа, ИдентификаторыИзмененныхДействий);
			
			СтрокаСрок.СрокИсполнения = СтрЭтап.СрокИсполненияПроцесса;
			
		КонецЦикла;
		
		// Заполним таблицу предшественников
		Для Каждого СтрПредшественникЭтапов Из Объект.ПредшественникиЭтапов Цикл
			
			СтрПредшественник = Предшественники.Добавить();
			СтрПредшественник.Последователь = СтрПредшественникЭтапов.ИдентификаторПоследователя;
			СтрПредшественник.Предшественник = СтрПредшественникЭтапов.ИдентификаторПредшественника;
			
		КонецЦикла;
		
		// Получим возможные пути КП и строки предшественников
		// замыкающие цикл.
		ПараметрыПутей = СрокиИсполненияПроцессовКлиентСерверКОРП.ПараметрыПутей(
			ОбщегоНазначения.ТаблицаЗначенийВМассив(Предшественники));
		
		Пути = ПараметрыПутей.Пути;
		СтрокиПредшественниковЗамыкающиеЦикл = ПараметрыПутей.СтрокиПредшественниковЗамыкающиеЦикл;
		
		// Заполним реквизит Пройден в таблице Сроки
		Если ЗначениеЗаполнено(ИдентификаторыИзмененныхДействий) Тогда
			
			ПройденныеДействия = Новый Соответствие;
			
			Для Каждого ИдентификаторИзмененногоДействия Из ИдентификаторыИзмененныхДействий Цикл
				Для Каждого Путь Из Пути Цикл
					Если Путь.Найти(ИдентификаторИзмененногоДействия) = Неопределено Тогда
						Продолжить;
					КонецЕсли;
					Для Каждого Действие Из Путь Цикл
						Если Действие = ИдентификаторИзмененногоДействия Тогда
							Прервать;
						КонецЕсли;
						ПройденныеДействия.Вставить(Действие, Истина);
					КонецЦикла;
				КонецЦикла;
			КонецЦикла;
			
			Для Каждого СтрокаСрок Из Сроки Цикл
				Пройден = ПройденныеДействия.Получить(СтрокаСрок.IDСрока);
				Если Пройден = Истина Тогда
					СтрокаСрок.Пройден = Истина;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
		// Удалим строки из таблицы предшественников замыкающие циклы.
		Для Каждого Строка Из СтрокиПредшественниковЗамыкающиеЦикл Цикл
			Отбор = Новый Структура;
			Отбор.Вставить("Предшественник", Строка.Предшественник);
			Отбор.Вставить("Последователь", Строка.Последователь);
			
			НайденныеСтроки = Предшественники.НайтиСтроки(Отбор);
			
			Для Каждого СтрокаКУдалению Из НайденныеСтроки Цикл
				Предшественники.Удалить(СтрокаКУдалению);
			КонецЦикла;
		КонецЦикла;
		
	КонецЕсли;
	
	Результат = СрокиИсполненияПроцессов.ДанныеДляРасчетаСроков();
	Результат.Ссылка = Объект.Ссылка;
	Результат.ИсходныйСрокИсполнения = Объект.СрокИсполненияПроцесса;
	Результат.Сроки = Сроки;
	Результат.Предшественники = Предшественники;
	Результат.IDРассчитанногоСрок = УникальныйИдентификаторПустой();
	Результат.ИдентификаторыСроков = ИдентификаторыСроков;
	
	Возврат Результат;
	
КонецФункции

// Рассчитывает точные сроки исполнения комплексного процесса/шаблона и его действий.
// Помещает рассчитанные сроки в объекты.
//
// Параметры:
//  Объект - ДанныеФормыСтруктура - объект комплексного процесса/шаблона в картчоке.
//  Параметры - Структура - см. СрокиИсполненияПроцессов.ПараметрыДляРасчетаСроков
//  Схема - ДанныеФормыСтруктура - объект схемы в карточке комплексного процесса/шаблона.
//                                 Если параметр равен значению Неопределено, то расчет ведется по данным Объекта.
//  ИдентификаторыИзмененныхДействий - Массив -  массив идентификаторов измененных действий комплексного процесса/шаблона.
//
Процедура РассчитатьСрокиКомплексногоПроцессаВКарточке(Объект, Схема, Параметры,
	ИдентификаторыИзмененныхДействий = Неопределено,
	ПроцессыДляПрерывания = Неопределено) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ДатаОтсчета = Параметры.ДатаОтсчета;
	Смещение = Параметры.Смещение;
	
	Если Не ЗначениеЗаполнено(ДатаОтсчета) Тогда
		Возврат;
	КонецЕсли;
	
	ИспользоватьДатуИВремяВСрокахЗадач = 
		ПолучитьФункциональнуюОпцию("ИспользоватьДатуИВремяВСрокахЗадач");
		
	// Сформируем структуру для расчета сроков.
	СтруктураДляРасчета = СтруктураДляРасчетаСрокаКомплексногоПроцесса(Объект, Схема);
	
	Если ПроцессыДляПрерывания = Неопределено Тогда
		ПроцессыДляПрерывания = Новый СписокЗначений;
	КонецЕсли;
	
	// Подготовим данные для расчета сроков действий процесса.
	ДанныеДляРасчета = ДанныеДляРасчетаСроковКомплексногоПроцесса(
		СтруктураДляРасчета, Параметры, ИдентификаторыИзмененныхДействий, ПроцессыДляПрерывания);
	
	// Рассчитаем сроки действий процесса.
	СрокиИсполненияПроцессов.РассчитатьСрокиИсполнения(
		ДанныеДляРасчета.Сроки, ДанныеДляРасчета.Предшественники,
		ДанныеДляРасчета.IDРассчитанногоСрок);
	
	ЗаполнитьСрокиКомплексногоПроцессаВКарточке(
		Объект, Схема, ДанныеДляРасчета.Сроки, ДанныеДляРасчета.ИдентификаторыСроков);
	
КонецПроцедуры

// Заполняет рассчитанные сроки в карточке комплексного процесса/шаблона.
//
// Параметры:
//  Объект - ДанныеФормыСтруктура - объект комплексного процесса/шаблона в карточке.
//  Схема - ДанныеФормыСтруктура - объект схемы комплексного процесса/шаблона в карточке.
//  Сроки - ТаблицаЗначений - таблица с рассчитами сроками. см. СрокиИсполненияПроцессов.ПустаяТаблицаСроков.
//  ИдентификаторыСроков - Соответствие - соответствие идентификаторов сроков и их предназначением в процессе.
//
Процедура ЗаполнитьСрокиКомплексногоПроцессаВКарточке(Объект, Схема, Сроки, ИдентификаторыСроков) Экспорт
	
	ЕстьДействияСНеРассчитаннымиСроками = Ложь;
	
	СрокИсполненияВсехДействий = Дата(1,1,1);
	
	// Заполним сроки.
	Если Схема = Неопределено Тогда
		
		// Сформируем кэш таблицы этапов.
		КэшТаблицыЭтапов = Новый Соответствие;
		Для Каждого СтрокаТаблицы Из Объект.Этапы Цикл
			КэшТаблицыЭтапов.Вставить(СтрокаТаблицы.ИдентификаторЭтапа, СтрокаТаблицы);
		КонецЦикла;
		
		// Заполним сроки этапов.
		Для Каждого СтрокаСрок Из Сроки Цикл
			
			РольСрокаВПроцесса = ИдентификаторыСроков.Получить(СтрокаСрок.IDСрока);
			Если РольСрокаВПроцесса = "ДатаОтсчета" Тогда
				Продолжить;
			КонецЕсли;
			
			СтрокаЭтапа = КэшТаблицыЭтапов.Получить(СтрокаСрок.IDСрока);
			СтрокаЭтапа.СрокИсполненияПроцесса = СтрокаСрок.СрокИсполнения;
			
			Если Не ЗначениеЗаполнено(СтрокаСрок.СрокИсполнения) Тогда
				ЕстьДействияСНеРассчитаннымиСроками = Истина;
			КонецЕсли;
			
			Если Не ЕстьДействияСНеРассчитаннымиСроками Тогда
				СрокИсполненияВсехДействий = Макс(СрокИсполненияВсехДействий, СтрокаСрок.СрокИсполнения);
			КонецЕсли;
			
		КонецЦикла;
		
	Иначе
		
		ИдентификаторОбъекта = Строка(Объект.ИдентификаторСсылки) + "_";
		
		// Сформируем кэш параметров действий.
		КэшПараметровДействий = Новый Соответствие;
		Для Каждого СтрокаТаблицы Из Схема.ПараметрыДействий Цикл
			КэшПараметровДействий.Вставить(СтрокаТаблицы.Имя, СтрокаТаблицы);
		КонецЦикла;
		
		// Заполним сроки этапов.
		Для Каждого СтрокаСрок Из Сроки Цикл
			
			РольСрокаВПроцесса = ИдентификаторыСроков.Получить(СтрокаСрок.IDСрока);
			Если РольСрокаВПроцесса = "ДатаОтсчета" Тогда
				Продолжить;
			КонецЕсли;
			
			ИмяДействия = СтрЗаменить(СтрокаСрок.IDСрока, ИдентификаторОбъекта, "");
			
			СтрокаДействия = КэшПараметровДействий.Получить(ИмяДействия);
			СтрокаДействия.СрокИсполненияПроцесса = СтрокаСрок.СрокИсполнения;
			
			Если Не ЗначениеЗаполнено(СтрокаСрок.СрокИсполнения) Тогда
				ЕстьДействияСНеРассчитаннымиСроками = Истина;
			КонецЕсли;
			
			Если Не ЕстьДействияСНеРассчитаннымиСроками Тогда
				СрокИсполненияВсехДействий = Макс(СрокИсполненияВсехДействий, СтрокаСрок.СрокИсполнения);
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	// Заполним общий срок процесса.
	Если ЕстьДействияСНеРассчитаннымиСроками Тогда
		Объект.СрокИсполненияПроцесса = Дата(1,1,1);
	Иначе
		Объект.СрокИсполненияПроцесса = СрокИсполненияВсехДействий;
	КонецЕсли;
	
КонецПроцедуры

// Производит расчет и заполнение сроков в процессе/шаблоне.
// При расчете перезаписываются сроки подчиненных действий.
//
// Параметры:
//  Процесс - БизнесПроцессОбъект.КомплексныйПроцесс,
//            СправочникОбъект.ШаблоныКомплексныхБизнесПроцессов - шаблон/процесс.
//  Параметры - Структура - см. СрокиИсполненияПроцессов.ПараметрыДляРасчетаСроков
//
Процедура РассчитатьСрокиКомплексногоПроцесса(Объект, Параметры) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ДатаОтсчета = Параметры.ДатаОтсчета;
	Смещение = Параметры.Смещение;
	
	Если Не ЗначениеЗаполнено(ДатаОтсчета) Тогда
		Возврат;
	КонецЕсли;
	
	ИспользоватьДатуИВремяВСрокахЗадач = 
		ПолучитьФункциональнуюОпцию("ИспользоватьДатуИВремяВСрокахЗадач");
		
	СтруктураДляРасчета = СтруктураДляРасчетаСрокаКомплексногоПроцесса(Объект);
	
	// Подготовим данные для расчета сроков действий процесса.
	ДанныеДляРасчета = ДанныеДляРасчетаСроковКомплексногоПроцесса(СтруктураДляРасчета, Параметры);
	
	// Рассчитаем сроки действий процесса.
	СрокиИсполненияПроцессов.РассчитатьСрокиИсполнения(
		ДанныеДляРасчета.Сроки, ДанныеДляРасчета.Предшественники,
		ДанныеДляРасчета.IDРассчитанногоСрок);
	
	// Заполнить сроки в процессе
	ЗаполнитьСрокиКомплексногоПроцесса(
		Объект,
		ДанныеДляРасчета.Сроки,
		ДанныеДляРасчета.ИдентификаторыСроков);
	
КонецПроцедуры

// Заполняет сроки в объекте комплексного процесса/шаблона по таблице с рассчитанными сроками.
// При заполнении перезаписываются сроки подчиненных действий.
//
// Параметры:
//  Объект - БизнесПроцессОбъект.КомплексныйПроцесс,
//            СправочникОбъект.ШаблоныКомплексныхБизнесПроцессов - шаблон/процесс.
//  Сроки - ТаблицаЗначений - таблица с рассчитами сроками. см. СрокиИсполненияПроцессов.ПустаяТаблицаСроков.
//  ИдентификаторыСроков - Соответствие - соответствие идентификаторов сроков и их предназначением
//                                       в процессе.
//
Процедура ЗаполнитьСрокиКомплексногоПроцесса(Объект, Сроки, ИдентификаторыСроков) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	СрокИсполненияВсехЭтапов = Дата(1,1,1);
	
	ЕстьДействияСНеРассчитаннымиСроками = Ложь;
	
	СрокИсполненияВсехДействий = Дата(1,1,1);
	
	Если ЗначениеЗаполнено(Объект.Схема) Тогда
		
		ИдентификаторОбъекта = Строка(Объект.ИдентификаторСсылки) + "_";
		
		ПараметрыДействий = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(
			Объект.Схема, "ПараметрыДействий").Выгрузить();
		
		// Сформируем кэш параметров действий.
		КэшПараметровДействий = Новый Соответствие;
		Для Каждого СтрокаТаблицы Из ПараметрыДействий Цикл
			КэшПараметровДействий.Вставить(СтрокаТаблицы.Имя, СтрокаТаблицы);
		КонецЦикла;
		
		Для Каждого СтрокаСрок Из Сроки Цикл
			
			РольСрокаВПроцесса = ИдентификаторыСроков.Получить(СтрокаСрок.IDСрока);
			Если РольСрокаВПроцесса = "ДатаОтсчета" Тогда
				Продолжить;
			КонецЕсли;
			
			Если СтрокаСрок.ПодчиненныеСроки.Свойство("Сроки")
				И СтрокаСрок.ПодчиненныеСроки.Свойство("ИдентификаторыСроков")
				И СтрокаСрок.Изменен Тогда
				
				Действие = СтрокаСрок.ПодчиненныеСроки.Ссылка;
				
				Если ЗначениеЗаполнено(Действие) Тогда
					
					ЗаблокироватьДанныеДляРедактирования(Действие);
					
					ДействиеОбъект = Действие.ПолучитьОбъект();
					
					ПереносСроковВыполненияЗадач.ПередатьПричинуИЗаявкуНаПереносаСрока(
						ДействиеОбъект, Объект.ДополнительныеСвойства);
					
					ЗаполнитьСрокиДействияКомплексногоПроцесса(
						ДействиеОбъект,
						СтрокаСрок.ПодчиненныеСроки.Сроки,
						СтрокаСрок.ПодчиненныеСроки.ИдентификаторыСроков);
					
					Если ШаблоныБизнесПроцессовКлиентСервер.ЭтоШаблонПроцесса(Действие) Тогда
						ШаблоныБизнесПроцессов.ЗаписатьШаблон(ДействиеОбъект, "ПростаяЗапись");
					Иначе
						РаботаСБизнесПроцессами.ЗаписатьПроцесс(ДействиеОбъект, "ЗаписьСОбновлениемОбщегоСпискаПроцессов");
					КонецЕсли;
					
					ПротоколированиеРаботыПользователей.ЗаписатьИзменение(ДействиеОбъект.Ссылка);
					
					РазблокироватьДанныеДляРедактирования(Действие);
					
				КонецЕсли;
				
			КонецЕсли;
			
			Если Не ЗначениеЗаполнено(СтрокаСрок.СрокИсполнения) Тогда
				ЕстьДействияСНеРассчитаннымиСроками = Истина;
			КонецЕсли;
			
			Если Не ЕстьДействияСНеРассчитаннымиСроками Тогда
				СрокИсполненияВсехДействий = Макс(СрокИсполненияВсехДействий, СтрокаСрок.СрокИсполнения);
			КонецЕсли;
			
		КонецЦикла;
		
	Иначе
		
		// Сформируем кэш таблицы этапов.
		КэшТаблицыЭтапов = Новый Соответствие;
		Для Каждого СтрокаТаблицы Из Объект.Этапы Цикл
			КэшТаблицыЭтапов.Вставить(СтрокаТаблицы.ИдентификаторЭтапа, СтрокаТаблицы);
		КонецЦикла;
		
		Для Каждого СтрокаСрок Из Сроки Цикл
			
			РольСрокаВПроцесса = ИдентификаторыСроков.Получить(СтрокаСрок.IDСрока);
			Если РольСрокаВПроцесса = "ДатаОтсчета" Тогда
				Продолжить;
			КонецЕсли;
			
			Если СтрокаСрок.ПодчиненныеСроки.Свойство("Сроки")
				И СтрокаСрок.ПодчиненныеСроки.Свойство("ИдентификаторыСроков")
				И СтрокаСрок.Изменен Тогда
				
				СтрокаЭтапа = КэшТаблицыЭтапов.Получить(СтрокаСрок.IDСрока);
				
				Действие = ДействиеЭтапа(СтрокаЭтапа);
				
				ЗаблокироватьДанныеДляРедактирования(Действие);
				
				ДействиеОбъект = Действие.ПолучитьОбъект();
				
				ПереносСроковВыполненияЗадач.ПередатьПричинуИЗаявкуНаПереносаСрока(
					ДействиеОбъект, Объект.ДополнительныеСвойства);
				
				ЗаполнитьСрокиДействияКомплексногоПроцесса(
					ДействиеОбъект,
					СтрокаСрок.ПодчиненныеСроки.Сроки,
					СтрокаСрок.ПодчиненныеСроки.ИдентификаторыСроков);
				
				Если ШаблоныБизнесПроцессовКлиентСервер.ЭтоШаблонПроцесса(Действие) Тогда
					ШаблоныБизнесПроцессов.ЗаписатьШаблон(ДействиеОбъект, "ПростаяЗапись");
				Иначе
					РаботаСБизнесПроцессами.ЗаписатьПроцесс(ДействиеОбъект, "ЗаписьСОбновлениемОбщегоСпискаПроцессов");
				КонецЕсли;
				
				ПротоколированиеРаботыПользователей.ЗаписатьИзменение(ДействиеОбъект.Ссылка);
				
				РазблокироватьДанныеДляРедактирования(Действие);
				
			КонецЕсли;
			
			Если Не ЗначениеЗаполнено(СтрокаСрок.СрокИсполнения) Тогда
				ЕстьДействияСНеРассчитаннымиСроками = Истина;
			КонецЕсли;
			
			Если Не ЕстьДействияСНеРассчитаннымиСроками Тогда
				СрокИсполненияВсехДействий = Макс(СрокИсполненияВсехДействий, СтрокаСрок.СрокИсполнения);
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	// Заполним общий срок процесса.
	Если ЕстьДействияСНеРассчитаннымиСроками Тогда
		Объект.СрокИсполненияПроцесса = Дата(1,1,1);
	Иначе
		Объект.СрокИсполненияПроцесса = СрокИсполненияВсехДействий;
	КонецЕсли;
	
КонецПроцедуры

// Рассчитывает точные сроки комплексного процесса по данным схемы в карточке процесса/шаблона.
// Рассчитанный результата сохраняет во временном хранилище.
// Используется для расчета сроков по команде в карточке процесса/шаблона.
//
// Параметры:
//  УправляемаяФорма - карточка процесса/шаблона со схемой.
//
// Возвращаемое значение:
//  СрокиПроцессовИзменены - Булево - признак изменения сроков процессов действий.
//  АдресХранилищаСРассчитаннымиСроками - Строка - адрес с результатом расчета во
//                                        временном хранилище.
//
Функция РассчитатьТочныеСрокиКомплексногоПроцессаПоСхеме(Форма) Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("СрокиПроцессовИзменены", Ложь);
	Результат.Вставить("АдресХранилищаСРассчитаннымиСроками", "");
	
	УстановитьПривилегированныйРежим(Истина);
	
	ПараметрыДляРасчетаСроков = СрокиИсполненияПроцессов.ПараметрыДляРасчетаСроков();
	ПараметрыДляРасчетаСроков.ДатаОтсчета = Форма.ДатаОтсчетаДляРасчетаСроков;
	
	// Сформируем структуру для расчета сроков.
	СтруктураДляРасчета = СрокиИсполненияПроцессовКОРП.СтруктураДляРасчетаСрокаКомплексногоПроцесса(
		Форма.Объект, Форма.СхемаКомплексногоПроцесса);
	
	// Подготовим данные для расчета сроков действий процесса.
	СтруктураПоиска = Новый Структура;
	СтруктураПоиска.Вставить("ПроцессыСхемыДляПрерывания", Неопределено);
	ЗаполнитьЗначенияСвойств(СтруктураПоиска, Форма);
	
	ДанныеДляРасчета = СрокиИсполненияПроцессовКОРП.ДанныеДляРасчетаСроковКомплексногоПроцесса(
		СтруктураДляРасчета, ПараметрыДляРасчетаСроков,, СтруктураПоиска.ПроцессыСхемыДляПрерывания);
	
	// Рассчитаем сроки действий процесса.
	СрокиИсполненияПроцессов.РассчитатьСрокиИсполнения(
		ДанныеДляРасчета.Сроки, ДанныеДляРасчета.Предшественники,
		ДанныеДляРасчета.IDРассчитанногоСрок);
	
	// Помещаем результат рассчета во временное хранилище.
	Результат.АдресХранилищаСРассчитаннымиСроками = 
		ПоместитьВоВременноеХранилище(ДанныеДляРасчета, Форма.УникальныйИдентификатор);
		
	// Определяем Факт изменения сроков процессов при расчете.
	Для Каждого СтрокаТаблицы Из ДанныеДляРасчета.Сроки Цикл
		
		Если Не СтрокаТаблицы.Изменен Тогда
			Продолжить;
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(СтрокаТаблицы.ПодчиненныеСроки) Тогда
			Продолжить;
		КонецЕсли;
		
		Если ШаблоныБизнесПроцессовКлиентСервер.ЭтоШаблонПроцесса(
			СтрокаТаблицы.ПодчиненныеСроки.Ссылка) Тогда
			
			Продолжить;
		КонецЕсли;
		
		Результат.СрокиПроцессовИзменены = Истина;
		
		Прервать;
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

// Записывает рассчитанные точные сроки комплексного процесса по схеме.
// Сроки записываются в подчиненные действия.
// Вызывается перед записью карточки шаблона или процесса.
//
// Параметры:
//  Форма - УправляемаяФорма - карточка процесса/шаблона.
//  ПроцессОбъект - СправочникОбъект.ШаблоныКомплексныхБизнесПроцессов,
//                  БизнесПроцессОбъект.КомплексныйПроцесс - объект процесса/шаблона.
//  ПараметрыЗаписи - Структура - стандартный параметр обработчика ПередЗаписьюНаСервере.
//
Процедура ЗаписатьРассчитанныеТочныеСрокиКомплексногоПроцессаПоСхеме(
	Форма, ПроцессОбъект, ПараметрыЗаписи) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если Не Форма.ИспользоватьСхемуПроцесса Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ПараметрыЗаписи.Свойство("ЗаписьТочныхСроковПоСхеме") Тогда
		Возврат;
	КонецЕсли;
	
	РассчитанныеДанные = ПолучитьИзВременногоХранилища(
		ПараметрыЗаписи.АдресХранилищаСРассчитаннымиСроками);
	
	ПереносСроковВыполненияЗадач.ПередатьПричинуИЗаявкуНаПереносаСрока(
		ПроцессОбъект, ПараметрыЗаписи);
	
	ЗаполнитьСрокиКомплексногоПроцесса(
		ПроцессОбъект, РассчитанныеДанные.Сроки, РассчитанныеДанные.ИдентификаторыСроков);
	Форма.Объект.СрокИсполненияПроцесса = ПроцессОбъект.СрокИсполненияПроцесса;
		
	СхемаОбъект = ПроцессОбъект.Схема.ПолучитьОбъект();
	Форма.КэшДанныхДействий = 
		РаботаСКомплекснымиБизнесПроцессамиСервер.КэшДанныхДействий(СхемаОбъект.ПараметрыДействий);
	
	Форма.ОбновитьСрокиИсполненияНаСервере();
	
КонецПроцедуры

// Приглашение

// Возвращает структура для расчета срока процесса/шаблона Приглашение.
//
// Параметры:
//  СсылкаНаПроцесс - БизнесПроцессОбъект.Приглашение,
//                    СправочникОбъект.ШаблоныПриглашения - ссылка на процесс/шаблон. Если параметр
//                    заполнен, то возвращает структура, заполненная по данным процесса/шаблона.
//
// Возвращаемое значение:
//  Структура - реквизиты и табличные части процесса/шаблона.
//   * Ссылка
//   * КоличествоИтераций
//   * Автор
//   * ВариантУстановкиСрокаОбработкиРезультатов
//   * СрокОбработкиРезультатов
//   * СрокОбработкиРезультатовДни
//   * СрокОбработкиРезультатовЧасы
//   * СрокОбработкиРезультатовМинуты
//   * Исполнители
//   * СрокИсполненияПроцесса
//
Функция СтруктураДляРасчетаСрокаПриглашения(СсылкаНаПроцесс = Неопределено) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	РеквизитыДляРасчета = "Ссылка, КоличествоИтераций, Автор,
			|ВариантУстановкиСрокаОбработкиРезультатов, СрокОбработкиРезультатов,
			|СрокОбработкиРезультатовДни, СрокОбработкиРезультатовЧасы,
			|СрокОбработкиРезультатовМинуты, Исполнители, СрокИсполненияПроцесса";
	
	Если ЗначениеЗаполнено(СсылкаНаПроцесс) Тогда
		СтруктураДляРасчета = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(СсылкаНаПроцесс, РеквизитыДляРасчета);
		СтруктураДляРасчета.Исполнители = СтруктураДляРасчета.Исполнители.Выгрузить();
	Иначе
		СтруктураДляРасчета = Новый Структура(РеквизитыДляРасчета);
	КонецЕсли;
	
	Возврат СтруктураДляРасчета;
	
КонецФункции

// Возвращает данные для расчета срока процесса Приглашение.
//
// Параметры:
//  Процесс - БизнесПроцессОбъект.Приглашение,
//            СправочникОбъект.ШаблоныПриглашения - шаблон/процесс.
//            Структура - см. СтруктураДляРасчетаСрокаПриглашения
//  Параметры - Структура - см. СрокиИсполненияПроцессов.ПараметрыДляРасчетаСроков
//
// Возвращаемое значение:
//  Структура - см. СрокиИсполненияПроцессов.ДанныеДляРасчетаСроков
//
Функция ДанныеДляРасчетаСроковПриглашения(Процесс, Параметры) Экспорт
	
	ДатаОтсчета = Параметры.ДатаОтсчета;
	Смещение = Параметры.Смещение;
	РеквизитТаблицаСИзмененнымСроком = Параметры.РеквизитТаблицаСИзмененнымСроком;
	ИндексСтроки = Параметры.ИндексСтроки;
	ТекущаяИтерация = Параметры.ТекущаяИтерация;
	
	// Данные для расчета сроков (возвращаемые данные).
	Сроки = СрокиИсполненияПроцессов.ПустаяТаблицаСроков();
	Предшественники = СрокиИсполненияПроцессов.ПустаяТаблицаПредшественников();
	ИдентификаторыСроков = Новый Соответствие;
	
	// Определим изменение сроков
	ИзмененныеСрокиПроцесса = СрокиИсполненияПроцессов.ИзмененныеСрокиПроцесса(Процесс, Параметры);
	ИндексИзменнойСтроки = ИзмененныеСрокиПроцесса.ИндексИзменнойСтроки;
	СрокАвтораИзменен = ИзмененныеСрокиПроцесса.СрокАвтораИзменен;
	
	// Переменные для формирования таблиц предшественников.
	ПредшественникиТекущегоЭтапа = Новый Массив;
	ПредшественникиСледующегоЭтапа = Новый Массив;
	
	// Добавляем дату отсчета.
	СрокиИсполненияПроцессов.ДобавитьДатуОтсчетаВДанныеДляРасчета(
		Сроки, Предшественники, ИдентификаторыСроков,
		Параметры, ПредшественникиСледующегоЭтапа);
		
	// Определим количество итераций и номер текущей итерации.
	Если СрокиИсполненияПроцессов.ВПроцессеЕстьТочныеСрокиИсполнения(Процесс) Тогда
		КоличествоИтераций = 1;
		ТекущаяИтерация = 1;
	Иначе
		КоличествоИтераций = Процесс.КоличествоИтераций;
	КонецЕсли;
	
	ИдентификаторыСроковДляТекИтерации = ИдентификаторыСроков;
	ИндексИзменнойСтрокиДляТекИтерации = ИндексИзменнойСтроки;
	СрокАвтораИзмененДляТекИтерации = СрокАвтораИзменен;
	
	КоличествоИсполнителей = Процесс.Исполнители.Количество();
	
	Пока ТекущаяИтерация <= КоличествоИтераций Цикл
		
		// Добавляем таблицу исполнителей
		СрокиИсполненияПроцессов.ДобавитьСрокиТаблицыИсполнителиПроцессаВДанныеДляРасчета(
			Сроки, Предшественники,
			Процесс.Исполнители,
			ИндексИзменнойСтрокиДляТекИтерации,
			ПредшественникиСледующегоЭтапа,
			ИдентификаторыСроковДляТекИтерации);
		
		Если КоличествоИсполнителей > 0 Тогда
			// Добавляем срок проверяющего
			
			СрокиИсполненияПроцессов.ДобавитьСрокАвтораВДанныеДляРасчета(
				Сроки, Предшественники,
				Процесс,
				СрокАвтораИзмененДляТекИтерации,
				ПредшественникиСледующегоЭтапа,
				ИдентификаторыСроковДляТекИтерации,
				"СрокАвтора");
		КонецЕсли;
		
		ТекущаяИтерация = ТекущаяИтерация + 1;
		
		ИдентификаторыСроковДляТекИтерации = Неопределено;
		ИндексИзменнойСтрокиДляТекИтерации = -1;
		СрокАвтораИзмененДляТекИтерации = Истина;
	КонецЦикла;
	
	IDПоследнегоРассчитанногоСрока = 
		СрокиИсполненияПроцессов.IDПоследнегоРассчитанногоСрока(Сроки, Предшественники);
		
	Результат = СрокиИсполненияПроцессов.ДанныеДляРасчетаСроков();
	Результат.Ссылка = Процесс.Ссылка;
	Результат.ИсходныйСрокИсполнения = Процесс.СрокИсполненияПроцесса;
	Результат.Сроки = Сроки;
	Результат.Предшественники = Предшественники;
	Результат.IDРассчитанногоСрок = IDПоследнегоРассчитанногоСрока;
	Результат.ИдентификаторыСроков = ИдентификаторыСроков;
	
	Возврат Результат;
	
КонецФункции

// Производит расчет и заполнение сроков в процессе/шаблоне/структуре процесса.
//
// Параметры:
//  Процесс - БизнесПроцессОбъект.Приглашение,
//            СправочникОбъект.ШаблоныПриглашения - шаблон/процесс.
//            Структура - см. СтруктураДляРасчетаСрокаПриглашения
//  Параметры - Структура - см. СрокиИсполненияПроцессов.ПараметрыДляРасчетаСроков
//
Процедура РассчитатьСрокиПриглашения(Процесс, Параметры) Экспорт
	
	Если Не ЗначениеЗаполнено(Параметры.ДатаОтсчета) Тогда
		Возврат;
	КонецЕсли;
	
	// Подготовим данные для рассчета сроков участников процесса.
	ДанныеДляРассчета = ДанныеДляРасчетаСроковПриглашения(Процесс, Параметры);
	
	// Рассчитаем сроки участников процессов.
	РассчитатьСрокиИсполнения(
		ДанныеДляРассчета.Сроки, ДанныеДляРассчета.Предшественники,
		ДанныеДляРассчета.IDРассчитанногоСрок);
		
	// Заполнить сроки в процессе
	ЗаполнитьСрокиПриглашения(
		Процесс,
		ДанныеДляРассчета.Сроки,
		ДанныеДляРассчета.ИдентификаторыСроков,
		Параметры.ЗаполнятьСрокПроцессаТолькоПриПревышении);
	
КонецПроцедуры

// Заполняет сроки в процессе/шаблоне/структуре процесса по таблице с рассчитанными сроками.
//
// Параметры:
//  Процесс - БизнесПроцессОбъект.Приглашение,
//            СправочникОбъект.ШаблоныПриглашения - шаблон/процесс.
//            Структура - см. СтруктураДляРасчетаСрокаПриглашения
//  Сроки - ТаблицаЗначений - таблица с рассчитами сроками. см. СрокиИсполненияПроцессов.ПустаяТаблицаСроков.
//  ИдентификаторыСроков - Соответствие - соответствие идентификаторов сроков и их предназначением
//                                       в процессе.
//  ЗаполнятьТолькоПриПревышении - Булево - признак указывающий, что срок исполнения процесса
//                                          заполняется только, если новый рассчитанный срок больше
//                                          предыдущего.
//
Процедура ЗаполнитьСрокиПриглашения(Процесс, Сроки, ИдентификаторыСроков,
	ЗаполнятьТолькоПриПревышении = Ложь) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	СрокИсполненияПроцессаВсемиУчастниками = Дата(1,1,1);
	
	ВариантыУстановкиСрокаИсполнения = СрокиИсполненияПроцессовКлиентСервер.ВариантыУстановкиСрокаИсполнения();
	
	Для Каждого СтрокаСрок Из Сроки Цикл
		
		РольСрокаВПроцесса = ИдентификаторыСроков.Получить(СтрокаСрок.IDСрока);
		
		Если РольСрокаВПроцесса = "ДатаОтсчета" Тогда
			Продолжить;
		ИначеЕсли РольСрокаВПроцесса = "СрокАвтора" Тогда
			Если Процесс.ВариантУстановкиСрокаОбработкиРезультатов
				= Перечисления.ВариантыУстановкиСрокаИсполнения.ТочныйСрок Тогда
			
				Процесс.СрокОбработкиРезультатовДни = СтрокаСрок.СрокИсполненияДни;
				Процесс.СрокОбработкиРезультатовЧасы = СтрокаСрок.СрокИсполненияЧасы;
				Процесс.СрокОбработкиРезультатовМинуты = СтрокаСрок.СрокИсполненияМинуты;
			Иначе
				Процесс.СрокОбработкиРезультатов = СтрокаСрок.СрокИсполнения;
			КонецЕсли;
		ИначеЕсли СтрНачинаетсяС(РольСрокаВПроцесса, "Исполнители") Тогда
			
			ИндексИсполнителя = Число(СтрЗаменить(РольСрокаВПроцесса, "Исполнители_", ""));
			
			Если Процесс.Исполнители[ИндексИсполнителя].ВариантУстановкиСрокаИсполнения
				= ВариантыУстановкиСрокаИсполнения.ТочныйСрок Тогда
				
				Процесс.Исполнители[ИндексИсполнителя].СрокИсполненияДни = СтрокаСрок.СрокИсполненияДни;
				Процесс.Исполнители[ИндексИсполнителя].СрокИсполненияЧасы = СтрокаСрок.СрокИсполненияЧасы;
				Процесс.Исполнители[ИндексИсполнителя].СрокИсполненияМинуты = СтрокаСрок.СрокИсполненияМинуты;
			Иначе
				Процесс.Исполнители[ИндексИсполнителя].СрокИсполнения = СтрокаСрок.СрокИсполнения;
			КонецЕсли;
			
		КонецЕсли;
		
		СрокИсполненияПроцессаВсемиУчастниками = 
			Макс(СрокИсполненияПроцессаВсемиУчастниками, СтрокаСрок.СрокИсполнения);
		
	КонецЦикла;
	
	Если Не СрокиИсполненияПроцессов.СрокиУчастниковПроцессаКорректны(Процесс) Тогда
		СрокИсполненияПроцессаВсемиУчастниками = Дата(1,1,1);
	КонецЕсли;
	
	Если ЗаполнятьТолькоПриПревышении Тогда
		
		Процесс.СрокИсполненияПроцесса = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(
			Процесс.Ссылка, "СрокИсполненияПроцесса");
		
		Процесс.СрокИсполненияПроцесса = 
			Макс(Процесс.СрокИсполненияПроцесса, СрокИсполненияПроцессаВсемиУчастниками);
		
	Иначе
		Процесс.СрокИсполненияПроцесса = СрокИсполненияПроцессаВсемиУчастниками;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ПрограммныйИнтерфейс_РасчетДлительностиПроцессов

// Возвращает смещение даты отсчета для расчета срока процесса/шаблона.
//
//  Процесс - СправочникСсыка,
//            БизнесПроцессСсылка, - ссылка на процесс/шаблон.
//
// Возвращаемое значение:
//  Дата - дата отложенного старта процесса, если был передан процесс.
//  Число - срок отложенного страта шаблона процесса, если был передан шаблон.
//
Функция СмещенияДатыОтсчета(Процесс) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если ШаблоныБизнесПроцессовКлиентСервер.ЭтоШаблонПроцесса(Процесс) Тогда
		СрокОтложенногоСтарта = 
			ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Процесс, "СрокОтложенногоСтарта");
		Возврат СрокОтложенногоСтарта;
	Иначе
		НастройкаСтарта = РегистрыСведений.ПроцессыДляЗапуска.ПолучитьСведенияОЗапускеПроцесса(Процесс);
		Если ЗначениеЗаполнено(НастройкаСтарта) Тогда
			Возврат НастройкаСтарта.ДатаОтложенногоСтарта;
		Иначе
			Возврат Дата(1,1,1);
		КонецЕсли;
	КонецЕсли;
	
КонецФункции

#КонецОбласти

#Область ПрограммныйИнтерфейс_ПереносСроков

// Проверяет изменение срока процесса в карточке шаблона процесса.
//
// Параметры:
//  Форма - УправляемаяФорма - карточка шаблона процесса.
//
Процедура ПроверитьИзменениеСроковВКарточкеШаблонаПроцесса(Форма) Экспорт
	
	Если Не Форма.КомплексныйПроцессСтартован Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураСроковПроцессаДляПроверки = 
		СрокиИсполненияПроцессов.СтруктураСроковПроцессаДляПроверкиИзменения();
	
	СтруктураСроковПроцессаДляПроверки.Ссылка = Форма.Объект.Ссылка;
	
	СтруктураСроковПроцессаДляПроверки.СрокИсполненияПроцесса = Форма.Объект.СрокИсполненияПроцесса;
	
	СрокиИсполненияПроцессов.ПроверитьИзменениеСроковПроцесса(
		Форма.Объект.Ссылка,
		СтруктураСроковПроцессаДляПроверки,
		Форма);
	
КонецПроцедуры

#КонецОбласти

#Область ПрограммныйИнтерфейс_КарточкиПроцессовИШаблонов

// Возвращает ширину для полей со сроком для комплексного процесса.
//
// Параметры:
//  Форма - УправляемаяФормы - форма процесса/шаблона.
//
// Возвращаемое значение:
//  Число
//
Функция ШиринаПоляСрокИсполненияДействияКомплексногоПроцесса(Форма) Экспорт
	
	ОтобразитьДатуИВремя = СрокиИсполненияПроцессовКлиентСерверКОРП.КомандаРасчетаСрокаДоступна(Форма)
		Или ЗначениеЗаполнено(Форма.ДатаОтсчетаДляРасчетаСроков);
	
	Если ШаблоныБизнесПроцессовКлиентСервер.ЭтоШаблонПроцесса(Форма.Объект.Ссылка) Тогда
		ОтобразитьДатуИВремя = ОтобразитьДатуИВремя
			И Не Форма.КомплексныйПроцессСтартован;
	Иначе
		ОтобразитьДатуИВремя = ОтобразитьДатуИВремя
			И Не Форма.Объект.Стартован;
	КонецЕсли;
	
	Если ОтобразитьДатуИВремя Тогда
		ШиринаПоляСрокЭтапа = 15;
		Если Форма.ИспользоватьДатуИВремяВСрокахЗадач Тогда
			ШиринаПоляСрокЭтапа = 25;
		КонецЕсли;
	Иначе
		ШиринаПоляСрокЭтапа = 10;
		Если Форма.ИспользоватьДатуИВремяВСрокахЗадач Тогда
			ШиринаПоляСрокЭтапа = 15;
		КонецЕсли;
	КонецЕсли;
	
	Возврат ШиринаПоляСрокЭтапа;
	
КонецФункции

/////////////////////////////////
// Условное оформление

// Возвращает соответствие запущенных действий комплексного процесса
// и их дат завершения.
//
// Параметры:
//  Процесс - БизнесПроцессСсылка.КомплексныйПроцесс - ссылка на процесс.
//
// Возвращаемое значение:
//  Соответствие
//   * Ключ - БизнесПроцессСсылка - ссылка на действие комплексного процесса.
//   * Значение - Дата - дата завершения.
//
Функция ДатыЗавершенияДействийКомплексногоПроцесса(Процесс) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ДатыЗавершенияЭтапов = Новый Соответствие;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ДанныеБизнесПроцессов.БизнесПроцесс КАК Действие,
		|	ДанныеБизнесПроцессов.ДатаЗавершения
		|ИЗ
		|	РегистрСведений.ДочерниеБизнесПроцессы КАК ДочерниеБизнесПроцессы
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ДанныеБизнесПроцессов КАК ДанныеБизнесПроцессов
		|		ПО ДочерниеБизнесПроцессы.ДочернийПроцесс = ДанныеБизнесПроцессов.БизнесПроцесс
		|ГДЕ
		|	ДочерниеБизнесПроцессы.РодительскийПроцесс = &РодительскийПроцесс";
		
	Запрос.УстановитьПараметр("РодительскийПроцесс", Процесс);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		ДатыЗавершенияЭтапов.Вставить(Выборка.Действие, Выборка.ДатаЗавершения);
	КонецЦикла;
	
	Возврат ДатыЗавершенияЭтапов;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Возвращает структуру параметров срока исполнения для определения длительности
// функцией ДлительностьПоСрокуИсполнения.
//
// Используется для переопределения одноименной функции в модуле СрокиИсполненияПроцессов.
// Вместо текущей следует использовать функцию из модуля СрокиИсполненияПроцессов.
//
// Возвращаемое значение:
//  Структура
//   * СрокИсполнения - Дата
//   * ДатаОтсчета - Дата
//   * ГрафикРаботы - СправочникСсылка.ГрафикиРаботы
//
Функция ПараметрыСрокаИсполненияДляОпределенияДлительности() Экспорт
	
	Параметры = Новый Структура;
	
	Параметры.Вставить("СрокИсполнения", Дата(1,1,1));
	Параметры.Вставить("ДатаОтсчета", Дата(1,1,1));
	Параметры.Вставить("ГрафикРаботы", Справочники.ГрафикиРаботы.ПустаяСсылка());
	
	Возврат Параметры
	
КонецФункции

// Возвращает длительность (количество дней, часов, минут) по сроку исполнения.
//
// Используется для переопределения одноименной функции в модуле СрокиИсполненияПроцессов.
// Вместо текущей следует использовать функцию из модуля СрокиИсполненияПроцессов.
//
// Параметры:
//  ПараметрыСрокаИсполнения - Структура - см. функцию ПараметрыСрокаИсполненияДляОпределенияДлительности.
//
// Возвращаемое значение:
//  Структура
//   * Дни - Число - количество дней.
//   * Часы - Число - количество часов.
//   * Минуты - Число - количество минут.
//
Функция ДлительностьПоСрокуИсполнения(ПараметрыСрокаИсполнения) Экспорт
	
	ДлительностьИсполнения = Новый Структура;
	ДлительностьИсполнения.Вставить("Дни", 0);
	ДлительностьИсполнения.Вставить("Часы", 0);
	ДлительностьИсполнения.Вставить("Минуты", 0);
	
	СрокИсполнения = ПараметрыСрокаИсполнения.СрокИсполнения;
	ДатаОтсчета = ПараметрыСрокаИсполнения.ДатаОтсчета;
	ГрафикРаботы = ПараметрыСрокаИсполнения.ГрафикРаботы;
	
	ДопПараметры = Новый Структура;
	ДопПараметры.Вставить("ГрафикРаботы", ГрафикРаботы);
	
	СрокиИсполненияПроцессов.СкорректироватьДатуОтсчета(ДатаОтсчета, ДопПараметры);
	
	Дни = 0;
	Часы = 0;
	Минуты = 0;
	
	Если ЗначениеЗаполнено(ГрафикРаботы) Тогда
		
		КоличествоРабочихЧасовВДне = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(
			ГрафикРаботы, "КоличествоРабочихЧасовВДне");
			
		ДлительностьРабочегоДня = 3600 * КоличествоРабочихЧасовВДне;
		
		ДатаНачала = ДатаОтсчета;
		ДатаОкончания = СрокИсполнения;
		
		ИнтервалыРабочегоВремени = ГрафикиРаботы.СформироватьТаблицуРабочегоВремени(
			ГрафикРаботы, ДатаНачала, ДатаОкончания);
		
		ДлительностьИсполненияВСекундах = ИнтервалыРабочегоВремени.Итог("Длительность");
		
	Иначе
		ДлительностьРабочегоДня = 86400;
		ДлительностьИсполненияВСекундах = СрокИсполнения - ДатаОтсчета;
		
		// Корректировка длительности рабочего для.
		// Нужно для корректного вычисления относительного срока.
		// Точный срок равный концу дня, то последней день должен включаться полностью.
		Если СрокИсполнения = КонецДня(СрокИсполнения) Тогда
			ДлительностьИсполненияВСекундах = ДлительностьИсполненияВСекундах + 1;
		КонецЕсли;
		
	КонецЕсли;
	
	Если ДлительностьИсполненияВСекундах > 0 Тогда
		Дни = Цел(ДлительностьИсполненияВСекундах / ДлительностьРабочегоДня);
		
		Если ПолучитьФункциональнуюОпцию("ИспользоватьДатуИВремяВСрокахЗадач") Тогда
			Часы = Цел((ДлительностьИсполненияВСекундах - Дни * ДлительностьРабочегоДня) / 3600);
			Минуты = Окр((ДлительностьИсполненияВСекундах - Дни * ДлительностьРабочегоДня - Часы * 3600) / 60);
		КонецЕсли;
	КонецЕсли;
	
	ДлительностьИсполнения.Дни = Дни;
	ДлительностьИсполнения.Часы = Часы;
	ДлительностьИсполнения.Минуты = Минуты;
	
	Возврат ДлительностьИсполнения;
	
КонецФункции

// Возвращает структуру параметров длительности исполнения для определения точного срока
// функцией СрокИсполненияПоДлительности.
//
// Используется для переопределения одноименной функции в модуле СрокиИсполненияПроцессов.
// Вместо текущей следует использовать функцию из модуля СрокиИсполненияПроцессов.
//
// Возвращаемое значение:
//  Структура
//   * Дни - Число
//   * Часы - Число
//   * Минуты - Число
//   * ДатаОтсчета - Дата
//   * ГрафикРаботы - СправочникСсылка.ГрафикиРаботы
//
Функция ПараметрыДлительностиИсполненияДляОпределенияТочногоСрока() Экспорт
	
	Параметры = Новый Структура;
	
	Параметры.Вставить("Дни", 0);
	Параметры.Вставить("Часы", 0);
	Параметры.Вставить("Минуты", 0);
	Параметры.Вставить("ДатаОтсчета", Дата(1,1,1));
	Параметры.Вставить("ГрафикРаботы", Справочники.ГрафикиРаботы.ПустаяСсылка());
	
	Возврат Параметры
	
КонецФункции

// Возвращает срок исполнения датой по длительности (срок в секундах).
//
// Используется для переопределения одноименной функции в модуле СрокиИсполненияПроцессов.
// Вместо текущей следует использовать функцию из модуля СрокиИсполненияПроцессов.
//
// Параметры:
//  ПараметрыДлительностиИсполнения - Структура - см. функцию ПараметрыДлительностиИсполненияДляОпределенияТочногоСрока.
//
// Возвращаемое значение:
//   Дата
//
Функция СрокИсполненияПоДлительности(ПараметрыДлительностиИсполнения) Экспорт
	
	СрокИсполнения = Дата(1,1,1);
	
	Дни = ПараметрыДлительностиИсполнения.Дни;
	Часы = ПараметрыДлительностиИсполнения.Часы;
	Минуты = ПараметрыДлительностиИсполнения.Минуты;
	ДатаОтсчета = ПараметрыДлительностиИсполнения.ДатаОтсчета;
	ГрафикРаботы = ПараметрыДлительностиИсполнения.ГрафикРаботы;
	
	Если Дни = 0 И Часы = 0 И Минуты = 0 Тогда
		Возврат СрокИсполнения;
	КонецЕсли;
	
	ДопПараметры = Новый Структура;
	ДопПараметры.Вставить("ГрафикРаботы", ГрафикРаботы);
	
	СрокиИсполненияПроцессов.СкорректироватьДатуОтсчета(ДатаОтсчета, ДопПараметры);
	
	ИспользоватьДатуИВремяВСрокахЗадач = 
		ПолучитьФункциональнуюОпцию("ИспользоватьДатуИВремяВСрокахЗадач");
	
	Если ЗначениеЗаполнено(ГрафикРаботы) Тогда
		
		РеквизитыГрафикаРаботы = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
			ГрафикРаботы, "Календарь, КоличествоРабочихЧасовВДне");
			
		Календарь = РеквизитыГрафикаРаботы.Календарь;
		КоличествоРабочихЧасовВДне = РеквизитыГрафикаРаботы.КоличествоРабочихЧасовВДне;
		
		Если ИспользоватьДатуИВремяВСрокахЗадач Тогда
			
			СрокВСекундах = Дни * 3600 * КоличествоРабочихЧасовВДне + Часы * 3600 + Минуты * 60;
			
			ДатаНачала = ДатаОтсчета;
			ДатаОкончания = КалендарныеГрафики.ПолучитьДатуПоКалендарю(Календарь, ДатаОтсчета, Дни);
			ДатаОкончания = КонецДня(ДатаОкончания);
			
			Пока СрокВСекундах > 0 Цикл
				
				ИнтервалыРабочегоВремени = ГрафикиРаботы.СформироватьТаблицуРабочегоВремени(
					ГрафикРаботы, ДатаНачала, ДатаОкончания);
			
				Для Каждого Интервал Из ИнтервалыРабочегоВремени Цикл
					
					Если СрокВСекундах = 0 Тогда
						Прервать;
					КонецЕсли;
					
					Если СрокВСекундах > Интервал.Длительность Тогда
						СрокВСекундах = СрокВСекундах - Интервал.Длительность;
					Иначе
						СрокИсполнения = Интервал.ДатаНачала + СрокВСекундах;
						СрокВСекундах = 0;
					КонецЕсли;
					
				КонецЦикла;
				
				Если СрокВСекундах = 0 Тогда
					Прервать;
				КонецЕсли;
				
				ДатаНачала = ДатаОкончания;
				ДатаОкончания = КалендарныеГрафики.ПолучитьДатуПоКалендарю(
					Календарь, ДатаОкончания, 2);
					
				ДатаОкончания = КонецДня(ДатаОкончания);
				
			КонецЦикла;
			
		Иначе
			// Срок исполнения с учетом того, что время должно равняться концу дня.
			СрокИсполнения = КалендарныеГрафики.ПолучитьДатуПоКалендарю(Календарь, ДатаОтсчета, Дни - 1);
			СрокИсполнения = КонецДня(СрокИсполнения);
		КонецЕсли;
		
	Иначе
		
		СрокВСекундах = Дни * 86400;
		Если ИспользоватьДатуИВремяВСрокахЗадач Тогда
			СрокВСекундах = СрокВСекундах + Часы * 3600 + Минуты * 60;
		Иначе
			// Корректировка срока на последнюю секунду дня.
			// Предполагается, что если не используется время в сроках, то оно
			// равно концу дня.
			СрокВСекундах = СрокВСекундах - 1;
		КонецЕсли;
		
		СрокИсполнения = ДатаОтсчета + СрокВСекундах;
		
	КонецЕсли;
	
	Возврат СрокИсполнения;
	
КонецФункции

// Возвращает дату от которой следует производить расчет сроков в процессе.
//
// Используется для переопределения одноименной функции в модуле СрокиИсполненияПроцессов.
// Вместо текущей следует использовать функцию из модуля СрокиИсполненияПроцессов.
//
// Параметры:
//  Процесс - БизнесПроцессОбъект, ДанныеФормыСтруктура - процесс.
//  ТочкаМаршрутаВозвратаНаДоработку -  ТочкаМаршрутаБизнесПроцессаСсылка - точка маршрута процесса
//                                     в которой создается задача ознакомления с возможность
//                                     отправить процесс на следующий цикл.
//  ДляНовойИтерации - Булево - если передано значение Истина, то будет возвращена текущая дата.
//
// Возвращаемое значение:
//  Дата - дата отсчета для расчета сроков процесса.
//
Функция ДатаОтсчетаДляРасчетаСроковПроцесса(Процесс,
	ТочкаМаршрутаВозвратаНаДоработку = Неопределено,
	ДляНовойИтерации = Ложь) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если ОбщегоНазначения.ЭтоСсылка(ТипЗнч(Процесс)) Тогда
		РеквизитыПроцесса = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
			Процесс, "Ссылка, ДатаНачала, Стартован, ВедущаяЗадача");
	Иначе
		РеквизитыПроцесса = Процесс;
	КонецЕсли;
	
	ДатаОтсчета = ТекущаяДатаСеанса();
	
	Если ЗначениеЗаполнено(РеквизитыПроцесса.Ссылка)
		И РеквизитыПроцесса.Стартован
		И Не ДляНовойИтерации Тогда
		
		ДатаОтсчета = РеквизитыПроцесса.ДатаНачала;
		
		Если ЗначениеЗаполнено(РеквизитыПроцесса.ВедущаяЗадача) Тогда
			
			ВедущийПроцесс = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(РеквизитыПроцесса.ВедущаяЗадача, "БизнесПроцесс");
			
			Если ТипЗнч(ВедущийПроцесс) = Тип("БизнесПроцессСсылка.КомплексныйПроцесс") Тогда
				
				СрокЗавершенияПредущихДействий = СрокЗавершенияПредущихДействийКомплексногоПроцесса(
					РеквизитыПроцесса.Ссылка, ВедущийПроцесс);
				
				Если ЗначениеЗаполнено(СрокЗавершенияПредущихДействий) Тогда
					ДатаОтсчета = СрокЗавершенияПредущихДействий;
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ТочкаМаршрутаВозвратаНаДоработку) Тогда
		
			Запрос = Новый Запрос;
			Запрос.Текст = 
				"ВЫБРАТЬ
				|	МАКСИМУМ(ЗадачаИсполнителя.ДатаИсполнения) КАК ДатаИсполнения
				|ИЗ
				|	Задача.ЗадачаИсполнителя КАК ЗадачаИсполнителя
				|ГДЕ
				|	ЗадачаИсполнителя.ТочкаМаршрута = &ТочкаМаршрута
				|	И ЗадачаИсполнителя.БизнесПроцесс = &БизнесПроцесс";
			Запрос.УстановитьПараметр("ТочкаМаршрута", ТочкаМаршрутаВозвратаНаДоработку);
			Запрос.УстановитьПараметр("БизнесПроцесс", РеквизитыПроцесса.Ссылка);
			Выборка = Запрос.Выполнить().Выбрать();
			
			Если Выборка.Следующий() И ЗначениеЗаполнено(Выборка.ДатаИсполнения)Тогда
				ДатаОтсчета = Выборка.ДатаИсполнения;
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат ДатаОтсчета;
	
КонецФункции

// Возвращает максимальный срок завершения всех предыдущих действий
// комплексного процесса.
//
// Параметры:
//  Действие - БизнесПроцессСсылка - действие комплексного процесса,
//             для которого определяет срок завершения предшественников.
//  КомплексныйПроцесс - БизнесПроцессСсылка.КомплексныйПроцесс - комплексный процесс действия.
//
// Возвращаемое значение:
//  Дата
//
Функция СрокЗавершенияПредущихДействийКомплексногоПроцесса(Действие, КомплексныйПроцесс)
	
	УстановитьПривилегированныйРежим(Истина);
	
	СрокЗавершенияПредущихДействи = Дата(1,1,1);
	
	РеквизитыКомплексногоПроцесса = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
		КомплексныйПроцесс, "Схема, Этапы, ПредшественникиЭтапов");
	
	ПредшествующиеДействия = Новый Массив;
	
	// Получим все предшествующие действия текущему.
	Если ЗначениеЗаполнено(РеквизитыКомплексногоПроцесса.Схема) Тогда
		
		// Получим данные схемы.
		РеквизитыСхемы = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
			РеквизитыКомплексногоПроцесса.Схема,
			"ПараметрыДействий, ЭлементыСхемы, ПредшественникиЭлементовСхемы");
		
		ПараметрыДействий = РеквизитыСхемы.ПараметрыДействий.Выгрузить();
		ЭлементыСхемы = РеквизитыСхемы.ЭлементыСхемы.Выгрузить();
		ПредшественникиЭлементовСхемы = РеквизитыСхемы.ПредшественникиЭлементовСхемы.Выгрузить();
		
		// Если действия нет в схеме, то ничего не определяем.
		ПараметрыТекущегоДействия = ПараметрыДействий.Найти(Действие, "Процесс");
		Если ПараметрыТекущегоДействия <> Неопределено Тогда
			
			ИмяТекущегоДействия = ПараметрыТекущегоДействия.Имя;
			
			// Сформируем кэш данных для быстрого обращения к ним по имени.
			КэшЭлементовСхемы = Новый Соответствие;
			Для Каждого СтрокаТаблицы Из ЭлементыСхемы Цикл
				КэшЭлементовСхемы.Вставить(СтрокаТаблицы.Имя, СтрокаТаблицы);
			КонецЦикла;
			КэшПараметровДействий = Новый Соответствие;
			Для Каждого СтрокаТаблицы Из ПараметрыДействий Цикл
				КэшПараметровДействий.Вставить(СтрокаТаблицы.Имя, СтрокаТаблицы);
			КонецЦикла;
			
			ПолучитьПредшествующиеДействияЭлементаСхемыКомплексногоПроцесса(
				ИмяТекущегоДействия,
				ПредшествующиеДействия,
				КэшЭлементовСхемы,
				ПредшественникиЭлементовСхемы,
				КэшПараметровДействий);
			
		КонецЕсли;
		
	Иначе
	
		Этапы = РеквизитыКомплексногоПроцесса.Этапы.Выгрузить();
		ПредшественникиЭтапов = РеквизитыКомплексногоПроцесса.ПредшественникиЭтапов.Выгрузить();
			
		СтрокаЭтапа = Этапы.Найти(Действие, "ЗапущенныйБизнесПроцесс");
		
		Если СтрокаЭтапа <> Неопределено Тогда
			
			ОтборПредшественников = Новый Структура;
			ОтборПредшественников.Вставить("ИдентификаторПоследователя", СтрокаЭтапа.ИдентификаторЭтапа);
			Предшественники = ПредшественникиЭтапов.НайтиСтроки(ОтборПредшественников);
			
			Для Каждого СтрПредшественник Из Предшественники Цикл
				СтрЭтапПредшественника = 
					Этапы.Найти(СтрПредшественник.ИдентификаторПредшественника);
					
				Если СтрЭтапПредшественника = Неопределено Тогда
					Продолжить;
				КонецЕсли;
					
				Если ЗначениеЗаполнено(СтрЭтапПредшественника.ЗапущенныйБизнесПроцесс) Тогда
					ПредшествующиеДействия.Добавить(СтрЭтапПредшественника.ЗапущенныйБизнесПроцесс);
				Иначе
					ПредшествующиеДействия.Добавить(СтрЭтапПредшественника.ШаблонБизнесПроцесса);
				КонецЕсли;
				
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЕсли;
	
	// Получим сроки исполнения предшествующих действий.
	
	Если ПредшествующиеДействия.Количество() > 0 Тогда
	
		ШаблонТекстаЗапроса = 
			"ВЫБРАТЬ
			|	ТаблицаДействий.Ссылка КАК Действие,
			|	ТаблицаДействий.СрокИсполненияПроцесса КАК СрокИсполнения
			|ИЗ
			|	#ТаблицаДействий КАК ТаблицаДействий
			|ГДЕ
			|	ТаблицаДействий.Ссылка В(&Действия)";
			
		ДобавленныеТипыВЗапрос = Новый Соответствие;
		
		ТекстЗапроса = "";
		Разделитель = "";
		
		Для Каждого ДействиеПредшественник Из ПредшествующиеДействия Цикл
			
			ТипДействия = ТипЗнч(ДействиеПредшественник);
			
			Если ДобавленныеТипыВЗапрос.Получить(ТипДействия) <> Неопределено Тогда
				Продолжить;
			КонецЕсли;
			
			ИмяТаблицыПоСсылке = ОбщегоНазначения.ИмяТаблицыПоСсылке(ДействиеПредшественник);
			
			ДобавленныеТипыВЗапрос.Вставить(ДействиеПредшественник, ИмяТаблицыПоСсылке);
			
			ТекстЗапроса = ТекстЗапроса
				+ Разделитель
				+ СтрЗаменить(ШаблонТекстаЗапроса, "#ТаблицаДействий", ИмяТаблицыПоСсылке);
			
			Разделитель = "
				|
				|ОБЪЕДИНИТЬ ВСЕ
				|
				|";
			
		КонецЦикла;
		
		Запрос = Новый Запрос;
		Запрос.Текст = ТекстЗапроса;
		Запрос.УстановитьПараметр("Действия", ПредшествующиеДействия);
		
		Выборка = Запрос.Выполнить().Выбрать();
		
		Пока Выборка.Следующий() Цикл
			СрокЗавершенияПредущихДействи = Макс(СрокЗавершенияПредущихДействи, Выборка.СрокИсполнения);
		КонецЦикла;
		
	КонецЕсли;
	
	Возврат СрокЗавершенияПредущихДействи;
	
КонецФункции

// Получает массив предшествующих действий элемента схемы комплексного процесса.
// Рекурсивная процедура.
//
// Параметры:
//  ИмяЭлемента - Строка - имя элемента схемы.
//  ПредшествующиеДействия - Массив - в этот параметр будет возвращен результат.
//  КэшЭлементовСхемы - Соотвествие - кэш строк табличной части ЭлементовСхемы схемы.
//  ПредшественникиЭлементовСхемы - ТаблицаЗначений - табличная часть ПредшественникиЭлементовСхемы.
//  КэшПараметровДействий - Соотвествие - кэш строк табличной части ПараметровДействий схемы.
//
Процедура ПолучитьПредшествующиеДействияЭлементаСхемыКомплексногоПроцесса(
	ИмяЭлемента, ПредшествующиеДействия, КэшЭлементовСхемы,
	ПредшественникиЭлементовСхемы, КэшПараметровДействий)
	
	Отбор = Новый Структура;
	Отбор.Вставить("Имя", ИмяЭлемента);
	ПредшественникиЭлемента = ПредшественникиЭлементовСхемы.НайтиСтроки(Отбор);
	
	Для Каждого СтрокаТаблицы Из ПредшественникиЭлемента Цикл
		
		ПараметрыЭлемента = КэшЭлементовСхемы.Получить(СтрокаТаблицы.ИмяПредшественника);
		
		ТипПредшественника = ПараметрыЭлемента.Тип;
		
		Если ТипПредшественника = Перечисления.ТипыЭлементовСхемыКомплексногоПроцесса.Действие
			Или ТипПредшественника = 
				Перечисления.ТипыЭлементовСхемыКомплексногоПроцесса.ВложенныйПроцесс Тогда
			
			ПараметрыДействия = КэшПараметровДействий.Получить(СтрокаТаблицы.ИмяПредшественника);
			
			СостояниеПроцесса = Неопределено;
			Если ЗначениеЗаполнено(ПараметрыДействия.Процесс) Тогда
				СостояниеПроцесса = ОбщегоНазначенияДокументооборот.ЗначениеРеквизитаОбъектаВПривилегированномРежиме(
					ПараметрыДействия.Процесс, "Состояние");
			КонецЕсли;
			
			Действие = РаботаСКомплекснымиБизнесПроцессамиКлиентСервер.СсылкаНаДействиеСхемы(
				ПараметрыДействия, ПараметрыЭлемента, СостояниеПроцесса, Новый СписокЗначений);
			
			Если ЗначениеЗаполнено(Действие) Тогда
				ПредшествующиеДействия.Добавить(Действие);
			КонецЕсли;
			
			Продолжить;
		КонецЕсли;
		
		ПолучитьПредшествующиеДействияЭлементаСхемыКомплексногоПроцесса(
			СтрокаТаблицы.ИмяПредшественника,
			ПредшествующиеДействия,
			КэшЭлементовСхемы,
			ПредшественникиЭлементовСхемы,
			КэшПараметровДействий);
		
	КонецЦикла;
	
КонецПроцедуры

// Корректирует дату отсчета для вычисления сроков исполнения.
//
// Используется для переопределения одноименной процедуры в модуле СрокиИсполненияПроцессов.
// Вместо текущей следует использовать процедуру из модуля СрокиИсполненияПроцессов.
//
// Параметры:
//  ДатаОтсчета - ДатаИВремя - дата отсчета.
//  ДопПарметры - Структура - дополнительные параметры для корректировки.
//   * ГрафикРаботы - СправочникСсылка.ГрафикиРаботы
//
Процедура СкорректироватьДатуОтсчета(ДатаОтсчета, ДопПарметры) Экспорт
	
	ГрафикРаботы = ДопПарметры.ГрафикРаботы;
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьДатуИВремяВСрокахЗадач") Тогда
		
		// Убираем секунды из даты отсчета
		СрокиИсполненияПроцессовКлиентСервер.УбратьСекундыВДате(ДатаОтсчета);
		
		// Смещение даты отсчета по графику работ.
		Если ЗначениеЗаполнено(ГрафикРаботы) Тогда
			
			ВремяДатыОтсчета = Дата(1,1,1) + Час(ДатаОтсчета) * 3600 + Минута(ДатаОтсчета) * 60;
			
			ВремяНачалаИОкончанияДатыОтсчета = 
				ГрафикиРаботы.ПолучитьНачалоИОкончаниеРабочегоДня(ДатаОтсчета, ГрафикРаботы);
				
			// Если время даты отсчета совпадает с окончанием рабочего дня, то смещаем дату отсчета
			// на начало следующего дня.
			Если ВремяДатыОтсчета = ВремяНачалаИОкончанияДатыОтсчета.ОкончаниеДня Тогда
				ДатаОтсчета = ДатаОтсчета + 1;
			КонецЕсли;
			
			// Смещаем дату отсчета к первому времени раб. дня.
			ДатаОтсчета = ГрафикиРаботы.ПолучитьПервоеРабочееВремяДня(ГрафикРаботы, ДатаОтсчета);
			
		КонецЕсли;
		
	Иначе
		
		ВремяДатыОтсчета = Дата(1,1,1) + Час(ДатаОтсчета) * 3600 + Минута(ДатаОтсчета) *60;
		
		Если ЗначениеЗаполнено(ГрафикРаботы) Тогда
			
			ВремяНачалаИОкончанияДатыОтсчета = 
				ГрафикиРаботы.ПолучитьНачалоИОкончаниеРабочегоДня(ДатаОтсчета, ГрафикРаботы);
			
			Если ВремяДатыОтсчета > ВремяНачалаИОкончанияДатыОтсчета.НачалоДня Тогда
				ДатаОтсчета = НачалоДня(ДатаОтсчета) + 86400;
			Иначе
				ДатаОтсчета = НачалоДня(ДатаОтсчета);
			КонецЕсли;
			
			// Смещение даты отсчета по графику работ.
			ДатаОтсчета = ГрафикиРаботы.ПолучитьПервоеРабочееВремяДня(ГрафикРаботы, ДатаОтсчета);
			ДатаОтсчета = НачалоДня(ДатаОтсчета);
			
		Иначе
			// Смещение даты отсчета на начало следующего дня, если
			// дата отсчета больше начала рабочего дня (9.00).
			Если ВремяДатыОтсчета > Дата(1,1,1) + 9 * 3600 Тогда
				ДатаОтсчета = НачалоДня(ДатаОтсчета) + 86400;
			Иначе
				ДатаОтсчета = НачалоДня(ДатаОтсчета);
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

// Удаляет строки из таблицы Этапы помеченные на удаление (с полем Удален = Истина) и соотвествующие
// им строки в таблице ПредшественникиЭтапов.
//
Процедура ИсключитьУдаленныеЭтапыИзТаблицКомплексногоПроцесса(Этапы, ПредшественникиЭтапов)
	
	// Исключим из обработки все удаленные этапы
	ИндексЭтапа = Этапы.Количество() - 1;
	Пока ИндексЭтапа >= 0 Цикл
		ТекущийЭтап = Этапы[ИндексЭтапа];
		Если ТекущийЭтап.Удален Тогда
			
			Отбор = Новый Структура;
			Отбор.Вставить("ИдентификаторПоследователя", ТекущийЭтап.ИдентификаторЭтапа);
			СтркиКУдалению = ПредшественникиЭтапов.НайтиСтроки(Отбор);
			Для Каждого СтрКУдалению Из СтркиКУдалению Цикл
				ПредшественникиЭтапов.Удалить(СтрКУдалению);
			КонецЦикла;
			
			Отбор = Новый Структура;
			Отбор.Вставить("ИдентификаторПредшественника", ТекущийЭтап.ИдентификаторЭтапа);
			СтркиКУдалению = ПредшественникиЭтапов.НайтиСтроки(Отбор);
			Для Каждого СтрКУдалению Из СтркиКУдалению Цикл
				ПредшественникиЭтапов.Удалить(СтрКУдалению);
			КонецЦикла;
			
			Этапы.Удалить(ТекущийЭтап);
		КонецЕсли;
		ИндексЭтапа = ИндексЭтапа - 1;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции_РасчетСроковПроцессов

// Возвращает структуру параметров для расчета сроков процесса.
//
// Используется для переопределения одноименной функции в модуле СрокиИсполненияПроцессов.
// Вместо текущей следует использовать функцию из модуля СрокиИсполненияПроцессов.
//
// Возвращаемое значение:
//  Структура
//   * ДатаОтсчета - Дата
//   * РеквизитТаблицаСИзмененнымСроком - Строка
//   * ИндексСтроки - Строка
//   * ТекущаяИтерация - Число
//   * ЗаполнятьСрокПроцессаТолькоПриПревышении - Булево
//
Функция ПараметрыДляРасчетаСроков() Экспорт
	
	Параметры = Новый Структура;
	
	Параметры.Вставить("ДатаОтсчета", Дата(1,1,1));
	Параметры.Вставить("РеквизитТаблицаСИзмененнымСроком", "");
	Параметры.Вставить("ИндексСтроки", 0);
	Параметры.Вставить("ТекущаяИтерация", 1);
	Параметры.Вставить("ЗаполнятьСрокПроцессаТолькоПриПревышении", Ложь);
	Параметры.Вставить("Смещение", 0);
	
	Возврат Параметры;
	
КонецФункции

// Возвращает пустую таблицу для расчета сроков процесса.
// Используется в процедуре РассчитатьСрокиИсполнения.
//
// Используется для переопределения одноименной функции в модуле СрокиИсполненияПроцессов.
// Вместо текущей следует использовать функцию из модуля СрокиИсполненияПроцессов.
//
// Возвращаемое значение:
//  ТаблицаЗначений
//   * IDСрока - строка с путым идентификатором является датой отсчета в текущей таблице.
//   * ГрафикРаботы - СправочникСсылка.ГрафикиРаботы
//   * СрокИсполнения - Дата
//   * СрокИсполненияДни - Число
//   * СрокИсполненияЧасы - Число
//   * СрокИсполненияМинуты - Число
//   * ВариантУстановкиСрокаИсполнения - ПеречислениеСсылка.ВариантыУстановкиСрокаИсполнения
//   * Пройден - Булево
//   * ПодчиненныеСроки - Структура
//      ** Сроки - см. ПустаяТаблицаСроков()
//      ** Предшественники - см. ПустаяТаблицаПредшественников()
//      ** IDРассчитанногоСрок - строка с рассчитанным сроком.
//
Функция ПустаяТаблицаСроков() Экспорт
	
	МассивТипов = Новый Массив;
	МассивТипов.Добавить(Тип("УникальныйИдентификатор"));
	МассивТипов.Добавить(Тип("Строка"));
	ОписаниеТиповУникальныйИдентификатор = Новый ОписаниеТипов(МассивТипов);
	
	МассивТипов = Новый Массив;
	МассивТипов.Добавить(Тип("СправочникСсылка.ГрафикиРаботы"));
	ОписаниеТиповГрафикиРаботы = Новый ОписаниеТипов(МассивТипов);
	
	МассивТипов = Новый Массив;
	МассивТипов.Добавить(Тип("Дата"));
	ОписаниеТиповДата = Новый ОписаниеТипов(МассивТипов);
	
	МассивТипов = Новый Массив;
	МассивТипов.Добавить(Тип("Число"));
	ОписаниеТиповЧисло = Новый ОписаниеТипов(МассивТипов);
	
	МассивТипов = Новый Массив;
	МассивТипов.Добавить(Тип("ПеречислениеСсылка.ВариантыУстановкиСрокаИсполнения"));
	ОписаниеТиповВариантыУстановкиСрокаИсполнения = Новый ОписаниеТипов(МассивТипов);
	
	МассивТипов = Новый Массив;
	МассивТипов.Добавить(Тип("Булево"));
	ОписаниеТиповБулево = Новый ОписаниеТипов(МассивТипов);
	
	МассивТипов = Новый Массив;
	МассивТипов.Добавить(Тип("Структура"));
	ОписаниеТиповСтруктура = Новый ОписаниеТипов(МассивТипов);
	
	МассивТипов = Новый Массив;
	МассивТипов.Добавить(Тип("Число"));
	МассивТипов.Добавить(Тип("Дата"));
	ОписаниеТиповЧислоДата = Новый ОписаниеТипов(МассивТипов);
	
	Сроки = Новый ТаблицаЗначений();
	Сроки.Колонки.Добавить("IDСрока", ОписаниеТиповУникальныйИдентификатор);
	Сроки.Колонки.Добавить("ГрафикРаботы", ОписаниеТиповГрафикиРаботы);
	Сроки.Колонки.Добавить("СрокИсполнения", ОписаниеТиповДата);
	Сроки.Колонки.Добавить("СрокИсполненияДни", ОписаниеТиповЧисло);
	Сроки.Колонки.Добавить("СрокИсполненияЧасы", ОписаниеТиповЧисло);
	Сроки.Колонки.Добавить("СрокИсполненияМинуты", ОписаниеТиповЧисло);
	
	Сроки.Колонки.Добавить("ВариантУстановкиСрокаИсполнения",
		ОписаниеТиповВариантыУстановкиСрокаИсполнения);
	
	Сроки.Колонки.Добавить("Пройден", ОписаниеТиповБулево);
	Сроки.Колонки.Добавить("Изменен", ОписаниеТиповБулево);
	Сроки.Колонки.Добавить("ПодчиненныеСроки", ОписаниеТиповСтруктура);
	
	Сроки.Колонки.Добавить("Смещение", ОписаниеТиповЧислоДата);
	
	Возврат Сроки;
	
КонецФункции

// Возвращает структуру измененных сроков процесса.
//
// Используется для переопределения одноименной функции в модуле СрокиИсполненияПроцессов.
// Вместо текущей следует использовать функцию из модуля СрокиИсполненияПроцессов.
//
// Параметры:
//  Процесс - БизнесПроцессОбъект,
//            СправочникОбъект.Шаблон<Имя шаблона процесса>,
//            ДанныеФормыСтуктура - процесс или шаблон процесса.
//  Параметры - Структура - см. функцию ПараметрыДляРасчетаСроков
//
// Возвращаемое значение:
//  Структура - измененные сроки процесса/шаблона.
//   * СрокАвтораИзменен - Булево
//   * СрокПроверяющегоИзменен - Булево
//   * СрокИсполнителяИзменен - Булево
//   * ИндексИзменнойСтроки - Число - индекс измененной строки Исполнители.
//
Функция ИзмененныеСрокиПроцесса(Процесс, Параметры) Экспорт
	
	Результат = Новый Структура;
	
	Результат.Вставить("СрокАвтораИзменен", Ложь);
	Результат.Вставить("СрокПроверяющегоИзменен", Ложь);
	Результат.Вставить("СрокИсполнителяИзменен", Ложь);
	Результат.Вставить("ИндексИзменнойСтроки", 0);
	
	РеквизитТаблицаСИзмененнымСроком = Параметры.РеквизитТаблицаСИзмененнымСроком;
	ИндексСтроки = Параметры.ИндексСтроки;
	
	МетаданныеПроцесса = Процесс.Ссылка.Метаданные();
	ЕстьТаблицаИсполнители = МетаданныеПроцесса.ТабличныеЧасти.Найти("Исполнители") <> Неопределено;
	
	Если ЕстьТаблицаИсполнители Тогда
		
		КоличествоИсполнителей = Процесс.Исполнители.Количество();
		
		Результат.ИндексИзменнойСтроки = КоличествоИсполнителей;
		
		Если РеквизитТаблицаСИзмененнымСроком = ""
			Или РеквизитТаблицаСИзмененнымСроком = "НастройкаСтарта"
			Или РеквизитТаблицаСИзмененнымСроком = "ОписаниеОтложенногоСтарта"
			Или РеквизитТаблицаСИзмененнымСроком = "ВариантИсполнения" Тогда
			
			Результат.ИндексИзменнойСтроки = 0;
			Результат.СрокАвтораИзменен = Истина;
			Результат.СрокПроверяющегоИзменен = Истина;
			
		ИначеЕсли РеквизитТаблицаСИзмененнымСроком = "Автор"
			Или РеквизитТаблицаСИзмененнымСроком = "Проверяющий"
			Или РеквизитТаблицаСИзмененнымСроком = "СрокОбработкиРезультатов" Тогда
			
			Результат.СрокАвтораИзменен = Истина;
			Результат.СрокПроверяющегоИзменен = Истина;
			
		ИначеЕсли РеквизитТаблицаСИзмененнымСроком = "Исполнители" Тогда
			
			Результат.ИндексИзменнойСтроки = ИндексСтроки;
			Если Результат.ИндексИзменнойСтроки < КоличествоИсполнителей Тогда
				Результат.СрокАвтораИзменен = Истина;
				Результат.СрокПроверяющегоИзменен = Истина;
			КонецЕсли;
			
		КонецЕсли;
		
	Иначе
		
		Если РеквизитТаблицаСИзмененнымСроком = ""
			Или РеквизитТаблицаСИзмененнымСроком = "НастройкаСтарта"
			Или РеквизитТаблицаСИзмененнымСроком = "ОписаниеОтложенногоСтарта"
			Или РеквизитТаблицаСИзмененнымСроком = "Исполнитель"
			Или РеквизитТаблицаСИзмененнымСроком = "СрокИсполнения" Тогда
			
			Результат.СрокИсполнителяИзменен = Истина;
			Результат.СрокАвтораИзменен = Истина;
			Результат.СрокПроверяющегоИзменен = Истина;
			
		ИначеЕсли РеквизитТаблицаСИзмененнымСроком = "Автор"
			Или РеквизитТаблицаСИзмененнымСроком = "Проверяющий"
			Или РеквизитТаблицаСИзмененнымСроком = "СрокОбработкиРезультатов"
			Или РеквизитТаблицаСИзмененнымСроком = "КоличествоИтераций" Тогда
			
			Результат.СрокАвтораИзменен = Истина;
			Результат.СрокПроверяющегоИзменен = Истина;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Добавляет дату отсчета в данные для расчета сроков.
// Дата отсчета должна добавляться всегда первой строкой.
// Дате отсчета соответствует уникальный идентификатор пустой.
//
// Используется для переопределения одноименной процедуры в модуле СрокиИсполненияПроцессов.
// Вместо текущей следует использовать процедуру из модуля СрокиИсполненияПроцессов.
//
// Параметры:
//  Сроки - ТаблицаЗначений - см. функцию ПустаяТаблицаСроков.
//  Предшественники - ТаблицаЗначений - см. функцию ПустаяТаблицаПредшественников.
//  ИдентификаторыСроков - Соответствие - соответствие идентификаторов сроков (ключ) и их описания (значение).
//  ПараметрыДляРасчета - Структура - см. функцию ПараметрыДляРасчетаСроков.
//  ПредшественникиСледующегоЭтапа - Массив - предшественники для след. исполнителей.
//
Процедура ДобавитьДатуОтсчетаВДанныеДляРасчета(
	Сроки, Предшественники, ИдентификаторыСроков, ПараметрыДляРасчета,
	ПредшественникиСледующегоЭтапа) Экспорт
	
	IDДатыОтсчета = УникальныйИдентификаторПустой();
	ИдентификаторыСроков.Вставить(IDДатыОтсчета, "ДатаОтсчета");
	
	СтрокаСрок = Сроки.Добавить();
	СтрокаСрок.IDСрока = IDДатыОтсчета;
	СтрокаСрок.СрокИсполнения = ПараметрыДляРасчета.ДатаОтсчета;
	СтрокаСрок.Смещение = ПараметрыДляРасчета.Смещение;
	СтрокаСрок.ВариантУстановкиСрокаИсполнения = Перечисления.ВариантыУстановкиСрокаИсполнения.ТочныйСрок;
	СтрокаСрок.Пройден = Истина; // считаем, что дата отсчета всегда рассчитана в таблице и ее пересчет не требуется.
	
	// Дата отсчета всегда является предшественников для следующих
	// участников.
	ПредшественникиСледующегоЭтапа.Добавить(IDДатыОтсчета);
	
КонецПроцедуры

// Добавляет сроки участника в данные для расчета сроков.
//
// Используется для переопределения одноименной процедуры в модуле СрокиИсполненияПроцессов.
// Вместо текущей следует использовать процедуру из модуля СрокиИсполненияПроцессов.
//
// Параметры:
//  Сроки - ТаблицаЗначений - см. функцию ПустаяТаблицаСроков.
//  Предшественники - ТаблицаЗначений - см. функцию ПустаяТаблицаПредшественников.
//  ПараметрыСрокаУчастника - Структура
//   * Участник - СправочникСсылка.Пользователи, СправочникСсылка.РолиИсполнителей
//   * СрокИсполнения - Дата
//   * СрокИсполненияДни - Число
//   * СрокИсполненияЧасы - Число
//   * СрокИсполненияМинуты - Число
//   * ВариантУстановкиСрока - ПеречислениеСсылка.ВариантыУстановкиСрокаИсполнения
//  СрокУчастникаИзменен - Булево - признак того, что срок исполнения изменился.
//  ПредшественникиСледующегоЭтапа - Массив - предшественники для след. исполнителей.
//  ИдентификаторыСроков - Соответствие - соответствие идентификаторов сроков (ключ) и их описания (значение).
//  ИмяИдентификатораСрока - Строка - описание (роль) срока в процессе.
//
Процедура ДобавитьСрокУчастникаПроцессаВДанныеДляРасчета(
	Сроки, Предшественники,
	ПараметрыСрокаУчастника,
	СрокУчастникаИзменен,
	ПредшественникиСледующегоЭтапа,
	ИдентификаторыСроков = Неопределено,
	ИмяИдентификатораСрока = Неопределено) Экспорт
	
	Участник = ПараметрыСрокаУчастника.Участник;
	СрокИсполнения = ПараметрыСрокаУчастника.СрокИсполнения;
	СрокИсполненияДни = ПараметрыСрокаУчастника.СрокИсполненияДни;
	СрокИсполненияЧасы = ПараметрыСрокаУчастника.СрокИсполненияЧасы;
	СрокИсполненияМинуты = ПараметрыСрокаУчастника.СрокИсполненияМинуты;
	ВариантУстановкиСрока = ПараметрыСрокаУчастника.ВариантУстановкиСрока;
	
	ПредшественникиТекущегоЭтапа = 
		ОбщегоНазначенияКлиентСервер.СкопироватьМассив(ПредшественникиСледующегоЭтапа);
	ПредшественникиСледующегоЭтапа.Очистить();
	
	IDТекущегоСрокаИсполнения = Новый УникальныйИдентификатор;
	
	СтрокаСрок = Сроки.Добавить();
	СтрокаСрок.IDСрока = IDТекущегоСрокаИсполнения;
	
	ГрафикРаботы = ГрафикРаботыУчастникаПроцесса(Участник);
	СтрокаСрок.ГрафикРаботы = ГрафикРаботы;
	
	СтрокаСрок.СрокИсполнения = СрокИсполнения;
	СтрокаСрок.СрокИсполненияДни = СрокИсполненияДни;
	СтрокаСрок.СрокИсполненияЧасы = СрокИсполненияЧасы;
	СтрокаСрок.СрокИсполненияМинуты = СрокИсполненияМинуты;
	
	// Если таблица сроков заполняется не для первой итерации,
	// то всем срокам устанавливается относительный срок,
	// т.к. по точному сроку расчет возможен только на первой или текущей итерации.
	Если ИдентификаторыСроков = Неопределено Тогда
		СтрокаСрок.ВариантУстановкиСрокаИсполнения = 
			Перечисления.ВариантыУстановкиСрокаИсполнения.ОтносительныйСрок;
	Иначе
		СтрокаСрок.ВариантУстановкиСрокаИсполнения = ВариантУстановкиСрока;
	КонецЕсли;
	
	СтрокаСрок.Пройден = Не СрокУчастникаИзменен;
	
	Если ИдентификаторыСроков <> Неопределено Тогда
		ИдентификаторыСроков.Вставить(IDТекущегоСрокаИсполнения, ИмяИдентификатораСрока);
	КонецЕсли;
		
	Для Каждого СтрПредшественникТекущегоЭтапа Из ПредшественникиТекущегоЭтапа Цикл
		СтрПредшественник = Предшественники.Добавить();
		СтрПредшественник.Последователь = IDТекущегоСрокаИсполнения;
		СтрПредшественник.Предшественник = СтрПредшественникТекущегоЭтапа;
	КонецЦикла;
	
	ПредшественникиСледующегоЭтапа.Добавить(IDТекущегоСрокаИсполнения);
	
КонецПроцедуры

// Добавляет сроки таблицы участников в данные для расчета сроков.
//
// Используется для переопределения одноименной процедуры в модуле СрокиИсполненияПроцессов.
// Вместо текущей следует использовать процедуру из модуля СрокиИсполненияПроцессов.
//
// Параметры:
//  Сроки - ТаблицаЗначений - см. функцию ПустаяТаблицаСроков.
//  Предшественники - ТаблицаЗначений - см. функцию ПустаяТаблицаПредшественников.
//  ТаблицаУчастников - ТаблицаЗначений, ТабличнаяЧасть, ДанныеФормыКоллекция - таблица исполнителей.
//  ИндексИзменнойСтроки - Число - индекс измененной строки.
//  ПредшественникиСледующегоЭтапа - Массив - предшественники для след. исполнителей.
//  ИдентификаторыСроков - Соответствие - соответствие идентификаторов сроков (ключ) и их описания (значение).
//  ВариантИсполнения - ПеречислениеСсылка.ВариантыМаршрутизацииЗадач - вариант исполнения процесса.
//  ИмяРеквизитаПорядокИсполнения - Строка - имя реквизита порядка исполнения в таблице исполнителей.
//
Процедура ДобавитьСрокиТаблицыИсполнителиПроцессаВДанныеДляРасчета(
	Сроки, Предшественники,
	ТаблицаУчастников, Знач ИндексИзменнойСтроки,
	ПредшественникиСледующегоЭтапа,
	ИдентификаторыСроков = Неопределено,
	ВариантИсполнения = Неопределено,
	ИмяРеквизитаПорядокИсполнения = "") Экспорт
	
	КоличествоУчастников = ТаблицаУчастников.Количество();
	
	Если КоличествоУчастников = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ПредшественникиТекущегоЭтапа = 
		ОбщегоНазначенияКлиентСервер.СкопироватьМассив(ПредшественникиСледующегоЭтапа);
	ПредшественникиСледующегоЭтапа.Очистить();
	
	Если ВариантИсполнения <> Перечисления.ВариантыМаршрутизацииЗадач.Последовательно
		И ВариантИсполнения <> Перечисления.ВариантыМаршрутизацииЗадач.Смешанно
		И ИндексИзменнойСтроки < КоличествоУчастников Тогда
		
		ИндексИзменнойСтроки = 0;
	КонецЕсли;
	
	IDСрокаИсполненияОтвественного = Неопределено;
	
	Для ИндексСтроки = 0 По КоличествоУчастников - 1 Цикл
		
		СтрУчастник = ТаблицаУчастников[ИндексСтроки];
		
		IDТекущегоСрокаИсполнения = Новый УникальныйИдентификатор;
		
		СтрокаСрок = Сроки.Добавить();
		СтрокаСрок.IDСрока = IDТекущегоСрокаИсполнения;
		
		ГрафикРаботы = ГрафикРаботыУчастникаПроцесса(СтрУчастник.Исполнитель);
		СтрокаСрок.ГрафикРаботы = ГрафикРаботы;
		
		СтрокаСрок.СрокИсполнения = СтрУчастник.СрокИсполнения;
		
		Если ИндексСтроки >= ИндексИзменнойСтроки Тогда
			СтрокаСрок.Пройден = Ложь;
		Иначе
			СтрокаСрок.Пройден = Истина;
		КонецЕсли;
		
		Если ИдентификаторыСроков <> Неопределено Тогда
			ИдентификаторыСроков.Вставить(
				IDТекущегоСрокаИсполнения, "Исполнители_" + Строка(ИндексСтроки));
		КонецЕсли;
		
		СтрокаСрок.СрокИсполненияДни = СтрУчастник.СрокИсполненияДни;
		СтрокаСрок.СрокИсполненияЧасы = СтрУчастник.СрокИсполненияЧасы;
		СтрокаСрок.СрокИсполненияМинуты = СтрУчастник.СрокИсполненияМинуты;
		
		// Если таблица сроков заполняется не для первой итерации,
		// то всем срокам устанавливается относительный срок,
		// т.к. по точному сроку расчет возможен только на первой или текущей итерации.
		Если ИдентификаторыСроков = Неопределено Тогда
			СтрокаСрок.ВариантУстановкиСрокаИсполнения = 
				Перечисления.ВариантыУстановкиСрокаИсполнения.ОтносительныйСрок;
		Иначе
			СтрокаСрок.ВариантУстановкиСрокаИсполнения = СтрУчастник.ВариантУстановкиСрокаИсполнения;
		КонецЕсли;
		
		Если ИндексСтроки > 0 И ЗначениеЗаполнено(ИмяРеквизитаПорядокИсполнения) Тогда
			Если СтрУчастник[ИмяРеквизитаПорядокИсполнения] = 
				Перечисления.ПорядокВыполненияЗадач.ПослеПредыдущего Тогда
				
				ПредшественникиТекущегоЭтапа = 
					ОбщегоНазначенияКлиентСервер.СкопироватьМассив(ПредшественникиСледующегоЭтапа);
				ПредшественникиСледующегоЭтапа.Очистить();
			КонецЕсли;
		КонецЕсли;
		
		Для Каждого СтрПредшественникТекущегоЭтапа Из ПредшественникиТекущегоЭтапа Цикл
			СтрПредшественник = Предшественники.Добавить();
			СтрПредшественник.Последователь = IDТекущегоСрокаИсполнения;
			СтрПредшественник.Предшественник = СтрПредшественникТекущегоЭтапа;
		КонецЦикла;
		
		Если СрокиИсполненияПроцессовКлиентСервер.ЭтоСтрокаОтвественного(СтрУчастник) Тогда
			ПредшественникиСледующегоЭтапа = 
				ОбщегоНазначенияКлиентСервер.СкопироватьМассив(ПредшественникиТекущегоЭтапа);
			IDСрокаИсполненияОтвественного = IDТекущегоСрокаИсполнения;
		Иначе
			ПредшественникиСледующегоЭтапа.Добавить(IDТекущегоСрокаИсполнения);
		КонецЕсли;
		
	КонецЦикла;
	
	Если ЗначениеЗаполнено(IDСрокаИсполненияОтвественного) Тогда
		ПредшественникиСледующегоЭтапа.Добавить(IDСрокаИсполненияОтвественного);
	КонецЕсли;
	
КонецПроцедуры

// Рассчитывает в таблице (Сроки) зависимые сроки от рассчитанного (РассчитанныйСрок).
// Рекурсивная процедура.
//
// Используется для переопределения одноименной процедуры в модуле СрокиИсполненияПроцессов.
// Вместо текущей следует использовать процедуру из модуля СрокиИсполненияПроцессов.
//
// Параметры:
//  Сроки - ТаблицаЗначений - см. ПустаяТаблицаСроков
//  Предшественники - ТаблицаЗначений - см. ПустаяТаблицаПредшественников
//  IDРассчитанногоСрок - УникальныйИдентификатор - идентификатор рассчитанного срока, для последователей которого следует произвести расчет сроков.
//
Процедура РассчитатьСрокиИсполнения(
	Сроки, Предшественники, IDРассчитанногоСрок) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ИспользоватьДатуИВремяВСрокахЗадач = 
		ПолучитьФункциональнуюОпцию("ИспользоватьДатуИВремяВСрокахЗадач");
	
	ВариантыУстановкиСрока = СрокиИсполненияПроцессовКлиентСервер.ВариантыУстановкиСрокаИсполнения();
	
	// Если текущий рассчитанный срок является датой отсчета
	// то обрабатываем его смещение.
	Если IDРассчитанногоСрок = УникальныйИдентификаторПустой() Тогда
		Отбор = Новый Структура;
		Отбор.Вставить("IDСрока", IDРассчитанногоСрок);
		НайденныеСроки = Сроки.НайтиСтроки(Отбор);
		СтрДатыОтсчета = НайденныеСроки[0];
		
		Если ЗначениеЗаполнено(СтрДатыОтсчета.Смещение) Тогда
			
			Если ТипЗнч(СтрДатыОтсчета.Смещение) = Тип("Дата") Тогда
				СтрДатыОтсчета.СрокИсполнения = Макс(СтрДатыОтсчета.СрокИсполнения, СтрДатыОтсчета.Смещение);
			Иначе
				СтрДатыОтсчета.СрокИсполнения = СтрДатыОтсчета.СрокИсполнения + СтрДатыОтсчета.Смещение;
			КонецЕсли;
			
		КонецЕсли;
	КонецЕсли;
	
	ЗависимыеСроки = СрокиИсполненияПроцессов.ЗависимыеСроки(Сроки ,Предшественники, IDРассчитанногоСрок);
	
	Для Каждого СрокДляРасчета Из ЗависимыеСроки Цикл
		
		ДатаОтсчетаДляЗависимогоСрока = СрокиИсполненияПроцессов.ДатаОтсчетаДляЗависимогоСрока(
			Сроки, Предшественники, СрокДляРасчета.IDСрока);
		
		Если ДатаОтсчетаДляЗависимогоСрока = Неопределено
			Или ДатаОтсчетаДляЗависимогоСрока = Дата(1,1,1) Тогда
			
			Продолжить;
		КонецЕсли;
		
		Если Не СрокДляРасчета.Пройден Тогда
			
			Если ЗначениеЗаполнено(СрокДляРасчета.ПодчиненныеСроки) Тогда
				
				ПодчиненныеСроки = СрокДляРасчета.ПодчиненныеСроки.Сроки;
				
				// Обновим даты отсчета в подчиненной таблице
				Отбор = Новый Структура("IDСрока", УникальныйИдентификаторПустой());
				СтрокиСДатойОтсчета = ПодчиненныеСроки.НайтиСтроки(Отбор);
				Для Каждого СтрСДатойОтсчета Из СтрокиСДатойОтсчета Цикл
					СтрСДатойОтсчета.СрокИсполнения = ДатаОтсчетаДляЗависимогоСрока;
				КонецЦикла;
				
				ПодчиненныеПредшественники = СрокДляРасчета.ПодчиненныеСроки.Предшественники;
				IDПодчиненногоРассчитанногоСрока = СрокДляРасчета.ПодчиненныеСроки.IDРассчитанногоСрок;
				
				ПредыдущийСрокИсполнения = СрокДляРасчета.СрокИсполнения;
				
				// Рассчитаем подчиненные сроки
				РассчитатьСрокиИсполнения(
					ПодчиненныеСроки,
					ПодчиненныеПредшественники,
					IDПодчиненногоРассчитанногоСрока);
					
				// Определим и заполним текущий срок по подчиненным.
				МаксимальныйПодчиненныйСрок = Дата(1,1,1);
					
				Для Каждого СтрПодчиненныйСрок Из ПодчиненныеСроки Цикл
					
					// Пропускаем дату отсчета.
					Если СтрПодчиненныйСрок.IDСрока = УникальныйИдентификаторПустой() Тогда
						Продолжить;
					КонецЕсли;
					
					Если Не ЗначениеЗаполнено(СтрПодчиненныйСрок.СрокИсполнения) Тогда
						МаксимальныйПодчиненныйСрок = Дата(1,1,1);
						Прервать;
					КонецЕсли;
					
					МаксимальныйПодчиненныйСрок = Макс(
						МаксимальныйПодчиненныйСрок, СтрПодчиненныйСрок.СрокИсполнения);
					
				КонецЦикла;
				
				СрокДляРасчета.СрокИсполнения = МаксимальныйПодчиненныйСрок;
				
				// Определим признак изменения для текущей строки сроков.
				Отбор = Новый Структура("Изменен", Истина);
				Если МаксимальныйПодчиненныйСрок <> ПредыдущийСрокИсполнения
					Или ПодчиненныеСроки.НайтиСтроки(Отбор).Количество() > 0 Тогда
					
					СрокДляРасчета.Изменен = Истина;
				КонецЕсли;
				
			Иначе
				
				СрокИсполнения = Дата(1,1,1);
				Дни = 0;
				Часы = 0;
				Минуты = 0;
				
				Если ДатаОтсчетаДляЗависимогоСрока <> Дата(1,1,1) Тогда
					Если СрокДляРасчета.ВариантУстановкиСрокаИсполнения = ВариантыУстановкиСрока.ТочныйСрок Тогда
						
						ПараметрыСрокаИсполнения = 
							СрокиИсполненияПроцессов.ПараметрыСрокаИсполненияДляОпределенияДлительности();
							
						ПараметрыСрокаИсполнения.СрокИсполнения = СрокДляРасчета.СрокИсполнения;
						ПараметрыСрокаИсполнения.ДатаОтсчета = ДатаОтсчетаДляЗависимогоСрока;
						ПараметрыСрокаИсполнения.ГрафикРаботы = СрокДляРасчета.ГрафикРаботы;
						
						Длительность = 
							СрокиИсполненияПроцессов.ДлительностьПоСрокуИсполнения(ПараметрыСрокаИсполнения);
						
						Дни = Длительность.Дни;
						Часы = Длительность.Часы;
						Минуты = Длительность.Минуты;
					Иначе
						
						ПараметрыДлительности = 
							СрокиИсполненияПроцессов.ПараметрыДлительностиИсполненияДляОпределенияТочногоСрока();
							
						ПараметрыДлительности.Дни = СрокДляРасчета.СрокИсполненияДни;
						ПараметрыДлительности.Часы = СрокДляРасчета.СрокИсполненияЧасы;
						ПараметрыДлительности.Минуты = СрокДляРасчета.СрокИсполненияМинуты;
						ПараметрыДлительности.ДатаОтсчета = ДатаОтсчетаДляЗависимогоСрока;
						ПараметрыДлительности.ГрафикРаботы = СрокДляРасчета.ГрафикРаботы;
							
						СрокИсполнения = 
							СрокиИсполненияПроцессов.СрокИсполненияПоДлительности(ПараметрыДлительности);
						
					КонецЕсли;
				КонецЕсли;
				
				Если СрокДляРасчета.ВариантУстановкиСрокаИсполнения = ВариантыУстановкиСрока.ТочныйСрок Тогда
					Если СрокДляРасчета.СрокИсполненияДни <> Дни
						Или СрокДляРасчета.СрокИсполненияЧасы <> Часы
						Или СрокДляРасчета.СрокИсполненияМинуты <> Минуты Тогда
						
						СрокДляРасчета.Изменен = Истина;
					КонецЕсли;
					
					СрокДляРасчета.СрокИсполненияДни = Дни;
					СрокДляРасчета.СрокИсполненияЧасы = Часы;
					СрокДляРасчета.СрокИсполненияМинуты = Минуты;
				Иначе
					Если СрокДляРасчета.СрокИсполнения <> СрокИсполнения Тогда
						СрокДляРасчета.Изменен = Истина;
					КонецЕсли;
					СрокДляРасчета.СрокИсполнения = СрокИсполнения;
				КонецЕсли;
				
			КонецЕсли;
			
			СрокДляРасчета.Пройден = Истина;
			
		КонецЕсли;
		
		РассчитатьСрокиИсполнения(
			Сроки, Предшественники, СрокДляРасчета.IDСрока);
		
	КонецЦикла;
	
КонецПроцедуры

// Рассчитывает сроки исполнения для нового процесса.
//
// Используется для переопределения одноименной процедуры в модуле СрокиИсполненияПроцессов.
// Вместо текущей следует использовать процедуру из модуля СрокиИсполненияПроцессов.
//
// Параметры:
//  ПроцессОбъект - БизнесПроцессОбъект
//
Процедура РассчитатьСрокИсполненияДляНовогоПроцесса(ПроцессОбъект) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Параметры = СрокиИсполненияПроцессов.ПараметрыДляРасчетаСроков();
	Параметры.ДатаОтсчета = ТекущаяДатаСеанса();
	
	Если ЗначениеЗаполнено(ПроцессОбъект.Ссылка) Тогда
		Параметры.Смещение = СмещенияДатыОтсчета(ПроцессОбъект.Ссылка);
	КонецЕсли;
	
	ТипПроцесса = ТипЗнч(ПроцессОбъект.Ссылка);
	
	Если ТипПроцесса = Тип("БизнесПроцессСсылка.Исполнение") Тогда
		
		СрокиИсполненияПроцессов.РассчитатьСрокиПроцессаИсполнения(ПроцессОбъект, Параметры);
		
	ИначеЕсли ТипПроцесса = Тип("БизнесПроцессСсылка.КомплексныйПроцесс") Тогда
		
		РассчитатьСрокиКомплексногоПроцесса(ПроцессОбъект, Параметры);
		
	ИначеЕсли ТипПроцесса = Тип("БизнесПроцессСсылка.ОбработкаВнутреннегоДокумента")
		Или ТипПроцесса = Тип("БизнесПроцессСсылка.ОбработкаВходящегоДокумента")
		Или ТипПроцесса = Тип("БизнесПроцессСсылка.ОбработкаИсходящегоДокумента") Тогда
		
		СрокиИсполненияПроцессов.РассчитатьСрокиСоставногоПроцесса(ПроцессОбъект, Параметры);
		
	ИначеЕсли ТипПроцесса = Тип("БизнесПроцессСсылка.Ознакомление") Тогда
		
		СрокиИсполненияПроцессов.РассчитатьСрокиОзнакомления(ПроцессОбъект, Параметры);
		
	ИначеЕсли ТипПроцесса = Тип("БизнесПроцессСсылка.Поручение") Тогда
		
		СрокиИсполненияПроцессов.РассчитатьСрокиПоручения(ПроцессОбъект, Параметры);
		
	ИначеЕсли ТипПроцесса = Тип("БизнесПроцессСсылка.Приглашение") Тогда
		
		РассчитатьСрокиПриглашения(ПроцессОбъект, Параметры);
		
	ИначеЕсли ТипПроцесса = Тип("БизнесПроцессСсылка.Рассмотрение") Тогда
		
		СрокиИсполненияПроцессов.РассчитатьСрокиРассмотрения(ПроцессОбъект, Параметры);
		
	ИначеЕсли ТипПроцесса = Тип("БизнесПроцессСсылка.Регистрация") Тогда
		
		СрокиИсполненияПроцессов.РассчитатьСрокиРегистрации(ПроцессОбъект, Параметры);
		
	ИначеЕсли ТипПроцесса = Тип("БизнесПроцессСсылка.Согласование") Тогда
		
		СрокиИсполненияПроцессов.РассчитатьСрокиСогласования(ПроцессОбъект, Параметры);
		
	ИначеЕсли ТипПроцесса = Тип("БизнесПроцессСсылка.Утверждение") Тогда
		
		СрокиИсполненияПроцессов.РассчитатьСрокиУтверждения(ПроцессОбъект, Параметры);
		
	КонецЕсли;
	
КонецПроцедуры

// Комплексный процесс

// Возвращает действие комплексного процесса по строке этапа комплексного процесса.
// Если по переданной строки этапов имеется процесс, то на него возвращается ссылка,
// иначе возвращается сслыка на шаблон.
//
// Параметры - СтрокаТабличнойЧасти, СтрокаТаблицыЗначений - строка таблицы этапов
//
// Возвращаемое значение:
//  БизнесПроцессСсылка, СправочникСсылка.<ИмяШаблонаПроцесса> - ссылка на шаблон или процесс.
//
Функция ДействиеЭтапа(СтрЭтап)
	
	СтруктураПоиска = Новый Структура;
	СтруктураПоиска.Вставить("ЗапущенныйБизнесПроцесс");
	СтруктураПоиска.Вставить("ШаблонБизнесПроцесса");
	
	ЗаполнитьЗначенияСвойств(СтруктураПоиска, СтрЭтап);
		
	Если ЗначениеЗаполнено(СтруктураПоиска.ЗапущенныйБизнесПроцесс) Тогда
		Возврат СтруктураПоиска.ЗапущенныйБизнесПроцесс;
	Иначе
		Возврат СтруктураПоиска.ШаблонБизнесПроцесса;
	КонецЕсли;
	
КонецФункции

// Заполняет сроки исполнения в действии комплексного процесса по таблицам с рассчитанными сроками.
//
// Параметры:
//  ДействиеОбъект - БизнесПроцессОбъект, СправочникОбъект.<ИмяШаблонаПроцесса> - процесс/шаблон
//  Сроки - ТаблицаЗначений - таблица с рассчитанными сроками. см. СрокиИсполненияПроцессов.ПустаяТаблицаСроков().
//  ИдентификаторыСроков - Соответствие - соответствие идентификаторов сроков (ключ) и их описания (значение).
//
Процедура ЗаполнитьСрокиДействияКомплексногоПроцесса(ДействиеОбъект, Сроки, ИдентификаторыСроков)
	
	ТипДействия = ТипЗнч(ДействиеОбъект.Ссылка);
	
	Если ТипДействия = Тип("СправочникСсылка.ШаблоныИсполнения")
		Или ТипДействия = Тип("БизнесПроцессСсылка.Исполнение") Тогда
		
		СрокиИсполненияПроцессов.ЗаполнитьСрокиПроцессаИсполнения(
			ДействиеОбъект, Сроки, ИдентификаторыСроков);
		
	ИначеЕсли ТипДействия = Тип("СправочникСсылка.ШаблоныКомплексныхБизнесПроцессов")
		Или ТипДействия = Тип("БизнесПроцессСсылка.КомплексныйПроцесс") Тогда
		
		ЗаполнитьСрокиКомплексногоПроцесса(ДействиеОбъект, Сроки, ИдентификаторыСроков);
		
	ИначеЕсли ТипДействия = Тип("СправочникСсылка.ШаблоныОзнакомления")
		Или ТипДействия = Тип("БизнесПроцессСсылка.Ознакомление") Тогда
		
		СрокиИсполненияПроцессов.ЗаполнитьСрокиОзнакомления(ДействиеОбъект, Сроки, ИдентификаторыСроков);
		
	ИначеЕсли ТипДействия = Тип("СправочникСсылка.ШаблоныПоручения")
		Или ТипДействия = Тип("БизнесПроцессСсылка.Поручение") Тогда
		
		СрокиИсполненияПроцессов.ЗаполнитьСрокиПоручения(ДействиеОбъект, Сроки, ИдентификаторыСроков);
		
	ИначеЕсли ТипДействия = Тип("СправочникСсылка.ШаблоныПриглашения")
		Или ТипДействия = Тип("БизнесПроцессСсылка.Приглашение") Тогда
		
		ЗаполнитьСрокиПриглашения(ДействиеОбъект, Сроки, ИдентификаторыСроков);
		
	ИначеЕсли ТипДействия = Тип("СправочникСсылка.ШаблоныРассмотрения")
		Или ТипДействия = Тип("БизнесПроцессСсылка.Рассмотрение") Тогда
		
		СрокиИсполненияПроцессов.ЗаполнитьСрокиРассмотрения(ДействиеОбъект, Сроки, ИдентификаторыСроков);
		
	ИначеЕсли ТипДействия = Тип("СправочникСсылка.ШаблоныРегистрации")
		Или ТипДействия = Тип("БизнесПроцессСсылка.Регистрация") Тогда
		
		СрокиИсполненияПроцессов.ЗаполнитьСрокиРегистрации(ДействиеОбъект, Сроки, ИдентификаторыСроков);
		
	ИначеЕсли ТипДействия = Тип("СправочникСсылка.ШаблоныСогласования")
		Или ТипДействия = Тип("БизнесПроцессСсылка.Согласование") Тогда
		
		СрокиИсполненияПроцессов.ЗаполнитьСрокиСогласования(ДействиеОбъект, Сроки, ИдентификаторыСроков);
		
	ИначеЕсли ТипДействия = Тип("СправочникСсылка.ШаблоныСоставныхБизнесПроцессов")
		Или ТипДействия = Тип("БизнесПроцессСсылка.ОбработкаВнутреннегоДокумента")
		Или ТипДействия = Тип("БизнесПроцессСсылка.ОбработкаВходящегоДокумента")
		Или ТипДействия = Тип("БизнесПроцессСсылка.ОбработкаИсходящегоДокумента") Тогда
		
		СрокиИсполненияПроцессов.ЗаполнитьСрокиСоставногоПроцесса(
			ДействиеОбъект, Сроки, ИдентификаторыСроков);
		
	ИначеЕсли ТипДействия = Тип("СправочникСсылка.ШаблоныУтверждения")
		Или ТипДействия = Тип("БизнесПроцессСсылка.Утверждение") Тогда
		
		СрокиИсполненияПроцессов.ЗаполнитьСрокиУтверждения(ДействиеОбъект, Сроки, ИдентификаторыСроков);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции_РасчетДлительностиПроцессов

// Возвращает длительность исполнения процесса/шаблона в днях, часах и минутах.
//
// Используется для переопределения одноименной функции в модуле СрокиИсполненияПроцессов.
// Вместо текущей следует использовать функцию из модуля СрокиИсполненияПроцессов.
//
// Параметры:
//  Объект - СправочникСсыка,
//           БизнесПроцессСсылка,
//           СправочникОбъект,
//           БизнесПроцессОбъект,
//           ДанныеФормыСтруктура - ссылка или объект на процесс/шаблон/схему.
//  Смещение - Число - смещение даты отсчета процесса в секундах.
//
// Возвращаемое значение:
//  Структура
//   * СрокИсполненияПроцессаДни - Число
//   * СрокИсполненияПроцессаЧасы - Число
//   * СрокИсполненияПроцессаМинуты - Число
//
Функция ДлительностьИсполненияПроцесса(Объект, Знач Смещение = Неопределено) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Длительность = Новый Структура;
	
	ТипОбъекта = ТипЗнч(Объект);
	ЭтоСсылкаНаОбъект = ОбщегоНазначения.ЭтоСсылка(ТипОбъекта);
	
	СсылкаНаОбъект = Объект;
	Если Не ЭтоСсылкаНаОбъект Тогда
		ТипОбъекта = ТипЗнч(Объект.Ссылка);
		СсылкаНаОбъект = Объект.Ссылка;
	КонецЕсли;
	
	Если Смещение <> Неопределено Тогда
		Смещение = Смещение;
	Иначе
		СсылкаНаПроцесс = СсылкаНаОбъект;
		Если ТипОбъекта = Тип("СправочникСсылка.СхемыКомплексныхПроцессов") Тогда
			Если ЭтоСсылкаНаОбъект Тогда
				СсылкаНаПроцесс = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СсылкаНаОбъект, "ВладелецСхемы");
			Иначе
				СсылкаНаПроцесс = Объект.ВладелецСхемы;
			КонецЕсли;
		КонецЕсли;
		Смещение = СмещенияДатыОтсчета(СсылкаНаПроцесс);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Смещение) Тогда
		Если ТипЗнч(Смещение) = Тип("Число") Тогда
			Смещение = Смещение;
		Иначе
			ТекущаяДатаСеанса = ТекущаяДатаСеанса();
			ТекущаяДатаСеанса = ТекущаяДатаСеанса - Секунда(ТекущаяДатаСеанса);
			Если ТекущаяДатаСеанса < Смещение Тогда
				Смещение = Смещение - ТекущаяДатаСеанса;
			Иначе
				Смещение = 0;
			КонецЕсли;
		КонецЕсли;
	Иначе
		Смещение = 0;
	КонецЕсли;
	
	Если ТипОбъекта = Тип("СправочникСсылка.ШаблоныИсполнения") Тогда
		
		Длительность = СрокиИсполненияПроцессов.ДлительностьИсполненияПроцессаИсполнения(Объект);
		
	ИначеЕсли ТипОбъекта = Тип("БизнесПроцессСсылка.КомплексныйПроцесс")
		Или ТипОбъекта = Тип("СправочникСсылка.ШаблоныКомплексныхБизнесПроцессов")
		Или ТипОбъекта = Тип("СправочникСсылка.СхемыКомплексныхПроцессов") Тогда
		
		Длительность = ДлительностьИсполненияКомплексногоПроцесса(Объект);
		
	ИначеЕсли ТипОбъекта = Тип("СправочникСсылка.ШаблоныСоставныхБизнесПроцессов") Тогда
		
		Длительность = СрокиИсполненияПроцессов.ДлительностьИсполненияСоставнегоПроцесса(Объект);
		
	ИначеЕсли ТипОбъекта = Тип("СправочникСсылка.ШаблоныОзнакомления") Тогда
		
		Длительность = СрокиИсполненияПроцессов.ДлительностьИсполненияОзнакомления(Объект);
		
	ИначеЕсли ТипОбъекта = Тип("СправочникСсылка.ШаблоныПоручения") Тогда
		
		Длительность = СрокиИсполненияПроцессов.ДлительностьИсполненияПоручения(Объект);
		
	ИначеЕсли ТипОбъекта = Тип("СправочникСсылка.ШаблоныПриглашения") Тогда
		
		Длительность = ДлительностьИсполненияПриглашения(Объект);
		
	ИначеЕсли ТипОбъекта = Тип("СправочникСсылка.ШаблоныРассмотрения") Тогда
		
		Длительность = СрокиИсполненияПроцессов.ДлительностьИсполненияРассмотрения(Объект);
		
	ИначеЕсли ТипОбъекта = Тип("СправочникСсылка.ШаблоныРегистрации") Тогда
		
		Длительность = СрокиИсполненияПроцессов.ДлительностьИсполненияРегистрации(Объект);
		
	ИначеЕсли ТипОбъекта = Тип("СправочникСсылка.ШаблоныСогласования") Тогда
		
		Длительность = СрокиИсполненияПроцессов.ДлительностьИсполненияСогласования(Объект);
		
	ИначеЕсли ТипОбъекта = Тип("СправочникСсылка.ШаблоныУтверждения") Тогда
		
		Длительность = СрокиИсполненияПроцессов.ДлительностьИсполненияУтверждения(Объект);
		
	КонецЕсли;
	
	КоличествоРабочихЧасовВДне = 
		СрокиИсполненияПроцессов.КоличествоРабочихЧасовПользователя(Неопределено);
	
	СмещениеДни = Цел(Смещение / 86400);
	СмещениеЧасы = Цел((Смещение % 86400) / 3600);
	
	Длительность = Длительность
		+ СмещениеДни * КоличествоРабочихЧасовВДне * 60
		+ СмещениеЧасы * 60;
	
	Длительность = 
		СрокиИсполненияПроцессов.ДлительностьВДняхЧасахМинутах(Длительность, КоличествоРабочихЧасовВДне);
	
	Результат = Новый Структура;
	Результат.Вставить("СрокИсполненияПроцессаДни", Длительность.Дни);
	Результат.Вставить("СрокИсполненияПроцессаЧасы", Длительность.Часы);
	Результат.Вставить("СрокИсполненияПроцессаМинуты", Длительность.Минуты);
	
	Возврат Результат;
	
КонецФункции

// Возвращает длительность исполнения процесса/шаблона в днях, часах и минутах
// по данным схемы.
//
// Параметры:
//  ЭлементыСхемы - ТабличнаяЧасть, ТаблицаЗначений - таблица ЭлементыСхемы схемы.
//  ПараметрыДействий - ТабличнаяЧасть, ТаблицаЗначений - таблица ПараметрыДействий схемы.
//  ПредшественникиЭлементовСхемы - ТабличнаяЧасть, ТаблицаЗначений - таблица ПредшественникиЭлементовСхемы схемы.
//  Смещение - Дата, Число - смещение даты отсчета, см. СмещенияДатыОтсчета.
//
// Возвращаемое значение:
//  Структура
//   * СрокИсполненияПроцессаДни - Число - длительность, кол. целых дней.
//   * СрокИсполненияПроцессаЧасы - Число - длительность, кол. целых часов.
//   * СрокИсполненияПроцессаМинуты - Число - длительность, кол. целых минут.
//
Функция ДлительностьИсполненияКомплексногоПроцессаПоДаннымСхемы(
	ЭлементыСхемы, ПараметрыДействий, ПредшественникиЭлементовСхемы, Смещение) Экспорт
	
	КоличествоРабочихЧасовВДне = 
		СрокиИсполненияПроцессов.КоличествоРабочихЧасовПользователя(Неопределено);
	
	Длительность = ДлительностьИсполненияКомплексногоПроцессаПоДаннымСхемыВМинутах(
		ЭлементыСхемы, ПараметрыДействий, ПредшественникиЭлементовСхемы, КоличествоРабочихЧасовВДне);
	
	Если ЗначениеЗаполнено(Смещение) Тогда
		Если ТипЗнч(Смещение) = Тип("Число") Тогда
			Смещение = Смещение;
		Иначе
			ТекущаяДатаСеанса = ТекущаяДатаСеанса();
			ТекущаяДатаСеанса = ТекущаяДатаСеанса - Секунда(ТекущаяДатаСеанса);
			Если ТекущаяДатаСеанса < Смещение Тогда
				Смещение = Смещение - ТекущаяДатаСеанса;
			Иначе
				Смещение = 0;
			КонецЕсли;
		КонецЕсли;
	Иначе
		Смещение = 0;
	КонецЕсли;
	
	СмещениеДни = Цел(Смещение / 86400);
	СмещениеЧасы = Цел((Смещение % 86400) / 3600);
	
	Длительность = Длительность
		+ СмещениеДни * КоличествоРабочихЧасовВДне * 60
		+ СмещениеЧасы * 60;
	
	Длительность = 
		СрокиИсполненияПроцессов.ДлительностьВДняхЧасахМинутах(Длительность, КоличествоРабочихЧасовВДне);
	
	Результат = Новый Структура;
	Результат.Вставить("СрокИсполненияПроцессаДни", Длительность.Дни);
	Результат.Вставить("СрокИсполненияПроцессаЧасы", Длительность.Часы);
	Результат.Вставить("СрокИсполненияПроцессаМинуты", Длительность.Минуты);
	
	Возврат Результат;
	
КонецФункции

// Возвращает длительность исполнения процесса/шаблона в минутах
// по данным схемы.
//
// Параметры:
//  ЭлементыСхемы - ТабличнаяЧасть, ТаблицаЗначений - таблица ЭлементыСхемы схемы.
//  ПараметрыДействий - ТабличнаяЧасть, ТаблицаЗначений - таблица ПараметрыДействий схемы.
//  ПредшественникиЭлементовСхемы - ТабличнаяЧасть, ТаблицаЗначений - таблица ПредшественникиЭлементовСхемы схемы.
//  КоличествоРабочихЧасовВДне - Число - количество рабочих часов в дне.
//
// Возвращаемое значение:
//  Число.
//
Функция ДлительностьИсполненияКомплексногоПроцессаПоДаннымСхемыВМинутах(
	ЭлементыСхемы, ПараметрыДействий, ПредшественникиЭлементовСхемы,
	КоличествоРабочихЧасовВДне)
	
	ДлительностьПроцесса = 0;
	
	Если ПараметрыДействий.Колонки.Найти("СрокИсполненияПроцессаДни") = Неопределено Тогда
		
		МассивТипов = Новый Массив;
		МассивТипов.Добавить(Тип("Число"));
		ОписаниеТиповЧисло = Новый ОписаниеТипов(МассивТипов);
		
		ПараметрыДействий.Колонки.Добавить("СрокИсполненияПроцессаДни", ОписаниеТиповЧисло);
		ПараметрыДействий.Колонки.Добавить("СрокИсполненияПроцессаЧасы", ОписаниеТиповЧисло);
		ПараметрыДействий.Колонки.Добавить("СрокИсполненияПроцессаМинуты", ОписаниеТиповЧисло);
		
		МассивТипов = Новый Массив;
		МассивТипов.Добавить(Тип("ПеречислениеСсылка.СостоянияБизнесПроцессов"));
		ОписаниеТиповСостояниеПроцесса = Новый ОписаниеТипов(МассивТипов);
		ПараметрыДействий.Колонки.Добавить("СостояниеПроцесса", ОписаниеТиповСостояниеПроцесса);
		
		КэшДанныхДействий = 
			РаботаСКомплекснымиБизнесПроцессамиСервер.КэшДанныхДействий(ПараметрыДействий);
		
		СрокиИсполненияПроцессовКлиентСерверКОРП.
			ЗаполнитьСрокиВПараметрахДействийСхемыКомплексногоПроцесса(
				ПараметрыДействий,
				ЭлементыСхемы,
				КэшДанныхДействий);
		
	КонецЕсли;
	
	ЭлементСхемыСтарт = ЭлементыСхемы.Найти(
		Перечисления.ТипыЭлементовСхемыКомплексногоПроцесса.Старт, "Тип");
	
	ИмяЭлементаСтарта = "";
	Если ЭлементСхемыСтарт <> Неопределено Тогда
		ИмяЭлементаСтарта = ЭлементСхемыСтарт.Имя;
	КонецЕсли;
	
	ПутиСхемы = РаботаСКомплекснымиБизнесПроцессамиКлиентСервер.ПутиСхемыКомплексногоПроцесса(
		ИмяЭлементаСтарта, ПредшественникиЭлементовСхемы);
	
	// Сформируем кэш строк параметров действий.
	КэшПараметровДействий = Новый Соответствие;
	Для Каждого ПараметрыДействия Из ПараметрыДействий Цикл
		КэшПараметровДействий.Вставить(ПараметрыДействия.Имя, ПараметрыДействия);
	КонецЦикла;
	
	Для Каждого ПутьСхемы Из ПутиСхемы Цикл
		
		СрокИсполненияПути = 0;
		
		Для Каждого ЭлементПути Из ПутьСхемы Цикл
			
			ПараметрыДействия = КэшПараметровДействий.Получить(ЭлементПути);
			
			// Если параметры отсутвуют, то элемент пути не влияет на срок исполнения процесса.
			Если ПараметрыДействия = Неопределено Тогда
				Продолжить;
			КонецЕсли;
			
			// Если попадается действие с нулевой длительностью, то
			// считаем, что длительность комплексного процесса
			// не определена.
			Если ПараметрыДействия.СрокИсполненияПроцессаДни = 0
				И ПараметрыДействия.СрокИсполненияПроцессаЧасы = 0
				И ПараметрыДействия.СрокИсполненияПроцессаМинуты = 0 Тогда
				
				Возврат 0;
			КонецЕсли;
			
			СрокИсполненияПути = СрокИсполненияПути
				+ ПараметрыДействия.СрокИсполненияПроцессаДни * КоличествоРабочихЧасовВДне * 60
				+ ПараметрыДействия.СрокИсполненияПроцессаЧасы * 60
				+ ПараметрыДействия.СрокИсполненияПроцессаМинуты;
			
		КонецЦикла;
		
		ДлительностьПроцесса = Макс(ДлительностьПроцесса, СрокИсполненияПути);
		
	КонецЦикла;
	
	Возврат ДлительностьПроцесса;
	
КонецФункции

// Возвращает длительность комплексного процесса/шаблона в минутах.
// Используется функцией ДлительностьИсполненияПроцесса.
//
// Параметры:
//  Процесс - СправочникСсылка.ШаблоныКомплексныхБизнесПроцессов,
//            БизнесПроцессСсылка.КомплексныйПроцесс,
//            СправочникОбъект.ШаблоныКомплексныхБизнесПроцессов,
//            БизнесПроцессОбъект.КомплексныйПроцесс,
//            ДанныеФормыСтруктура - ссылка/объект на процесс/шаблон, схема процесса в виде данных формы.
//
// Возвращаемое значение:
//  Число - длительность процесса/шаблона в минутах.
//
Функция ДлительностьИсполненияКомплексногоПроцесса(Объект)
	
	УстановитьПривилегированныйРежим(Истина);
	
	ДлительностьПроцесса = 0;
	
	КоличествоРабочихЧасовВДне = 
		СрокиИсполненияПроцессов.КоличествоРабочихЧасовПользователя(Неопределено);
	
	ТипОбъекта = ТипЗнч(Объект);
	
	Этапы = Неопределено;
	ПредшественникиЭтапов = Неопределено;
	ПараметрыДействий = Неопределено;
	ПредшественникиЭлементовСхемы = Неопределено;
	ЭлементыСхемы = Неопределено;
	
	Если ТипОбъекта = Тип("ДанныеФормыСтруктура") Тогда
		
		Если ТипЗнч(Объект.Ссылка) = Тип("СправочникСсылка.СхемыКомплексныхПроцессов") Тогда
			ПараметрыДействий = Объект.ПараметрыДействий.Выгрузить();
			ПредшественникиЭлементовСхемы = Объект.ПредшественникиЭлементовСхемы.Выгрузить();
			ЭлементыСхемы = Объект.ЭлементыСхемы.Выгрузить();
		Иначе
			Если ЗначениеЗаполнено(Объект.Схема) Тогда
				РеквизитыСхемы = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
					Объект.Схема, "ПараметрыДействий, ПредшественникиЭлементовСхемы, ЭлементыСхемы");
				ПараметрыДействий = РеквизитыСхемы.ПараметрыДействий.Выгрузить();
				ПредшественникиЭлементовСхемы = РеквизитыСхемы.ПредшественникиЭлементовСхемы.Выгрузить();
				ЭлементыСхемы = РеквизитыСхемы.ЭлементыСхемы.Выгрузить();
			Иначе
				Этапы = Объект.Этапы.Выгрузить();
				ПредшественникиЭтапов = Объект.ПредшественникиЭтапов.Выгрузить();
				
				ИсключитьУдаленныеЭтапыИзТаблицКомплексногоПроцесса(Этапы, ПредшественникиЭтапов);
			КонецЕсли;
		КонецЕсли;
		
	ИначеЕсли ОбщегоНазначения.ЭтоСсылка(ТипОбъекта) Тогда
		
		РеквизитыПроцесса = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
			Объект,"Схема, Этапы, ПредшественникиЭтапов");
		
		Если ЗначениеЗаполнено(РеквизитыПроцесса.Схема) Тогда
			РеквизитыСхемы = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
				РеквизитыПроцесса.Схема, "ПараметрыДействий, ПредшественникиЭлементовСхемы, ЭлементыСхемы");
			ПараметрыДействий = РеквизитыСхемы.ПараметрыДействий.Выгрузить();
			ПредшественникиЭлементовСхемы = РеквизитыСхемы.ПредшественникиЭлементовСхемы.Выгрузить();
			ЭлементыСхемы = РеквизитыСхемы.ЭлементыСхемы.Выгрузить();
		Иначе
			Этапы = РеквизитыПроцесса.Этапы.Выгрузить();
			ПредшественникиЭтапов = РеквизитыПроцесса.ПредшественникиЭтапов.Выгрузить();
		КонецЕсли;
		
	Иначе
		
		Если ЗначениеЗаполнено(Объект.Схема) Тогда
			РеквизитыСхемы = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
				Объект.Схема, "ПараметрыДействий, ПредшественникиЭлементовСхемы, ЭлементыСхемы");
			ПараметрыДействий = РеквизитыСхемы.ПараметрыДействий.Выгрузить();
			ПредшественникиЭлементовСхемы = РеквизитыСхемы.ПредшественникиЭлементовСхемы.Выгрузить();
			ЭлементыСхемы = РеквизитыСхемы.ЭлементыСхемы.Выгрузить();
		Иначе
			
			Если ТипОбъекта = Тип("Структура") Тогда
				Этапы = Объект.Этапы;
				ПредшественникиЭтапов = Объект.ПредшественникиЭтапов;
			Иначе
				Этапы = Объект.Этапы.Выгрузить();
				ПредшественникиЭтапов = Объект.ПредшественникиЭтапов.Выгрузить();
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
	Если ПараметрыДействий <> Неопределено Тогда
		
		ДлительностьПроцесса = ДлительностьИсполненияКомплексногоПроцессаПоДаннымСхемыВМинутах(
			ЭлементыСхемы, ПараметрыДействий, ПредшественникиЭлементовСхемы, КоличествоРабочихЧасовВДне);
		
	Иначе
		
		Если Этапы.Колонки.Найти("СрокИсполненияПроцессаДни") = Неопределено Тогда
			
			МассивТипов = Новый Массив;
			МассивТипов.Добавить(Тип("Число"));
			ОписаниеТиповЧисло = Новый ОписаниеТипов(МассивТипов);
			Этапы.Колонки.Добавить("СрокИсполненияПроцессаДни", ОписаниеТиповЧисло);
			Этапы.Колонки.Добавить("СрокИсполненияПроцессаМинуты", ОписаниеТиповЧисло);
			Этапы.Колонки.Добавить("СрокИсполненияПроцессаЧасы", ОписаниеТиповЧисло);
			
			ЗаполнитьСрокиИсполненияЭтаповКомплексногоПроцесса(Этапы);
			
		КонецЕсли;
		
		ПредшественникиДляОпределенияПутей = Новый Массив;
		Для Каждого СтрПредшественник Из ПредшественникиЭтапов Цикл
			СтруктураСтрПредшественника = Новый Структура;
			СтруктураСтрПредшественника.Вставить(
				"Предшественник", СтрПредшественник.ИдентификаторПредшественника);
			СтруктураСтрПредшественника.Вставить(
				"Последователь", СтрПредшественник.ИдентификаторПоследователя);
			ПредшественникиДляОпределенияПутей.Добавить(СтруктураСтрПредшественника);
		КонецЦикла;
		
		ПараметрыПутей = 
		СрокиИсполненияПроцессовКлиентСерверКОРП.ПараметрыПутей(ПредшественникиДляОпределенияПутей);
		
		ПутиКомплексногоПроцесса = ПараметрыПутей.Пути;
		
		// Определим максимальную длительность исполнения комплекнсого процесса в минутах.
		
		СоотвествияИдентификаторовИСтрокЭтапов = Новый Соответствие;
		Для Каждого Путь Из ПутиКомплексногоПроцесса Цикл
			
			СрокИсполненияПути = 0;
			
			Для Каждого ЭтапПути Из Путь Цикл
				
				Если ЭтапПути = УникальныйИдентификаторПустой() Тогда
					Продолжить;
				КонецЕсли;
				
				СтрЭтап = СоотвествияИдентификаторовИСтрокЭтапов.Получить(ЭтапПути);
				Если СтрЭтап = Неопределено Тогда
					Отбор = Новый Структура;
					Отбор.Вставить("ИдентификаторЭтапа", ЭтапПути);
					НайденныеЭтапы = Этапы.НайтиСтроки(Отбор);
					Если НайденныеЭтапы.Количество() = 0 Тогда
						Возврат 0;
					КонецЕсли;
					СтрЭтап = НайденныеЭтапы[0];
					СоотвествияИдентификаторовИСтрокЭтапов.Вставить(ЭтапПути, СтрЭтап);
				КонецЕсли;
				
				// Если попадается этап с нулевой длительностью, то
				// считаем, что длительность комплексного процесса
				// не определена.
				Если СтрЭтап.СрокИсполненияПроцессаДни = 0
					И СтрЭтап.СрокИсполненияПроцессаЧасы = 0
					И СтрЭтап.СрокИсполненияПроцессаМинуты = 0 Тогда
					
					Возврат 0;
				КонецЕсли;
				
				СрокИсполненияПути = СрокИсполненияПути
					+ СтрЭтап.СрокИсполненияПроцессаДни * КоличествоРабочихЧасовВДне * 60
					+ СтрЭтап.СрокИсполненияПроцессаЧасы * 60
					+ СтрЭтап.СрокИсполненияПроцессаМинуты;
				
			КонецЦикла;
			
			ДлительностьПроцесса = Макс(ДлительностьПроцесса, СрокИсполненияПути);
			
		КонецЦикла;
		
	КонецЕсли;
	
	Возврат ДлительностьПроцесса;
	
КонецФункции

// Возвращает длительность процесса/шаблона Приглашения в минутах.
// Используется функцией ДлительностьИсполненияПроцесса.
//
// Параметры:
//  Процесс - СправочникСсыка,
//            БизнесПроцессСсылка,
//            СправочникОбъект,
//            БизнесПроцессОбъект,
//            ДанныеФормыСтруктура - ссылка или объект на процесс/шаблон.
//
// Возвращаемое значение:
//  Число - длительность процесса/шаблона в минутах.
//
Функция ДлительностьИсполненияПриглашения(Процесс)
	
	УстановитьПривилегированныйРежим(Истина);
	
	ДлительностьПроцесса = 0;
	
	ТипПроцесса = ТипЗнч(Процесс);
	Если ОбщегоНазначения.ЭтоСсылка(ТипПроцесса) Тогда
		СтрокаРеквизитовПроцессов = 
			"Исполнители, 
			|Автор, СрокОбработкиРезультатовДни, СрокОбработкиРезультатовЧасы, СрокОбработкиРезультатовМинуты,
			|КоличествоИтераций";
		РеквизитыПроцесса = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
			Процесс, СтрокаРеквизитовПроцессов);
		РеквизитыПроцесса.Исполнители = РеквизитыПроцесса.Исполнители.Выгрузить();
	Иначе
		РеквизитыПроцесса = Процесс;
	КонецЕсли;
	
	ДлительностьИсполненияИсполнителями = 
		СрокиИсполненияПроцессов.ДлительностьИсполненияПроцессаУчастникамиВМинутах(
			РеквизитыПроцесса.Исполнители);
	
	ДлительностьИсполненияАвтором = 
		СрокиИсполненияПроцессов.ДлительностьИсполненияУчастникаПроцессаВМинутах(
			РеквизитыПроцесса.Автор,
			РеквизитыПроцесса.СрокОбработкиРезультатовДни,
			РеквизитыПроцесса.СрокОбработкиРезультатовЧасы,
			РеквизитыПроцесса.СрокОбработкиРезультатовМинуты);
	
	КоличествоИтераций = 1;
	Если ЗначениеЗаполнено(РеквизитыПроцесса.КоличествоИтераций) Тогда
		КоличествоИтераций = РеквизитыПроцесса.КоличествоИтераций;
	КонецЕсли;
	
	Если ДлительностьИсполненияИсполнителями <> 0 И ДлительностьИсполненияАвтором <> 0 Тогда
		ДлительностьПроцесса = 
			(ДлительностьИсполненияИсполнителями + ДлительностьИсполненияАвтором) * КоличествоИтераций;
	КонецЕсли;
	
	Возврат ДлительностьПроцесса;
	
КонецФункции

// Корректирует срок исполнения (точный и относительный) ответственного, если
// он меньше общего срока остальных исполнителей.
//
// Параметры:
//  Исполнители - ТабличнаяЧасть, ДанныеФормыКоллекция - таблица исполнителей Исполнения.
//  ДатаОтсчета - Дата - дата отсчета для расчета сроков исполнителей.
//
Процедура СкорректироватьСрокИсполненияОтветственного(Исполнители, ДатаОтсчета) Экспорт
	
	Если Исполнители.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Если Не СрокиИсполненияПроцессовКлиентСервер.ЭтоСтрокаОтвественного(Исполнители[0]) Тогда
		Возврат;
	КонецЕсли;
	
	СрокИсполненияВсемиИсполнителями = Дата(1,1,1);
	Для Каждого СтрокаИсполнителя Из Исполнители Цикл
		СрокИсполненияВсемиИсполнителями = 
			Макс(СрокИсполненияВсемиИсполнителями, СтрокаИсполнителя.СрокИсполнения);
	КонецЦикла;
	
	Если Исполнители[0].СрокИсполнения >= СрокИсполненияВсемиИсполнителями Тогда
		Возврат;
	КонецЕсли;
	
	// Присвоим максимальный срок исполнения всеми участниками ответственному.
	Исполнители[0].СрокИсполнения = СрокИсполненияВсемиИсполнителями;
	
	// Рассчитаем  и присвоим новую длительность срока исполнения для ответственного.
	ПараметрыСрокаИсполнения = 
		СрокиИсполненияПроцессов.ПараметрыСрокаИсполненияДляОпределенияДлительности();
	
	ПараметрыСрокаИсполнения.СрокИсполнения = СрокИсполненияВсемиИсполнителями;
	ПараметрыСрокаИсполнения.ДатаОтсчета = ДатаОтсчета;
	ПараметрыСрокаИсполнения.ГрафикРаботы = ГрафикРаботыУчастникаПроцесса(Исполнители[0].Исполнитель);
	
	ДлительностьСрокаОтветственногоИсполнителя = 
		СрокиИсполненияПроцессов.ДлительностьПоСрокуИсполнения(ПараметрыСрокаИсполнения);
	
	Исполнители[0].СрокИсполненияДни = ДлительностьСрокаОтветственногоИсполнителя.Дни;
	Исполнители[0].СрокИсполненияЧасы = ДлительностьСрокаОтветственногоИсполнителя.Часы;
	Исполнители[0].СрокИсполненияМинуты = ДлительностьСрокаОтветственногоИсполнителя.Минуты;
	
КонецПроцедуры

// Возвращает количество рабочих часов пользователя с учетом графика работ.
//
// Используется для переопределения одноименной функции в модуле СрокиИсполненияПроцессов.
// Вместо текущей следует использовать функцию из модуля СрокиИсполненияПроцессов.
// 
// Параметры:
//  Пользователь - СправочникСсылка.Пользователи - участник процесса.
//
// Возвращаемое значение:
//  Число - количество рабочих часов в дней для пользователя.
//
Функция КоличествоРабочихЧасовПользователя(Пользователь) Экспорт
	
	КоличествоРабочихЧасовВДне = 24;
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьГрафикиРаботы") Тогда
		
		Если Пользователь = Неопределено Тогда
			ГрафикРаботыПользователя = ГрафикиРаботы.ПолучитьОсновнойГрафикРаботы();
		Иначе
			ГрафикРаботыПользователя = ГрафикРаботыУчастникаПроцесса(Пользователь);
		КонецЕсли;
		
		КоличествоРабочихЧасовВДне = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(
			ГрафикРаботыПользователя, "КоличествоРабочихЧасовВДне");
			
	КонецЕсли;
	
	Возврат КоличествоРабочихЧасовВДне;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции_ПереносСроков

// Возвращает структуру сроков процесса.
//
// Используется для переопределения одноименной функции в модуле СрокиИсполненияПроцессов.
// Вместо текущей следует использовать функцию из модуля СрокиИсполненияПроцессов.
//
// Врозвращаемое значение:
//  Ссылка - БизнесПроцессСсылка - ссылка на процесс
//  СрокиИсполнителей - Соотвествие - соотвествие в котором
//                      Ключа - ссылка на задачу или точку маршрута, Значение - срок ее исполнения.
//  СрокИсполненияПроцесса - Дата
//
Функция СтруктураСроковПроцессаДляПроверкиИзменения() Экспорт
	
	СтруктураСроков = Новый Структура;
	СтруктураСроков.Вставить("Ссылка");
	СтруктураСроков.Вставить("СрокиИсполнителей", Новый Соответствие);
	СтруктураСроков.Вставить("СрокИсполненияПроцесса", Дата(1,1,1));
	
	Возврат СтруктураСроков;
	
КонецФункции

// Проверяет изменение сроков процесса и невыполненных задач.
// В случае изменения присваивает реквизитам СрокИсполненияПроцессаИзменен и СрокиИсполненияЗадачИзменены
// формы значение Иситина.
//
// Используется для переопределения одноименной функции в модуле СрокиИсполненияПроцессов.
// Вместо текущей следует использовать функцию из модуля СрокиИсполненияПроцессов.
//
// Параметры:
//  СсылкаНаПроцесс - БизнессПроцессСсылка - ссылкан на процесс.
//  Сроки - Структура - см. Функция СтруктураСроковПроцессаДляПроверкиИзменения()
//  Форма - УправляемаяФорма - форма процесса.
//
Процедура ПроверитьИзменениеСроковПроцесса(СсылкаНаПроцесс, НовыеСрокиПроцесса, Форма) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Форма.СрокИсполненияПроцессаИзменен = Ложь;
	
	СтруктураПоиска = Новый Структура;
	СтруктураПоиска.Вставить("СрокиИсполненияЗадачИзменены", Неопределено);
	ЗаполнитьЗначенияСвойств(СтруктураПоиска, Форма);
	ЕстьРеквизитСрокиИсполненияЗадачИзменены =
		(СтруктураПоиска.СрокиИсполненияЗадачИзменены <> Неопределено);
		
	Если ЕстьРеквизитСрокиИсполненияЗадачИзменены Тогда
		Форма.СрокиИсполненияЗадачИзменены = Ложь;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(СсылкаНаПроцесс) Тогда
		Возврат;
	КонецЕсли;
	
	// Проверка изменения срока исполнения процесса
	СрокИсполненияПроцессаПоСсылке = 
		ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СсылкаНаПроцесс, "СрокИсполненияПроцесса");
	Если СрокИсполненияПроцессаПоСсылке <> НовыеСрокиПроцесса.СрокИсполненияПроцесса Тогда
		Форма.СрокИсполненияПроцессаИзменен = Истина;
	КонецЕсли;
	
	Если ЕстьРеквизитСрокиИсполненияЗадачИзменены
		И НовыеСрокиПроцесса.СрокиИсполнителей.Количество() > 0 Тогда
	
		// Проверка изменения сроков задач
		Запрос = Новый Запрос;
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	ЗадачаИсполнителя.Ссылка,
			|	ЗадачаИсполнителя.СрокИсполнения,
			|	ЗадачаИсполнителя.ТочкаМаршрута
			|ИЗ
			|	Задача.ЗадачаИсполнителя КАК ЗадачаИсполнителя
			|ГДЕ
			|	ЗадачаИсполнителя.Выполнена = ЛОЖЬ
			|	И ЗадачаИсполнителя.ИсключенаИзПроцесса = ЛОЖЬ
			|	И ЗадачаИсполнителя.ПометкаУдаления = ЛОЖЬ
			|	И ЗадачаИсполнителя.СостояниеБизнесПроцесса = ЗНАЧЕНИЕ(Перечисление.СостоянияБизнесПроцессов.Активен)
			|	И ЗадачаИсполнителя.БизнесПроцесс = &БизнесПроцесс";
		
		Запрос.УстановитьПараметр("БизнесПроцесс", СсылкаНаПроцесс);
		
		Выборка = Запрос.Выполнить().Выбрать();
		
		Пока Выборка.Следующий() Цикл
			СрокИсполнения = НовыеСрокиПроцесса.СрокиИсполнителей.Получить(Выборка.Ссылка);
			Если СрокИсполнения = Неопределено Тогда
				СрокИсполнения = НовыеСрокиПроцесса.СрокиИсполнителей.Получить(Выборка.ТочкаМаршрута);
			КонецЕсли;
				
			Если СрокИсполнения <> Неопределено И СрокИсполнения <> Выборка.СрокИсполнения Тогда
				Форма.СрокиИсполненияЗадачИзменены = Истина;
				Прервать;
			КонецЕсли;
		КонецЦикла;
	
	КонецЕсли;
	
КонецПроцедуры

// Обработчик подписки СрокИсполненияПроцессовПередЗаписью
// Обработчик предназначен для распространения изменения срока
// по иерархии процессов.
//
// Используется для переопределения одноименной процедуры в модуле СрокиИсполненияПроцессов.
// Вместо текущей следует использовать процедуру из модуля СрокиИсполненияПроцессов.
//
Процедура СрокИсполненияПроцессовПередЗаписью(Источник, Отказ) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если Источник.ДополнительныеСвойства.Свойство("ВидЗаписи")
		И Источник.ДополнительныеСвойства.ВидЗаписи <> "ЗаписьСОбновлениемОбщегоСпискаИПереносомСроковВИерархииПроцессов" Тогда
		
		Возврат;
	КонецЕсли;
	
	Если Источник.ЭтоНовый() Тогда
		Возврат;
	КонецЕсли;
	
	СрокИсполненияПроцесса = 
		ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Источник.Ссылка, "СрокИсполненияПроцесса");
		
	Источник.ДополнительныеСвойства.Вставить("СтарыйСрокИсполненияПроцесса", СрокИсполненияПроцесса);
	
КонецПроцедуры

// Обработчик подписки СрокИсполненияПроцессовПриЗаписи
// Обработчик предназначен для распространения изменения срока
// по иерархии процессов.
//
// Используется для переопределения одноименной процедуры в модуле СрокиИсполненияПроцессов.
// Вместо текущей следует использовать процедуру из модуля СрокиИсполненияПроцессов.
//
Процедура СрокИсполненияПроцессовПриЗаписи(Источник, Отказ) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если Источник.ДополнительныеСвойства.Свойство("ВидЗаписи")
		И Источник.ДополнительныеСвойства.ВидЗаписи <> "ЗаписьСОбновлениемОбщегоСпискаИПереносомСроковВИерархииПроцессов" Тогда
		
		Возврат;
	КонецЕсли;
	
	Если Не Источник.ДополнительныеСвойства.Свойство("СтарыйСрокИсполненияПроцесса")
		Или Источник.ДополнительныеСвойства.СтарыйСрокИсполненияПроцесса = Источник.СрокИсполненияПроцесса Тогда
		
		Возврат;
	КонецЕсли;
	
	ЭтоШаблон = ШаблоныБизнесПроцессовКлиентСервер.ЭтоШаблонПроцесса(Источник.Ссылка);
	
	ВедущийПроцессЯвляетсяКомплексным = Ложь;
	
	ТекущийУзелОбмена = РаботаСБизнесПроцессами.ЭтотУзелОбменаДляОбработкиПроцессов();
	
	Если ЭтоШаблон Тогда
		
		Если Не ЗначениеЗаполнено(Источник.КомплексныйПроцесс) Тогда
			Возврат;
		КонецЕсли;
		
		РеквизитыКомплексногоПроцессаСтрокой = 
			"Стартован,
			|ПометкаУдаления,
			|Завершен,
			|Состояние,
			|УзелОбмена";
		
		РеквизитыКомплексногоПроцесса = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
			Источник.КомплексныйПроцесс, РеквизитыКомплексногоПроцессаСтрокой);
		
		Если Не РеквизитыКомплексногоПроцесса.Стартован
			Или РеквизитыКомплексногоПроцесса.ПометкаУдаления
			Или РеквизитыКомплексногоПроцесса.Завершен
			Или РеквизитыКомплексногоПроцесса.УзелОбмена <> ТекущийУзелОбмена
			
			Или РеквизитыКомплексногоПроцесса.Состояние <> 
				Перечисления.СостоянияБизнесПроцессов.Активен Тогда
				
			Возврат;
		КонецЕсли;
		
		ВедущийПроцесс = Источник.ВладелецШаблона;
		
		ВедущийПроцессЯвляетсяКомплексным = Истина;
		
	Иначе
		
		Если Не ЗначениеЗаполнено(Источник.ВедущаяЗадача)
			Или Не Источник.Стартован
			Или Источник.ПометкаУдаления
			Или Источник.Завершен
			Или Источник.УзелОбмена <> ТекущийУзелОбмена
			Или Источник.Состояние <> Перечисления.СостоянияБизнесПроцессов.Активен Тогда
			
			Возврат;
		КонецЕсли;
		
		ВедущаяЗадача = Источник.ВедущаяЗадача;
		ВедущийПроцесс = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ВедущаяЗадача, "БизнесПроцесс");
		
		Если ТипЗнч(ВедущийПроцесс) = Тип("БизнесПроцессСсылка.КомплексныйПроцесс") Тогда
			ВедущийПроцессЯвляетсяКомплексным = Истина;
		КонецЕсли;
		
	КонецЕсли;
	
	Если ВедущийПроцессЯвляетсяКомплексным Тогда
		
		ИдентификаторыИзмененныхДействий = Новый Массив;
		
		НовыйВедущийПроцесс = ВедущийПроцесс;
		ВедущийПроцесс = Источник.Ссылка;
		
		Пока ЗначениеЗаполнено(НовыйВедущийПроцесс) Цикл
			
			РеквизитыВедущегоПроцесса = 
				ОбщегоНазначения.ЗначенияРеквизитовОбъекта(НовыйВедущийПроцесс, "ИдентификаторСсылки, Схема, Этапы");
			
			Если ЗначениеЗаполнено(РеквизитыВедущегоПроцесса.Схема) Тогда
				ПараметрыДействий = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(
					РеквизитыВедущегоПроцесса.Схема, "ПараметрыДействий").Выгрузить();
				ИмяРеквизитаШаблон = "ШаблонПроцесса";
				ИмяРеквизитаПроцесс = "Процесс";
				ИмяРеквизитаИдентификатор = "Имя";
			Иначе
				ПараметрыДействий = РеквизитыВедущегоПроцесса.Этапы.Выгрузить();
				ИмяРеквизитаШаблон = "ШаблонБизнесПроцесса";
				ИмяРеквизитаПроцесс = "ЗапущенныйБизнесПроцесс";
				ИмяРеквизитаИдентификатор = "ИдентификаторЭтапа";
			КонецЕсли;
			
			Если ШаблоныБизнесПроцессовКлиентСервер.ЭтоШаблонПроцесса(ВедущийПроцесс) Тогда
				ПараметрыДействия = ПараметрыДействий.Найти(ВедущийПроцесс, ИмяРеквизитаШаблон);
			Иначе
				ПараметрыДействия = ПараметрыДействий.Найти(ВедущийПроцесс, ИмяРеквизитаПроцесс);
			КонецЕсли;
			
			Если ПараметрыДействия = Неопределено Тогда
				Возврат;
			КонецЕсли;
			
			Если ВедущийПроцесс = Источник.Ссылка Тогда
				Если ЗначениеЗаполнено(РеквизитыВедущегоПроцесса.Схема) Тогда
					ИдентификаторИзмененногоДействия = 
						Строка(РеквизитыВедущегоПроцесса.ИдентификаторСсылки)
						+ "_"
						+ ПараметрыДействия[ИмяРеквизитаИдентификатор];
				Иначе
					ИдентификаторИзмененногоДействия = ПараметрыДействия[ИмяРеквизитаИдентификатор];
				КонецЕсли;
				ИдентификаторыИзмененныхДействий.Добавить(ИдентификаторИзмененногоДействия);
			КонецЕсли;
			
			ВедущийПроцесс = НовыйВедущийПроцесс;
			
			Если ШаблоныБизнесПроцессовКлиентСервер.ЭтоШаблонПроцесса(НовыйВедущийПроцесс) Тогда
				НовыйВедущийПроцесс = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(НовыйВедущийПроцесс, "ВладелецШаблона");
			Иначе
				ВедущаяЗадача = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(НовыйВедущийПроцесс, "ВедущаяЗадача");
				Если ЗначениеЗаполнено(ВедущаяЗадача) Тогда
					НовыйВедущийПроцесс = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ВедущаяЗадача, "БизнесПроцесс");
				Иначе
					НовыйВедущийПроцесс = Неопределено;
				КонецЕсли;
			КонецЕсли;
			
		КонецЦикла;
		
		ЗаблокироватьДанныеДляРедактирования(ВедущийПроцесс);
		ОбъектВедущегоПроцесса = ВедущийПроцесс.ПолучитьОбъект();
		
		// Учет переносов сроков
		ПереносСроковВыполненияЗадач.ПередатьПричинуИЗаявкуНаПереносаСрока(
			ОбъектВедущегоПроцесса, Источник.ДополнительныеСвойства);
		
		// Расчет сроков
		СтруктураДляРасчета = СтруктураДляРасчетаСрокаКомплексногоПроцесса(ОбъектВедущегоПроцесса);
		
		ПараметрыДляРасчетаСроков = СрокиИсполненияПроцессов.ПараметрыДляРасчетаСроков();
		ПараметрыДляРасчетаСроков.ДатаОтсчета = ОбъектВедущегоПроцесса.ДатаНачала;
		
		ДанныеДляРасчета = ДанныеДляРасчетаСроковКомплексногоПроцесса(
			СтруктураДляРасчета, ПараметрыДляРасчетаСроков, ИдентификаторыИзмененныхДействий);
		
		ПометитьРассчитаннымСрокПроцессаВТаблицеСроков(ДанныеДляРасчета.Сроки, Источник.Ссылка);
		
		РассчитатьСрокиИсполнения(
			ДанныеДляРасчета.Сроки, ДанныеДляРасчета.Предшественники,
			ДанныеДляРасчета.IDРассчитанногоСрок);
	
		ЗаполнитьСрокиКомплексногоПроцесса(
			ОбъектВедущегоПроцесса,
			ДанныеДляРасчета.Сроки,
			ДанныеДляРасчета.ИдентификаторыСроков);
		
		РаботаСБизнесПроцессами.ЗаписатьПроцесс(ОбъектВедущегоПроцесса, "ЗаписьСОбновлениемОбщегоСпискаПроцессов");
		ПротоколированиеРаботыПользователей.ЗаписатьИзменение(ОбъектВедущегоПроцесса.Ссылка);
		
		РазблокироватьДанныеДляРедактирования(ВедущийПроцесс);
		
	Иначе // если ведущим является процесс обработки документов
		
		ЗаблокироватьДанныеДляРедактирования(ВедущийПроцесс);
		ОбъектВедущегоПроцесса = ВедущийПроцесс.ПолучитьОбъект();
		
		// Учет переносов сроков
		ПереносСроковВыполненияЗадач.ПередатьПричинуИЗаявкуНаПереносаСрока(
			ОбъектВедущегоПроцесса, Источник.ДополнительныеСвойства);
			
		СтруктураДляРасчетаСрока = 
			СрокиИсполненияПроцессов.СтруктураДляРасчетаСрокаСоставногоПроцесса(ОбъектВедущегоПроцесса);
		
		ПараметрыДляРасчетаСроков = СрокиИсполненияПроцессов.ПараметрыДляРасчетаСроков();
		ПараметрыДляРасчетаСроков.ДатаОтсчета = ОбъектВедущегоПроцесса.ДатаНачала;
		
		СрокиИсполненияПроцессов.РассчитатьСрокиСоставногоПроцесса(
			СтруктураДляРасчетаСрока, ПараметрыДляРасчетаСроков);
			
		ОбъектВедущегоПроцесса.СрокИсполненияПроцесса = СтруктураДляРасчетаСрока.СрокИсполненияПроцесса;
		
		РаботаСБизнесПроцессами.ЗаписатьПроцесс(ОбъектВедущегоПроцесса, "ЗаписьСОбновлениемОбщегоСпискаПроцессов");
		ПротоколированиеРаботыПользователей.ЗаписатьИзменение(ОбъектВедущегоПроцесса.Ссылка);
		
		РазблокироватьДанныеДляРедактирования(ВедущийПроцесс);
		
	КонецЕсли;
	
КонецПроцедуры

// Помечает рассчитанным срок исполнения процесса в таблице сроков для расчета.
//
// Параметры:
//  Сроки - ТаблицаЗначений - см. ПустаяТаблицаСроков
//  Процесс - БизнесПроцессСсылка, СправочникСсылка - ссылка на процесс/действие в таблице сроков.
//
Процедура ПометитьРассчитаннымСрокПроцессаВТаблицеСроков(Сроки, Процесс)
	
	Для Каждого СтрокаСрок Из Сроки Цикл
		
		Если СтрокаСрок.ПодчиненныеСроки.Количество() = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		Если СтрокаСрок.ПодчиненныеСроки.Ссылка = Процесс Тогда
			СтрокаСрок.Пройден = Истина;
		КонецЕсли;
		
		ПометитьРассчитаннымСрокПроцессаВТаблицеСроков(СтрокаСрок.ПодчиненныеСроки.Сроки, Процесс);
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции_КарточкиПроцессовИШаблонов

// Подготавливает форму процесса/шаблона для работы со сроками.
//
// Используется для переопределения одноименной процедуры в модуле СрокиИсполненияПроцессов.
// Вместо текущей следует использовать процедуру из модуля СрокиИсполненияПроцессов.
//
// Параметры:
//  Форма - УправляемаяФормы - форма процесса/шаблона.
//
Процедура ПриСозданииНаСервере(Форма) Экспорт
	
	Объект = Форма.Объект;
	
	Форма.ИспользоватьДатуИВремяВСрокахЗадач = 
		ПолучитьФункциональнуюОпцию("ИспользоватьДатуИВремяВСрокахЗадач");
		
	ПодсказкаВводаПоляСрокИсполнения = 
		СрокиИсполненияПроцессов.ПодсказкаВводаПоляСрокИсполнения(Форма);
	
	ШиринаПоляСрок = СрокиИсполненияПроцессов.ШиринаПоляСрокИсполнения(Форма);
	
	ИспользоватьГрафикиРаботы = ПолучитьФункциональнуюОпцию("ИспользоватьГрафикиРаботы");
	СтрокаЗаменыДляВсплывающейПодсказки = НСтр("ru = '(по графику работ)'; en = '(according to schedule)'");
	
	Если Форма.Элементы.Найти("ИсполнителиСрокИсполненияПредставление") <> Неопределено Тогда
		Форма.Элементы.ИсполнителиСрокИсполненияПредставление.ПодсказкаВвода = 
			ПодсказкаВводаПоляСрокИсполнения;
		Форма.Элементы.ИсполнителиСрокИсполненияПредставление.Ширина = ШиринаПоляСрок;
		
		Форма.Элементы.ИсполнителиСрокИсполненияПредставление.КнопкаВыбора = 
			Форма.ВозможенВыборТочнойДатыВСроках;
		
		Если Не ИспользоватьГрафикиРаботы Тогда
			Форма.Элементы.ИсполнителиСрокИсполненияПредставление.Подсказка = СтрЗаменить(
				Форма.Элементы.ИсполнителиСрокИсполненияПредставление.Подсказка,
				СтрокаЗаменыДляВсплывающейПодсказки, "");
		КонецЕсли;
	КонецЕсли;
	
	Если Форма.Элементы.Найти("СрокИсполненияПредставление") <> Неопределено Тогда
		Форма.Элементы.СрокИсполненияПредставление.ПодсказкаВвода = 
			ПодсказкаВводаПоляСрокИсполнения;
		Форма.Элементы.СрокИсполненияПредставление.Ширина = ШиринаПоляСрок;
		
		Форма.Элементы.СрокИсполненияПредставление.КнопкаВыбора = 
			Форма.ВозможенВыборТочнойДатыВСроках;
			
		Если Не ИспользоватьГрафикиРаботы Тогда
			Форма.Элементы.СрокИсполненияПредставление.Подсказка = СтрЗаменить(
				Форма.Элементы.СрокИсполненияПредставление.Подсказка,
				СтрокаЗаменыДляВсплывающейПодсказки, "");
		КонецЕсли;
	КонецЕсли;
	
	Если Форма.Элементы.Найти("СрокОбработкиРезультатовПредставление") <> Неопределено Тогда
		Форма.Элементы.СрокОбработкиРезультатовПредставление.ПодсказкаВвода = 
			ПодсказкаВводаПоляСрокИсполнения;
		Форма.Элементы.СрокОбработкиРезультатовПредставление.Ширина = ШиринаПоляСрок;
		
		Форма.Элементы.СрокОбработкиРезультатовПредставление.КнопкаВыбора = 
			Форма.ВозможенВыборТочнойДатыВСроках;
			
		Если Не ИспользоватьГрафикиРаботы Тогда
			Форма.Элементы.СрокОбработкиРезультатовПредставление.Подсказка = СтрЗаменить(
				Форма.Элементы.СрокОбработкиРезультатовПредставление.Подсказка,
				СтрокаЗаменыДляВсплывающейПодсказки, "");
		КонецЕсли;
	КонецЕсли;
	
	Если Форма.Элементы.Найти("СрокИсполненияПроцессаПредставление") <> Неопределено Тогда
		Если Не ИспользоватьГрафикиРаботы Тогда
			Форма.Элементы.СрокИсполненияПроцессаПредставление.Подсказка = СтрЗаменить(
				Форма.Элементы.СрокИсполненияПроцессаПредставление.Подсказка,
				СтрокаЗаменыДляВсплывающейПодсказки, "");
		КонецЕсли;
	КонецЕсли;
	
	Если Форма.Элементы.Найти("КоличествоИтераций") <> Неопределено
		И Форма.Объект.Свойство("НомерИтерации")
		И Форма.Объект.НомерИтерации > 0 Тогда
		
		Форма.Элементы.КоличествоИтераций.МинимальноеЗначение = Форма.Объект.НомерИтерации;
	КонецЕсли;
	
	Форма.ОбновитьСрокиИсполненияНаСервере();
	
КонецПроцедуры

// Определяет необходимость расчета сроков в форме процесса.
//
// Используется для переопределения одноименной функции в модуле СрокиИсполненияПроцессов.
// Вместо текущей следует использовать функцию из модуля СрокиИсполненияПроцессов.
//
// Параметры:
//  Форма - УправляемаяФормы - форма процесса.
//
// Возвращаемое значение:
//  Булево
//
Функция ТребуетсяРасчетСроков(Форма) Экспорт
	
	Возврат Форма.Объект.Стартован = Ложь
		И Форма.Объект.Состояние = Перечисления.СостоянияБизнесПроцессов.Активен
		И Форма.Объект.ПометкаУдаления = Ложь
		И (Форма.НастройкаСтарта = Неопределено
			Или Форма.НастройкаСтарта.Состояние = Перечисления.СостоянияПроцессовДляЗапуска.ПустаяСсылка()
			Или Форма.НастройкаСтарта.Состояние = Перечисления.СостоянияПроцессовДляЗапуска.СтартОтменен);
	
КонецФункции

// Подготавливает форму шаблона процесса для работы со сроками.
//
// Используется для переопределения одноименной процедуры в модуле СрокиИсполненияПроцессов.
// Вместо текущей следует использовать процедуру из модуля СрокиИсполненияПроцессов.
//
// Параметры:
//  Форма - УправляемаяФормы - форма шаблона процесса.
//
Процедура КарточкаШаблонаПриСозданииНаСервере(Форма) Экспорт
	
	Форма.ДатаОтсчетаДляРасчетаСроков = Дата(1,1,1);
	
	Если Форма.Параметры.Свойство("ДатаОтсчетаДляРасчетаСроков")
		И ЗначениеЗаполнено(Форма.Параметры.ДатаОтсчетаДляРасчетаСроков) Тогда
		
		Форма.ДатаОтсчетаДляРасчетаСроков = Форма.Параметры.ДатаОтсчетаДляРасчетаСроков;
	КонецЕсли;
	
	КомплексныйПроцесс = КомплексныйПроцессДействия(Форма.Объект);
	
	ВозможенВыборТочнойДатыВСроках = ЗначениеЗаполнено(Форма.ДатаОтсчетаДляРасчетаСроков)
		И ЗначениеЗаполнено(КомплексныйПроцесс)
		И (Форма.КомплексныйПроцессСтартован
			Или Не Форма.ЭтоДействиеКомплексногоПроцессаПоШаблону);
	
	СтруктураЗаполнения = Новый Структура;
	СтруктураЗаполнения.Вставить(
		"ВозможенВыборТочнойДатыВСроках", ВозможенВыборТочнойДатыВСроках);
	ЗаполнитьЗначенияСвойств(Форма, СтруктураЗаполнения);
	
	СрокиИсполненияПроцессов.ПриСозданииНаСервере(Форма);
	
КонецПроцедуры

// Определяет принадлежность действия к комплексному процессу и возвращает ссылку на
// комплексный процесс.
//
// Параметры:
//  Действие - ДанныеФормыСтруктура - шаблон объект в форме.
//
// Возвращаемое значение:
//  БизнесПроцессСсылка.КомплексныйПроцесс, Неопределено
//
Функция КомплексныйПроцессДействия(Действие)
	
	УстановитьПривилегированныйРежим(Истина);
	
	ВладелецВерхнегоУровня = 
		РаботаСКомплекснымиБизнесПроцессамиСервер.ВладелецВерхнегоУровня(Действие);
	
	Если ЗначениеЗаполнено(ВладелецВерхнегоУровня)
		И ТипЗнч(ВладелецВерхнегоУровня) = Тип("БизнесПроцессСсылка.КомплексныйПроцесс") Тогда
		
		Возврат ВладелецВерхнегоУровня;
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции_Обновление

// Возвращает количество неактивных процессов, шаблонов и протоколов мероприятий для обработки.
//
// Используется для переопределения одноименной функции в модуле СрокиИсполненияПроцессов.
// Вместо текущей следует использовать функцию из модуля СрокиИсполненияПроцессов.
//
// Возвращаемое значение:
//  Число
//
Функция КоличествоНеактивныхОбъектовДляОбработки() Экспорт
	
	Возврат ВыборкаНеактивныхШаблоновИсполненияДляОбработки(0).Количество()
		+ ВыборкаНеактивныхШаблоновОзнакомленияДляОбработки(0).Количество()
		+ ВыборкаНеактивныхШаблоновПорученияДляОбработки(0).Количество()
		+ ВыборкаНеактивныхШаблоновПриглашенияДляОбработки(0).Количество()
		+ ВыборкаНеактивныхШаблоновРассмотренияДляОбработки(0).Количество()
		+ ВыборкаНеактивныхШаблоновРегистрацииДляОбработки(0).Количество()
		+ ВыборкаНеактивныхШаблоновСогласованияДляОбработки(0).Количество()
		+ ВыборкаНеактивныхШаблоновУтвержденияДляОбработки(0).Количество()
		+ ВыборкаНеактивныхПроцессовИсполненияДляОбработки(0).Количество()
		+ ВыборкаНеактивныхПроцессовОзнакомленияДляОбработки(0).Количество()
		+ ВыборкаНеактивныхПроцессовПорученияДляОбработки(0).Количество()
		+ ВыборкаНеактивныхПроцессовПриглашенияДляОбработки(0).Количество()
		+ ВыборкаНеактивныхПроцессовРассмотренияДляОбработки(0).Количество()
		+ ВыборкаНеактивныхПроцессовРегристрацииДляОбработки(0).Количество()
		+ ВыборкаНеактивныхПроцессовСогласованияДляОбработки(0).Количество()
		+ ВыборкаНеактивныхПроцессовУтвержденияДляОбработки(0).Количество()
		+ ВыборкаНеактивныхКомплексныхПроцессовДляОбработки(0).Количество()
		+ ВыборкаНеактивныхПротоколовМероприятийДляОбработки(0).Количество();
	
КонецФункции

// Обработчик для отложенного обновления сроков неактивных процессов, шаблонов, протоколов мероприятий
//
// Используется для переопределения одноименной функции в модуле СрокиИсполненияПроцессов.
// Вместо текущей следует использовать функцию из модуля СрокиИсполненияПроцессов.
//
// Возвращаемое значение:
//  Число - количество обработанных данных.
//
Функция ОбновитьСрокиИсполненияОтложенноНеАктивныхОбъектов() Экспорт
	
	// Ниже важен порядок обработки. Нельзя запускать обновление составных и комплексных процессов
	// без обновления сроков в их действиях.
	
	// Простые процессы
	
	// Протоколы мероприятий
	КоличествоОбработанныхОбъектов = 
		ОбновитьСрокиИсполненияПроколовМероприятий(Ложь);
	Если КоличествоОбработанныхОбъектов > 0 Тогда
		Возврат КоличествоОбработанныхОбъектов;
	КонецЕсли;
	
	КоличествоОбработанныхОбъектов = 
		ОбновитьСрокиИсполненияПроцессовИсполнения(Ложь);
	Если КоличествоОбработанныхОбъектов > 0 Тогда
		Возврат КоличествоОбработанныхОбъектов;
	КонецЕсли;
		
	КоличествоОбработанныхОбъектов = 
		ОбновитьСрокиИсполненияПроцессовОзнакомления(Ложь);
	Если КоличествоОбработанныхОбъектов > 0 Тогда
		Возврат КоличествоОбработанныхОбъектов;
	КонецЕсли;
		
	КоличествоОбработанныхОбъектов = 
		ОбновитьСрокиИсполненияПроцессовПоручения(Ложь);
	Если КоличествоОбработанныхОбъектов > 0 Тогда
		Возврат КоличествоОбработанныхОбъектов;
	КонецЕсли;
		
	КоличествоОбработанныхОбъектов = 
		ОбновитьСрокиИсполненияПроцессовПриглашения(Ложь);
	Если КоличествоОбработанныхОбъектов > 0 Тогда
		Возврат КоличествоОбработанныхОбъектов;
	КонецЕсли;
		
	КоличествоОбработанныхОбъектов = 
		ОбновитьСрокиИсполненияПроцессовРассмотрения(Ложь);
	Если КоличествоОбработанныхОбъектов > 0 Тогда
		Возврат КоличествоОбработанныхОбъектов;
	КонецЕсли;
		
	КоличествоОбработанныхОбъектов = 
		ОбновитьСрокиИсполненияПроцессовРегристрации(Ложь);
	Если КоличествоОбработанныхОбъектов > 0 Тогда
		Возврат КоличествоОбработанныхОбъектов;
	КонецЕсли;
		
	КоличествоОбработанныхОбъектов = 
		ОбновитьСрокиИсполненияПроцессовСогласования(Ложь);
	Если КоличествоОбработанныхОбъектов > 0 Тогда
		Возврат КоличествоОбработанныхОбъектов;
	КонецЕсли;
		
	КоличествоОбработанныхОбъектов = 
		ОбновитьСрокиИсполненияПроцессовУтверждения(Ложь);
	Если КоличествоОбработанныхОбъектов > 0 Тогда
		Возврат КоличествоОбработанныхОбъектов;
	КонецЕсли;
	
	// Шаблоны
	
	КоличествоОбработанныхОбъектов = 
		ОбновитьСрокиИсполненияШаблоновИсполнения(Ложь);
	Если КоличествоОбработанныхОбъектов > 0 Тогда
		Возврат КоличествоОбработанныхОбъектов;
	КонецЕсли;
		
	КоличествоОбработанныхОбъектов = 
		ОбновитьСрокиИсполненияШаблоновОзнакомления(Ложь);
	Если КоличествоОбработанныхОбъектов > 0 Тогда
		Возврат КоличествоОбработанныхОбъектов;
	КонецЕсли;
		
	КоличествоОбработанныхОбъектов = 
		ОбновитьСрокиИсполненияШаблоновПоручения(Ложь);
	Если КоличествоОбработанныхОбъектов > 0 Тогда
		Возврат КоличествоОбработанныхОбъектов;
	КонецЕсли;
		
	КоличествоОбработанныхОбъектов = 
		ОбновитьСрокиИсполненияШаблоновПриглашения(Ложь);
	Если КоличествоОбработанныхОбъектов > 0 Тогда
		Возврат КоличествоОбработанныхОбъектов;
	КонецЕсли;
		
	КоличествоОбработанныхОбъектов = 
		ОбновитьСрокиИсполненияШаблоновРассмотрения(Ложь);
	Если КоличествоОбработанныхОбъектов > 0 Тогда
		Возврат КоличествоОбработанныхОбъектов;
	КонецЕсли;
		
	КоличествоОбработанныхОбъектов = 
		ОбновитьСрокиИсполненияШаблоновРегистрации(Ложь);
	Если КоличествоОбработанныхОбъектов > 0 Тогда
		Возврат КоличествоОбработанныхОбъектов;
	КонецЕсли;
		
	КоличествоОбработанныхОбъектов = 
		ОбновитьСрокиИсполненияШаблоновСогласования(Ложь);
	Если КоличествоОбработанныхОбъектов > 0 Тогда
		Возврат КоличествоОбработанныхОбъектов;
	КонецЕсли;
		
	КоличествоОбработанныхОбъектов = 
		ОбновитьСрокиИсполненияШаблоновУтверждения(Ложь);
	Если КоличествоОбработанныхОбъектов > 0 Тогда
		Возврат КоличествоОбработанныхОбъектов;
	КонецЕсли;
	
	// Комплексные
	
	КоличествоОбработанныхОбъектов = 
		ОбновитьСрокиИсполненияКомплексныхПроцессов(Ложь);
	Если КоличествоОбработанныхОбъектов > 0 Тогда
		Возврат КоличествоОбработанныхОбъектов;
	КонецЕсли;
	
	// Составные процессы. Обрабатываются один раз все составные процессы.
	// Нет деления на порции, т.к. нет возможности отличить
	// обработанные процессы.
	КоличествоОбработанныхОбъектов = 
		ОбновитьСрокиИсполненияСоставныхПроцессов(Ложь);
	Возврат 0;
	
КонецФункции

// Обработчик обновления сроков активных процессов, шаблонов и протоколов мероприятий
//
// Используется для переопределения одноименной функции в модуле СрокиИсполненияПроцессов.
// Вместо текущей следует использовать функцию из модуля СрокиИсполненияПроцессов.
//
Процедура ОбновитьСрокиИсполненияАктивныхОбъектов() Экспорт
	
	// Ниже важен порядок обработки. Нельзя запускать обновление составных и комплексных процессов
	// без обновления сроков в их действиях.
	
	// шаблоны 
	ОбновитьСрокиИсполненияШаблоновИсполнения();
	ОбновитьСрокиИсполненияШаблоновОзнакомления();
	ОбновитьСрокиИсполненияШаблоновПоручения();
	ОбновитьСрокиИсполненияШаблоновПриглашения();
	ОбновитьСрокиИсполненияШаблоновРассмотрения();
	ОбновитьСрокиИсполненияШаблоновРегистрации();
	ОбновитьСрокиИсполненияШаблоновСогласования();
	ОбновитьСрокиИсполненияШаблоновУтверждения();
	
	// процессы
	ОбновитьСрокиИсполненияПроцессовИсполнения();
	ОбновитьСрокиИсполненияПроцессовОзнакомления();
	ОбновитьСрокиИсполненияПроцессовПоручения();
	ОбновитьСрокиИсполненияПроцессовПриглашения();
	ОбновитьСрокиИсполненияПроцессовРассмотрения();
	ОбновитьСрокиИсполненияПроцессовРегристрации();
	ОбновитьСрокиИсполненияПроцессовСогласования();
	ОбновитьСрокиИсполненияПроцессовУтверждения();
	ОбновитьСрокиИсполненияСоставныхПроцессов();
	ОбновитьСрокиИсполненияКомплексныхПроцессов();
	
	// протоколы мероприятий
	ОбновитьСрокиИсполненияПроколовМероприятий();
	
КонецПроцедуры

//////////////////////////
// Шаблоны процессов

// Исполнение

// Обновляет сроки в шаблонах процессов Исполнений.
//
// Параметры:
//  ТолькоАктивные - Булево - при значении Истина обновляет только активные (которые находятся в работе) шаблоны.
//
Функция ОбновитьСрокиИсполненияШаблоновИсполнения(ТолькоАктивные = Истина)
	
	Если ТолькоАктивные Тогда
		
		Запрос = Новый Запрос;
		
		ТекстЗапроса = 
			"ВЫБРАТЬ
			|	ШаблоныИсполнения.Ссылка
			|ИЗ
			|	Справочник.ШаблоныИсполнения КАК ШаблоныИсполнения
			|		ЛЕВОЕ СОЕДИНЕНИЕ БизнесПроцесс.КомплексныйПроцесс КАК ПроцессВладелец
			|		ПО ШаблоныИсполнения.КомплексныйПроцесс = ПроцессВладелец.Ссылка
			|ГДЕ
			|	(ПроцессВладелец.Ссылка ЕСТЬ NULL 
			|			ИЛИ ПроцессВладелец.Завершен = ЛОЖЬ
			|				И ПроцессВладелец.ПометкаУдаления = ЛОЖЬ
			|				И ПроцессВладелец.Состояние = ЗНАЧЕНИЕ(Перечисление.СостоянияБизнесПроцессов.Активен)
			|				И &УсловиеПоУзлуОбмена)
			|	И ШаблоныИсполнения.ПометкаУдаления = ЛОЖЬ
			|	И ШаблоныИсполнения.КоличествоИтераций = 0
			|
			|СГРУППИРОВАТЬ ПО
			|	ШаблоныИсполнения.Ссылка";
			
		Если Не Константы.ВыполняетсяПлавноеОбновлениеС20.Получить() Тогда
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
				"И &УсловиеПоУзлуОбмена", "И ПроцессВладелец.УзелОбмена = &УзелОбмена");
			
			Запрос.УстановитьПараметр("УзелОбмена", РаботаСБизнесПроцессами.ЭтотУзелОбменаДляОбработкиПроцессов());
		Иначе
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "И &УсловиеПоУзлуОбмена", "");
		КонецЕсли;
		
		Запрос.Текст = ТекстЗапроса;
		
		Выборка = Запрос.Выполнить().Выбрать();
	Иначе
		Выборка = ВыборкаНеактивныхШаблоновИсполненияДляОбработки();
	КонецЕсли;
	
	КоличествоОбработанныхПроцессов = 0;
	
	Пока Выборка.Следующий() Цикл
		
		Попытка
			ЗаблокироватьДанныеДляРедактирования(Выборка.Ссылка);
		Исключение
			Продолжить;
		КонецПопытки;
		
		ПеренестиСрокиШаблонаИсполненияВНовыеРеквизиты(Выборка.Ссылка);
		
		РазблокироватьДанныеДляРедактирования(Выборка.Ссылка);
		
		КоличествоОбработанныхПроцессов = КоличествоОбработанныхПроцессов + 1;
		
	КонецЦикла;
	
	Возврат КоличествоОбработанныхПроцессов;
	
КонецФункции

// Возвращает выборку неактивных шаблонов процессов исполнений.
//
// Параметры:
//  Количество - Число - количество шаблонов в выборке
//
Функция ВыборкаНеактивныхШаблоновИсполненияДляОбработки(Количество = 1000)
	
	Запрос = Новый Запрос;
	
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	ШаблоныИсполнения.Ссылка
		|ИЗ
		|	Справочник.ШаблоныИсполнения КАК ШаблоныИсполнения
		|		ЛЕВОЕ СОЕДИНЕНИЕ БизнесПроцесс.КомплексныйПроцесс КАК ПроцессВладелец
		|		ПО ШаблоныИсполнения.КомплексныйПроцесс = ПроцессВладелец.Ссылка
		|ГДЕ
		|	ШаблоныИсполнения.ПометкаУдаления = ЛОЖЬ
		|	И &УсловиеПоУзлуОбмена
		|	И ШаблоныИсполнения.КоличествоИтераций = 0
		|
		|СГРУППИРОВАТЬ ПО
		|	ШаблоныИсполнения.Ссылка";
	
	Если Не Константы.ВыполняетсяПлавноеОбновлениеС20.Получить() Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
			"И &УсловиеПоУзлуОбмена", "И ПроцессВладелец.УзелОбмена = &УзелОбмена");
		
		Запрос.УстановитьПараметр("УзелОбмена", РаботаСБизнесПроцессами.ЭтотУзелОбменаДляОбработкиПроцессов());
	Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "И &УсловиеПоУзлуОбмена", "");
	КонецЕсли;
	
	Если Количество > 0 Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ВЫБРАТЬ", "ВЫБРАТЬ ПЕРВЫЕ " + Формат(Количество, "ЧГ=0"));
	КонецЕсли;
	
	Запрос.Текст = ТекстЗапроса;
	
	Возврат Запрос.Выполнить().Выбрать();
	
КонецФункции

// Переносит сроки шаблона исполнения из старых реквизитов в новые.
//
// Параметры:
//  СсылкаНаШаблон - СправочникСсылка.ШаблоныИсполнения - ссылка на шаблон.
//
Процедура ПеренестиСрокиШаблонаИсполненияВНовыеРеквизиты(СсылкаНаШаблон) Экспорт
	
	ШаблонОбъект = СсылкаНаШаблон.ПолучитьОбъект();
	
	ДлительностьИсполненияИсполнителямиВЧасах = 0;
	
	ДлительностьЭтапаВЧасах = 0;
	
	Для Каждого СтрИсполнители Из ШаблонОбъект.Исполнители Цикл
		
		Если ШаблонОбъект.ВариантИсполнения = 
			Перечисления.ПорядокВыполненияЗадач.ПослеПредыдущего Тогда
			
			ДлительностьИсполненияИсполнителямиВЧасах = 
				ДлительностьИсполненияИсполнителямиВЧасах + ДлительностьЭтапаВЧасах;
			
			ДлительностьЭтапаВЧасах = 0;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(СтрИсполнители.УдалитьСрокИсполнения)
			Или ЗначениеЗаполнено(СтрИсполнители.УдалитьСрокИсполненияЧас) Тогда
			
			СтрИсполнители.СрокИсполненияДни = СтрИсполнители.УдалитьСрокИсполнения;
			СтрИсполнители.СрокИсполненияЧасы = СтрИсполнители.УдалитьСрокИсполненияЧас;
		Иначе
			СтрИсполнители.СрокИсполненияДни = ШаблонОбъект.УдалитьСрокИсполнения;
			СтрИсполнители.СрокИсполненияЧасы = ШаблонОбъект.УдалитьСрокИсполненияЧас;
		КонецЕсли;
		
		СтрИсполнители.ВариантУстановкиСрокаИсполнения = 
			Перечисления.ВариантыУстановкиСрокаИсполнения.ОтносительныйСрок;
			
		ДлительностьИсполненияВЧасах = 
			СтрИсполнители.СрокИсполненияДни * 24 + СтрИсполнители.СрокИсполненияЧасы;
			
		ДлительностьЭтапаВЧасах = Макс(
			ДлительностьЭтапаВЧасах, 
			ДлительностьИсполненияВЧасах);
		
	КонецЦикла;
	
	ДлительностьИсполненияИсполнителямиВЧасах = 
		ДлительностьИсполненияИсполнителямиВЧасах + ДлительностьЭтапаВЧасах;
	
	Если ЗначениеЗаполнено(ШаблонОбъект.Проверяющий) Тогда
		
		ДлительностьИсполненияДляПроверяющего = 24;
		
		Если ЗначениеЗаполнено(ШаблонОбъект.УдалитьСрокИсполнения)
			Или ЗначениеЗаполнено(ШаблонОбъект.УдалитьСрокИсполненияЧас) Тогда
			
			ДлительностьИсполненияПроцессаВЧасах = ШаблонОбъект.УдалитьСрокИсполнения * 24
				+ ШаблонОбъект.УдалитьСрокИсполненияЧас;
			
			Если ДлительностьИсполненияПроцессаВЧасах > ДлительностьИсполненияИсполнителямиВЧасах Тогда
				ДлительностьИсполненияДляПроверяющего = 
					ДлительностьИсполненияПроцессаВЧасах - ДлительностьИсполненияИсполнителямиВЧасах;
			КонецЕсли;
		КонецЕсли;
		
		ШаблонОбъект.СрокОбработкиРезультатовДни = Цел(ДлительностьИсполненияДляПроверяющего / 24);
		ШаблонОбъект.СрокОбработкиРезультатовЧасы = ДлительностьИсполненияДляПроверяющего % 24;
		ШаблонОбъект.ВариантУстановкиСрокаОбработкиРезультатов = 
			Перечисления.ВариантыУстановкиСрокаИсполнения.ОтносительныйСрок;
		
	КонецЕсли;
	
	ШаблонОбъект.КоличествоИтераций = 1;
	
	ШаблоныБизнесПроцессов.ЗаписатьШаблон(ШаблонОбъект, "ПростаяЗапись");
	
КонецПроцедуры

// Ознакомление

// Обновляет сроки в шаблонах процессов Ознакомлений.
//
// Параметры:
//  ТолькоАктивные - Булево - при значении Истина обновляет только активные (которые находятся в работе) шаблоны.
//
Функция ОбновитьСрокиИсполненияШаблоновОзнакомления(ТолькоАктивные = Истина)
	
	Если ТолькоАктивные Тогда
		Запрос = Новый Запрос;
		
		ТекстЗапроса =
			"ВЫБРАТЬ
			|	ШаблоныОзнакомления.Ссылка
			|ИЗ
			|	Справочник.ШаблоныОзнакомления.Исполнители КАК ШаблоныОзнакомленияИсполнители
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ШаблоныОзнакомления КАК ШаблоныОзнакомления
			|			ЛЕВОЕ СОЕДИНЕНИЕ БизнесПроцесс.КомплексныйПроцесс КАК ПроцессВладелец
			|			ПО ШаблоныОзнакомления.КомплексныйПроцесс = ПроцессВладелец.Ссылка
			|		ПО ШаблоныОзнакомленияИсполнители.Ссылка = ШаблоныОзнакомления.Ссылка
			|ГДЕ
			|	(ШаблоныОзнакомления.УдалитьСрокИсполнения <> 0
			|			ИЛИ ШаблоныОзнакомления.УдалитьСрокИсполненияЧас <> 0)
			|	И ШаблоныОзнакомленияИсполнители.СрокИсполненияДни = 0
			|	И ШаблоныОзнакомленияИсполнители.СрокИсполненияЧасы = 0
			|	И (ПроцессВладелец.Ссылка ЕСТЬ NULL 
			|			ИЛИ ПроцессВладелец.Завершен = ЛОЖЬ
			|				И ПроцессВладелец.ПометкаУдаления = ЛОЖЬ
			|				И ПроцессВладелец.Состояние = ЗНАЧЕНИЕ(Перечисление.СостоянияБизнесПроцессов.Активен)
			|				И &УсловиеПоУзлуОбмена)
			|	И ШаблоныОзнакомления.ПометкаУдаления = ЛОЖЬ
			|
			|СГРУППИРОВАТЬ ПО
			|	ШаблоныОзнакомления.Ссылка";
			
		Если Не Константы.ВыполняетсяПлавноеОбновлениеС20.Получить() Тогда
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
				"И &УсловиеПоУзлуОбмена", "И ПроцессВладелец.УзелОбмена = &УзелОбмена");
			
			Запрос.УстановитьПараметр("УзелОбмена", РаботаСБизнесПроцессами.ЭтотУзелОбменаДляОбработкиПроцессов());
		Иначе
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "И &УсловиеПоУзлуОбмена", "");
		КонецЕсли;
		
		Запрос.Текст = ТекстЗапроса;
		
		Выборка = Запрос.Выполнить().Выбрать();
	Иначе
		Выборка = ВыборкаНеактивныхШаблоновОзнакомленияДляОбработки();
	КонецЕсли;
	
	КоличествоОбработанныхПроцессов = 0;
	
	Пока Выборка.Следующий() Цикл
		
		Попытка
			ЗаблокироватьДанныеДляРедактирования(Выборка.Ссылка);
		Исключение
			Продолжить;
		КонецПопытки;
		
		ПеренестиСрокиШаблонаОзнакомленияВНовыеРеквизиты(Выборка.Ссылка);
		
		РазблокироватьДанныеДляРедактирования(Выборка.Ссылка);
		
		КоличествоОбработанныхПроцессов = КоличествоОбработанныхПроцессов + 1;
		
	КонецЦикла;
	
	Возврат КоличествоОбработанныхПроцессов;
	
КонецФункции

// Возвращает выборку неактивных шаблонов процессов ознакомлений.
//
// Параметры:
//  Количество - Число - количество шаблонов в выборке
//
Функция ВыборкаНеактивныхШаблоновОзнакомленияДляОбработки(Количество = 1000)
	
	Запрос = Новый Запрос;
	
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	ШаблоныОзнакомления.Ссылка
		|ИЗ
		|	Справочник.ШаблоныОзнакомления.Исполнители КАК ШаблоныОзнакомленияИсполнители
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ШаблоныОзнакомления КАК ШаблоныОзнакомления
		|			ЛЕВОЕ СОЕДИНЕНИЕ БизнесПроцесс.КомплексныйПроцесс КАК ПроцессВладелец
		|			ПО ШаблоныОзнакомления.КомплексныйПроцесс = ПроцессВладелец.Ссылка
		|		ПО ШаблоныОзнакомленияИсполнители.Ссылка = ШаблоныОзнакомления.Ссылка
		|ГДЕ
		|	(ШаблоныОзнакомления.УдалитьСрокИсполнения <> 0
		|			ИЛИ ШаблоныОзнакомления.УдалитьСрокИсполненияЧас <> 0)
		|	И ШаблоныОзнакомленияИсполнители.СрокИсполненияДни = 0
		|	И ШаблоныОзнакомленияИсполнители.СрокИсполненияЧасы = 0
		|	И &УсловиеПоУзлуОбмена
		|	И ШаблоныОзнакомления.ПометкаУдаления = ЛОЖЬ
		|
		|СГРУППИРОВАТЬ ПО
		|	ШаблоныОзнакомления.Ссылка";
		
	Если Не Константы.ВыполняетсяПлавноеОбновлениеС20.Получить() Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
			"И &УсловиеПоУзлуОбмена", "И ПроцессВладелец.УзелОбмена = &УзелОбмена");
		
		Запрос.УстановитьПараметр("УзелОбмена", РаботаСБизнесПроцессами.ЭтотУзелОбменаДляОбработкиПроцессов());
	Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "И &УсловиеПоУзлуОбмена", "");
	КонецЕсли;
	
	Если Количество > 0 Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ВЫБРАТЬ", "ВЫБРАТЬ ПЕРВЫЕ " + Формат(Количество, "ЧГ=0"));
	КонецЕсли;
	
	Запрос.Текст = ТекстЗапроса;
	
	Возврат Запрос.Выполнить().Выбрать();
	
КонецФункции

// Переносит сроки шаблона ознакомления из старых реквизитов в новые.
//
// Параметры:
//  СсылкаНаШаблон - СправочникСсылка.ШаблоныОзнакомления - ссылка на шаблон.
//
Процедура ПеренестиСрокиШаблонаОзнакомленияВНовыеРеквизиты(СсылкаНаШаблон) Экспорт
	
	ШаблонОбъект = СсылкаНаШаблон.ПолучитьОбъект();
	
	Для Каждого СтрИсполнители Из ШаблонОбъект.Исполнители Цикл
		СтрИсполнители.СрокИсполненияДни = ШаблонОбъект.УдалитьСрокИсполнения;
		СтрИсполнители.СрокИсполненияЧасы = ШаблонОбъект.УдалитьСрокИсполненияЧас;
		
		СтрИсполнители.ВариантУстановкиСрокаИсполнения = 
			Перечисления.ВариантыУстановкиСрокаИсполнения.ОтносительныйСрок;
	КонецЦикла;
	
	ШаблоныБизнесПроцессов.ЗаписатьШаблон(ШаблонОбъект, "ПростаяЗапись");
	
КонецПроцедуры

// Поручение

// Обновляет сроки в шаблонах процессов Поручений.
//
// Параметры:
//  ТолькоАктивные - Булево - при значении Истина обновляет только активные (которые находятся в работе) шаблоны.
//
Функция ОбновитьСрокиИсполненияШаблоновПоручения(ТолькоАктивные = Истина)
	
	Если ТолькоАктивные Тогда
		Запрос = Новый Запрос;
		
		ТекстЗапроса =
			"ВЫБРАТЬ
			|	ШаблоныПоручения.Ссылка
			|ИЗ
			|	Справочник.ШаблоныПоручения КАК ШаблоныПоручения
			|		ЛЕВОЕ СОЕДИНЕНИЕ БизнесПроцесс.КомплексныйПроцесс КАК ПроцессВладелец
			|		ПО ШаблоныПоручения.КомплексныйПроцесс = ПроцессВладелец.Ссылка
			|ГДЕ
			|	(ШаблоныПоручения.УдалитьСрокИсполнения <> 0
			|			ИЛИ ШаблоныПоручения.УдалитьСрокИсполненияЧас <> 0)
			|	И ШаблоныПоручения.СрокИсполненияДни = 0
			|	И ШаблоныПоручения.СрокИсполненияЧасы = 0
			|	И (ПроцессВладелец.Ссылка ЕСТЬ NULL 
			|			ИЛИ ПроцессВладелец.Завершен = ЛОЖЬ
			|				И ПроцессВладелец.ПометкаУдаления = ЛОЖЬ
			|				И ПроцессВладелец.Состояние = ЗНАЧЕНИЕ(Перечисление.СостоянияБизнесПроцессов.Активен)
			|				И &УсловиеПоУзлуОбмена)
			|	И ШаблоныПоручения.ПометкаУдаления = ЛОЖЬ
			|
			|СГРУППИРОВАТЬ ПО
			|	ШаблоныПоручения.Ссылка";
			
		Если Не Константы.ВыполняетсяПлавноеОбновлениеС20.Получить() Тогда
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
				"И &УсловиеПоУзлуОбмена", "И ПроцессВладелец.УзелОбмена = &УзелОбмена");
			
			Запрос.УстановитьПараметр("УзелОбмена", РаботаСБизнесПроцессами.ЭтотУзелОбменаДляОбработкиПроцессов());
		Иначе
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "И &УсловиеПоУзлуОбмена", "");
		КонецЕсли;
		
		Запрос.Текст = ТекстЗапроса;
		
		Выборка = Запрос.Выполнить().Выбрать();
	Иначе
		Выборка = ВыборкаНеактивныхШаблоновПорученияДляОбработки();
	КонецЕсли;
	
	КоличествоОбработанныхПроцессов = 0;
	
	Пока Выборка.Следующий() Цикл
		
		Попытка
			ЗаблокироватьДанныеДляРедактирования(Выборка.Ссылка);
		Исключение
			Продолжить;
		КонецПопытки;
		
		ПеренестиСрокиШаблонаПорученияВНовыеРеквизиты(Выборка.Ссылка);
		
		РазблокироватьДанныеДляРедактирования(Выборка.Ссылка);
		
		КоличествоОбработанныхПроцессов = КоличествоОбработанныхПроцессов + 1;
		
	КонецЦикла;
	
	Возврат КоличествоОбработанныхПроцессов;
	
КонецФункции

// Возвращает выборку неактивных шаблонов процессов поручений.
//
// Параметры:
//  Количество - Число - количество шаблонов в выборке
//
Функция ВыборкаНеактивныхШаблоновПорученияДляОбработки(Количество = 1000)
	
	Запрос = Новый Запрос;
	
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	ШаблоныПоручения.Ссылка
		|ИЗ
		|	Справочник.ШаблоныПоручения КАК ШаблоныПоручения
		|		ЛЕВОЕ СОЕДИНЕНИЕ БизнесПроцесс.КомплексныйПроцесс КАК ПроцессВладелец
		|		ПО ШаблоныПоручения.КомплексныйПроцесс = ПроцессВладелец.Ссылка
		|ГДЕ
		|	(ШаблоныПоручения.УдалитьСрокИсполнения <> 0
		|			ИЛИ ШаблоныПоручения.УдалитьСрокИсполненияЧас <> 0)
		|	И ШаблоныПоручения.СрокИсполненияДни = 0
		|	И ШаблоныПоручения.СрокИсполненияЧасы = 0
		|	И &УсловиеПоУзлуОбмена
		|	И ШаблоныПоручения.ПометкаУдаления = ЛОЖЬ
		|
		|СГРУППИРОВАТЬ ПО
		|	ШаблоныПоручения.Ссылка";
		
	Если Не Константы.ВыполняетсяПлавноеОбновлениеС20.Получить() Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
			"И &УсловиеПоУзлуОбмена", "И ПроцессВладелец.УзелОбмена = &УзелОбмена");
		
		Запрос.УстановитьПараметр("УзелОбмена", РаботаСБизнесПроцессами.ЭтотУзелОбменаДляОбработкиПроцессов());
	Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "И &УсловиеПоУзлуОбмена", "");
	КонецЕсли;
	
	Если Количество > 0 Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ВЫБРАТЬ", "ВЫБРАТЬ ПЕРВЫЕ " + Формат(Количество, "ЧГ=0"));
	КонецЕсли;
	
	Запрос.Текст = ТекстЗапроса;
	
	Возврат Запрос.Выполнить().Выбрать();
	
КонецФункции

// Переносит сроки шаблона поручения из старых реквизитов в новые.
//
// Параметры:
//  СсылкаНаШаблон - СправочникСсылка.ШаблоныПоручения - ссылка на шаблон.
//
Процедура ПеренестиСрокиШаблонаПорученияВНовыеРеквизиты(СсылкаНаШаблон) Экспорт
	
	ШаблонОбъект = СсылкаНаШаблон.ПолучитьОбъект();
		
		ШаблонОбъект.СрокИсполненияДни = ШаблонОбъект.УдалитьСрокИсполнения;
		ШаблонОбъект.СрокИсполненияЧасы = ШаблонОбъект.УдалитьСрокИсполненияЧас;
		
		ШаблонОбъект.ВариантУстановкиСрокаИсполнения = 
			Перечисления.ВариантыУстановкиСрокаИсполнения.ОтносительныйСрок;
		
		Если ЗначениеЗаполнено(ШаблонОбъект.Проверяющий) Тогда
			ШаблонОбъект.СрокОбработкиРезультатовДни = 1;
			ШаблонОбъект.ВариантУстановкиСрокаОбработкиРезультатов = 
				Перечисления.ВариантыУстановкиСрокаИсполнения.ОтносительныйСрок;
		КонецЕсли;
		
		ШаблоныБизнесПроцессов.ЗаписатьШаблон(ШаблонОбъект, "ПростаяЗапись");
	
КонецПроцедуры

// Приглашение

// Обновляет сроки в шаблонах процессов Приглашений.
//
// Параметры:
//  ТолькоАктивные - Булево - при значении Истина обновляет только активные (которые находятся в работе) шаблоны.
//
Функция ОбновитьСрокиИсполненияШаблоновПриглашения(ТолькоАктивные = Истина)
	
	Если ТолькоАктивные Тогда
		Запрос = Новый Запрос;
		
		ТекстЗапроса =
			"ВЫБРАТЬ
			|	ШаблоныПриглашения.Ссылка
			|ИЗ
			|	Справочник.ШаблоныПриглашения КАК ШаблоныПриглашения
			|		ЛЕВОЕ СОЕДИНЕНИЕ БизнесПроцесс.КомплексныйПроцесс КАК ПроцессВладелец
			|		ПО ШаблоныПриглашения.КомплексныйПроцесс = ПроцессВладелец.Ссылка
			|ГДЕ
			|	(ПроцессВладелец.Ссылка ЕСТЬ NULL 
			|			ИЛИ ПроцессВладелец.Завершен = ЛОЖЬ
			|				И ПроцессВладелец.ПометкаУдаления = ЛОЖЬ
			|				И ПроцессВладелец.Состояние = ЗНАЧЕНИЕ(Перечисление.СостоянияБизнесПроцессов.Активен)
			|				И &УсловиеПоУзлуОбмена)
			|	И ШаблоныПриглашения.ПометкаУдаления = ЛОЖЬ
			|	И ШаблоныПриглашения.КоличествоИтераций = 0
			|
			|СГРУППИРОВАТЬ ПО
			|	ШаблоныПриглашения.Ссылка";
		
		Если Не Константы.ВыполняетсяПлавноеОбновлениеС20.Получить() Тогда
			
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
				"И &УсловиеПоУзлуОбмена", "И ПроцессВладелец.УзелОбмена = &УзелОбмена");
			
			Запрос.УстановитьПараметр("УзелОбмена", РаботаСБизнесПроцессами.ЭтотУзелОбменаДляОбработкиПроцессов());
		Иначе
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "И &УсловиеПоУзлуОбмена", "");
		КонецЕсли;
		
		Запрос.Текст = ТекстЗапроса;
		
		Выборка = Запрос.Выполнить().Выбрать();
	Иначе
		Выборка = ВыборкаНеактивныхШаблоновПриглашенияДляОбработки();
	КонецЕсли;
	
	КоличествоОбработанныхПроцессов = 0;
	
	Пока Выборка.Следующий() Цикл
		
		Попытка
			ЗаблокироватьДанныеДляРедактирования(Выборка.Ссылка);
		Исключение
			Продолжить;
		КонецПопытки;
		
		ПеренестиСрокиШаблонаПриглашенияВНовыеРеквизиты(Выборка.Ссылка);
		
		РазблокироватьДанныеДляРедактирования(Выборка.Ссылка);
		
		КоличествоОбработанныхПроцессов = КоличествоОбработанныхПроцессов + 1;
		
	КонецЦикла;
	
	Возврат КоличествоОбработанныхПроцессов;
	
КонецФункции

// Возвращает выборку неактивных шаблонов процессов приглашений.
//
// Параметры:
//  Количество - Число - количество шаблонов в выборке
//
Функция ВыборкаНеактивныхШаблоновПриглашенияДляОбработки(Количество = 1000)
	
	Запрос = Новый Запрос;
	
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	ШаблоныПриглашения.Ссылка
		|ИЗ
		|	Справочник.ШаблоныПриглашения КАК ШаблоныПриглашения
		|		ЛЕВОЕ СОЕДИНЕНИЕ БизнесПроцесс.КомплексныйПроцесс КАК ПроцессВладелец
		|		ПО ШаблоныПриглашения.КомплексныйПроцесс = ПроцессВладелец.Ссылка
		|ГДЕ
		|	ШаблоныПриглашения.ПометкаУдаления = ЛОЖЬ
		|	И ШаблоныПриглашения.КоличествоИтераций = 0
		|	И &УсловиеПоУзлуОбмена
		|
		|СГРУППИРОВАТЬ ПО
		|	ШаблоныПриглашения.Ссылка";
		
	Если Не Константы.ВыполняетсяПлавноеОбновлениеС20.Получить() Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
			"И &УсловиеПоУзлуОбмена", "И ПроцессВладелец.УзелОбмена = &УзелОбмена");
		
		Запрос.УстановитьПараметр("УзелОбмена", РаботаСБизнесПроцессами.ЭтотУзелОбменаДляОбработкиПроцессов());
	Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "И &УсловиеПоУзлуОбмена", "");
	КонецЕсли;
	
	Если Количество > 0 Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ВЫБРАТЬ", "ВЫБРАТЬ ПЕРВЫЕ " + Формат(Количество, "ЧГ=0"));
	КонецЕсли;
	
	Запрос.Текст = ТекстЗапроса;
	
	Возврат Запрос.Выполнить().Выбрать();
	
КонецФункции

// Переносит сроки шаблона приглашения из старых реквизитов в новые.
//
// Параметры:
//  СсылкаНаШаблон - СправочникСсылка.ШаблоныПриглашения - ссылка на шаблон.
//
Процедура ПеренестиСрокиШаблонаПриглашенияВНовыеРеквизиты(СсылкаНаШаблон) Экспорт
	
	ШаблонОбъект = СсылкаНаШаблон.ПолучитьОбъект();
		
	Для Каждого СтрИсполнители Из ШаблонОбъект.Исполнители Цикл
		СтрИсполнители.СрокИсполненияДни = ШаблонОбъект.УдалитьСрокИсполнения;
		СтрИсполнители.СрокИсполненияЧасы = ШаблонОбъект.УдалитьСрокИсполненияЧас;
		
		СтрИсполнители.ВариантУстановкиСрокаИсполнения = 
			Перечисления.ВариантыУстановкиСрокаИсполнения.ОтносительныйСрок;
	КонецЦикла;
	
	ШаблонОбъект.СрокОбработкиРезультатовДни = 1;
	ШаблонОбъект.ВариантУстановкиСрокаОбработкиРезультатов = 
		Перечисления.ВариантыУстановкиСрокаИсполнения.ОтносительныйСрок;
	
	ШаблонОбъект.КоличествоИтераций = 1;
	
	ШаблоныБизнесПроцессов.ЗаписатьШаблон(ШаблонОбъект, "ПростаяЗапись");
	
КонецПроцедуры

// Рассмотрение

// Обновляет сроки в шаблонах процессов Рассмотрений.
//
// Параметры:
//  ТолькоАктивные - Булево - при значении Истина обновляет только активные (которые находятся в работе) шаблоны.
//
Функция ОбновитьСрокиИсполненияШаблоновРассмотрения(ТолькоАктивные = Истина)
	
	Если ТолькоАктивные Тогда
		Запрос = Новый Запрос;
		
		ТекстЗапроса =
			"ВЫБРАТЬ
			|	ШаблоныРассмотрения.Ссылка
			|ИЗ
			|	Справочник.ШаблоныРассмотрения КАК ШаблоныРассмотрения
			|		ЛЕВОЕ СОЕДИНЕНИЕ БизнесПроцесс.КомплексныйПроцесс КАК ПроцессВладелец
			|		ПО ШаблоныРассмотрения.КомплексныйПроцесс = ПроцессВладелец.Ссылка
			|ГДЕ
			|	(ШаблоныРассмотрения.УдалитьСрокИсполнения <> 0
			|			ИЛИ ШаблоныРассмотрения.УдалитьСрокИсполненияЧас <> 0)
			|	И ШаблоныРассмотрения.СрокИсполненияДни = 0
			|	И ШаблоныРассмотрения.СрокИсполненияЧасы = 0
			|	И (ПроцессВладелец.Ссылка ЕСТЬ NULL 
			|			ИЛИ ПроцессВладелец.Завершен = ЛОЖЬ
			|				И ПроцессВладелец.ПометкаУдаления = ЛОЖЬ
			|				И ПроцессВладелец.Состояние = ЗНАЧЕНИЕ(Перечисление.СостоянияБизнесПроцессов.Активен)
			|				И &УсловиеПоУзлуОбмена)
			|	И ШаблоныРассмотрения.ПометкаУдаления = ЛОЖЬ
			|
			|СГРУППИРОВАТЬ ПО
			|	ШаблоныРассмотрения.Ссылка";
		
		Если Не Константы.ВыполняетсяПлавноеОбновлениеС20.Получить() Тогда
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
				"И &УсловиеПоУзлуОбмена", "И ПроцессВладелец.УзелОбмена = &УзелОбмена");
			
			Запрос.УстановитьПараметр("УзелОбмена", РаботаСБизнесПроцессами.ЭтотУзелОбменаДляОбработкиПроцессов());
		Иначе
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "И &УсловиеПоУзлуОбмена", "");
		КонецЕсли;
		
		Запрос.Текст = ТекстЗапроса;
		
		Выборка = Запрос.Выполнить().Выбрать();
	Иначе
		Выборка = ВыборкаНеактивныхШаблоновРассмотренияДляОбработки();
	КонецЕсли;
	
	КоличествоОбработанныхПроцессов = 0;
	
	Пока Выборка.Следующий() Цикл
		
		Попытка
			ЗаблокироватьДанныеДляРедактирования(Выборка.Ссылка);
		Исключение
			Продолжить;
		КонецПопытки;
		
		ПеренестиСрокиШаблонаРассмотренияВНовыеРеквизиты(Выборка.Ссылка);
		
		РазблокироватьДанныеДляРедактирования(Выборка.Ссылка);
		
		КоличествоОбработанныхПроцессов = КоличествоОбработанныхПроцессов + 1;
		
	КонецЦикла;
	
	Возврат КоличествоОбработанныхПроцессов;
	
КонецФункции

// Возвращает выборку неактивных шаблонов процессов рассмотрений.
//
// Параметры:
//  Количество - Число - количество шаблонов в выборке
//
Функция ВыборкаНеактивныхШаблоновРассмотренияДляОбработки(Количество = 1000)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("УзелОбмена", РаботаСБизнесПроцессами.ЭтотУзелОбменаДляОбработкиПроцессов());
	
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	ШаблоныРассмотрения.Ссылка
		|ИЗ
		|	Справочник.ШаблоныРассмотрения КАК ШаблоныРассмотрения
		|		ЛЕВОЕ СОЕДИНЕНИЕ БизнесПроцесс.КомплексныйПроцесс КАК ПроцессВладелец
		|		ПО ШаблоныРассмотрения.КомплексныйПроцесс = ПроцессВладелец.Ссылка
		|ГДЕ
		|	(ШаблоныРассмотрения.УдалитьСрокИсполнения <> 0
		|			ИЛИ ШаблоныРассмотрения.УдалитьСрокИсполненияЧас <> 0)
		|	И ШаблоныРассмотрения.СрокИсполненияДни = 0
		|	И ШаблоныРассмотрения.СрокИсполненияЧасы = 0
		|	И &УсловиеПоУзлуОбмена
		|	И ШаблоныРассмотрения.ПометкаУдаления = ЛОЖЬ
		|
		|СГРУППИРОВАТЬ ПО
		|	ШаблоныРассмотрения.Ссылка";
		
	Если Не Константы.ВыполняетсяПлавноеОбновлениеС20.Получить() Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
			"И &УсловиеПоУзлуОбмена", "И ПроцессВладелец.УзелОбмена = &УзелОбмена");
		
		Запрос.УстановитьПараметр("УзелОбмена", РаботаСБизнесПроцессами.ЭтотУзелОбменаДляОбработкиПроцессов());
	Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "И &УсловиеПоУзлуОбмена", "");
	КонецЕсли;
	
	Если Количество > 0 Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ВЫБРАТЬ", "ВЫБРАТЬ ПЕРВЫЕ " + Формат(Количество, "ЧГ=0"));
	КонецЕсли;
	
	Запрос.Текст = ТекстЗапроса;
	
	Возврат Запрос.Выполнить().Выбрать();
	
КонецФункции

// Переносит сроки шаблона рассмотрения из старых реквизитов в новые.
//
// Параметры:
//  СсылкаНаШаблон - СправочникСсылка.ШаблоныРассмотрения - ссылка на шаблон.
//
Процедура ПеренестиСрокиШаблонаРассмотренияВНовыеРеквизиты(СсылкаНаШаблон) Экспорт
	
	ШаблонОбъект = СсылкаНаШаблон.ПолучитьОбъект();
	
	ШаблонОбъект.СрокИсполненияДни = ШаблонОбъект.УдалитьСрокИсполнения;
	ШаблонОбъект.СрокИсполненияЧасы = ШаблонОбъект.УдалитьСрокИсполненияЧас;
	
	ШаблонОбъект.ВариантУстановкиСрокаИсполнения = 
		Перечисления.ВариантыУстановкиСрокаИсполнения.ОтносительныйСрок;
	
	ШаблонОбъект.СрокОбработкиРезультатовДни = 1;
	ШаблонОбъект.ВариантУстановкиСрокаОбработкиРезультатов = 
		Перечисления.ВариантыУстановкиСрокаИсполнения.ОтносительныйСрок;
	
	ШаблоныБизнесПроцессов.ЗаписатьШаблон(ШаблонОбъект, "ПростаяЗапись");
	
КонецПроцедуры

// Регистрация

// Обновляет сроки в шаблонах процессов Регистраций.
//
// Параметры:
//  ТолькоАктивные - Булево - при значении Истина обновляет только активные (которые находятся в работе) шаблоны.
//
Функция ОбновитьСрокиИсполненияШаблоновРегистрации(ТолькоАктивные = Истина)
	
	Если ТолькоАктивные Тогда
		Запрос = Новый Запрос;
		ТекстЗапроса =
			"ВЫБРАТЬ
			|	ШаблоныРегистрации.Ссылка
			|ИЗ
			|	Справочник.ШаблоныРегистрации КАК ШаблоныРегистрации
			|		ЛЕВОЕ СОЕДИНЕНИЕ БизнесПроцесс.КомплексныйПроцесс КАК ПроцессВладелец
			|		ПО ШаблоныРегистрации.КомплексныйПроцесс = ПроцессВладелец.Ссылка
			|ГДЕ
			|	ШаблоныРегистрации.КоличествоИтераций = 0
			|	И (ПроцессВладелец.Ссылка ЕСТЬ NULL 
			|			ИЛИ ПроцессВладелец.Завершен = ЛОЖЬ
			|				И ПроцессВладелец.ПометкаУдаления = ЛОЖЬ
			|				И ПроцессВладелец.Состояние = ЗНАЧЕНИЕ(Перечисление.СостоянияБизнесПроцессов.Активен)
			|				И &УсловиеПоУзлуОбмена)
			|	И ШаблоныРегистрации.ПометкаУдаления = ЛОЖЬ
			|
			|СГРУППИРОВАТЬ ПО
			|	ШаблоныРегистрации.Ссылка";
		
		Если Не Константы.ВыполняетсяПлавноеОбновлениеС20.Получить() Тогда
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
				"И &УсловиеПоУзлуОбмена", "И ПроцессВладелец.УзелОбмена = &УзелОбмена");
			
			Запрос.УстановитьПараметр("УзелОбмена", РаботаСБизнесПроцессами.ЭтотУзелОбменаДляОбработкиПроцессов());
		Иначе
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "И &УсловиеПоУзлуОбмена", "");
		КонецЕсли;
		
		Запрос.Текст = ТекстЗапроса;
		
		Выборка = Запрос.Выполнить().Выбрать();
	Иначе
		Выборка = ВыборкаНеактивныхШаблоновРегистрацииДляОбработки();
	КонецЕсли;
	
	КоличествоОбработанныхПроцессов = 0;
	
	Пока Выборка.Следующий() Цикл
		
		Попытка
			ЗаблокироватьДанныеДляРедактирования(Выборка.Ссылка);
		Исключение
			Продолжить;
		КонецПопытки;
		
		ПеренестиСрокиШаблонаРегистрацииВНовыеРеквизиты(Выборка.Ссылка);
		
		РазблокироватьДанныеДляРедактирования(Выборка.Ссылка);
		
		КоличествоОбработанныхПроцессов = КоличествоОбработанныхПроцессов + 1;
		
	КонецЦикла;
	
	Возврат КоличествоОбработанныхПроцессов;
	
КонецФункции

// Возвращает выборку неактивных шаблонов процессов регистраций.
//
// Параметры:
//  Количество - Число - количество шаблонов в выборке
//
Функция ВыборкаНеактивныхШаблоновРегистрацииДляОбработки(Количество = 1000)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("УзелОбмена", РаботаСБизнесПроцессами.ЭтотУзелОбменаДляОбработкиПроцессов());
	
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	ШаблоныРегистрации.Ссылка
		|ИЗ
		|	Справочник.ШаблоныРегистрации КАК ШаблоныРегистрации
		|		ЛЕВОЕ СОЕДИНЕНИЕ БизнесПроцесс.КомплексныйПроцесс КАК ПроцессВладелец
		|		ПО ШаблоныРегистрации.КомплексныйПроцесс = ПроцессВладелец.Ссылка
		|ГДЕ
		|	ШаблоныРегистрации.КоличествоИтераций = 0
		|	И &УсловиеПоУзлуОбмена
		|	И ШаблоныРегистрации.ПометкаУдаления = ЛОЖЬ
		|
		|СГРУППИРОВАТЬ ПО
		|	ШаблоныРегистрации.Ссылка";
		
	Если Не Константы.ВыполняетсяПлавноеОбновлениеС20.Получить() Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
			"И &УсловиеПоУзлуОбмена", "И ПроцессВладелец.УзелОбмена = &УзелОбмена");
		
		Запрос.УстановитьПараметр("УзелОбмена", РаботаСБизнесПроцессами.ЭтотУзелОбменаДляОбработкиПроцессов());
	Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "И &УсловиеПоУзлуОбмена", "");
	КонецЕсли;
	
	Если Количество > 0 Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ВЫБРАТЬ", "ВЫБРАТЬ ПЕРВЫЕ " + Формат(Количество, "ЧГ=0"));
	КонецЕсли;
	
	Запрос.Текст = ТекстЗапроса;
		
	Возврат Запрос.Выполнить().Выбрать();
	
КонецФункции

// Переносит сроки шаблона регистрации из старых реквизитов в новые.
//
// Параметры:
//  СсылкаНаШаблон - СправочникСсылка.ШаблоныРегистрации - ссылка на шаблон.
//
Процедура ПеренестиСрокиШаблонаРегистрацииВНовыеРеквизиты(СсылкаНаШаблон) Экспорт
	
	ШаблонОбъект = СсылкаНаШаблон.ПолучитьОбъект();
	
	ШаблонОбъект.СрокИсполненияДни = ШаблонОбъект.УдалитьСрокИсполнения;
	ШаблонОбъект.СрокИсполненияЧасы = ШаблонОбъект.УдалитьСрокИсполненияЧас;
	
	ШаблонОбъект.ВариантУстановкиСрокаИсполнения = 
		Перечисления.ВариантыУстановкиСрокаИсполнения.ОтносительныйСрок;
	
	ШаблонОбъект.СрокОбработкиРезультатовДни = 1;
	ШаблонОбъект.ВариантУстановкиСрокаОбработкиРезультатов = 
		Перечисления.ВариантыУстановкиСрокаИсполнения.ОтносительныйСрок;
		
	ШаблонОбъект.КоличествоИтераций = 1;
		
	ШаблоныБизнесПроцессов.ЗаписатьШаблон(ШаблонОбъект, "ПростаяЗапись");
	
КонецПроцедуры

// Согласование

// Обновляет сроки в шаблонах процессов Согласований.
//
// Параметры:
//  ТолькоАктивные - Булево - при значении Истина обновляет только активные (которые находятся в работе) шаблоны.
//
Функция ОбновитьСрокиИсполненияШаблоновСогласования(ТолькоАктивные = Истина)
	
	Если ТолькоАктивные Тогда
		Запрос = Новый Запрос;
		
		ТекстЗапроса =
			"ВЫБРАТЬ
			|	ШаблоныСогласования.Ссылка
			|ИЗ
			|	Справочник.ШаблоныСогласования КАК ШаблоныСогласования
			|		ЛЕВОЕ СОЕДИНЕНИЕ БизнесПроцесс.КомплексныйПроцесс КАК ПроцессВладелец
			|		ПО ШаблоныСогласования.КомплексныйПроцесс = ПроцессВладелец.Ссылка
			|ГДЕ
			|	(ПроцессВладелец.Ссылка ЕСТЬ NULL 
			|			ИЛИ ПроцессВладелец.Завершен = ЛОЖЬ
			|				И ПроцессВладелец.ПометкаУдаления = ЛОЖЬ
			|				И ПроцессВладелец.Состояние = ЗНАЧЕНИЕ(Перечисление.СостоянияБизнесПроцессов.Активен)
			|				И &УсловиеПоУзлуОбмена)
			|	И ШаблоныСогласования.ПометкаУдаления = ЛОЖЬ
			|	И ШаблоныСогласования.КоличествоИтераций = 0
			|
			|СГРУППИРОВАТЬ ПО
			|	ШаблоныСогласования.Ссылка";
			
		Если Не Константы.ВыполняетсяПлавноеОбновлениеС20.Получить() Тогда
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
				"И &УсловиеПоУзлуОбмена", "И ПроцессВладелец.УзелОбмена = &УзелОбмена");
			
			Запрос.УстановитьПараметр("УзелОбмена", РаботаСБизнесПроцессами.ЭтотУзелОбменаДляОбработкиПроцессов());
		Иначе
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "И &УсловиеПоУзлуОбмена", "");
		КонецЕсли;
		
		Запрос.Текст = ТекстЗапроса;
		
		Выборка = Запрос.Выполнить().Выбрать();
	Иначе
		Выборка = ВыборкаНеактивныхШаблоновСогласованияДляОбработки();
	КонецЕсли;
	
	КоличествоОбработанныхПроцессов = 0;
	
	Пока Выборка.Следующий() Цикл
		
		Попытка
			ЗаблокироватьДанныеДляРедактирования(Выборка.Ссылка);
		Исключение
			Продолжить;
		КонецПопытки;
		
		ПеренестиСрокиШаблонаСогласованияВНовыеРеквизиты(Выборка.Ссылка);
		
		РазблокироватьДанныеДляРедактирования(Выборка.Ссылка);
		
		КоличествоОбработанныхПроцессов = КоличествоОбработанныхПроцессов + 1;
		
	КонецЦикла;
	
	Возврат КоличествоОбработанныхПроцессов;
	
КонецФункции

// Возвращает выборку неактивных шаблонов процессов согласований.
//
// Параметры:
//  Количество - Число - количество шаблонов в выборке
//
Функция ВыборкаНеактивныхШаблоновСогласованияДляОбработки(Количество = 1000)
	
	Запрос = Новый Запрос;
	
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	ШаблоныСогласования.Ссылка
		|ИЗ
		|	Справочник.ШаблоныСогласования КАК ШаблоныСогласования
		|		ЛЕВОЕ СОЕДИНЕНИЕ БизнесПроцесс.КомплексныйПроцесс КАК ПроцессВладелец
		|		ПО ШаблоныСогласования.КомплексныйПроцесс = ПроцессВладелец.Ссылка
		|ГДЕ
		|	ШаблоныСогласования.ПометкаУдаления = ЛОЖЬ
		|	И ШаблоныСогласования.КоличествоИтераций = 0
		|	И &УсловиеПоУзлуОбмена
		|
		|СГРУППИРОВАТЬ ПО
		|	ШаблоныСогласования.Ссылка";
		
	Если Не Константы.ВыполняетсяПлавноеОбновлениеС20.Получить() Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
			"И &УсловиеПоУзлуОбмена", "И ПроцессВладелец.УзелОбмена = &УзелОбмена");
		
		Запрос.УстановитьПараметр("УзелОбмена", РаботаСБизнесПроцессами.ЭтотУзелОбменаДляОбработкиПроцессов());
	Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "И &УсловиеПоУзлуОбмена", "");
	КонецЕсли;
	
	Если Количество > 0 Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ВЫБРАТЬ", "ВЫБРАТЬ ПЕРВЫЕ " + Формат(Количество, "ЧГ=0"));
	КонецЕсли;
	
	Запрос.Текст = ТекстЗапроса;
	
	Возврат Запрос.Выполнить().Выбрать();
	
КонецФункции

// Переносит сроки шаблона согласования из старых реквизитов в новые.
//
// Параметры:
//  СсылкаНаШаблон - СправочникСсылка.ШаблоныСогласования - ссылка на шаблон.
//
Процедура ПеренестиСрокиШаблонаСогласованияВНовыеРеквизиты(СсылкаНаШаблон) Экспорт
	
	ШаблонОбъект = СсылкаНаШаблон.ПолучитьОбъект();
	
	Для Каждого СтрИсполнители Из ШаблонОбъект.Исполнители Цикл
		
		Если ШаблонОбъект.УдалитьРазныеСроки Тогда
			СтрИсполнители.СрокИсполненияДни = СтрИсполнители.УдалитьСрокИсполнения;
			СтрИсполнители.СрокИсполненияЧасы = СтрИсполнители.УдалитьСрокИсполненияЧас;
		Иначе
			СтрИсполнители.СрокИсполненияДни = ШаблонОбъект.УдалитьСрокИсполнения;
			СтрИсполнители.СрокИсполненияЧасы = ШаблонОбъект.УдалитьСрокИсполненияЧас;
		КонецЕсли;
		
		СтрИсполнители.ВариантУстановкиСрокаИсполнения = 
			Перечисления.ВариантыУстановкиСрокаИсполнения.ОтносительныйСрок;
	КонецЦикла;
	
	ШаблонОбъект.СрокОбработкиРезультатовДни = 1;
	ШаблонОбъект.ВариантУстановкиСрокаОбработкиРезультатов = 
		Перечисления.ВариантыУстановкиСрокаИсполнения.ОтносительныйСрок;
	
	ШаблонОбъект.КоличествоИтераций = 1;
	
	ШаблоныБизнесПроцессов.ЗаписатьШаблон(ШаблонОбъект, "ПростаяЗапись");
	
КонецПроцедуры

// Утверждение

// Обновляет сроки в шаблонах процессов Утверждений.
//
// Параметры:
//  ТолькоАктивные - Булево - при значении Истина обновляет только активные (которые находятся в работе) шаблоны.
//
Функция ОбновитьСрокиИсполненияШаблоновУтверждения(ТолькоАктивные = Истина)
	
	Если ТолькоАктивные Тогда
		Запрос = Новый Запрос;
		
		ТекстЗапроса =
			"ВЫБРАТЬ
			|	ШаблоныУтверждения.Ссылка
			|ИЗ
			|	Справочник.ШаблоныУтверждения КАК ШаблоныУтверждения
			|		ЛЕВОЕ СОЕДИНЕНИЕ БизнесПроцесс.КомплексныйПроцесс КАК ПроцессВладелец
			|		ПО ШаблоныУтверждения.КомплексныйПроцесс = ПроцессВладелец.Ссылка
			|ГДЕ
			|	(ПроцессВладелец.Ссылка ЕСТЬ NULL 
			|			ИЛИ ПроцессВладелец.Завершен = ЛОЖЬ
			|				И ПроцессВладелец.ПометкаУдаления = ЛОЖЬ
			|				И ПроцессВладелец.Состояние = ЗНАЧЕНИЕ(Перечисление.СостоянияБизнесПроцессов.Активен)
			|				И &УсловиеПоУзлуОбмена)
			|	И ШаблоныУтверждения.ПометкаУдаления = ЛОЖЬ
			|	И ШаблоныУтверждения.КоличествоИтераций = 0
			|
			|СГРУППИРОВАТЬ ПО
			|	ШаблоныУтверждения.Ссылка";
			
		Если Не Константы.ВыполняетсяПлавноеОбновлениеС20.Получить() Тогда
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
				"И &УсловиеПоУзлуОбмена", "И ПроцессВладелец.УзелОбмена = &УзелОбмена");
			
			Запрос.УстановитьПараметр("УзелОбмена", РаботаСБизнесПроцессами.ЭтотУзелОбменаДляОбработкиПроцессов());
		Иначе
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "И &УсловиеПоУзлуОбмена", "");
		КонецЕсли;
		
		Запрос.Текст = ТекстЗапроса;
			
		Выборка = Запрос.Выполнить().Выбрать();
	Иначе
		Выборка = ВыборкаНеактивныхШаблоновУтвержденияДляОбработки();
	КонецЕсли;
	
	КоличествоОбработанныхПроцессов = 0;
	
	Пока Выборка.Следующий() Цикл
		
		Попытка
			ЗаблокироватьДанныеДляРедактирования(Выборка.Ссылка);
		Исключение
			Продолжить;
		КонецПопытки;
		
		ПеренестиСрокиШаблонаУтвержденияВНовыеРеквизиты(Выборка.Ссылка);
		
		РазблокироватьДанныеДляРедактирования(Выборка.Ссылка);
		
		КоличествоОбработанныхПроцессов = КоличествоОбработанныхПроцессов + 1;
		
	КонецЦикла;
	
	Возврат КоличествоОбработанныхПроцессов;
	
КонецФункции

// Возвращает выборку неактивных шаблонов процессов утверждений.
//
// Параметры:
//  Количество - Число - количество шаблонов в выборке
//
Функция ВыборкаНеактивныхШаблоновУтвержденияДляОбработки(Количество = 1000)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("УзелОбмена", РаботаСБизнесПроцессами.ЭтотУзелОбменаДляОбработкиПроцессов());
	
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	ШаблоныУтверждения.Ссылка
		|ИЗ
		|	Справочник.ШаблоныУтверждения КАК ШаблоныУтверждения
		|		ЛЕВОЕ СОЕДИНЕНИЕ БизнесПроцесс.КомплексныйПроцесс КАК ПроцессВладелец
		|		ПО ШаблоныУтверждения.КомплексныйПроцесс = ПроцессВладелец.Ссылка
		|ГДЕ
		|	ШаблоныУтверждения.КоличествоИтераций = 0
		|	И &УсловиеПоУзлуОбмена
		|	И ШаблоныУтверждения.ПометкаУдаления = ЛОЖЬ
		|
		|СГРУППИРОВАТЬ ПО
		|	ШаблоныУтверждения.Ссылка";
		
	Если Не Константы.ВыполняетсяПлавноеОбновлениеС20.Получить() Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
			"И &УсловиеПоУзлуОбмена", "И ПроцессВладелец.УзелОбмена = &УзелОбмена");
		
		Запрос.УстановитьПараметр("УзелОбмена", РаботаСБизнесПроцессами.ЭтотУзелОбменаДляОбработкиПроцессов());
	Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "И &УсловиеПоУзлуОбмена", "");
	КонецЕсли;
	
	Если Количество > 0 Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ВЫБРАТЬ", "ВЫБРАТЬ ПЕРВЫЕ " + Формат(Количество, "ЧГ=0"));
	КонецЕсли;
	
	Запрос.Текст = ТекстЗапроса;
		
	Возврат Запрос.Выполнить().Выбрать();
	
КонецФункции

// Переносит сроки шаблона утверждения из старых реквизитов в новые.
//
// Параметры:
//  СсылкаНаШаблон - СправочникСсылка.ШаблоныУтверждения - ссылка на шаблон.
//
Процедура ПеренестиСрокиШаблонаУтвержденияВНовыеРеквизиты(СсылкаНаШаблон) Экспорт
	
	ШаблонОбъект = СсылкаНаШаблон.ПолучитьОбъект();
	
	ШаблонОбъект.СрокИсполненияДни = ШаблонОбъект.УдалитьСрокИсполнения;
	ШаблонОбъект.СрокИсполненияЧасы = ШаблонОбъект.УдалитьСрокИсполненияЧас;
	
	ШаблонОбъект.ВариантУстановкиСрокаИсполнения = 
	Перечисления.ВариантыУстановкиСрокаИсполнения.ОтносительныйСрок;
	
	ШаблонОбъект.СрокОбработкиРезультатовДни = 1;
	ШаблонОбъект.ВариантУстановкиСрокаОбработкиРезультатов = 
	Перечисления.ВариантыУстановкиСрокаИсполнения.ОтносительныйСрок;
	
	ШаблонОбъект.КоличествоИтераций = 1;
	
	ШаблоныБизнесПроцессов.ЗаписатьШаблон(ШаблонОбъект, "ПростаяЗапись");
	
КонецПроцедуры

//////////////////////////
// Процессы

// Помещает активные процессы во временную таблицу запроса АктивныеПроцессы.
//
// Параметры:
//  Запрос - Запрос в котором создается временная таблица АктивныеПроцессы.
//  ИмяТаблицыПроцессов - Строка - полное имя таблицы бизнес-процессов (пример: БизнесПроцесс.Исполнение).
//  ИмяТаблицыШаблоновПроцессов - Строка - полное имя таблицы шаблона процесса (пример: Справочник.ШаблоныИсполнения).
//
Процедура ПоместитьАктивныеПроцессыВВиртуальнуюТаблицуЗапроса(
	Запрос, ИмяТаблицыПроцессов, ИмяТаблицыШаблоновПроцессов)
	
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	Процессы.Ссылка
		|ПОМЕСТИТЬ АктивныеПроцессы
		|ИЗ
		|	#ТаблицаПроцессов КАК Процессы
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ДочерниеБизнесПроцессы КАК ДочерниеБизнесПроцессы
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ДанныеБизнесПроцессов КАК ДанныеБизнесПроцессов
		|			ПО ДочерниеБизнесПроцессы.РодительскийПроцесс = ДанныеБизнесПроцессов.БизнесПроцесс
		|		ПО Процессы.Ссылка = ДочерниеБизнесПроцессы.ДочернийПроцесс
		|			И Процессы.ВедущаяЗадача = ДочерниеБизнесПроцессы.СвязующаяЗадача
		|ГДЕ
		|	Процессы.ПометкаУдаления = ЛОЖЬ
		|	И Процессы.Состояние = ЗНАЧЕНИЕ(Перечисление.СостоянияБизнесПроцессов.Активен)
		|	И ДанныеБизнесПроцессов.Завершен = ЛОЖЬ
		|	И &УсловиеПоУзлуОбмена
		|
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ
		|	Процессы.Ссылка
		|ИЗ
		|	#ТаблицаПроцессов КАК Процессы
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ #ТаблицаШаблонов КАК ШаблоныПроцесса
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ БизнесПроцесс.КомплексныйПроцесс КАК ВедущиеПроцессы
		|			ПО ШаблоныПроцесса.КомплексныйПроцесс = ВедущиеПроцессы.Ссылка
		|		ПО Процессы.Шаблон = ШаблоныПроцесса.Ссылка
		|ГДЕ
		|	Процессы.ПометкаУдаления = ЛОЖЬ
		|	И Процессы.Состояние = ЗНАЧЕНИЕ(Перечисление.СостоянияБизнесПроцессов.Активен)
		|	И ВедущиеПроцессы.Завершен = ЛОЖЬ
		|	И &УсловиеПоУзлуОбмена
		|
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ
		|	Процессы.Ссылка
		|ИЗ
		|	#ТаблицаПроцессов КАК Процессы
		|ГДЕ
		|	Процессы.ПометкаУдаления = ЛОЖЬ
		|	И Процессы.Состояние = ЗНАЧЕНИЕ(Перечисление.СостоянияБизнесПроцессов.Активен)
		|	И Процессы.Завершен = ЛОЖЬ
		|	И &УсловиеПоУзлуОбмена";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "#ТаблицаПроцессов", ИмяТаблицыПроцессов);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "#ТаблицаШаблонов", ИмяТаблицыШаблоновПроцессов);
	
	Если Не Константы.ВыполняетсяПлавноеОбновлениеС20.Получить() Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
			"И &УсловиеПоУзлуОбмена", "И Процессы.УзелОбмена = &УзелОбмена");
		
		Запрос.УстановитьПараметр("УзелОбмена", РаботаСБизнесПроцессами.ЭтотУзелОбменаДляОбработкиПроцессов());
	Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "И &УсловиеПоУзлуОбмена", "");
	КонецЕсли;
	
	Запрос.Текст = ТекстЗапроса;
	
	Запрос.Выполнить();
	
КонецПроцедуры

// Помечает рассчитанными сроки простых процессов в таблице сроков
// для расчета общего срока составных и комплексных процессов.
//
// Параметры:
//  Сроки - ТаблицаЗначений - см. функцию СрокиИсполненияПроцессов.ПустаяТаблицаСроков.
//
Процедура ПометитьРассчитаннымиСрокиПростыхПроцессовВТаблицеСроков(Сроки)
	
	Для Каждого СтрокаСрок Из Сроки Цикл
		
		Если СтрокаСрок.ПодчиненныеСроки.Количество() = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		Если ШаблоныБизнесПроцессовКлиентСервер.ЭтоШаблонПроцесса(СтрокаСрок.ПодчиненныеСроки.Ссылка) Тогда
			Продолжить;
		КонецЕсли;
		
		ТипПроцесса = ТипЗнч(СтрокаСрок.ПодчиненныеСроки.Ссылка);
		
		Если ТипПроцесса <> Тип("БизнесПроцессСсылка.КомплексныйПроцесс")
			И ТипПроцесса <> Тип("БизнесПроцессСсылка.ОбработкаВнутреннегоДокумента")
			И ТипПроцесса <> Тип("БизнесПроцессСсылка.ОбработкаВходящегоДокумента")
			И ТипПроцесса <> Тип("БизнесПроцессСсылка.ОбработкаИсходящегоДокумента") Тогда
			
			СтрокаСрок.Пройден = Истина;
		КонецЕсли;
		
		ПометитьРассчитаннымиСрокиПростыхПроцессовВТаблицеСроков(СтрокаСрок.ПодчиненныеСроки.Сроки);
		
	КонецЦикла;
	
КонецПроцедуры

// Исполнение

// Обновляет сроки в процессах Исполнение.
//
// Параметры:
//  ТолькоАктивные - Булево - при значении Истина обновляет только активные (которые находятся в работе) процессы.
//
Функция ОбновитьСрокиИсполненияПроцессовИсполнения(ТолькоАктивные = Истина)
	
	Если ТолькоАктивные Тогда
		Запрос = Новый Запрос;
		
		ПоместитьАктивныеПроцессыВВиртуальнуюТаблицуЗапроса(
			Запрос, "БизнесПроцесс.Исполнение", "Справочник.ШаблоныИсполнения");
			
		Запрос.Текст =
			"ВЫБРАТЬ
			|	АктивныеПроцессы.Ссылка,
			|	Исполнение.Дата,
			|	ПроцессыДляЗапуска.ДатаОтложенногоСтарта
			|ИЗ
			|	АктивныеПроцессы КАК АктивныеПроцессы
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ БизнесПроцесс.Исполнение КАК Исполнение
			|		ПО АктивныеПроцессы.Ссылка = Исполнение.Ссылка
			|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПроцессыДляЗапуска КАК ПроцессыДляЗапуска
			|		ПО АктивныеПроцессы.Ссылка = ПроцессыДляЗапуска.БизнесПроцесс
			|ГДЕ
			|	Исполнение.КоличествоИтераций = 0
			|
			|СГРУППИРОВАТЬ ПО
			|	АктивныеПроцессы.Ссылка,
			|	Исполнение.Дата,
			|	ПроцессыДляЗапуска.ДатаОтложенногоСтарта";
			
		Выборка = Запрос.Выполнить().Выбрать();
	Иначе
		Выборка = ВыборкаНеактивныхПроцессовИсполненияДляОбработки();
	КонецЕсли;
	
	КоличествоОбработанныхПроцессов = 0;
	
	Пока Выборка.Следующий() Цикл
		
		Попытка
			ЗаблокироватьДанныеДляРедактирования(Выборка.Ссылка);
		Исключение
			Продолжить;
		КонецПопытки;
		
		ДатаОтсчета = ТекущаяДатаСеанса();
		Если ЗначениеЗаполнено(Выборка.ДатаОтложенногоСтарта) Тогда
			ДатаОтсчета = Выборка.ДатаОтложенногоСтарта;
		КонецЕсли;
		Если ЗначениеЗаполнено(Выборка.Дата) Тогда
			ДатаОтсчета = Выборка.Дата;
		КонецЕсли;
		
		ПеренестиСрокиИсполненияВНовыеРеквизиты(Выборка.Ссылка, ДатаОтсчета);
		
		РазблокироватьДанныеДляРедактирования(Выборка.Ссылка);
		
		КоличествоОбработанныхПроцессов = КоличествоОбработанныхПроцессов + 1;
		
	КонецЦикла;
	
	Возврат КоличествоОбработанныхПроцессов;
	
КонецФункции

// Возвращает выборку неактивных процессов Исполнение.
//
// Параметры:
//  Количество - Число - количество процессов в выборке
//
Функция ВыборкаНеактивныхПроцессовИсполненияДляОбработки(Количество = 1000)
	
	Запрос = Новый Запрос;
	
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	Исполнение.Ссылка,
		|	Исполнение.Дата,
		|	ПроцессыДляЗапуска.ДатаОтложенногоСтарта
		|ИЗ
		|	БизнесПроцесс.Исполнение КАК Исполнение
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПроцессыДляЗапуска КАК ПроцессыДляЗапуска
		|		ПО Исполнение.Ссылка = ПроцессыДляЗапуска.БизнесПроцесс
		|ГДЕ
		|	Исполнение.ПометкаУдаления = ЛОЖЬ
		|	И Исполнение.КоличествоИтераций = 0
		|	И &УсловиеПоУзлуОбмена
		|
		|СГРУППИРОВАТЬ ПО
		|	Исполнение.Ссылка,
		|	Исполнение.Дата,
		|	ПроцессыДляЗапуска.ДатаОтложенногоСтарта";
		
	Если Не Константы.ВыполняетсяПлавноеОбновлениеС20.Получить() Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
			"И &УсловиеПоУзлуОбмена", "И Исполнение.УзелОбмена = &УзелОбмена");
		
		Запрос.УстановитьПараметр("УзелОбмена", РаботаСБизнесПроцессами.ЭтотУзелОбменаДляОбработкиПроцессов());
	Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "И &УсловиеПоУзлуОбмена", "");
	КонецЕсли;
	
	Если Количество > 0 Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ВЫБРАТЬ", "ВЫБРАТЬ ПЕРВЫЕ " + Формат(Количество, "ЧГ=0"));
	КонецЕсли;
	
	Запрос.Текст = ТекстЗапроса;
	
	Возврат Запрос.Выполнить().Выбрать();
	
КонецФункции

// Переносит сроки Исполнения из старых реквизитов в новые.
//
// Параметры:
//  СсылкаНаШаблон - БизнесПроцессСсылка.Исполнение - ссылка на процесс.
//  ДатаОтсчета - Дата - дата отсчета для рассчета сроков исполнителей.
//
Процедура ПеренестиСрокиИсполненияВНовыеРеквизиты(СсылкаНаПроцесс, ДатаОтсчета = '00010101') Экспорт
	
	ПроцессОбъект = СсылкаНаПроцесс.ПолучитьОбъект();
	
	ПолучитьСведенияОЗапускеПроцесса = 
		РегистрыСведений.ПроцессыДляЗапуска.ПолучитьСведенияОЗапускеПроцесса(СсылкаНаПроцесс);
	
	Если Не ЗначениеЗаполнено(ДатаОтсчета) Тогда
		ДатаОтсчета = СрокиИсполненияПроцессов.ДатаОтсчетаДляРасчетаСроковПроцесса(ПроцессОбъект);
		Смещение = СмещенияДатыОтсчета(СсылкаНаПроцесс);
		
		ДатаОтсчета = Макс(ДатаОтсчета, Смещение);
	КонецЕсли;
	
	Если ПроцессОбъект.ВариантИсполнения = Перечисления.ВариантыМаршрутизацииЗадач.Параллельно Тогда
		Для Каждого СтрИсполнитель Из ПроцессОбъект.Исполнители Цикл
			
			Если ЗначениеЗаполнено(СтрИсполнитель.УдалитьСрокИсполнения) Тогда
				СтрИсполнитель.СрокИсполнения = СтрИсполнитель.УдалитьСрокИсполнения;
			Иначе
				СтрИсполнитель.СрокИсполнения = ПроцессОбъект.УдалитьСрокИсполнения;
			КонецЕсли;
			
			СтрИсполнитель.ВариантУстановкиСрокаИсполнения = 
				Перечисления.ВариантыУстановкиСрокаИсполнения.ТочныйСрок;
				
			Попытка
				
				ГрафикРаботыИсполнителя = ГрафикРаботыУчастникаПроцесса(СтрИсполнитель.Исполнитель);
				
				ПараметрыСрокаИсполнения = 
					СрокиИсполненияПроцессов.ПараметрыСрокаИсполненияДляОпределенияДлительности();
				
				ПараметрыСрокаИсполнения.СрокИсполнения = СтрИсполнитель.СрокИсполнения;
				ПараметрыСрокаИсполнения.ДатаОтсчета = ДатаОтсчета;
				ПараметрыСрокаИсполнения.ГрафикРаботы = ГрафикРаботыИсполнителя;
				
				Длительность = 
					СрокиИсполненияПроцессов.ДлительностьПоСрокуИсполнения(ПараметрыСрокаИсполнения);
				
				СтрИсполнитель.СрокИсполненияДни = Длительность.Дни;
				СтрИсполнитель.СрокИсполненияЧасы = Длительность.Часы;
				СтрИсполнитель.СрокИсполненияМинуты = Длительность.Минуты;
			Исключение
				// Длительность может и не рассчитаться, из-за незаполненного графика работ.
				// В этом случае обновление будет прервано и база останется неработоспособной.
			КонецПопытки;
			
		КонецЦикла;
	Иначе
		Для Каждого СтрИсполнитель Из ПроцессОбъект.Исполнители Цикл
			СтрИсполнитель.СрокИсполненияДни = СтрИсполнитель.УдалитьДлительностьИсполненияДни;
			СтрИсполнитель.СрокИсполненияЧасы = СтрИсполнитель.УдалитьДлительностьИсполненияЧасы;
			СтрИсполнитель.ВариантУстановкиСрокаИсполнения = 
				Перечисления.ВариантыУстановкиСрокаИсполнения.ОтносительныйСрок;
			Если ЗначениеЗаполнено(СтрИсполнитель.ЗадачаИсполнителя) Тогда
				СтрИсполнитель.СрокИсполнения = 
					ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СтрИсполнитель.ЗадачаИсполнителя, "СрокИсполнения");
			Иначе
				СтрИсполнитель.СрокИсполнения = СтрИсполнитель.УдалитьСрокИсполнения;
			КонецЕсли;
			
		КонецЦикла;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ПроцессОбъект.Проверяющий) Тогда
		ПроцессОбъект.СрокОбработкиРезультатов = ПроцессОбъект.УдалитьСрокИсполнения;
		ПроцессОбъект.ВариантУстановкиСрокаОбработкиРезультатов = 
			Перечисления.ВариантыУстановкиСрокаИсполнения.ТочныйСрок;
	КонецЕсли;
	
	ПроцессОбъект.СрокИсполненияПроцесса = ПроцессОбъект.УдалитьСрокИсполнения;
	
	ПроцессОбъект.КоличествоИтераций = 1;
	
	РаботаСБизнесПроцессами.ЗаписатьПроцесс(ПроцессОбъект, "ЗаписьСОбновлениемОбщегоСпискаПроцессов");
	
КонецПроцедуры

// Ознакомление

// Обновляет сроки в процессах Ознакомление.
//
// Параметры:
//  ТолькоАктивные - Булево - при значении Истина обновляет только активные (которые находятся в работе) процессы.
//
Функция ОбновитьСрокиИсполненияПроцессовОзнакомления(ТолькоАктивные = Истина)
	
	Если ТолькоАктивные Тогда
		
		Запрос = Новый Запрос;
		
		ПоместитьАктивныеПроцессыВВиртуальнуюТаблицуЗапроса(
			Запрос, "БизнесПроцесс.Ознакомление", "Справочник.ШаблоныОзнакомления");
		
		Запрос.Текст =
			"ВЫБРАТЬ
			|	АктивныеПроцессы.Ссылка,
			|	Ознакомление.Дата,
			|	ПроцессыДляЗапуска.ДатаОтложенногоСтарта
			|ИЗ
			|	АктивныеПроцессы КАК АктивныеПроцессы
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ БизнесПроцесс.Ознакомление КАК Ознакомление
			|		ПО АктивныеПроцессы.Ссылка = Ознакомление.Ссылка
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ БизнесПроцесс.Ознакомление.Исполнители КАК ОзнакомлениеИсполнители
			|		ПО АктивныеПроцессы.Ссылка = ОзнакомлениеИсполнители.Ссылка
			|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПроцессыДляЗапуска КАК ПроцессыДляЗапуска
			|		ПО АктивныеПроцессы.Ссылка = ПроцессыДляЗапуска.БизнесПроцесс
			|ГДЕ
			|	Ознакомление.УдалитьСрокИсполнения <> ДАТАВРЕМЯ(1, 1, 1)
			|	И Ознакомление.СрокИсполненияПроцесса = ДАТАВРЕМЯ(1, 1, 1)
			|	И ОзнакомлениеИсполнители.СрокИсполнения = ДАТАВРЕМЯ(1, 1, 1)
			|	И ОзнакомлениеИсполнители.СрокИсполненияДни = 0
			|	И ОзнакомлениеИсполнители.СрокИсполненияЧасы = 0
			|	И ОзнакомлениеИсполнители.СрокИсполненияМинуты = 0
			|
			|СГРУППИРОВАТЬ ПО
			|	АктивныеПроцессы.Ссылка,
			|	Ознакомление.Дата,
			|	ПроцессыДляЗапуска.ДатаОтложенногоСтарта";
			
		Выборка = Запрос.Выполнить().Выбрать();
	Иначе
		Выборка = ВыборкаНеактивныхПроцессовОзнакомленияДляОбработки();
	КонецЕсли;
	
	КоличествоОбработанныхПроцессов = 0;
	
	Пока Выборка.Следующий() Цикл
		
		Попытка
			ЗаблокироватьДанныеДляРедактирования(Выборка.Ссылка);
		Исключение
			Продолжить;
		КонецПопытки;
		
		ДатаОтсчета = ТекущаяДатаСеанса();
		Если ЗначениеЗаполнено(Выборка.ДатаОтложенногоСтарта) Тогда
			ДатаОтсчета = Выборка.ДатаОтложенногоСтарта;
		КонецЕсли;
		Если ЗначениеЗаполнено(Выборка.Дата) Тогда
			ДатаОтсчета = Выборка.Дата;
		КонецЕсли;
		
		ПеренестиСрокиОзнакомленияВНовыеРеквизиты(Выборка.Ссылка, ДатаОтсчета);
		
		РазблокироватьДанныеДляРедактирования(Выборка.Ссылка);
		
		КоличествоОбработанныхПроцессов = КоличествоОбработанныхПроцессов + 1;
		
	КонецЦикла;
	
	Возврат КоличествоОбработанныхПроцессов;
	
КонецФункции

// Возвращает выборку неактивных процессов Ознакомление.
//
// Параметры:
//  Количество - Число - количество процессов в выборке
//
Функция ВыборкаНеактивныхПроцессовОзнакомленияДляОбработки(Количество = 1000)
	
	Запрос = Новый Запрос;
	
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	Ознакомление.Ссылка,
		|	Ознакомление.Дата,
		|	ПроцессыДляЗапуска.ДатаОтложенногоСтарта
		|ИЗ
		|	БизнесПроцесс.Ознакомление.Исполнители КАК ОзнакомлениеИсполнители
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ БизнесПроцесс.Ознакомление КАК Ознакомление
		|		ПО ОзнакомлениеИсполнители.Ссылка = Ознакомление.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПроцессыДляЗапуска КАК ПроцессыДляЗапуска
		|		ПО Ознакомление.Ссылка = ПроцессыДляЗапуска.БизнесПроцесс
		|ГДЕ
		|	Ознакомление.ПометкаУдаления = ЛОЖЬ
		|	И Ознакомление.УдалитьСрокИсполнения <> ДАТАВРЕМЯ(1, 1, 1)
		|	И ОзнакомлениеИсполнители.СрокИсполнения = ДАТАВРЕМЯ(1, 1, 1)
		|	И ОзнакомлениеИсполнители.СрокИсполненияДни = 0
		|	И ОзнакомлениеИсполнители.СрокИсполненияЧасы = 0
		|	И ОзнакомлениеИсполнители.СрокИсполненияМинуты = 0
		|	И Ознакомление.СрокИсполненияПроцесса = ДАТАВРЕМЯ(1, 1, 1)
		|	И &УсловиеПоУзлуОбмена
		|
		|СГРУППИРОВАТЬ ПО
		|	Ознакомление.Ссылка,
		|	Ознакомление.Дата,
		|	ПроцессыДляЗапуска.ДатаОтложенногоСтарта";
		
	Если Не Константы.ВыполняетсяПлавноеОбновлениеС20.Получить() Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
			"И &УсловиеПоУзлуОбмена", "И Ознакомление.УзелОбмена = &УзелОбмена");
		
		Запрос.УстановитьПараметр("УзелОбмена", РаботаСБизнесПроцессами.ЭтотУзелОбменаДляОбработкиПроцессов());
	Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "И &УсловиеПоУзлуОбмена", "");
	КонецЕсли;
	
	Если Количество > 0 Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ВЫБРАТЬ", "ВЫБРАТЬ ПЕРВЫЕ " + Формат(Количество, "ЧГ=0"));
	КонецЕсли;
	
	Запрос.Текст = ТекстЗапроса;
		
	Возврат Запрос.Выполнить().Выбрать();
	
КонецФункции

// Переносит сроки Ознакомления из старых реквизитов в новые.
//
// Параметры:
//  СсылкаНаШаблон - БизнесПроцессСсылка.Ознакомление - ссылка на процесс.
//  ДатаОтсчета - Дата - дата отсчета для рассчета сроков исполнителей.
//
Процедура ПеренестиСрокиОзнакомленияВНовыеРеквизиты(СсылкаНаПроцесс, ДатаОтсчета = '00010101') Экспорт
	
	ПроцессОбъект = СсылкаНаПроцесс.ПолучитьОбъект();
	
	Если Не ЗначениеЗаполнено(ДатаОтсчета) Тогда
		ДатаОтсчета = СрокиИсполненияПроцессов.ДатаОтсчетаДляРасчетаСроковПроцесса(ПроцессОбъект);
		Смещение = СмещенияДатыОтсчета(СсылкаНаПроцесс);
		
		ДатаОтсчета = Макс(ДатаОтсчета, Смещение);
	КонецЕсли;
	
	Для Каждого СтрИсполнитель Из ПроцессОбъект.Исполнители Цикл
		
		СтрИсполнитель.СрокИсполнения = ПроцессОбъект.УдалитьСрокИсполнения;
		СтрИсполнитель.ВариантУстановкиСрокаИсполнения = 
		Перечисления.ВариантыУстановкиСрокаИсполнения.ТочныйСрок;
		
		Попытка
			
			ГрафикРаботыИсполнителя = ГрафикРаботыУчастникаПроцесса(СтрИсполнитель.Исполнитель);
			
			ПараметрыСрокаИсполнения = 
			СрокиИсполненияПроцессов.ПараметрыСрокаИсполненияДляОпределенияДлительности();
			
			ПараметрыСрокаИсполнения.СрокИсполнения = СтрИсполнитель.СрокИсполнения;
			ПараметрыСрокаИсполнения.ДатаОтсчета = ДатаОтсчета;
			ПараметрыСрокаИсполнения.ГрафикРаботы = ГрафикРаботыИсполнителя;
			
			Длительность = 
			СрокиИсполненияПроцессов.ДлительностьПоСрокуИсполнения(ПараметрыСрокаИсполнения);
			
			СтрИсполнитель.СрокИсполненияДни = Длительность.Дни;
			СтрИсполнитель.СрокИсполненияЧасы = Длительность.Часы;
			СтрИсполнитель.СрокИсполненияМинуты = Длительность.Минуты;
		Исключение
			// Длительность может и не рассчитаться, из-за незаполненного графика работ.
			// В этом случае ничего не делаем и продолжаем обработку, чтобы не прерывать обновление.
		КонецПопытки;
		
	КонецЦикла;
	
	ПроцессОбъект.СрокИсполненияПроцесса = ПроцессОбъект.УдалитьСрокИсполнения;
	
	РаботаСБизнесПроцессами.ЗаписатьПроцесс(ПроцессОбъект, "ЗаписьСОбновлениемОбщегоСпискаПроцессов");
	
КонецПроцедуры

// Поручение

// Обновляет сроки в процессах Поручение.
//
// Параметры:
//  ТолькоАктивные - Булево - при значении Истина обновляет только активные (которые находятся в работе) процессы.
//
Функция ОбновитьСрокиИсполненияПроцессовПоручения(ТолькоАктивные = Истина)
	
	Если ТолькоАктивные Тогда
		
		Запрос = Новый Запрос;
		
		ПоместитьАктивныеПроцессыВВиртуальнуюТаблицуЗапроса(
			Запрос, "БизнесПроцесс.Поручение", "Справочник.ШаблоныПоручения");
		
		Запрос.Текст =
			"ВЫБРАТЬ
			|	АктивныеПроцессы.Ссылка,
			|	Поручение.Дата,
			|	ПроцессыДляЗапуска.ДатаОтложенногоСтарта
			|ИЗ
			|	АктивныеПроцессы КАК АктивныеПроцессы
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ БизнесПроцесс.Поручение КАК Поручение
			|		ПО АктивныеПроцессы.Ссылка = Поручение.Ссылка
			|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПроцессыДляЗапуска КАК ПроцессыДляЗапуска
			|		ПО АктивныеПроцессы.Ссылка = ПроцессыДляЗапуска.БизнесПроцесс
			|ГДЕ
			|	Поручение.УдалитьСрокИсполнения <> ДАТАВРЕМЯ(1, 1, 1)
			|	И Поручение.СрокИсполненияПроцесса = ДАТАВРЕМЯ(1, 1, 1)
			|	И Поручение.СрокИсполнения = ДАТАВРЕМЯ(1, 1, 1)
			|
			|СГРУППИРОВАТЬ ПО
			|	АктивныеПроцессы.Ссылка,
			|	Поручение.Дата,
			|	ПроцессыДляЗапуска.ДатаОтложенногоСтарта";
			
		Выборка = Запрос.Выполнить().Выбрать();
	Иначе
		Выборка = ВыборкаНеактивныхПроцессовПорученияДляОбработки();
	КонецЕсли;
	
	КоличествоОбработанныхПроцессов = 0;
	
	Пока Выборка.Следующий() Цикл
		
		Попытка
			ЗаблокироватьДанныеДляРедактирования(Выборка.Ссылка);
		Исключение
			Продолжить;
		КонецПопытки;
		
		ДатаОтсчета = ТекущаяДатаСеанса();
		Если ЗначениеЗаполнено(Выборка.ДатаОтложенногоСтарта) Тогда
			ДатаОтсчета = Выборка.ДатаОтложенногоСтарта;
		КонецЕсли;
		Если ЗначениеЗаполнено(Выборка.Дата) Тогда
			ДатаОтсчета = Выборка.Дата;
		КонецЕсли;
		
		ПеренестиСрокиПорученияВНовыеРеквизиты(Выборка.Ссылка, ДатаОтсчета);
		
		РазблокироватьДанныеДляРедактирования(Выборка.Ссылка);
		
		КоличествоОбработанныхПроцессов = КоличествоОбработанныхПроцессов + 1;
		
	КонецЦикла;
	
	Возврат КоличествоОбработанныхПроцессов;
	
КонецФункции

// Возвращает выборку неактивных процессов Поручение.
//
// Параметры:
//  Количество - Число - количество процессов в выборке
//
Функция ВыборкаНеактивныхПроцессовПорученияДляОбработки(Количество = 1000)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("УзелОбмена", РаботаСБизнесПроцессами.ЭтотУзелОбменаДляОбработкиПроцессов());
	
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	Поручение.Ссылка,
		|	Поручение.Дата,
		|	ПроцессыДляЗапуска.ДатаОтложенногоСтарта
		|ИЗ
		|	БизнесПроцесс.Поручение КАК Поручение
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПроцессыДляЗапуска КАК ПроцессыДляЗапуска
		|		ПО Поручение.Ссылка = ПроцессыДляЗапуска.БизнесПроцесс
		|ГДЕ
		|	Поручение.ПометкаУдаления = ЛОЖЬ
		|	И Поручение.УдалитьСрокИсполнения <> ДАТАВРЕМЯ(1, 1, 1)
		|	И Поручение.СрокИсполнения = ДАТАВРЕМЯ(1, 1, 1)
		|	И Поручение.СрокИсполненияПроцесса = ДАТАВРЕМЯ(1, 1, 1)
		|	И &УсловиеПоУзлуОбмена";
		
	Если Не Константы.ВыполняетсяПлавноеОбновлениеС20.Получить() Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
			"И &УсловиеПоУзлуОбмена", "И Поручение.УзелОбмена = &УзелОбмена");
		
		Запрос.УстановитьПараметр("УзелОбмена", РаботаСБизнесПроцессами.ЭтотУзелОбменаДляОбработкиПроцессов());
	Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "И &УсловиеПоУзлуОбмена", "");
	КонецЕсли;
	
	Если Количество > 0 Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ВЫБРАТЬ", "ВЫБРАТЬ ПЕРВЫЕ " + Формат(Количество, "ЧГ=0"));
	КонецЕсли;
	
	Запрос.Текст = ТекстЗапроса;
		
	Возврат Запрос.Выполнить().Выбрать();
	
КонецФункции

// Переносит сроки Поручения из старых реквизитов в новые.
//
// Параметры:
//  СсылкаНаШаблон - БизнесПроцессСсылка.Поручение - ссылка на процесс.
//  ДатаОтсчета - Дата - дата отсчета для рассчета сроков исполнителей.
//
Процедура ПеренестиСрокиПорученияВНовыеРеквизиты(СсылкаНаПроцесс, ДатаОтсчета = '00010101') Экспорт
	
	ПроцессОбъект = СсылкаНаПроцесс.ПолучитьОбъект();
	
	Если Не ЗначениеЗаполнено(ДатаОтсчета) Тогда
		ДатаОтсчета = СрокиИсполненияПроцессов.ДатаОтсчетаДляРасчетаСроковПроцесса(ПроцессОбъект);
		Смещение = СмещенияДатыОтсчета(СсылкаНаПроцесс);
		
		ДатаОтсчета = Макс(ДатаОтсчета, Смещение);
	КонецЕсли;
	
	ПроцессОбъект.СрокИсполнения = ПроцессОбъект.УдалитьСрокИсполнения;
	ПроцессОбъект.ВариантУстановкиСрокаИсполнения = 
		Перечисления.ВариантыУстановкиСрокаИсполнения.ТочныйСрок;
		
	Попытка
		
		ГрафикРаботыИсполнителя = ГрафикРаботыУчастникаПроцесса(ПроцессОбъект.Исполнитель);
		
		ПараметрыСрокаИсполнения = 
			СрокиИсполненияПроцессов.ПараметрыСрокаИсполненияДляОпределенияДлительности();
			
		ПараметрыСрокаИсполнения.СрокИсполнения = ПроцессОбъект.СрокИсполнения;
		ПараметрыСрокаИсполнения.ДатаОтсчета = ДатаОтсчета;
		ПараметрыСрокаИсполнения.ГрафикРаботы = ГрафикРаботыИсполнителя;
		
		Длительность = 
			СрокиИсполненияПроцессов.ДлительностьПоСрокуИсполнения(ПараметрыСрокаИсполнения);
		
		ПроцессОбъект.СрокИсполненияДни = Длительность.Дни;
		ПроцессОбъект.СрокИсполненияЧасы = Длительность.Часы;
		ПроцессОбъект.СрокИсполненияМинуты = Длительность.Минуты;
	Исключение
		// Длительность может и не рассчитаться, из-за незаполненного графика работ.
		// В этом случае ничего не делаем и продолжаем обработку, чтобы не прерывать обновление.
	КонецПопытки;
	
	Если ЗначениеЗаполнено(ПроцессОбъект.Проверяющий) Тогда
		ПроцессОбъект.СрокОбработкиРезультатов = ПроцессОбъект.УдалитьСрокИсполнения;
		ПроцессОбъект.ВариантУстановкиСрокаОбработкиРезультатов = 
			Перечисления.ВариантыУстановкиСрокаИсполнения.ТочныйСрок;
	КонецЕсли;
	
	ПроцессОбъект.СрокИсполненияПроцесса = ПроцессОбъект.УдалитьСрокИсполнения;
	
	РаботаСБизнесПроцессами.ЗаписатьПроцесс(ПроцессОбъект, "ЗаписьСОбновлениемОбщегоСпискаПроцессов");
	
КонецПроцедуры

// Приглашение

// Обновляет сроки в процессах Приглашения.
//
// Параметры:
//  ТолькоАктивные - Булево - при значении Истина обновляет только активные (которые находятся в работе) процессы.
//
Функция ОбновитьСрокиИсполненияПроцессовПриглашения(ТолькоАктивные = Истина)
	
	Если ТолькоАктивные Тогда
		
		Запрос = Новый Запрос;
		
		ПоместитьАктивныеПроцессыВВиртуальнуюТаблицуЗапроса(
			Запрос, "БизнесПроцесс.Приглашение", "Справочник.ШаблоныПриглашения");
		
		Запрос.Текст =
			"ВЫБРАТЬ
			|	АктивныеПроцессы.Ссылка,
			|	Приглашение.Дата,
			|	ПроцессыДляЗапуска.ДатаОтложенногоСтарта
			|ИЗ
			|	АктивныеПроцессы КАК АктивныеПроцессы
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ БизнесПроцесс.Приглашение КАК Приглашение
			|		ПО АктивныеПроцессы.Ссылка = Приглашение.Ссылка
			|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПроцессыДляЗапуска КАК ПроцессыДляЗапуска
			|		ПО АктивныеПроцессы.Ссылка = ПроцессыДляЗапуска.БизнесПроцесс
			|ГДЕ
			|	Приглашение.УдалитьСрокИсполнения <> ДАТАВРЕМЯ(1, 1, 1)
			|	И Приглашение.КоличествоИтераций = 0
			|
			|СГРУППИРОВАТЬ ПО
			|	АктивныеПроцессы.Ссылка,
			|	Приглашение.Дата,
			|	ПроцессыДляЗапуска.ДатаОтложенногоСтарта";
			
		Выборка = Запрос.Выполнить().Выбрать();
	Иначе
		Выборка = ВыборкаНеактивныхПроцессовПриглашенияДляОбработки();
	КонецЕсли;
	
	КоличествоОбработанныхПроцессов = 0;
	
	Пока Выборка.Следующий() Цикл
		
		Попытка
			ЗаблокироватьДанныеДляРедактирования(Выборка.Ссылка);
		Исключение
			Продолжить;
		КонецПопытки;
		
		ДатаОтсчета = ТекущаяДатаСеанса();
		Если ЗначениеЗаполнено(Выборка.ДатаОтложенногоСтарта) Тогда
			ДатаОтсчета = Выборка.ДатаОтложенногоСтарта;
		КонецЕсли;
		Если ЗначениеЗаполнено(Выборка.Дата) Тогда
			ДатаОтсчета = Выборка.Дата;
		КонецЕсли;
		
		ПеренестиСрокиПриглашенияВНовыеРеквизиты(Выборка.Ссылка, ДатаОтсчета);
		
		РазблокироватьДанныеДляРедактирования(Выборка.Ссылка);
		
		КоличествоОбработанныхПроцессов = КоличествоОбработанныхПроцессов + 1;
		
	КонецЦикла;
	
	Возврат КоличествоОбработанныхПроцессов;
	
КонецФункции

// Возвращает выборку неактивных процессов Приглашение.
//
// Параметры:
//  Количество - Число - количество процессов в выборке
//
Функция ВыборкаНеактивныхПроцессовПриглашенияДляОбработки(Количество = 1000)
	
	Запрос = Новый Запрос;
	
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	Приглашение.Ссылка,
		|	Приглашение.Дата,
		|	ПроцессыДляЗапуска.ДатаОтложенногоСтарта
		|ИЗ
		|	БизнесПроцесс.Приглашение КАК Приглашение
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПроцессыДляЗапуска КАК ПроцессыДляЗапуска
		|		ПО Приглашение.Ссылка = ПроцессыДляЗапуска.БизнесПроцесс
		|ГДЕ
		|	Приглашение.ПометкаУдаления = ЛОЖЬ
		|	И Приглашение.КоличествоИтераций = 0
		|	И &УсловиеПоУзлуОбмена
		|
		|СГРУППИРОВАТЬ ПО
		|	Приглашение.Ссылка,
		|	ПроцессыДляЗапуска.ДатаОтложенногоСтарта,
		|	Приглашение.Дата";
		
	Если Не Константы.ВыполняетсяПлавноеОбновлениеС20.Получить() Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
			"И &УсловиеПоУзлуОбмена", "И Приглашение.УзелОбмена = &УзелОбмена");
		
		Запрос.УстановитьПараметр("УзелОбмена", РаботаСБизнесПроцессами.ЭтотУзелОбменаДляОбработкиПроцессов());
	Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "И &УсловиеПоУзлуОбмена", "");
	КонецЕсли;
	
	Если Количество > 0 Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ВЫБРАТЬ", "ВЫБРАТЬ ПЕРВЫЕ " + Формат(Количество, "ЧГ=0"));
	КонецЕсли;
	
	Запрос.Текст = ТекстЗапроса;
		
	Возврат Запрос.Выполнить().Выбрать();
	
КонецФункции

// Переносит сроки Приглашения из старых реквизитов в новые.
//
// Параметры:
//  СсылкаНаШаблон - БизнесПроцессСсылка.Приглашение - ссылка на процесс.
//  ДатаОтсчета - Дата - дата отсчета для рассчета сроков исполнителей.
//
Процедура ПеренестиСрокиПриглашенияВНовыеРеквизиты(СсылкаНаПроцесс, ДатаОтсчета = '00010101') Экспорт
	
	ПроцессОбъект = СсылкаНаПроцесс.ПолучитьОбъект();
	
	Если Не ЗначениеЗаполнено(ДатаОтсчета) Тогда
		ДатаОтсчета = СрокиИсполненияПроцессов.ДатаОтсчетаДляРасчетаСроковПроцесса(ПроцессОбъект);
		Смещение = СмещенияДатыОтсчета(СсылкаНаПроцесс);
		
		ДатаОтсчета = Макс(ДатаОтсчета, Смещение);
	КонецЕсли;
	
	Для Каждого СтрИсполнитель Из ПроцессОбъект.Исполнители Цикл
		
		СтрИсполнитель.СрокИсполнения = ПроцессОбъект.УдалитьСрокИсполнения;
		
		СтрИсполнитель.ВариантУстановкиСрокаИсполнения = 
			Перечисления.ВариантыУстановкиСрокаИсполнения.ТочныйСрок;
			
		Попытка
			
			ГрафикРаботыИсполнителя = ГрафикРаботыУчастникаПроцесса(СтрИсполнитель.Исполнитель);
				
			ПараметрыСрокаИсполнения = 
					СрокиИсполненияПроцессов.ПараметрыСрокаИсполненияДляОпределенияДлительности();
				
			ПараметрыСрокаИсполнения.СрокИсполнения = СтрИсполнитель.СрокИсполнения;
			ПараметрыСрокаИсполнения.ДатаОтсчета = ДатаОтсчета;
			ПараметрыСрокаИсполнения.ГрафикРаботы = ГрафикРаботыИсполнителя;
			
			Длительность = 
				СрокиИсполненияПроцессов.ДлительностьПоСрокуИсполнения(ПараметрыСрокаИсполнения);
			
			СтрИсполнитель.СрокИсполненияДни = Длительность.Дни;
			СтрИсполнитель.СрокИсполненияЧасы = Длительность.Часы;
			СтрИсполнитель.СрокИсполненияМинуты = Длительность.Минуты;
		Исключение
			// Длительность может и не рассчитаться, из-за незаполненного графика работ.
			// В этом случае обновление будет прервано и база останется неработоспособной.
		КонецПопытки;
		
	КонецЦикла;
	
	ПроцессОбъект.СрокОбработкиРезультатов = ПроцессОбъект.УдалитьСрокИсполнения;
	ПроцессОбъект.ВариантУстановкиСрокаОбработкиРезультатов = 
		Перечисления.ВариантыУстановкиСрокаИсполнения.ТочныйСрок;
	
	ПроцессОбъект.СрокИсполненияПроцесса = ПроцессОбъект.УдалитьСрокИсполнения;
	
	ПроцессОбъект.КоличествоИтераций = 1;
	
	РаботаСБизнесПроцессами.ЗаписатьПроцесс(ПроцессОбъект, "ЗаписьСОбновлениемОбщегоСпискаПроцессов");
	
КонецПроцедуры

// Рассмотрение

// Обновляет сроки в процессах Рассмотрение.
//
// Параметры:
//  ТолькоАктивные - Булево - при значении Истина обновляет только активные (которые находятся в работе) процессы.
//
Функция ОбновитьСрокиИсполненияПроцессовРассмотрения(ТолькоАктивные = Истина)
	
	Если ТолькоАктивные Тогда
		
		Запрос = Новый Запрос;
		
		ПоместитьАктивныеПроцессыВВиртуальнуюТаблицуЗапроса(
			Запрос, "БизнесПроцесс.Рассмотрение", "Справочник.ШаблоныРассмотрения");
		
		Запрос.Текст =
			"ВЫБРАТЬ
			|	АктивныеПроцессы.Ссылка,
			|	Рассмотрение.Дата,
			|	ПроцессыДляЗапуска.ДатаОтложенногоСтарта
			|ИЗ
			|	АктивныеПроцессы КАК АктивныеПроцессы
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ БизнесПроцесс.Рассмотрение КАК Рассмотрение
			|		ПО АктивныеПроцессы.Ссылка = Рассмотрение.Ссылка
			|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПроцессыДляЗапуска КАК ПроцессыДляЗапуска
			|		ПО АктивныеПроцессы.Ссылка = ПроцессыДляЗапуска.БизнесПроцесс
			|ГДЕ
			|	Рассмотрение.КоличествоИтерацийИсполнения = 0
			|
			|СГРУППИРОВАТЬ ПО
			|	АктивныеПроцессы.Ссылка,
			|	Рассмотрение.Дата,
			|	ПроцессыДляЗапуска.ДатаОтложенногоСтарта";
			
		Выборка = Запрос.Выполнить().Выбрать();
	Иначе
		Выборка = ВыборкаНеактивныхПроцессовРассмотренияДляОбработки();
	КонецЕсли;
	
	КоличествоОбработанныхПроцессов = 0;
	
	Пока Выборка.Следующий() Цикл
		
		Попытка
			ЗаблокироватьДанныеДляРедактирования(Выборка.Ссылка);
		Исключение
			Продолжить;
		КонецПопытки;
		
		ДатаОтсчета = ТекущаяДатаСеанса();
		Если ЗначениеЗаполнено(Выборка.ДатаОтложенногоСтарта) Тогда
			ДатаОтсчета = Выборка.ДатаОтложенногоСтарта;
		КонецЕсли;
		Если ЗначениеЗаполнено(Выборка.Дата) Тогда
			ДатаОтсчета = Выборка.Дата;
		КонецЕсли;
		
		ПеренестиСрокиРассмотренияВНовыеРеквизиты(Выборка.Ссылка, ДатаОтсчета);
		
		РазблокироватьДанныеДляРедактирования(Выборка.Ссылка);
		
		КоличествоОбработанныхПроцессов = КоличествоОбработанныхПроцессов + 1;
		
	КонецЦикла;
	
	Возврат КоличествоОбработанныхПроцессов;
	
КонецФункции

// Возвращает выборку неактивных процессов Рассмотрение.
//
// Параметры:
//  Количество - Число - количество процессов в выборке
//
Функция ВыборкаНеактивныхПроцессовРассмотренияДляОбработки(Количество = 1000)
	
	Запрос = Новый Запрос;
	
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	Рассмотрение.Ссылка,
		|	Рассмотрение.Дата,
		|	ПроцессыДляЗапуска.ДатаОтложенногоСтарта
		|ИЗ
		|	БизнесПроцесс.Рассмотрение КАК Рассмотрение
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПроцессыДляЗапуска КАК ПроцессыДляЗапуска
		|		ПО Рассмотрение.Ссылка = ПроцессыДляЗапуска.БизнесПроцесс
		|ГДЕ
		|	Рассмотрение.ПометкаУдаления = ЛОЖЬ
		|	И Рассмотрение.КоличествоИтерацийИсполнения = 0
		|	И &УсловиеПоУзлуОбмена
		|
		|СГРУППИРОВАТЬ ПО
		|	Рассмотрение.Ссылка,
		|	Рассмотрение.Дата,
		|	ПроцессыДляЗапуска.ДатаОтложенногоСтарта";
		
	Если Не Константы.ВыполняетсяПлавноеОбновлениеС20.Получить() Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
			"И &УсловиеПоУзлуОбмена", "И Рассмотрение.УзелОбмена = &УзелОбмена");
		
		Запрос.УстановитьПараметр("УзелОбмена", РаботаСБизнесПроцессами.ЭтотУзелОбменаДляОбработкиПроцессов());
	Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "И &УсловиеПоУзлуОбмена", "");
	КонецЕсли;
	
	Если Количество > 0 Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ВЫБРАТЬ", "ВЫБРАТЬ ПЕРВЫЕ " + Формат(Количество, "ЧГ=0"));
	КонецЕсли;
	
	Запрос.Текст = ТекстЗапроса;
	
	Возврат Запрос.Выполнить().Выбрать();
	
КонецФункции

// Переносит сроки Рассмотрения из старых реквизитов в новые.
//
// Параметры:
//  СсылкаНаШаблон - БизнесПроцессСсылка.Рассмотрение - ссылка на процесс.
//  ДатаОтсчета - Дата - дата отсчета для рассчета сроков исполнителей.
//
Процедура ПеренестиСрокиРассмотренияВНовыеРеквизиты(СсылкаНаПроцесс, ДатаОтсчета = '00010101') Экспорт
	
	ПроцессОбъект = СсылкаНаПроцесс.ПолучитьОбъект();
	
	Если Не ЗначениеЗаполнено(ДатаОтсчета) Тогда
		ДатаОтсчета = СрокиИсполненияПроцессов.ДатаОтсчетаДляРасчетаСроковПроцесса(ПроцессОбъект);
		Смещение = СмещенияДатыОтсчета(СсылкаНаПроцесс);
		
		ДатаОтсчета = Макс(ДатаОтсчета, Смещение);
	КонецЕсли;
	
	ПроцессОбъект.СрокИсполнения = ПроцессОбъект.УдалитьСрокИсполнения;
	ПроцессОбъект.ВариантУстановкиСрокаИсполнения = 
		Перечисления.ВариантыУстановкиСрокаИсполнения.ТочныйСрок;
	
	ПроцессОбъект.СрокИсполненияПроцесса = ПроцессОбъект.УдалитьСрокИсполнения;
	
	// Перенос установленных настроек Исполнения/Ознакомления.
	
	Если ПроцессОбъект.ВариантИсполнения = Перечисления.ВариантыМаршрутизацииЗадач.Параллельно Тогда
		Для Каждого СтрИсполнитель Из ПроцессОбъект.ИсполнителиИсполнения Цикл
			
			Если ЗначениеЗаполнено(СтрИсполнитель.УдалитьСрокИсполнения) Тогда
				СтрИсполнитель.СрокИсполнения = СтрИсполнитель.УдалитьСрокИсполнения;
			Иначе
				СтрИсполнитель.СрокИсполнения = ПроцессОбъект.УдалитьСрокИсполненияОбщий;
			КонецЕсли;
			
			СтрИсполнитель.ВариантУстановкиСрокаИсполнения = 
				Перечисления.ВариантыУстановкиСрокаИсполнения.ТочныйСрок;
		КонецЦикла;
	Иначе
		Для Каждого СтрИсполнитель Из ПроцессОбъект.ИсполнителиИсполнения Цикл
			СтрИсполнитель.СрокИсполненияДни = СтрИсполнитель.УдалитьДлительностьИсполненияДни;
			СтрИсполнитель.СрокИсполненияЧасы = СтрИсполнитель.УдалитьДлительностьИсполненияЧасы;
			СтрИсполнитель.ВариантУстановкиСрокаИсполнения = 
				Перечисления.ВариантыУстановкиСрокаИсполнения.ОтносительныйСрок;
			СтрИсполнитель.СрокИсполнения = СтрИсполнитель.УдалитьСрокИсполнения;
		КонецЦикла;
	КонецЕсли;
	Если ЗначениеЗаполнено(ПроцессОбъект.ПроверяющийИсполнения) Тогда
		ПроцессОбъект.СрокОбработкиРезультатовИсполнения = ПроцессОбъект.УдалитьСрокИсполненияОбщий;
		ПроцессОбъект.ВариантУстановкиСрокаОбработкиРезультатовИсполнения = 
			Перечисления.ВариантыУстановкиСрокаИсполнения.ТочныйСрок;
	КонецЕсли;
	ПроцессОбъект.СрокИсполненияПроцессаИсполнения = ПроцессОбъект.УдалитьСрокИсполненияОбщий;
	ПроцессОбъект.КоличествоИтерацийИсполнения = 1;
	
	Для Каждого СтрИсполнитель Из ПроцессОбъект.ИсполнителиОзнакомления Цикл
		СтрИсполнитель.СрокИсполнения = ПроцессОбъект.УдалитьСрокОзнакомления;
		СтрИсполнитель.ВариантУстановкиСрокаИсполнения = 
			Перечисления.ВариантыУстановкиСрокаИсполнения.ТочныйСрок;
	КонецЦикла;
	ПроцессОбъект.СрокИсполненияПроцессаОзнакомления = ПроцессОбъект.УдалитьСрокОзнакомления;
	
	РаботаСБизнесПроцессами.ЗаписатьПроцесс(ПроцессОбъект, "ЗаписьСОбновлениемОбщегоСпискаПроцессов");
	
КонецПроцедуры

// Регистрация

// Обновляет сроки в процессах Регистрация
//
// Параметры:
//  ТолькоАктивные - Булево - при значении Истина обновляет только активные (которые находятся в работе) процессы.
//
Функция ОбновитьСрокиИсполненияПроцессовРегристрации(ТолькоАктивные = Истина)
	
	Если ТолькоАктивные Тогда
		
		Запрос = Новый Запрос;
		
		ПоместитьАктивныеПроцессыВВиртуальнуюТаблицуЗапроса(
			Запрос, "БизнесПроцесс.Регистрация", "Справочник.ШаблоныРегистрации");
		
		Запрос.Текст =
			"ВЫБРАТЬ
			|	АктивныеПроцессы.Ссылка,
			|	Регистрация.Дата,
			|	ПроцессыДляЗапуска.ДатаОтложенногоСтарта
			|ИЗ
			|	АктивныеПроцессы КАК АктивныеПроцессы
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ БизнесПроцесс.Регистрация КАК Регистрация
			|		ПО АктивныеПроцессы.Ссылка = Регистрация.Ссылка
			|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПроцессыДляЗапуска КАК ПроцессыДляЗапуска
			|		ПО АктивныеПроцессы.Ссылка = ПроцессыДляЗапуска.БизнесПроцесс
			|ГДЕ
			|	Регистрация.КоличествоИтераций = 0
			|
			|СГРУППИРОВАТЬ ПО
			|	АктивныеПроцессы.Ссылка,
			|	Регистрация.Дата,
			|	ПроцессыДляЗапуска.ДатаОтложенногоСтарта";
			
		Выборка = Запрос.Выполнить().Выбрать();
	Иначе
		Выборка = ВыборкаНеактивныхПроцессовРегристрацииДляОбработки();
	КонецЕсли;
	
	КоличествоОбработанныхПроцессов = 0;
	
	Пока Выборка.Следующий() Цикл
		
		Попытка
			ЗаблокироватьДанныеДляРедактирования(Выборка.Ссылка);
		Исключение
			Продолжить;
		КонецПопытки;
		
		ДатаОтсчета = ТекущаяДатаСеанса();
		Если ЗначениеЗаполнено(Выборка.ДатаОтложенногоСтарта) Тогда
			ДатаОтсчета = Выборка.ДатаОтложенногоСтарта;
		КонецЕсли;
		Если ЗначениеЗаполнено(Выборка.Дата) Тогда
			ДатаОтсчета = Выборка.Дата;
		КонецЕсли;
		
		ПеренестиСрокиРегистрацииВНовыеРеквизиты(Выборка.Ссылка, ДатаОтсчета);
		
		РазблокироватьДанныеДляРедактирования(Выборка.Ссылка);
		
		КоличествоОбработанныхПроцессов = КоличествоОбработанныхПроцессов + 1;
		
	КонецЦикла;
	
	Возврат КоличествоОбработанныхПроцессов;
	
КонецФункции

// Возвращает выборку неактивных процессов Регистрация.
//
// Параметры:
//  Количество - Число - количество процессов в выборке
//
Функция ВыборкаНеактивныхПроцессовРегристрацииДляОбработки(Количество = 1000)
	
	Запрос = Новый Запрос;
	
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	Регистрация.Ссылка,
		|	Регистрация.Дата,
		|	ПроцессыДляЗапуска.ДатаОтложенногоСтарта
		|ИЗ
		|	БизнесПроцесс.Регистрация КАК Регистрация
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПроцессыДляЗапуска КАК ПроцессыДляЗапуска
		|		ПО Регистрация.Ссылка = ПроцессыДляЗапуска.БизнесПроцесс
		|ГДЕ
		|	Регистрация.ПометкаУдаления = ЛОЖЬ
		|	И Регистрация.КоличествоИтераций = 0
		|	И &УсловиеПоУзлуОбмена";
		
	Если Не Константы.ВыполняетсяПлавноеОбновлениеС20.Получить() Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
			"И &УсловиеПоУзлуОбмена", "И Регистрация.УзелОбмена = &УзелОбмена");
		
		Запрос.УстановитьПараметр("УзелОбмена", РаботаСБизнесПроцессами.ЭтотУзелОбменаДляОбработкиПроцессов());
	Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "И &УсловиеПоУзлуОбмена", "");
	КонецЕсли;
	
	Если Количество > 0 Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ВЫБРАТЬ", "ВЫБРАТЬ ПЕРВЫЕ " + Формат(Количество, "ЧГ=0"));
	КонецЕсли;
	
	Запрос.Текст = ТекстЗапроса;
		
	Возврат Запрос.Выполнить().Выбрать();
	
КонецФункции

// Переносит сроки Регистрации из старых реквизитов в новые.
//
// Параметры:
//  СсылкаНаШаблон - БизнесПроцессСсылка.Регистрация - ссылка на процесс.
//  ДатаОтсчета - Дата - дата отсчета для рассчета сроков исполнителей.
//
Процедура ПеренестиСрокиРегистрацииВНовыеРеквизиты(СсылкаНаПроцесс, ДатаОтсчета = '00010101') Экспорт
	
	ПроцессОбъект = СсылкаНаПроцесс.ПолучитьОбъект();
	
	Если Не ЗначениеЗаполнено(ДатаОтсчета) Тогда
		ДатаОтсчета = СрокиИсполненияПроцессов.ДатаОтсчетаДляРасчетаСроковПроцесса(ПроцессОбъект);
		Смещение = СмещенияДатыОтсчета(СсылкаНаПроцесс);
		
		ДатаОтсчета = Макс(ДатаОтсчета, Смещение);
	КонецЕсли;
	
	ПроцессОбъект.СрокИсполнения = ПроцессОбъект.УдалитьСрокИсполнения;
	ПроцессОбъект.ВариантУстановкиСрокаИсполнения = 
			Перечисления.ВариантыУстановкиСрокаИсполнения.ТочныйСрок;
	
	Попытка
		
		ГрафикРаботыИсполнителя = ГрафикРаботыУчастникаПроцесса(ПроцессОбъект.Исполнитель);
		
		ПараметрыСрокаИсполнения = 
				СрокиИсполненияПроцессов.ПараметрыСрокаИсполненияДляОпределенияДлительности();
			
		ПараметрыСрокаИсполнения.СрокИсполнения = ПроцессОбъект.СрокИсполнения;
		ПараметрыСрокаИсполнения.ДатаОтсчета = ДатаОтсчета;
		ПараметрыСрокаИсполнения.ГрафикРаботы = ГрафикРаботыИсполнителя;
		
		Длительность = 
			СрокиИсполненияПроцессов.ДлительностьПоСрокуИсполнения(ПараметрыСрокаИсполнения);
		
		ПроцессОбъект.СрокИсполненияДни = Длительность.Дни;
		ПроцессОбъект.СрокИсполненияЧасы = Длительность.Часы;
		ПроцессОбъект.СрокИсполненияМинуты = Длительность.Минуты;
	Исключение
		// Длительность может и не рассчитаться, из-за незаполненного графика работ.
		// В этом случае ничего не делаем и продолжаем обработку, чтобы не прерывать обновление.
	КонецПопытки;
	
	ПроцессОбъект.СрокОбработкиРезультатов = ПроцессОбъект.УдалитьСрокИсполнения;
	ПроцессОбъект.ВариантУстановкиСрокаОбработкиРезультатов = 
		Перечисления.ВариантыУстановкиСрокаИсполнения.ТочныйСрок;
	
	ПроцессОбъект.СрокИсполненияПроцесса = ПроцессОбъект.УдалитьСрокИсполнения;
	
	ПроцессОбъект.КоличествоИтераций = 1;
	
	РаботаСБизнесПроцессами.ЗаписатьПроцесс(ПроцессОбъект, "ЗаписьСОбновлениемОбщегоСпискаПроцессов");
	
КонецПроцедуры

// Согласование

// Обновляет сроки в процессах Согласование
//
// Параметры:
//  ТолькоАктивные - Булево - при значении Истина обновляет только активные (которые находятся в работе) процессы.
//
Функция ОбновитьСрокиИсполненияПроцессовСогласования(ТолькоАктивные = Истина)
	
	Если ТолькоАктивные Тогда
		
		Запрос = Новый Запрос;
		
		ПоместитьАктивныеПроцессыВВиртуальнуюТаблицуЗапроса(
			Запрос, "БизнесПроцесс.Согласование", "Справочник.ШаблоныСогласования");
		
		Запрос.Текст =
			"ВЫБРАТЬ
			|	АктивныеПроцессы.Ссылка,
			|	Согласование.Дата,
			|	ПроцессыДляЗапуска.ДатаОтложенногоСтарта
			|ИЗ
			|	АктивныеПроцессы КАК АктивныеПроцессы
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ БизнесПроцесс.Согласование КАК Согласование
			|		ПО АктивныеПроцессы.Ссылка = Согласование.Ссылка
			|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПроцессыДляЗапуска КАК ПроцессыДляЗапуска
			|		ПО АктивныеПроцессы.Ссылка = ПроцессыДляЗапуска.БизнесПроцесс
			|ГДЕ
			|	Согласование.КоличествоИтераций = 0
			|
			|СГРУППИРОВАТЬ ПО
			|	АктивныеПроцессы.Ссылка,
			|	Согласование.Дата,
			|	ПроцессыДляЗапуска.ДатаОтложенногоСтарта";
		
		Выборка = Запрос.Выполнить().Выбрать();
		
	Иначе
		Выборка = ВыборкаНеактивныхПроцессовСогласованияДляОбработки();
	КонецЕсли;
	
	КоличествоОбработанныхПроцессов = 0;
	
	Пока Выборка.Следующий() Цикл
		
		Попытка
			ЗаблокироватьДанныеДляРедактирования(Выборка.Ссылка);
		Исключение
			Продолжить;
		КонецПопытки;
		
		ДатаОтсчета = ТекущаяДатаСеанса();
		Если ЗначениеЗаполнено(Выборка.ДатаОтложенногоСтарта) Тогда
			ДатаОтсчета = Выборка.ДатаОтложенногоСтарта;
		КонецЕсли;
		Если ЗначениеЗаполнено(Выборка.Дата) Тогда
			ДатаОтсчета = Выборка.Дата;
		КонецЕсли;
		
		ПеренестиСрокиСогласованияВНовыеРеквизиты(Выборка.Ссылка, ДатаОтсчета);
		
		РазблокироватьДанныеДляРедактирования(Выборка.Ссылка);
		
		КоличествоОбработанныхПроцессов = КоличествоОбработанныхПроцессов + 1;
		
	КонецЦикла;
	
	Возврат КоличествоОбработанныхПроцессов;
	
КонецФункции

// Возвращает выборку неактивных процессов Согласование.
//
// Параметры:
//  Количество - Число - количество процессов в выборке
//
Функция ВыборкаНеактивныхПроцессовСогласованияДляОбработки(Количество = 1000)
	
	Запрос = Новый Запрос;
	
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	Согласование.Ссылка,
		|	Согласование.Дата,
		|	ПроцессыДляЗапуска.ДатаОтложенногоСтарта
		|ИЗ
		|	БизнесПроцесс.Согласование КАК Согласование
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПроцессыДляЗапуска КАК ПроцессыДляЗапуска
		|		ПО Согласование.Ссылка = ПроцессыДляЗапуска.БизнесПроцесс
		|ГДЕ
		|	Согласование.ПометкаУдаления = ЛОЖЬ
		|	И Согласование.КоличествоИтераций = 0
		|	И &УсловиеПоУзлуОбмена
		|
		|СГРУППИРОВАТЬ ПО
		|	Согласование.Ссылка,
		|	Согласование.Дата,
		|	ПроцессыДляЗапуска.ДатаОтложенногоСтарта";
		
	Если Не Константы.ВыполняетсяПлавноеОбновлениеС20.Получить() Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
			"И &УсловиеПоУзлуОбмена", "И Согласование.УзелОбмена = &УзелОбмена");
		
		Запрос.УстановитьПараметр("УзелОбмена", РаботаСБизнесПроцессами.ЭтотУзелОбменаДляОбработкиПроцессов());
	Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "И &УсловиеПоУзлуОбмена", "");
	КонецЕсли;
	
	Если Количество > 0 Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ВЫБРАТЬ", "ВЫБРАТЬ ПЕРВЫЕ " + Формат(Количество, "ЧГ=0"));
	КонецЕсли;
	
	Запрос.Текст = ТекстЗапроса;
		
	Возврат Запрос.Выполнить().Выбрать();
	
КонецФункции

// Переносит сроки Согласования из старых реквизитов в новые.
//
// Параметры:
//  СсылкаНаШаблон - БизнесПроцессСсылка.Согласование - ссылка на процесс.
//  ДатаОтсчета - Дата - дата отсчета для рассчета сроков исполнителей.
//
Процедура ПеренестиСрокиСогласованияВНовыеРеквизиты(СсылкаНаПроцесс, ДатаОтсчета = '00010101') Экспорт
	
	ПроцессОбъект = СсылкаНаПроцесс.ПолучитьОбъект();
	
	Если Не ЗначениеЗаполнено(ДатаОтсчета) Тогда
		ДатаОтсчета = СрокиИсполненияПроцессов.ДатаОтсчетаДляРасчетаСроковПроцесса(ПроцессОбъект);
		Смещение = СмещенияДатыОтсчета(СсылкаНаПроцесс);
		
		ДатаОтсчета = Макс(ДатаОтсчета, Смещение);
	КонецЕсли;
	
	МаксимальныйСрокЭтапа = ДатаОтсчета;
	
	Для Каждого СтрИсполнитель Из ПроцессОбъект.Исполнители Цикл
		Если ПроцессОбъект.УдалитьРазныеСроки Тогда
			СтрИсполнитель.СрокИсполненияДни = СтрИсполнитель.УдалитьСрокИсполнения;
			СтрИсполнитель.СрокИсполненияЧасы = СтрИсполнитель.УдалитьСрокИсполненияЧас;
		Иначе
			СтрИсполнитель.СрокИсполненияДни = ПроцессОбъект.УдалитьСрокИсполнения;
			СтрИсполнитель.СрокИсполненияЧасы = ПроцессОбъект.УдалитьСрокИсполненияЧас;
		КонецЕсли;
		СтрИсполнитель.ВариантУстановкиСрокаИсполнения = 
		Перечисления.ВариантыУстановкиСрокаИсполнения.ОтносительныйСрок;
		
		Если СтрИсполнитель.ПорядокСогласования = 
			Перечисления.ПорядокВыполненияЗадач.ПослеПредыдущего Тогда
			
			ДатаОтсчета = Макс(МаксимальныйСрокЭтапа, ДатаОтсчета);
			МаксимальныйСрокЭтапа = Дата(1,1,1);
		КонецЕсли;
		
		Если ЗначениеЗаполнено(СтрИсполнитель.ЗадачаИсполнителя) Тогда
			СтрИсполнитель.СрокИсполнения = 
			ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СтрИсполнитель.ЗадачаИсполнителя, "СрокИсполнения");
		Иначе
			Попытка
				
				ГрафикРаботыИсполнителя = ГрафикРаботыУчастникаПроцесса(СтрИсполнитель.Исполнитель);
				
				ПараметрыДлительности = ПараметрыДлительностиИсполненияДляОпределенияТочногоСрока();
				ПараметрыДлительности.Дни = СтрИсполнитель.СрокИсполненияДни;
				ПараметрыДлительности.Часы = СтрИсполнитель.СрокИсполненияЧасы;
				ПараметрыДлительности.Минуты = СтрИсполнитель.СрокИсполненияМинуты;
				ПараметрыДлительности.ДатаОтсчета = ДатаОтсчета;
				ПараметрыДлительности.ГрафикРаботы = ГрафикРаботыИсполнителя;
				
				СтрИсполнитель.СрокИсполнения = 
				СрокиИсполненияПроцессов.СрокИсполненияПоДлительности(ПараметрыДлительности);
			Исключение
				// Длительность может и не рассчитаться, из-за незаполненного графика работ.
				// В этом случае обновление будет прервано и база останется неработоспособной.
			КонецПопытки;
		КонецЕсли;
		
		МаксимальныйСрокЭтапа = Макс(МаксимальныйСрокЭтапа, СтрИсполнитель.СрокИсполнения);
		
	КонецЦикла;
	
	ПроцессОбъект.КоличествоИтераций = 1;
	
	ПроцессОбъект.СрокОбработкиРезультатов = ПроцессОбъект.УдалитьСрокИсполненияДатой;
	ПроцессОбъект.ВариантУстановкиСрокаОбработкиРезультатов = 
	Перечисления.ВариантыУстановкиСрокаИсполнения.ТочныйСрок;
	
	ПроцессОбъект.СрокИсполненияПроцесса = ПроцессОбъект.УдалитьСрокИсполненияДатой;
	
	РаботаСБизнесПроцессами.ЗаписатьПроцесс(ПроцессОбъект, "ЗаписьСОбновлениемОбщегоСпискаПроцессов");
	
КонецПроцедуры

// Утверждение

// Обновляет сроки в процессах Утверждение
//
// Параметры:
//  ТолькоАктивные - Булево - при значении Истина обновляет только активные (которые находятся в работе) процессы.
//
Функция ОбновитьСрокиИсполненияПроцессовУтверждения(ТолькоАктивные = Истина)
	
	Если ТолькоАктивные Тогда
		
		Запрос = Новый Запрос;
		
		ПоместитьАктивныеПроцессыВВиртуальнуюТаблицуЗапроса(
			Запрос, "БизнесПроцесс.Утверждение", "Справочник.ШаблоныУтверждения");
		
		Запрос.Текст =
			"ВЫБРАТЬ
			|	АктивныеПроцессы.Ссылка,
			|	Утверждение.Дата,
			|	ПроцессыДляЗапуска.ДатаОтложенногоСтарта
			|ИЗ
			|	АктивныеПроцессы КАК АктивныеПроцессы
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ БизнесПроцесс.Утверждение КАК Утверждение
			|		ПО АктивныеПроцессы.Ссылка = Утверждение.Ссылка
			|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПроцессыДляЗапуска КАК ПроцессыДляЗапуска
			|		ПО АктивныеПроцессы.Ссылка = ПроцессыДляЗапуска.БизнесПроцесс
			|ГДЕ
			|	Утверждение.КоличествоИтераций = 0
			|
			|СГРУППИРОВАТЬ ПО
			|	АктивныеПроцессы.Ссылка,
			|	Утверждение.Дата,
			|	ПроцессыДляЗапуска.ДатаОтложенногоСтарта";
			
		Выборка = Запрос.Выполнить().Выбрать();
	Иначе
		Выборка = ВыборкаНеактивныхПроцессовУтвержденияДляОбработки();
	КонецЕсли;
	
	КоличествоОбработанныхПроцессов = 0;
	
	Пока Выборка.Следующий() Цикл
		
		Попытка
			ЗаблокироватьДанныеДляРедактирования(Выборка.Ссылка);
		Исключение
			Продолжить;
		КонецПопытки;
		
		ДатаОтсчета = ТекущаяДатаСеанса();
		Если ЗначениеЗаполнено(Выборка.ДатаОтложенногоСтарта) Тогда
			ДатаОтсчета = Выборка.ДатаОтложенногоСтарта;
		КонецЕсли;
		Если ЗначениеЗаполнено(Выборка.Дата) Тогда
			ДатаОтсчета = Выборка.Дата;
		КонецЕсли;
		
		ПеренестиСрокиУтвержденияВНовыеРеквизиты(Выборка.Ссылка, ДатаОтсчета);
		
		РазблокироватьДанныеДляРедактирования(Выборка.Ссылка);
		
		КоличествоОбработанныхПроцессов = КоличествоОбработанныхПроцессов + 1;
		
	КонецЦикла;
	
	Возврат КоличествоОбработанныхПроцессов;
	
КонецФункции

// Возвращает выборку неактивных процессов Утверждение.
//
// Параметры:
//  Количество - Число - количество процессов в выборке
//
Функция ВыборкаНеактивныхПроцессовУтвержденияДляОбработки(Количество = 1000)
	
	Запрос = Новый Запрос;
	
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	Утверждение.Ссылка,
		|	Утверждение.Дата,
		|	ПроцессыДляЗапуска.ДатаОтложенногоСтарта
		|ИЗ
		|	БизнесПроцесс.Утверждение КАК Утверждение
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПроцессыДляЗапуска КАК ПроцессыДляЗапуска
		|		ПО Утверждение.Ссылка = ПроцессыДляЗапуска.БизнесПроцесс
		|ГДЕ
		|	Утверждение.ПометкаУдаления = ЛОЖЬ
		|	И Утверждение.КоличествоИтераций = 0
		|	И &УсловиеПоУзлуОбмена";
		
	Если Не Константы.ВыполняетсяПлавноеОбновлениеС20.Получить() Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
			"И &УсловиеПоУзлуОбмена", "И Утверждение.УзелОбмена = &УзелОбмена");
		
		Запрос.УстановитьПараметр("УзелОбмена", РаботаСБизнесПроцессами.ЭтотУзелОбменаДляОбработкиПроцессов());
	Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "И &УсловиеПоУзлуОбмена", "");
	КонецЕсли;
	
	Если Количество > 0 Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ВЫБРАТЬ", "ВЫБРАТЬ ПЕРВЫЕ " + Формат(Количество, "ЧГ=0"));
	КонецЕсли;
	
	Запрос.Текст = ТекстЗапроса;
		
	Возврат Запрос.Выполнить().Выбрать();
	
КонецФункции

// Переносит сроки Утверждения из старых реквизитов в новые.
//
// Параметры:
//  СсылкаНаШаблон - БизнесПроцессСсылка.Утверждение - ссылка на процесс.
//  ДатаОтсчета - Дата - дата отсчета для рассчета сроков исполнителей.
//
Процедура ПеренестиСрокиУтвержденияВНовыеРеквизиты(СсылкаНаПроцесс, ДатаОтсчета = '00010101') Экспорт
	
	ПроцессОбъект = СсылкаНаПроцесс.ПолучитьОбъект();
	
	Если Не ЗначениеЗаполнено(ДатаОтсчета) Тогда
		ДатаОтсчета = СрокиИсполненияПроцессов.ДатаОтсчетаДляРасчетаСроковПроцесса(ПроцессОбъект);
		Смещение = СмещенияДатыОтсчета(СсылкаНаПроцесс);
		
		ДатаОтсчета = Макс(ДатаОтсчета, Смещение);
	КонецЕсли;
	
	ПроцессОбъект.СрокИсполнения = ПроцессОбъект.УдалитьСрокИсполнения;
	ПроцессОбъект.ВариантУстановкиСрокаИсполнения = 
			Перечисления.ВариантыУстановкиСрокаИсполнения.ТочныйСрок;
	
	Попытка
		ГрафикРаботыИсполнителя = ГрафикРаботыУчастникаПроцесса(ПроцессОбъект.Исполнитель);
		
		ПараметрыСрокаИсполнения = 
				СрокиИсполненияПроцессов.ПараметрыСрокаИсполненияДляОпределенияДлительности();
			
		ПараметрыСрокаИсполнения.СрокИсполнения = ПроцессОбъект.СрокИсполнения;
		ПараметрыСрокаИсполнения.ДатаОтсчета = ДатаОтсчета;
		ПараметрыСрокаИсполнения.ГрафикРаботы = ГрафикРаботыИсполнителя;
		
		Длительность = 
			СрокиИсполненияПроцессов.ДлительностьПоСрокуИсполнения(ПараметрыСрокаИсполнения);
		
		ПроцессОбъект.СрокИсполненияДни = Длительность.Дни;
		ПроцессОбъект.СрокИсполненияЧасы = Длительность.Часы;
		ПроцессОбъект.СрокИсполненияМинуты = Длительность.Минуты;
	Исключение
		// Длительность может и не рассчитаться, из-за незаполненного графика работ.
		// В этом случае ничего не делаем и продолжаем обработку, чтобы не прерывать обновление.
	КонецПопытки;
	
	ПроцессОбъект.СрокОбработкиРезультатов = ПроцессОбъект.УдалитьСрокИсполнения;
	ПроцессОбъект.ВариантУстановкиСрокаОбработкиРезультатов = 
		Перечисления.ВариантыУстановкиСрокаИсполнения.ТочныйСрок;
	
	ПроцессОбъект.СрокИсполненияПроцесса = ПроцессОбъект.УдалитьСрокИсполнения;
	
	ПроцессОбъект.КоличествоИтераций = 1;
	
	РаботаСБизнесПроцессами.ЗаписатьПроцесс(ПроцессОбъект, "ЗаписьСОбновлениемОбщегоСпискаПроцессов");
	
КонецПроцедуры

// Составные процессы

// Обновляет сроки в Составных процессах
//
// Параметры:
//  ТолькоАктивные - Булево - при значении Истина обновляет только активные (которые находятся в работе) процессы.
//
Функция ОбновитьСрокиИсполненияСоставныхПроцессов(ТолькоАктивные = Истина)
	
	Если ТолькоАктивные Тогда
		Запрос = Новый Запрос;
		
		ТекстЗапроса = 
			"ВЫБРАТЬ
			|	ОбработкаВнутреннегоДокумента.Ссылка,
			|	ПроцессыДляЗапуска.ДатаОтложенногоСтарта,
			|	ОбработкаВнутреннегоДокумента.Дата
			|ИЗ
			|	БизнесПроцесс.ОбработкаВнутреннегоДокумента КАК ОбработкаВнутреннегоДокумента
			|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПроцессыДляЗапуска КАК ПроцессыДляЗапуска
			|		ПО ОбработкаВнутреннегоДокумента.Ссылка = ПроцессыДляЗапуска.БизнесПроцесс
			|ГДЕ
			|	ОбработкаВнутреннегоДокумента.СрокИсполненияПроцесса = ДАТАВРЕМЯ(1, 1, 1)
			|	И ОбработкаВнутреннегоДокумента.ПометкаУдаления = ЛОЖЬ
			|	И ОбработкаВнутреннегоДокумента.Завершен = ЛОЖЬ
			|	И ОбработкаВнутреннегоДокумента.Состояние = ЗНАЧЕНИЕ(Перечисление.СостоянияБизнесПроцессов.Активен)
			|	И &УсловиеПоУзлуОбменаДляОбработкиВнДокументов
			|	И ОбработкаВнутреннегоДокумента.ВедущаяЗадача = ЗНАЧЕНИЕ(Задача.ЗадачаИсполнителя.ПустаяСсылка)
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	ОбработкаВходящегоДокумента.Ссылка,
			|	ПроцессыДляЗапуска.ДатаОтложенногоСтарта,
			|	ОбработкаВходящегоДокумента.Дата
			|ИЗ
			|	БизнесПроцесс.ОбработкаВходящегоДокумента КАК ОбработкаВходящегоДокумента
			|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПроцессыДляЗапуска КАК ПроцессыДляЗапуска
			|		ПО ОбработкаВходящегоДокумента.Ссылка = ПроцессыДляЗапуска.БизнесПроцесс
			|ГДЕ
			|	ОбработкаВходящегоДокумента.СрокИсполненияПроцесса = ДАТАВРЕМЯ(1, 1, 1)
			|	И ОбработкаВходящегоДокумента.ПометкаУдаления = ЛОЖЬ
			|	И ОбработкаВходящегоДокумента.Завершен = ЛОЖЬ
			|	И ОбработкаВходящегоДокумента.Состояние = ЗНАЧЕНИЕ(Перечисление.СостоянияБизнесПроцессов.Активен)
			|	И &УсловиеПоУзлуОбменаДляОбработкиВхДокументов
			|	И ОбработкаВходящегоДокумента.ВедущаяЗадача = ЗНАЧЕНИЕ(Задача.ЗадачаИсполнителя.ПустаяСсылка)
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	ОбработкаИсходящегоДокумента.Ссылка,
			|	ПроцессыДляЗапуска.ДатаОтложенногоСтарта,
			|	ОбработкаИсходящегоДокумента.Дата
			|ИЗ
			|	БизнесПроцесс.ОбработкаИсходящегоДокумента КАК ОбработкаИсходящегоДокумента
			|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПроцессыДляЗапуска КАК ПроцессыДляЗапуска
			|		ПО ОбработкаИсходящегоДокумента.Ссылка = ПроцессыДляЗапуска.БизнесПроцесс
			|ГДЕ
			|	ОбработкаИсходящегоДокумента.СрокИсполненияПроцесса = ДАТАВРЕМЯ(1, 1, 1)
			|	И ОбработкаИсходящегоДокумента.ПометкаУдаления = ЛОЖЬ
			|	И ОбработкаИсходящегоДокумента.Завершен = ЛОЖЬ
			|	И ОбработкаИсходящегоДокумента.Состояние = ЗНАЧЕНИЕ(Перечисление.СостоянияБизнесПроцессов.Активен)
			|	И &УсловиеПоУзлуОбменаДляОбработкиИсхДокументов
			|	И ОбработкаИсходящегоДокумента.ВедущаяЗадача = ЗНАЧЕНИЕ(Задача.ЗадачаИсполнителя.ПустаяСсылка)";
		
		Если Не Константы.ВыполняетсяПлавноеОбновлениеС20.Получить() Тогда
			
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
				"И &УсловиеПоУзлуОбменаДляОбработкиВнДокументов", "И ОбработкаВнутреннегоДокумента.УзелОбмена = &УзелОбмена");
				
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
				"И &УсловиеПоУзлуОбменаДляОбработкиВхДокументов", "И ОбработкаВходящегоДокумента.УзелОбмена = &УзелОбмена");
				
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
				"И &УсловиеПоУзлуОбменаДляОбработкиИсхДокументов", "И ОбработкаИсходящегоДокумента.УзелОбмена = &УзелОбмена");
			
			Запрос.УстановитьПараметр("УзелОбмена", РаботаСБизнесПроцессами.ЭтотУзелОбменаДляОбработкиПроцессов());
		Иначе
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
				"И &УсловиеПоУзлуОбменаДляОбработкиВнДокументов", "");
				
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
				"И &УсловиеПоУзлуОбменаДляОбработкиВхДокументов", "");
				
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
				"И &УсловиеПоУзлуОбменаДляОбработкиИсхДокументов", "");
		КонецЕсли;
		
		Запрос.Текст = ТекстЗапроса;
		
		Выборка = Запрос.Выполнить().Выбрать();
	Иначе
		Выборка = ВыборкаНеактивныхСоставныхПроцессовДляОбработки();
	КонецЕсли;
	
	Пока Выборка.Следующий() Цикл
		
		ДатаОтсчета = ТекущаяДатаСеанса();
		
		Если ЗначениеЗаполнено(Выборка.ДатаОтложенногоСтарта) Тогда
			ДатаОтсчета = Выборка.ДатаОтложенногоСтарта;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Выборка.Дата) Тогда
			ДатаОтсчета = Выборка.Дата;
		КонецЕсли;
		
		Попытка
			ЗаблокироватьДанныеДляРедактирования(Выборка.Ссылка);
		Исключение
			Продолжить;
		КонецПопытки;
		
		ОбновитьСрокИсполненияСоставногоПроцесса(Выборка.Ссылка, ДатаОтсчета);
		
		РазблокироватьДанныеДляРедактирования(Выборка.Ссылка);
		
	КонецЦикла;
	
	КоличествоОбработанныхПроцессов = Выборка.Количество();
	
	Возврат КоличествоОбработанныхПроцессов;
	
КонецФункции

// Возвращает выборку неактивных составных процессов.
//
// Параметры:
//  Количество - Число - количество процессов в выборке
//
Функция ВыборкаНеактивныхСоставныхПроцессовДляОбработки()
	
	Запрос = Новый Запрос;
	
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	ОбработкаВнутреннегоДокумента.Ссылка,
		|	ОбработкаВнутреннегоДокумента.Дата,
		|	ПроцессыДляЗапуска.ДатаОтложенногоСтарта
		|ИЗ
		|	БизнесПроцесс.ОбработкаВнутреннегоДокумента КАК ОбработкаВнутреннегоДокумента
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПроцессыДляЗапуска КАК ПроцессыДляЗапуска
		|		ПО ОбработкаВнутреннегоДокумента.Ссылка = ПроцессыДляЗапуска.БизнесПроцесс
		|ГДЕ
		|	ОбработкаВнутреннегоДокумента.СрокИсполненияПроцесса = ДАТАВРЕМЯ(1, 1, 1)
		|	И ОбработкаВнутреннегоДокумента.ПометкаУдаления = ЛОЖЬ
		|	И &УсловиеПоУзлуОбменаДляОбработкиВнДокументов
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ОбработкаВходящегоДокумента.Ссылка,
		|	ОбработкаВходящегоДокумента.Дата,
		|	ПроцессыДляЗапуска.ДатаОтложенногоСтарта
		|ИЗ
		|	БизнесПроцесс.ОбработкаВходящегоДокумента КАК ОбработкаВходящегоДокумента
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПроцессыДляЗапуска КАК ПроцессыДляЗапуска
		|		ПО ОбработкаВходящегоДокумента.Ссылка = ПроцессыДляЗапуска.БизнесПроцесс
		|ГДЕ
		|	ОбработкаВходящегоДокумента.СрокИсполненияПроцесса = ДАТАВРЕМЯ(1, 1, 1)
		|	И ОбработкаВходящегоДокумента.ПометкаУдаления = ЛОЖЬ
		|	И &УсловиеПоУзлуОбменаДляОбработкиВхДокументов
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ОбработкаИсходящегоДокумента.Ссылка,
		|	ОбработкаИсходящегоДокумента.Дата,
		|	ПроцессыДляЗапуска.ДатаОтложенногоСтарта
		|ИЗ
		|	БизнесПроцесс.ОбработкаИсходящегоДокумента КАК ОбработкаИсходящегоДокумента
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПроцессыДляЗапуска КАК ПроцессыДляЗапуска
		|		ПО ОбработкаИсходящегоДокумента.Ссылка = ПроцессыДляЗапуска.БизнесПроцесс
		|ГДЕ
		|	ОбработкаИсходящегоДокумента.СрокИсполненияПроцесса = ДАТАВРЕМЯ(1, 1, 1)
		|	И ОбработкаИсходящегоДокумента.ПометкаУдаления = ЛОЖЬ
		|	И &УсловиеПоУзлуОбменаДляОбработкиИсхДокументов";
	
	Если Не Константы.ВыполняетсяПлавноеОбновлениеС20.Получить() Тогда
		
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
			"И &УсловиеПоУзлуОбменаДляОбработкиВнДокументов", "И ОбработкаВнутреннегоДокумента.УзелОбмена = &УзелОбмена");
		
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
			"И &УсловиеПоУзлуОбменаДляОбработкиВхДокументов", "И ОбработкаВходящегоДокумента.УзелОбмена = &УзелОбмена");
		
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
			"И &УсловиеПоУзлуОбменаДляОбработкиИсхДокументов", "И ОбработкаИсходящегоДокумента.УзелОбмена = &УзелОбмена");
		
		Запрос.УстановитьПараметр("УзелОбмена", РаботаСБизнесПроцессами.ЭтотУзелОбменаДляОбработкиПроцессов());
	Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
			"И &УсловиеПоУзлуОбменаДляОбработкиВнДокументов", "");
		
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
			"И &УсловиеПоУзлуОбменаДляОбработкиВхДокументов", "");
		
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
			"И &УсловиеПоУзлуОбменаДляОбработкиИсхДокументов", "");
	КонецЕсли;
	
	Запрос.Текст = ТекстЗапроса;
	
	Возврат Запрос.Выполнить().Выбрать();
	
КонецФункции

// Заполняет срок Составного процесса на основании сроков его действий.
//
// Параметры:
//  СсылкаНаШаблон - БизнесПроцессСсылка.ОбработкаВнутреннегоДокумента
//                   БизнесПроцессСсылка.ОбработкаВходящегоДокумента
//                   БизнесПроцессСсылка.ОбработкаИсходящегоДокумента - ссылка на процесс.
//  ДатаОтсчета - Дата - дата отсчета для рассчета сроков исполнителей.
//
Процедура ОбновитьСрокИсполненияСоставногоПроцесса(СсылкаНаПроцесс, ДатаОтсчета = '00010101') Экспорт
	
	ПроцессОбъект = СсылкаНаПроцесс.ПолучитьОбъект();
	
	Если Не ЗначениеЗаполнено(ДатаОтсчета) Тогда
		ДатаОтсчета = СрокиИсполненияПроцессов.ДатаОтсчетаДляРасчетаСроковПроцесса(ПроцессОбъект);
		Смещение = СмещенияДатыОтсчета(СсылкаНаПроцесс);
		
		ДатаОтсчета = Макс(ДатаОтсчета, Смещение);
	КонецЕсли;
	
	Попытка
		СтруктураДляРасчета = СрокиИсполненияПроцессов.СтруктураДляРасчетаСрокаСоставногоПроцесса(ПроцессОбъект);
		
		ПараметрыДляРасчетаСроков = СрокиИсполненияПроцессов.ПараметрыДляРасчетаСроков();
		ПараметрыДляРасчетаСроков.ДатаОтсчета = ДатаОтсчета;
		
		ДанныеДляРасчета = СрокиИсполненияПроцессов.ДанныеДляРасчетаСроковСоставногоПроцесса(
			СтруктураДляРасчета, ПараметрыДляРасчетаСроков);
		
		ПометитьРассчитаннымиСрокиПростыхПроцессовВТаблицеСроков(ДанныеДляРасчета.Сроки);
		
		СрокиИсполненияПроцессов.РассчитатьСрокиИсполнения(
			ДанныеДляРасчета.Сроки, ДанныеДляРасчета.Предшественники, УникальныйИдентификаторПустой());
			
		СрокиИсполненияПроцессов.ЗаполнитьСрокиСоставногоПроцесса(
			СтруктураДляРасчета,
			ДанныеДляРасчета.Сроки,
			ДанныеДляРасчета.ИдентификаторыСроков);
		
		ПроцессОбъект.СрокИсполненияПроцесса = СтруктураДляРасчета.СрокИсполненияПроцесса;
		
		ПроцессОбъект.Записать();
	Исключение
		// При наличии ссылок на несуществующие объекты в составном процессе
		// срок может и не рассчитаться. Такие процессы пропускаем.
	КонецПопытки;
	
КонецПроцедуры

// Комплексные процессы

// Обновляет сроки в Комплексных процессах
//
// Параметры:
//  ТолькоАктивные - Булево - при значении Истина обновляет только активные (которые находятся в работе) процессы.
//
Функция ОбновитьСрокиИсполненияКомплексныхПроцессов(ТолькоАктивные = Истина)
	
	Если ТолькоАктивные Тогда
		
		Запрос = Новый Запрос;
		
		ТекстЗапроса = 
			"ВЫБРАТЬ
			|	КомплексныйПроцесс.Ссылка,
			|	КомплексныйПроцесс.Дата,
			|	ПроцессыДляЗапуска.ДатаОтложенногоСтарта
			|ИЗ
			|	БизнесПроцесс.КомплексныйПроцесс КАК КомплексныйПроцесс
			|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПроцессыДляЗапуска КАК ПроцессыДляЗапуска
			|		ПО КомплексныйПроцесс.Ссылка = ПроцессыДляЗапуска.БизнесПроцесс
			|ГДЕ
			|	КомплексныйПроцесс.ВедущаяЗадача = ЗНАЧЕНИЕ(Задача.ЗадачаИсполнителя.ПустаяСсылка)
			|	И КомплексныйПроцесс.УдалитьСрокИсполнения <> ДАТАВРЕМЯ(1, 1, 1)
			|	И КомплексныйПроцесс.СрокИсполненияПроцесса = ДАТАВРЕМЯ(1, 1, 1)
			|	И КомплексныйПроцесс.ПометкаУдаления = ЛОЖЬ
			|	И КомплексныйПроцесс.Завершен = ЛОЖЬ
			|	И КомплексныйПроцесс.Состояние = ЗНАЧЕНИЕ(Перечисление.СостоянияБизнесПроцессов.Активен)
			|	И &УсловиеПоУзлуОбмена";
			
		Если Не Константы.ВыполняетсяПлавноеОбновлениеС20.Получить() Тогда
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
				"И &УсловиеПоУзлуОбмена", "И КомплексныйПроцесс.УзелОбмена = &УзелОбмена");
			
			Запрос.УстановитьПараметр("УзелОбмена", РаботаСБизнесПроцессами.ЭтотУзелОбменаДляОбработкиПроцессов());
		Иначе
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "И &УсловиеПоУзлуОбмена", "");
		КонецЕсли;
		
		Запрос.Текст = ТекстЗапроса;
		
		Выборка = Запрос.Выполнить().Выбрать();
	Иначе
		Выборка = ВыборкаНеактивныхКомплексныхПроцессовДляОбработки();
	КонецЕсли;
	
	КоличествоОбработанныхПроцессов = 0;
	
	Пока Выборка.Следующий() Цикл
		
		ДатаОтсчета = ТекущаяДатаСеанса();
		
		Если ЗначениеЗаполнено(Выборка.ДатаОтложенногоСтарта) Тогда
			ДатаОтсчета = Выборка.ДатаОтложенногоСтарта;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Выборка.Дата) Тогда
			ДатаОтсчета = Выборка.Дата;
		КонецЕсли;
		
		Попытка
			ЗаблокироватьДанныеДляРедактирования(Выборка.Ссылка);
		Исключение
			Продолжить;
		КонецПопытки;
		
		ПеренестиСрокиКомплексныйПроцессВНовыеРеквизиты(Выборка.Ссылка, ДатаОтсчета);
		
		РазблокироватьДанныеДляРедактирования(Выборка.Ссылка);
		
		КоличествоОбработанныхПроцессов = КоличествоОбработанныхПроцессов + 1;
		
	КонецЦикла;
	
	Возврат КоличествоОбработанныхПроцессов;
	
КонецФункции

// Возвращает выборку неактивных комплексных процессов.
//
// Параметры:
//  Количество - Число - количество процессов в выборке
//
Функция ВыборкаНеактивныхКомплексныхПроцессовДляОбработки(Количество = 1000)
	
	Запрос = Новый Запрос;
	
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	КомплексныйПроцесс.Ссылка,
		|	КомплексныйПроцесс.Дата,
		|	ПроцессыДляЗапуска.ДатаОтложенногоСтарта
		|ИЗ
		|	БизнесПроцесс.КомплексныйПроцесс КАК КомплексныйПроцесс
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПроцессыДляЗапуска КАК ПроцессыДляЗапуска
		|		ПО КомплексныйПроцесс.Ссылка = ПроцессыДляЗапуска.БизнесПроцесс
		|ГДЕ
		|	КомплексныйПроцесс.ВедущаяЗадача = ЗНАЧЕНИЕ(Задача.ЗадачаИсполнителя.ПустаяСсылка)
		|	И КомплексныйПроцесс.ПометкаУдаления = ЛОЖЬ
		|	И КомплексныйПроцесс.УдалитьСрокИсполнения <> ДАТАВРЕМЯ(1, 1, 1)
		|	И КомплексныйПроцесс.СрокИсполненияПроцесса = ДАТАВРЕМЯ(1, 1, 1)
		|	И &УсловиеПоУзлуОбмена";
		
	Если Не Константы.ВыполняетсяПлавноеОбновлениеС20.Получить() Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
			"И &УсловиеПоУзлуОбмена", "И КомплексныйПроцесс.УзелОбмена = &УзелОбмена");
		
		Запрос.УстановитьПараметр("УзелОбмена", РаботаСБизнесПроцессами.ЭтотУзелОбменаДляОбработкиПроцессов());
	Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "И &УсловиеПоУзлуОбмена", "");
	КонецЕсли;
	
	Если Количество > 0 Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ВЫБРАТЬ", "ВЫБРАТЬ ПЕРВЫЕ " + Формат(Количество, "ЧГ=0"));
	КонецЕсли;
	
	Запрос.Текст = ТекстЗапроса;
	
	Возврат Запрос.Выполнить().Выбрать();
	
КонецФункции

// Переносит сроки Комплексного процесса из старых реквизитов в новые.
//
// Параметры:
//  СсылкаНаШаблон - БизнесПроцессСсылка.КомплексныйПроцесс - ссылка на процесс.
//  ДатаОтсчета - Дата - дата отсчета для рассчета сроков исполнителей.
//
Процедура ПеренестиСрокиКомплексныйПроцессВНовыеРеквизиты(СсылкаНаПроцесс, ДатаОтсчета = '00010101') Экспорт
	
	ПроцессОбъект = СсылкаНаПроцесс.ПолучитьОбъект();
	
	Если Не ЗначениеЗаполнено(ДатаОтсчета) Тогда
		ДатаОтсчета = СрокиИсполненияПроцессов.ДатаОтсчетаДляРасчетаСроковПроцесса(ПроцессОбъект);
		Смещение = СмещенияДатыОтсчета(СсылкаНаПроцесс);
		
		ДатаОтсчета = Макс(ДатаОтсчета, Смещение);
	КонецЕсли;
	
	ПеренестиСрокИсполнениБезРасчета = Ложь;
		
	НачатьТранзакцию();
	
	Попытка
		СтруктураДляРасчета = СтруктураДляРасчетаСрокаКомплексногоПроцесса(СсылкаНаПроцесс);
		
		Параметры = СрокиИсполненияПроцессов.ПараметрыДляРасчетаСроков();
		Параметры.ДатаОтсчета = ДатаОтсчета;
		
		ДанныеДляРасчета = ДанныеДляРасчетаСроковКомплексногоПроцесса(СтруктураДляРасчета, Параметры);
		ПометитьРассчитаннымиСрокиПростыхПроцессовВТаблицеСроков(ДанныеДляРасчета.Сроки);
		СрокиИсполненияПроцессов.РассчитатьСрокиИсполнения(
			ДанныеДляРасчета.Сроки, ДанныеДляРасчета.Предшественники, УникальныйИдентификаторПустой());
		ЗаписатьСрокиКомплексногоПроцесса(ДанныеДляРасчета);
		ЗафиксироватьТранзакцию();
	Исключение
		// При наличии ссылок на несуществующие объекты в комплексном процессе или его действиях
		// срок может и не рассчитаться. В таких процессах просто переносим старый срок в сам процесс.
		ОтменитьТранзакцию();
		ПеренестиСрокИсполнениБезРасчета = Истина;
	КонецПопытки;
	
	Если ПеренестиСрокИсполнениБезРасчета Тогда
		
		КомплексныйПроцессОбъект = СсылкаНаПроцесс.ПолучитьОбъект();
		КомплексныйПроцессОбъект.СрокИсполненияПроцесса = КомплексныйПроцессОбъект.УдалитьСрокИсполнения;
		РаботаСБизнесПроцессами.ЗаписатьПроцесс(КомплексныйПроцессОбъект, "ЗаписьСОбновлениемОбщегоСпискаПроцессов");
	КонецЕсли;
	
КонецПроцедуры

// Записывает сроки комплексного процесса и его действий из данных для рассчета.
//
// Параметры:
//  ДанныеДляРасчета - Структура - см. функцию ДанныеДляРасчетаСроковКомплексногоПроцесса
//
Процедура ЗаписатьСрокиКомплексногоПроцесса(ДанныеДляРасчета)
	
	ТипПроцесса = ТипЗнч(ДанныеДляРасчета.Ссылка);
	
	ЭтоШаблон = ШаблоныБизнесПроцессовКлиентСервер.ЭтоШаблонПроцесса(ДанныеДляРасчета.Ссылка);
	
	Если ТипПроцесса = Тип("БизнесПроцессСсылка.КомплексныйПроцесс") Тогда
		
		ЗаблокироватьДанныеДляРедактирования(ДанныеДляРасчета.Ссылка);
		
		ПроцессОбъект = ДанныеДляРасчета.Ссылка.ПолучитьОбъект();
		ПроцессОбъект.СрокИсполненияПроцесса = ПроцессОбъект.УдалитьСрокИсполнения;
		РаботаСБизнесПроцессами.ЗаписатьПроцесс(ПроцессОбъект, "ЗаписьСОбновлениемОбщегоСпискаПроцессов");
		
		РазблокироватьДанныеДляРедактирования(ДанныеДляРасчета.Ссылка);
		
	ИначеЕсли ТипПроцесса = Тип("БизнесПроцессСсылка.ОбработкаВнутреннегоДокумента")
		Или ТипПроцесса = Тип("БизнесПроцессСсылка.ОбработкаВходящегоДокумента")
		Или ТипПроцесса = Тип("БизнесПроцессСсылка.ОбработкаИсходящегоДокумента")
		Или ЭтоШаблон Тогда
		
		ЗаблокироватьДанныеДляРедактирования(ДанныеДляРасчета.Ссылка);
		
		ПроцессОбъект = ДанныеДляРасчета.Ссылка.ПолучитьОбъект();
		
		ЗаполнитьСрокиДействияКомплексногоПроцесса(
			ПроцессОбъект, ДанныеДляРасчета.Сроки, ДанныеДляРасчета.ИдентификаторыСроков);
			
		Если ЭтоШаблон Тогда
			ШаблоныБизнесПроцессов.ЗаписатьШаблон(ПроцессОбъект, "ПростаяЗапись");
		Иначе
			РаботаСБизнесПроцессами.ЗаписатьПроцесс(ПроцессОбъект, "ЗаписьСОбновлениемОбщегоСпискаПроцессов");
		КонецЕсли;
		
		РазблокироватьДанныеДляРедактирования(ДанныеДляРасчета.Ссылка);
		
	КонецЕсли;
	
	Для Каждого СтрокаСрок Из ДанныеДляРасчета.Сроки Цикл
		Если СтрокаСрок.ПодчиненныеСроки.Количество() > 0 Тогда
			ЗаписатьСрокиКомплексногоПроцесса(СтрокаСрок.ПодчиненныеСроки);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

//////////////////////////
// Протоколы мероприятий

// Обновляет сроки исполнения в протоколах мероприятий
//
// Параметры:
//  ТолькоАктивные - Булево - при значении Истина обновляет только активные (которые находятся в работе) процессы.
//
Функция ОбновитьСрокиИсполненияПроколовМероприятий(ТолькоАктивные = Истина)
	
	Запрос = Новый Запрос;
	
	Если ТолькоАктивные Тогда
		Запрос.Текст =
			"ВЫБРАТЬ
			|	ПротоколыМероприятий.Ссылка
			|ИЗ
			|	Справочник.ПротоколыМероприятий КАК ПротоколыМероприятий
			|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ПротоколыМероприятий.Исполнители КАК ПротоколыМероприятийИсполнители
			|		ПО (ПротоколыМероприятийИсполнители.Ссылка = ПротоколыМероприятий.Ссылка)
			|ГДЕ
			|	ПротоколыМероприятий.СостояниеИсполнения <> ЗНАЧЕНИЕ(Перечисление.СостоянияПротоколовМероприятий.Исполнен)
			|	И ПротоколыМероприятий.ПометкаУдаления = ЛОЖЬ
			|	И (ПротоколыМероприятий.УдалитьСрокИсполнения <> ДАТАВРЕМЯ(1, 1, 1)
			|			ИЛИ ПротоколыМероприятийИсполнители.УдалитьСрокИсполнения <> ДАТАВРЕМЯ(1, 1, 1)
			|			ИЛИ ПротоколыМероприятийИсполнители.УдалитьДлительностьИсполненияДни <> 0
			|			ИЛИ ПротоколыМероприятийИсполнители.УдалитьДлительностьИсполненияЧасы <> 0)
			|	И ПротоколыМероприятий.КоличествоИтераций = 0
			|
			|СГРУППИРОВАТЬ ПО
			|	ПротоколыМероприятий.Ссылка";
			
		Выборка = Запрос.Выполнить().Выбрать();
	Иначе
		Выборка = ВыборкаНеактивныхПротоколовМероприятийДляОбработки();
	КонецЕсли;
	
	КоличествоОбработанныхПроцессов = 0;
	
	Пока Выборка.Следующий() Цикл
		
		Попытка
			ЗаблокироватьДанныеДляРедактирования(Выборка.Ссылка);
		Исключение
			Продолжить;
		КонецПопытки;
		
		ПеренестиСрокиПротоколаМероприятийВНовыеРеквизиты(Выборка.Ссылка);
		
		РазблокироватьДанныеДляРедактирования(Выборка.Ссылка);
		
		КоличествоОбработанныхПроцессов = КоличествоОбработанныхПроцессов + 1;
		
	КонецЦикла;
	
	Возврат КоличествоОбработанныхПроцессов;
	
КонецФункции

// Возвращает выборку неактивных протоколов мероприятий.
//
// Параметры:
//  Количество - Число - количество процессов в выборке
//
Функция ВыборкаНеактивныхПротоколовМероприятийДляОбработки(Количество = 1000)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("УзелОбмена", РаботаСБизнесПроцессами.ЭтотУзелОбменаДляОбработкиПроцессов());
	
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	ПротоколыМероприятий.Ссылка
		|ИЗ
		|	Справочник.ПротоколыМероприятий КАК ПротоколыМероприятий
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ПротоколыМероприятий.Исполнители КАК ПротоколыМероприятийИсполнители
		|		ПО (ПротоколыМероприятийИсполнители.Ссылка = ПротоколыМероприятий.Ссылка)
		|ГДЕ
		|	ПротоколыМероприятий.ПометкаУдаления = ЛОЖЬ
		|	И (ПротоколыМероприятий.УдалитьСрокИсполнения <> ДАТАВРЕМЯ(1, 1, 1)
		|			ИЛИ ПротоколыМероприятийИсполнители.УдалитьСрокИсполнения <> ДАТАВРЕМЯ(1, 1, 1)
		|			ИЛИ ПротоколыМероприятийИсполнители.УдалитьДлительностьИсполненияДни <> 0
		|			ИЛИ ПротоколыМероприятийИсполнители.УдалитьДлительностьИсполненияЧасы <> 0)
		|	И ПротоколыМероприятий.КоличествоИтераций = 0
		|
		|СГРУППИРОВАТЬ ПО
		|	ПротоколыМероприятий.Ссылка";
		
	Если Количество > 0 Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ВЫБРАТЬ", "ВЫБРАТЬ ПЕРВЫЕ " + Формат(Количество, "ЧГ=0"));
	КонецЕсли;
	
	Запрос.Текст = ТекстЗапроса;
		
	Возврат Запрос.Выполнить().Выбрать();
	
КонецФункции

// Переносит сроки Протокола мероприятий из старых реквизитов в новые.
//
// Параметры:
//  СсылкаНаПротокол - СправочникСсылка.ПротоколыМероприятий - ссылка на протокол.
//
Процедура ПеренестиСрокиПротоколаМероприятийВНовыеРеквизиты(СсылкаНаПротокол) Экспорт
	
	Протокол = СсылкаНаПротокол.ПолучитьОбъект();
	
	Если Протокол.ВариантИсполнения = Перечисления.ВариантыМаршрутизацииЗадач.Параллельно Тогда
		Для Каждого СтрИсполнитель Из Протокол.Исполнители Цикл
			
			Если ЗначениеЗаполнено(СтрИсполнитель.УдалитьСрокИсполнения) Тогда
				СтрИсполнитель.СрокИсполнения = СтрИсполнитель.УдалитьСрокИсполнения;
			Иначе
				СтрИсполнитель.СрокИсполнения = Протокол.УдалитьСрокИсполнения;
			КонецЕсли;
			
			СтрИсполнитель.ВариантУстановкиСрокаИсполнения = 
				Перечисления.ВариантыУстановкиСрокаИсполнения.ТочныйСрок;
		КонецЦикла;
	Иначе
		Для Каждого СтрИсполнитель Из Протокол.Исполнители Цикл
			СтрИсполнитель.СрокИсполненияДни = СтрИсполнитель.УдалитьДлительностьИсполненияДни;
			СтрИсполнитель.СрокИсполненияЧасы = СтрИсполнитель.УдалитьДлительностьИсполненияЧасы;
			СтрИсполнитель.ВариантУстановкиСрокаИсполнения = 
				Перечисления.ВариантыУстановкиСрокаИсполнения.ОтносительныйСрок;
			СтрИсполнитель.СрокИсполнения = СтрИсполнитель.УдалитьСрокИсполнения;
		КонецЦикла;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Протокол.Проверяющий) Тогда
		Протокол.СрокОбработкиРезультатов = Протокол.УдалитьСрокИсполнения;
		Протокол.ВариантУстановкиСрокаОбработкиРезультатов = 
			Перечисления.ВариантыУстановкиСрокаИсполнения.ТочныйСрок;
	КонецЕсли;
	
	Протокол.СрокИсполненияПроцесса = Протокол.УдалитьСрокИсполнения;
	
	Протокол.КоличествоИтераций = 1;
	
	ОбновлениеИнформационнойБазы.ЗаписатьДанные(Протокол, Истина);
	
КонецПроцедуры

#КонецОбласти
