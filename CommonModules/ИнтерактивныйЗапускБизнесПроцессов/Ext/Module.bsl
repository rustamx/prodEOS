////////////////////////////////////////////////////////////////////////////////
// Программный интерфейс интерактивного запуска бизнес-процессов

// Получает шаблоны для интерактивного автозапуска бизнес-процессов
Функция ПолучитьШаблоныДляАвтоЗапуска(ВидИнтерактивногоБизнесСобытия, ВидДокумента, Организация, Документ) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	МассивВидовДокумента = Делопроизводство.ПолучитьВидДокументаИРодителей(ВидДокумента);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	НастройкаШаблоновБизнесПроцессов.ШаблонБизнесПроцесса КАК ШаблонБизнесПроцесса,
		|	НастройкаШаблоновБизнесПроцессов.ВидДокумента,
		|	НастройкаШаблоновБизнесПроцессов.ИнтерактивныйЗапуск,
		|	НастройкаШаблоновБизнесПроцессов.ВидИнтерактивногоСобытия,
		|	НастройкаШаблоновБизнесПроцессов.Условие
		|ИЗ
		|	РегистрСведений.НастройкаШаблоновБизнесПроцессов КАК НастройкаШаблоновБизнесПроцессов
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ДоступностьШаблоновПроцессов КАК ДоступностьШаблоновПроцессов
		|		ПО НастройкаШаблоновБизнесПроцессов.ШаблонБизнесПроцесса = ДоступностьШаблоновПроцессов.Шаблон
		|ГДЕ
		|	НастройкаШаблоновБизнесПроцессов.ВидДокумента В(&ВидДокумента)
		|	И (НастройкаШаблоновБизнесПроцессов.Организация = &Организация
		|			ИЛИ НастройкаШаблоновБизнесПроцессов.Организация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
		|			ИЛИ &Организация = НЕОПРЕДЕЛЕНО)
		|	И НастройкаШаблоновБизнесПроцессов.ИнтерактивныйЗапуск = ИСТИНА
		|	И НастройкаШаблоновБизнесПроцессов.ВидИнтерактивногоСобытия = &ВидИнтерактивногоСобытия
		|	И ДоступностьШаблоновПроцессов.РучнойЗапуск";
		
	Запрос.УстановитьПараметр("ВидДокумента", МассивВидовДокумента);
	Запрос.УстановитьПараметр("ВидИнтерактивногоСобытия", ВидИнтерактивногоБизнесСобытия);
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьУчетПоОрганизациям") Тогда
		Запрос.УстановитьПараметр("Организация", Организация);
	Иначе	
		Запрос.УстановитьПараметр("Организация", Неопределено);
	КонецЕсли;	
	
	Таблица = Запрос.Выполнить().Выгрузить();	
	
	СписокЗначений = Новый СписокЗначений;
	ТекущийПользователь = ПользователиКлиентСервер.ТекущийПользователь();
	
	Для Каждого Строка Из Таблица Цикл
		
		ТипШаблона = ТипЗнч(Строка.ШаблонБизнесПроцесса);
		
		РеквизитыШаблона = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
			Строка.ШаблонБизнесПроцесса, "ПометкаУдаления");
		
		Если РеквизитыШаблона.ПометкаУдаления Тогда
			Продолжить;
		КонецЕсли;
		
		Если ДокументооборотПраваДоступа.ПолучитьПраваПоОбъекту(Строка.ШаблонБизнесПроцесса).Чтение Тогда
			// Проверим условие маршрутизации
			Если РаботаСУсловиямиМаршрутизации.ПроверитьПрименимостьУсловияМаршрутизацииКОбъекту(Документ, Строка.Условие) Тогда
			СписокЗначений.Добавить(Строка.ШаблонБизнесПроцесса, Строка(Строка.ШаблонБизнесПроцесса), Истина);
			КонецЕсли;	
		КонецЕсли;
		
	КонецЦикла;	
	
	Возврат СписокЗначений;
	
КонецФункции

