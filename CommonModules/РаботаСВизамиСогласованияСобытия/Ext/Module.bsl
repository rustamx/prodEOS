
////////////////////////////////////////////////////////////////////////////////
// Работа с визами согласования события
//  
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ВизыСогласованияПередЗаписьюЗадачиПередЗаписью(Источник, Отказ) Экспорт
	
	Если Источник.ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если Источник.ДополнительныеСвойства.Свойство("ВидЗаписи")
		И Источник.ДополнительныеСвойства.ВидЗаписи <>
			"ЗаписьСОбновлением_МоихДокументов_КешаИнформацииОбОбъектах_ВизСогласования_ПредметовПодчиненныхПроцессов_ДопРеквизитовПоПредметам_СобытийИзмененияПредметов" Тогда
		
		Возврат;
	КонецЕсли;
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьВизыСогласования") Тогда 
		Возврат;
	КонецЕсли;	
	
	Если Источник.ТочкаМаршрута <> БизнесПроцессы.Согласование.ТочкиМаршрута.Согласовать Тогда 
		Возврат;
	КонецЕсли;	
	
	ЭтоНовый = Источник.ЭтоНовый();
	Источник.ДополнительныеСвойства.Вставить("ЭтоНовый", ЭтоНовый);
	
	ПометкаУдаления = Источник.Ссылка.ПометкаУдаления;
	Источник.ДополнительныеСвойства.Вставить("ПометкаУдаления", ПометкаУдаления);
	
	ИсключенаИзПроцесса = Источник.Ссылка.ИсключенаИзПроцесса;
	Источник.ДополнительныеСвойства.Вставить("ИсключенаИзПроцесса", ИсключенаИзПроцесса);
	
КонецПроцедуры

Процедура ВизыСогласованияПриЗаписиЗадачиПриЗаписи(Источник, Отказ) Экспорт
	
	Если Источник.ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если Источник.ДополнительныеСвойства.Свойство("ВидЗаписи")
		И Источник.ДополнительныеСвойства.ВидЗаписи <>
			"ЗаписьСОбновлением_МоихДокументов_КешаИнформацииОбОбъектах_ВизСогласования_ПредметовПодчиненныхПроцессов_ДопРеквизитовПоПредметам_СобытийИзмененияПредметов" Тогда
		
		Возврат;
	КонецЕсли;
	
	Если Источник.ТочкаМаршрута <> БизнесПроцессы.Согласование.ТочкиМаршрута.Согласовать Тогда 
		Возврат;
	КонецЕсли;	
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьВизыСогласования") Тогда 
		Возврат;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	ОбрабатываемыеПредметы = МультипредметностьКлиентСервер.ПолучитьМассивПредметовОбъекта(Источник,, Истина);
	
	Для Каждого Предмет Из ОбрабатываемыеПредметы Цикл
		Если Источник.ДополнительныеСвойства.ЭтоНовый Тогда
			// создание визы
			РаботаСВизамиСогласования.СоздатьВизуПоЗадаче(Предмет, Источник);
		Иначе	
			Если Источник.ДополнительныеСвойства.ПометкаУдаления <> Источник.ПометкаУдаления Тогда
				// отметить удаление
				РаботаСВизамиСогласования.ОтметитьУдалениеВизыПоЗадаче(Предмет, Источник, Источник.ПометкаУдаления);
			ИначеЕсли Источник.ДополнительныеСвойства.ИсключенаИзПроцесса <> Источник.ИсключенаИзПроцесса Тогда
				// отметить удаление
				РаботаСВизамиСогласования.ОтметитьУдалениеВизыПоЗадаче(Предмет, Источник, Источник.ИсключенаИзПроцесса);
			Иначе	
				// установить результат согласования в визе
				РаботаСВизамиСогласования.ЗаполнитьВизуПоЗадаче(Предмет, Источник);
			КонецЕсли;	
		КонецЕсли;
	КонецЦикла;
			
КонецПроцедуры

#КонецОбласти
