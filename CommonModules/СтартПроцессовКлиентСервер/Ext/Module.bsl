////////////////////////////////////////////////////////////////////////////////
// Старт бизнес процессов: в модуле содержатся процедуры для работы
// со стартом бизнес-процессов на клиенте и на сервере.

////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс_КарточкаШаблонаПроцесса

// Устанавливает описание отложенного старта.
//
// Параметры:
//   - ОтложенныйСтартДни - Число
//   - ОтложенныйСтартЧасы - Число
//
// Возвращаемое значение:
//   Строка.
//
Функция УстановитьОписанияОтложенногоСтарта(ОтложенныйСтартДни = Неопределено,
	ОтложенныйСтартЧасы = Неопределено) Экспорт
	
	Если ЗначениеЗаполнено(ОтложенныйСтартДни) И ЗначениеЗаполнено(ОтложенныйСтартЧасы) Тогда
		ОписаниеОтложенногоСтарта = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'через %1 %2 и %3 %4'; en = 'after %1 %2 and %3 %4'"),
			Строка(ОтложенныйСтартДни),
			ДелопроизводствоКлиентСервер.ПолучитьПодписьДней(ОтложенныйСтартДни),
			Строка(ОтложенныйСтартЧасы),
			ДелопроизводствоКлиентСервер.ПолучитьПодписьЧасов(ОтложенныйСтартЧасы));
	ИначеЕсли ЗначениеЗаполнено(ОтложенныйСтартДни) Тогда
		ОписаниеОтложенногоСтарта = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'через %1 %2'; en = 'after %1 %2'"),
			Строка(ОтложенныйСтартДни),
			ДелопроизводствоКлиентСервер.ПолучитьПодписьДней(ОтложенныйСтартДни));
	ИначеЕсли ЗначениеЗаполнено(ОтложенныйСтартЧасы) Тогда
		ОписаниеОтложенногоСтарта = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'через %1 %2'; en = 'after %1 %2'"),
			Строка(ОтложенныйСтартЧасы),
			ДелопроизводствоКлиентСервер.ПолучитьПодписьЧасов(ОтложенныйСтартЧасы));
	Иначе
		ОписаниеОтложенногоСтарта = НСтр("ru = 'не настроен'; en = 'not configured'");
	КонецЕсли;
	
	Возврат ОписаниеОтложенногоСтарта;
	
КонецФункции

// Возвращает стуктуру настройки старта процесса.
//
// Возвращаемое значение:
//  Структура
//   * БизнесПроцесс
//   * ДатаОтложенногоСтарта
//   * Состояние
//   * ПричинаОтменыСтарта
//
Функция СтруктураНастройкиСтартаПроцессов() Экспорт
	
	СтруктураНастройки = Новый Структура(
		"БизнесПроцесс, ДатаОтложенногоСтарта, Состояние, ПричинаОтменыСтарта",
		Неопределено,
		Дата(1,1,1),
		ПредопределенноеЗначение("Перечисление.СостоянияПроцессовДляЗапуска.ПустаяСсылка"),
		"");
		
	Возврат СтруктураНастройки;
	
КонецФункции

#КонецОбласти



