
////////////////////////////////////////////////////////////////////////////////
// Работа с бизнес-процессами клиент (переопределяемый): модуль содержит переопределяемые
// процедуры для поддержки особой логики работы процессов в редакциях КОРП и ДГУ.
//  
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс_ПомощникСозданияПроцессов

// Вызывается из РаботаСБизнесПроцессамиКлиент.ОткрытьПомощникСозданияОсновныхПроцессов при
// открытии помощника создания основных процессов.
//
// Параметры:
//  ТипыПроцессов - Массив, Строка - типы процессов, которые следует отображать в помощнике создания процессов.
//                                   Наименования типов соответствуют наименованию процессов в конфигураторе.
//                                   Если необходимо отображать только один тип процессов, тогда его можно передать
//                                   в виде строки в данный параметр, если несколько - тогда массив строк.
//  Предмет - Массив или СправочникСсылка.ВнутренниеДокументы и т.д. - ссылка или массив ссылок на предметы, на основании
//                                                                     которых следует создать процесс.
//  ФормаВладелец - УправляемаяФорма - форма объекта или списка с предметами.
//  ТипФормы - Строка - тип формы владельца, принимает значения "ФормаСписка" и "ФормаОбъекта".
//  СтандартнаяОбработка - Булево - в случае значения Истина открытие будет выполнено стандартным образом.
//
Процедура ПриОткрытииПомощникаСозданияОсновныхПроцессов(
	ТипыПроцессов, Предмет, ФормаВладелец, ТипФормы, СтандартнаяОбработка) Экспорт
	
	СтандартнаяОбработка = Ложь;
	
	ТипПредмета = ТипЗнч(Предмет);
	ТипМассив = Тип("Массив");
	
	Если Предмет = Неопределено
		Или (ТипПредмета = ТипМассив И Предмет.Количество() = 0) Тогда
		
		ПоказатьПредупреждение(, НСтр("ru = 'Команда не может быть выполнена для указанного объекта!'; en = 'Cannot execute the command for the specified object!'"));
		Возврат;
	КонецЕсли;
	
	ДопПараметры = Новый Структура;
	ДопПараметры.Вставить("ТипыПроцессов", ТипыПроцессов);
	ДопПараметры.Вставить("Предмет", Предмет);
	ДопПараметры.Вставить("ФормаВладелец", ФормаВладелец);
	ДопПараметры.Вставить("ТипФормы", ТипФормы);
	
	ОписаниеОповещения = Новый ОписаниеОповещения(
		"ОткрытьПомощникСозданияОсновныхПроцессовЗавершение", РаботаСБизнесПроцессамиКлиент, ДопПараметры);
	
	Если ТипФормы = "ФормаОбъекта" И Не ЗначениеЗаполнено(Предмет) Тогда // Выполняется для новых объектов.
		
		НименованиеДействия = "";
			
		Если ТипПредмета = Тип("СправочникСсылка.Пользователи") Тогда
			Если ТипыПроцессов = "Исполнение" Тогда
				НименованиеДействия = НСтр("ru = '""Исполнение""'; en = '""Performance""'");
			ИначеЕсли ТипыПроцессов = "Ознакомление" Тогда
				НименованиеДействия = НСтр("ru = '""Ознакомление""'; en = '""Examination""'");
			КонецЕсли;
		Иначе
			Если ТипЗнч(ТипыПроцессов) = ТипМассив
				Или ТипыПроцессов = "КомплексныйПроцесс"
				Или ТипыПроцессов = "ОбработкаВнутреннегоДокумента"
				Или ТипыПроцессов = "ОбработкаВходящегоДокумента"
				Или ТипыПроцессов = "ОбработкаИсходящегоДокумента" Тогда
				
				НименованиеДействия = НСтр("ru = '""В обработку""'; en = '""For processing""'");
			ИначеЕсли ТипыПроцессов = "Исполнение" Тогда
				НименованиеДействия = НСтр("ru = '""На исполнение""'; en = '""For performance""'");
			ИначеЕсли ТипыПроцессов = "Ознакомление" Тогда
				НименованиеДействия = НСтр("ru = '""На ознакомление""'; en = '""For examination""'");
			ИначеЕсли ТипыПроцессов = "Рассмотрение" Тогда
				НименованиеДействия = НСтр("ru = '""На рассмотрение""'; en = '""For review""'");
			ИначеЕсли ТипыПроцессов = "Регистрация" Тогда
				НименованиеДействия = НСтр("ru = '""На регистрацию""'; en = '""For registration""'");
			ИначеЕсли ТипыПроцессов = "Согласование" Тогда
				НименованиеДействия = НСтр("ru = '""На согласование""'; en = '""For approval""'");
			ИначеЕсли ТипыПроцессов = "Утверждение"
				И ДелопроизводствоКлиентСервер.ЭтоВнутреннийДокумент(Предмет)
				И ФормаВладелец.ИспользоватьПодписание Тогда
					НименованиеДействия = НСтр("ru = '""На подписание""'; en = '""For signing""'");
			ИначеЕсли ТипыПроцессов = "Утверждение" Тогда
				НименованиеДействия = НСтр("ru = '""На утверждение""'; en = '""For confirmation""'");
			КонецЕсли;
		КонецЕсли;
		
		ТекстВопроса = НСтр("ru = 'Данные еще не записаны.
			|Выполнение действия %1 возможно только после записи данных.
			|Данные будут записаны.';
			|en = 'The data is not saved yet. 
			|Executing action %1 is possible only after the data is saved. 
			|The data will be saved.'");
			
		ТекстВопроса = СтрШаблон(ТекстВопроса, НименованиеДействия);
		
		КнопкиВопроса = Новый СписокЗначений;
		КнопкиВопроса.Добавить(Истина, НСтр("ru = 'ОК'; en = 'ОК'"));
		КнопкиВопроса.Добавить(Ложь, НСтр("ru = 'Отмена'; en = 'Cancel'"));
		
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, КнопкиВопроса);
		
		Возврат;
	КонецЕсли;
	
	// Запись объекта, если он существует и был изменен в карточке.
	Если ТипФормы = "ФормаОбъекта"
		И ТипПредмета <> ТипМассив
		И ЗначениеЗаполнено(Предмет)
		И ФормаВладелец.Модифицированность Тогда
		
		Если Не ФормаВладелец.ПроверитьЗаполнение()
			Или Не ФормаВладелец.Записать() Тогда
			
			Возврат;
		КонецЕсли;
		
	КонецЕсли;
	
	ВыполнитьОбработкуОповещения(ОписаниеОповещения, Неопределено);
	
КонецПроцедуры

// Вызывается из РаботаСБизнесПроцессамиКлиент.ОткрытьПомощникСозданияОсновныхПроцессов при
// завершении открытия помощника создания основных процессов.
//
// Параметры:
//  Результат - Булево - результат открытия.
//  ДопПараметры - Структура - доп. параметры открытия.
//  СтандартнаяОбработка - Булево - в случае значения Истина завершение открытия будет выполнено стандартным образом.
//
Процедура ПриОткрытииПомощникаСозданияОсновныхПроцессовЗавершение(
	Результат, ДопПараметры, СтандартнаяОбработка) Экспорт
	
	СтандартнаяОбработка = Ложь;
	
	Если Результат = Ложь Тогда
		Возврат;
	КонецЕсли;
	
	ТипыПроцессов = ДопПараметры.ТипыПроцессов;
	Предмет = ДопПараметры.Предмет;
	ФормаВладелец = ДопПараметры.ФормаВладелец;
	ТипФормы = ДопПараметры.ТипФормы;
	
	ТипМассив = Тип("Массив");
	ТипПредмета = ТипЗнч(Предмет);
	
	Если Результат = Истина
		И ТипФормы = "ФормаОбъекта"
		И ТипПредмета <> ТипМассив Тогда
		
		Если Не ФормаВладелец.ПроверитьЗаполнение()
			Или Не ФормаВладелец.Записать() Тогда
			
			Возврат;
		КонецЕсли;
		
		Предмет = ФормаВладелец.Объект.Ссылка;
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура;
	
	Если ТипПредмета = Тип("СправочникСсылка.Проекты") Тогда
		ПараметрыФормы.Вставить("Проект", Предмет);
	ИначеЕсли ТипПредмета = Тип("СправочникСсылка.ПроектныеЗадачи") Тогда
		ПараметрыФормы.Вставить("ПроектнаяЗадача", Предмет);
	Иначе
		Предметы = Предмет;
		Если Не ТипПредмета = Тип("Массив")
			И Не ТипПредмета = Тип("ЗадачаСсылка.ЗадачаИсполнителя") Тогда
			
			Предметы = Новый Массив;
			Предметы.Добавить(Предмет);
		КонецЕсли;
		ПараметрыФормы.Вставить("Предметы", Предметы);
	КонецЕсли;
	
	ПараметрыФормы.Вставить("ТипыПроцессов", ТипыПроцессов);
	
	ОткрытьФорму("ОбщаяФорма.СозданиеБизнесПроцесса", ПараметрыФормы, ФормаВладелец);
	
КонецПроцедуры

#КонецОбласти
